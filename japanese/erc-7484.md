---
original: cf5495ec3caaede48301f7654761f058caa279f81dc3dc2f988ffdf1ee12da9b
---

---
eip: 7484
title: ERC-7579のためのレジストリ拡張
description: モジュラースマートアカウントがモジュールレジストリを使ってモジュールのセキュリティを検証できるアダプター
author: Konrad Kopp (@kopy-kat), zeroknots (@zeroknots)
discussions-to: https://ethereum-magicians.org/t/erc-7484-registry-adapters-for-smart-accounts/15434
status: Draft
type: Standards Track
category: ERC
created: 2023-08-14
requires: 7579
---

## 概要

このプロポーザルは、モジュールレジストリのインターフェースと機能を標準化し、モジュラースマートアカウントがレジストリアダプターを使ってモジュールのセキュリティを検証できるようにします。また、シングルトンモジュールレジストリの参照実装も提供します。

## 動機

[ERC-4337](./eip-4337.md)はコントラクトアカウントの実行フローを標準化し、[ERC-7579](./eip-7579.md)はこれらのアカウント(以下スマートアカウント)のモジュラー実装を標準化しています。これにより、任意の開発者がこれらのモジュラーアカウントのためのモジュールを構築できるようになりました。しかし、検証されていないサードパーティモジュールをスマートアカウントに追加すると、さまざまな攻撃ベクトルが生まれる可能性があります。

このセキュリティ問題に対する1つの解決策は、モジュールのセキュリティアテステーションを保存し、スマートアカウントがモジュールを使う前にこれらのアテステーションを照会できるモジュールレジストリを作成することです。この標準は2つのことを達成することを目的としています:

1. モジュールレジストリのインターフェースと必要な機能を標準化する。
2. スマートアカウントがモジュールレジストリを照会し、レジストリの動作を適切に処理するためのアダプターの機能を標準化する。

これにより、スマートアカウントは、アーキテクチャ、実行フロー、セキュリティ前提条件に関係なく、モジュールレジストリを安全に照会し、レジストリの動作を適切に処理できるようになります。この標準はまた、所有者のいないシングルトンモジュールレジストリの参照実装も提供します。このシングルモジュールレジストリを使うメリットは多数ありますが(「根拠」を参照)、シングルトンを使うことにはトレードオフもあるため、この標準ではスマートアカウントにこの参照実装の使用を義務付けていません。したがって、この標準により、スマートアカウントは必要なインターフェースと機能を実装しているあらゆるモジュールレジストリを照会できるようになり、統合の手間が減り、スマートアカウントの相互運用性が向上します。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

### 定義

- **スマートアカウント** - ERC-7579準拠のモジュラースマートアカウント。
- **モジュール** - 自己完結型のスマートアカウント機能。
- **アテステーション** - モジュールのセキュリティに関する on-chain の主張。
- **アテスター** - モジュールに関するアテステーションを行う主体。
- **(モジュール) レジストリ** - モジュールに関するアテステーションを保存する on-chain のコントラクト。
- **アダプター** - レジストリからアテステーションを取得・検証するスマートアカウントの機能。

### レジストリに必要な機能

レジストリのコアインターフェースは以下の通りです:

```solidity
interface IERC7484Registry {
    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*               内部アテスターによる検証                    */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/
    function check(address module) external view;

    function checkForAccount(address smartAccount, address module) external view;

    function check(address module, uint256 moduleType) external view;

    function checkForAccount(
        address smartAccount,
        address module,
        uint256 moduleType
    )
        external
        view;

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*                   内部アテスターの設定                     */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function trustAttesters(uint8 threshold, address[] calldata attesters) external;


    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*              外部アテスターによる検証                      */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function check(
        address module,
        address[] calldata attesters,
        uint256 threshold
    )
        external
        view;

    function check(
        address module,
        uint256 moduleType,
        address[] calldata attesters,
        uint256 threshold
    )
        external
        view;
}
```

レジストリはさらに以下の機能も実装しなければなりません:

- アテスターがアテステーションの作成者であることを検証する(例えば `msg.sender` を確認するか、署名を使う)。
- アテスターがこれまで行ったアテステーションを取り消せるようにする。
- アテステーションデータそのものか、アテステーションデータへの参照を保存する。

レジストリはさらに以下の追加機能を実装することが望ましいです:

- アテスターがアテステーションの有効期限を指定できるようにし、有効期限切れの場合は検証時にエラーを返す。
- アダプターやオフチェーンクライアントがある特定のアテステーションのデータを読み取れるビュー関数を実装する。

#### `check` 関数

- `module` に対して `attesters` 数が `threshold` 未満の場合、レジストリはエラーを返さなければならない。
- `attesters` のいずれかがその `module` に対するアテステーションを取り消している場合、レジストリはエラーを返さなければならない。
- 提供された `attesters` は一意かつ昇順でなければならず、そうでない場合はレジストリはエラーを返さなければならない。

#### `moduleType` を含む `check` 関数

- `module` に保存されているモジュールタイプが提供された `moduleType` と一致しない場合、レジストリはエラーを返さなければならない。

#### 内部アテスターを含む関数

- レジストリは `smartAccount` または `msg.sender`(前者が引数にない場合)に対して保存されたアテスター(複数可)を使わなければならない。
- `smartAccount` または `msg.sender`(前者が引数にない場合)に対してアテスターが保存されていない場合、レジストリはエラーを返さなければならない。

#### `trustAttesters`

- レジストリは `msg.sender` に対して `threshold` と `attesters` を保存しなければならない。
- 提供された `attesters` は一意かつ昇順でなければならず、そうでない場合はレジストリはエラーを返さなければならない。

### アダプターの動作

スマートアカウントは、アカウント自体かモジュールの一部として、以下のアダプター機能を実装しなければなりません。このアダプター機能は以下を保証しなければなりません:

- モジュール `A` を初めて呼び出す前または呼び出し中に、必ずレジストリに照会する。
- レジストリがエラーを返した場合は、セキュリティリスクとして扱う。

さらに、アダプターは以下の機能を実装することが望ましいです:

- レジストリがエラーを返した場合は、トランザクションフローをエラーにする。
- モジュール `A` をインストールする際にレジストリに照会する。
- モジュール `A` を実行する際にレジストリに照会する。

例: `check` を使ったアダプターのフロー
![check を使ったアダプターのフロー](../assets/eip-7484/check-sequence.jpg)

## 根拠

### アテステーション

アテステーションとは、モジュールに関する on-chain の主張です。これらの主張は、モジュールのセキュリティ(通常のスマートコントラクトの監査と同様)、モジュールが特定の標準に準拠しているかどうか、あるいはこれらのモジュールに関するその他の種類の記述などを含むことができます。これらの主張の一部は on-chain で検証可能ですが、ほとんどは不可能です。

例えば、スマートアカウントがDELEGATECALLを使ってモジュールを呼び出す場合、そのモジュールが書き込める記憶域スロットを特定することが有用です。しかし、これを on-chain で検証するのは実際上不可能です。代わりに、アテスターがこの確認を off-chain で行い、その結果を on-chain のアテステーションとして公開することができます。

アテステーションは常にモジュールに関する特定の主張ですが、この提案では、アテステーションデータを任意のデータやデータへの参照とすることを意図的に許可しています。これにより、単純なセキュアフラグから複雑なモジュールの実行時動作の証明まで、あらゆる種類のデータを主張として使うことができます。

### シングルトンレジストリ

アテステーションを on-chain で照会可能にするには、スマートコントラクト内のリストに保存する必要があります。この提案には、所有者のいないシングルトンレジストリの参照実装が含まれています。

シングルトンレジストリを提案する理由は以下の通りです:

**セキュリティ**: シングルトンレジストリは、アカウントの統合を単一の真理の源に集中させることで、より高いセキュリティを生み出します。これにはいくつかの利点があります: a) 1つのモジュールに対する最大限の数と種類のアテステーションを可能にし、b) 異なるレジストリの信頼性と安全性を検証する必要がなくなり、アテステーションを行うオンチェーンエンティティへの信頼委譲に集中できます。その結果、アカウントはセキュリティ保証を高めるために複数のアテスターを低コストで照会でき、異なるレジストリの安全性を検証する必要がなくなります。

**相互運用性**: シングルトンレジストリは「アテステーションの流動性」を高めるだけでなく、モジュールの流動性も高め、モジュールの相互運用性を向上させます。開発者はアテステーションを受け取るためにモジュールを1か所にのみデプロイすれば良く、統合されたすべてのアカウントにモジュールを最大限に配布できます。アテスターも、過去の監査作業を連鎖させてアテステーションを派生させることで、これらの依存関係の連鎖から継続的なセキュリティ恩恵を受けられます。これにより、アテステーションの履歴を辿ったり、開発者によるバージョン管理などの恩恵が得られます。

ただし、シングルトンを使うことにはトレードオフもあります。シングルトンレジストリは単一障害点を生み出し、攻撃者に悪用されると深刻な結果を招く可能性があります。最も深刻な攻撃ベクトルの1つは、攻撃者が信頼されたアテスターに代わってマルウェアモジュールにアテステーションを行うことです。このトレードオフの1つは、複数のレジストリを使う場合、セキュリティアテステーションの変更(例えば脆弱性が見つかりアテステーションが取り消された場合)がエコシステム全体に遅れて伝播するため、攻撃者にそのギャップを悪用される機会が長く続くか、特定のレジストリで問題が指
摘された後に他のレジストリでは見つからないうちに悪用される可能性があるということです。

シングルトンであるため、レジストリは非常に柔軟性が高く、そのため計算効率は低くなる可能性があります。つまり、シングルトンレジストリを照会するのはコンピューショナル(およびガス)コストがより高くなる可能性があります。その一方で、シングルトンを使えば、複数のパーティのアテステーションを同時に照会するのがより安価になります。つまり、レジストリのアーキテクチャによっては、照会するアテステーションの数(N)が一定以上になると、柔軟なシングルトンを使う方が計算コストが低くなる可能性があります。ただし、参照実装はガス使用量も考慮して設計されているため、専門的なレジストリがこの参照実装のベンチマークを大幅に下回ることは考えにくいでしょう。

### モジュールタイプ

モジュールにはさまざまなタイプがあり、アカウントがモジュールの特定のタイプを確認することが重要な場合があります。例えば、アカウントがアカウントの検証ロジックを処理するモジュールをインストールしたい場合、アテスターがそのモジュールがその検証ロジックを実行できることを確認していることを確認したいかもしれません。そうしないと、アカウントが使用不能になる可能性のあるモジュールをインストールしてしまう危険があります。

ただし、レジストリ自体は特定のモジュールタイプの意味を気にする必要はありません。代わりに、アテスターがこれらのタイプを提供し、レジストリがそれらを保存することができます。

### 関連する取り組み

レジストリの参照実装は、Ethereum Attestation Serviceを大いに参考にしています。ただし、この提案の具体的な用途には、EASにいくつかのカスタマイズと追加が必要だったため、EASの既存のコントラクトをそのままモジュールレジストリとして使うのは最適ではありませんでした。ただし、いくつかの変更を加えれば、EASをモジュールレジストリとして使うことは可能です。

## 下位互換性

下位互換性の問題はありません。

## 参照実装

### Adapter.sol

```solidity
contract Adapter {
    IRegistry registry;

    function checkModule(address module) internal {
        // レジストリでモジュールのアテステーションを確認
        registry.check(module);
    }

    function checkModuleWithModuleTypeAndAttesters(address module, address[] memory attesters, uint256 threshold,  uint16 moduleType) internal {
        // レジストリでモジュールのアテステーションリストを確認
        registry.check(module, attesters, threshold, moduleType);
    }

}
```

### Account.sol

**注**: これは上記の「仕様」に準拠した特定の例ですが、この実装は強制ではありません。

```solidity
contract Account is Adapter {
    ...

    // モジュールをインストールする
    function installModule(
        uint256 moduleTypeId,
        address module,
        bytes calldata initData
    )
        external
        payable
    {
        checkModule(module);
        ...
    }

    // モジュールを実行する
    function executeFromExecutor(
        ModeCode mode,
        bytes calldata executionCalldata
    )
        external
        payable
        returns (bytes[] memory returnData)
    {
        checkModule(module);
        ...
    }

    ...
}
```

### Registry

```solidity
/**
* @dev この実装は読みやすさのために最適化されていません
* @dev 一部の関数実装は省略しています
*/
contract Registry is IERC7484Registry {
    ...

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*               内部アテスターによる検証                    */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/
    function check(address module) external view {
        (address[] calldata attesters, uint256 threshold) = _getAttesters(msg.sender);

        uint256 validCount = 0;
        for (uint256 i = 0; i < attesters.length; i++) {
            bool isValid = _check(module, attesters[i]);
            if (isValid) validCount++;
        }
        if (validCount < threshold) revert AttestationThresholdNotMet();
    }

    function checkForAccount(address smartAccount, address module) external view {
        (address[] calldata attesters, uint256 threshold) = _getAttesters(smartAccount);

        ...
    }

    function check(address module, uint256 moduleType) external view {
        (address[] calldata attesters, uint256 threshold) = _getAttesters(msg.sender);

        uint256 validCount = 0;
        for (uint256 i = 0; i < attesters.length; i++) {
            bool isValid = _check(module, attesters[i]);
            if (isValid) validCount++;

            AttestationRecord storage attestation = _getAttestation(module, attester);
            if (attestation.moduleType != moduleType) revert ModuleTypeMismatch();
        }
        if (validCount < threshold) revert AttestationThresholdNotMet();
    }

    function checkForAccount(
        address smartAccount,
        address module,
        uint256 moduleType
    )
        external
        view {
        (address[] calldata attesters, uint256 threshold) = _getAttesters(smartAccount);

        ...
    }

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*                   内部アテスターの設定                     */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function trustAttesters(uint8 threshold, address[] calldata attesters) external {
        ...
    }

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*              外部アテスターによる検証                      */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function check(
        address module,
        address[] calldata attesters,
        uint256 threshold
    )
        external
        view
    {
        uint256 validCount = 0;
        for (uint256 i = 0; i < attesters.length; i++) {
            bool isValid = _check(module, attesters[i]);
            if (isValid) validCount++;
        }
        if (validCount < threshold) revert AttestationThresholdNotMet();
    }

    function check(
        address module,
        uint256 moduleType,
        address[] calldata attesters,
        uint256 threshold
    )
        external
        view
    {
        uint256 validCount = 0;
        for (uint256 i = 0; i < attesters.length; i++) {
            bool isValid = _check(module, attesters[i]);
            if (isValid) validCount++;

            AttestationRecord storage attestation = _getAttestation(module, attester);
            if (attestation.moduleType != moduleType) revert ModuleTypeMismatch();
        }
        if (validCount < threshold) revert AttestationThresholdNotMet();
    }

    /*´:°•.°+.*•´.*:˚.°*.˚•´.°:°•.°•.*•´.*:˚.°*.˚•´.°:°•.°+.*•´.*:*/
    /*                         内部                           */
    /*.•°:°.´+˚.*°.˚:*.´•*.+°.•°:´*.´•*.•°.•°:°.´:•˚°.*°.˚:*.´+°.•*/

    function _check(address module, address attester) external view returns (bool isValid){
        AttestationRecord storage attestation = _getAttestation(module, attester);

        uint48 expirationTime = attestation.expirationTime;
        uint48 attestedAt =
            expirationTime != 0 && expirationTime < block.timestamp ? 0 : attestation.time;
        if (attestedAt == 0) return;

        uint48 revokedAt = attestation.revocationTime;
        if (revokedAt != 0) return;

        isValid = true;
    }

    function _getAttestation(
        address module,
        address attester
    )
        internal
        view
        virtual
        returns (AttestationRecord storage)
    {
        return _moduleToAttesterToAttestations[module][attester];
    }

    function _getAttesters(
        address account
    )
        internal
        view
        virtual
        returns (address[] calldata attesters, uint256 threshold)
    {
        ...
    }

    ...
}
```

## セキュリティ上の考慮事項

議論が必要。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。