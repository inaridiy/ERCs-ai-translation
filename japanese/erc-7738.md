---
original: be00b8f2fd84e3b9ee95b329825eb0b344e3a2cf2efb7d789bd39bf3cd4b8745
---

---
eip: 7738
title: パーミッションレススクリプトレジストリ
description: コントラクトに関連付けられた実行可能スクリプトを取得するためのパーミッションレスレジストリ
author: Victor Zhang (@zhangzhongnan928), James Brown (@JamesSmartCell)
discussions-to: https://ethereum-magicians.org/t/erc-7738-permissionless-script-registry/20503
status: Draft
type: Standards Track
category: ERC
created: 2024-07-01
requires: 173
---
## 概要

このEIPは、トークンに関連付けられた実行可能スクリプトを検索するための標準レジストリを作成する手段を提供します。

## 動機

[ERC-5169](./eip-5169.md)は、コントラクトのクライアントスクリプトルックアップメソッドを提供します。これには、コントラクトが構築時に`ERC-5169`インターフェイスを実装しているか、アップグレードパスを許可する必要があります。

この提案では、プロトタイプおよび認証済みスクリプトを提供できるコントラクトについて説明します。このコントラクトは、マルチチェーンのシングルトンインスタンスになり、サポートされているチェーン上で同一のアドレスにデプロイされます。

### 概要

レジストリコントラクトは、特定のコントラクトアドレスに対するURIリンクのセットを提供します。これらのURIリンクは、ウォレット、ビューア、またはミニdappによって取得できるスクリプトプログラムを指しています。

ポインタは、レジストリコントラクトのセッターを使ってパーミッションレスに設定できます。

## 仕様

このドキュメントの「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されます。

コントラクトは`IERC7738`インターフェイスを実装しなければなりません。
コントラクトは、スクリプトが更新されたときに`ScriptUpdate`イベントを発行しなければなりません。
コントラクトは、`ERC-173`の`owner()`がコントラクトのスクリプトエントリを最初に返すように、`scriptURI`を順序付けるべきです(単純な実装の場合、ウォレットは最初の`scriptURI`を選択します)。
コントラクトは、スクリプトURIエントリが多数ある場合にページングする手段を提供するべきです。

```solidity
interface IERC7738 {
    /// @dev このイベントは、scriptURIが更新されたときに発行されます。
    /// これにより、このインターフェイスを実装するウォレットがキャッシュされたスクリプトを更新できます。
    event ScriptUpdate(address indexed contractAddress, string[] newScriptURI);

    /// @notice コントラクトのscriptURIを取得する
    /// @return The scriptURI
    function scriptURI(address contractAddress) external view returns (string[] memory);

    /// @notice scriptURIを更新する
    /// emits event ScriptUpdate(address indexed contractAddress, scriptURI memory newScriptURI);
    function setScriptURI(address contractAddress, string[] memory scriptURIList) external;
}
```

このドキュメントのキーワード「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」は、RFC 2119およびRFC 8174に記載されているように解釈されます。

## 根拠

この方法により、[ERC-5169](./eip-5169.md)インターフェイスを実装していないコントラクトでもスクリプトを関連付けることができ、中央集権型のオンラインサーバーを必要とせず、セキュリティの必要性や、データベースのゲートキーパーとなる組織を必要としません。

## テストケース

Hardhatのコントラクトとテストスクリプトの2つの実装は、アセットフォルダにあります。

## 参考実装

```solidity
import "@openzeppelin/contracts/access/Ownable.sol";

contract DecentralisedRegistry is IERC7738 {
    struct ScriptEntry {
        mapping(address => string[]) scriptURIs;
        address[] addrList;
    }

    mapping(address => ScriptEntry) private _scriptURIs;

    function setScriptURI(
        address contractAddress,
        string[] memory scriptURIList
    ) public {
        require (scriptURIList.length > 0, "> 0 entries required in scriptURIList");
        bool isOwnerOrExistingEntry = Ownable(contractAddress).owner() == msg.sender 
            || _scriptURIs[contractAddress].scriptURIs[msg.sender].length > 0;
        _scriptURIs[contractAddress].scriptURIs[msg.sender] = scriptURIList;
        if (!isOwnerOrExistingEntry) {
            _scriptURIs[contractAddress].addrList.push(msg.sender);
        }
        
        emit ScriptUpdate(contractAddress, msg.sender, scriptURIList);
    }

    // コントラクトのscriptURIリストを返す。
    // オーナー割り当てのスクリプトがリストの先頭に来るように順序付ける
    function scriptURI(
        address contractAddress
    ) public view returns (string[] memory) {
        //scriptURIの返却リストを構築、オーナーが先
        address contractOwner = Ownable(contractAddress).owner();
        address[] memory addrList = _scriptURIs[contractAddress].addrList;
        uint256 i;

        //リストの長さを計算
        uint256 listLen = _scriptURIs[contractAddress].scriptURIs[contractOwner].length;
        for (i = 0; i < addrList.length; i++) {
            listLen += _scriptURIs[contractAddress].scriptURIs[addrList[i]].length;
        }

        string[] memory ownerScripts = new string[](listLen);

        // オーナースクリプトを追加
        uint256 scriptIndex = _addScriptURIs(contractOwner, contractAddress, ownerScripts, 0);

        // 残りのスクリプトを追加
        for (uint256 i = 0; i < addrList.length; i++) {
            scriptIndex = _addScriptURIs(addrList[i], contractAddress, ownerScripts, scriptIndex);
        }

        return ownerScripts;
    }

    function _addScriptURIs(
        address user,
        address contractAddress,
        string[] memory ownerScripts,
        uint256 scriptIndex
    ) internal view returns (uint256) {
        for (uint256 j = 0; j < _scriptURIs[contractAddress].scriptURIs[user].length; j++) {
            string memory thisScriptURI = _scriptURIs[contractAddress].scriptURIs[user][j];
            if (bytes(thisScriptURI).length > 0) {
                ownerScripts[scriptIndex++] = thisScriptURI;
            }
        }
        return scriptIndex;
    }
}
```

## セキュリティ上の考慮事項

提供されるスクリプトは、さまざまな方法で認証できます:

1. セッターが指定するターゲットコントラクトが[ERC-173](./eip-173.md)の`Ownable`インターフェイスを実装しています。スクリプトが取得されると、署名がOwner()と一致することを確認できます。TokenScriptの場合、dappやウォレットがTokenScript SDKを使用したり、TokenScript オンライン検証サービスを使用したり、XMLから署名を抽出してkeccak256を取り、ecrecoverでサインキーアドレスを確認したりできます。
2. コントラクトがOwnableを実装していない場合、さらに次のような対策を講じることができます:
 a. ホスティングアプリ/ウォレットが、サードパーティのAPIやブロックエクスプローラを使ってデプロイメントキーを確認できます。実装するウォレット、dapp、ビューアは、この配置キーと署名が一致することを確認します。
 b. 署名キーを事前に認証したホスティングアプリに組み込まれたキーチェーンを使用できます。
 c. ガバナンストークンを使って、スクリプト評議会がキーの認証を行うことができます。

これらの基準を満たさない場合:
- メインネットの実装では、ウォレットはスクリプトの使用に慎重であるべきです - アプリや/またはユーザーの裁量になります。
- テストネットでは、ウォレットプロバイダの裁量でスクリプトを機能させることが許容されます。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。