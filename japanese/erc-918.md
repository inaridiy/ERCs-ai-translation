---
original: 76f86b2b6cab2b2ac3a1eed125c291f5f96a8f8ba77e0c2b58be5a249c353705
---

---
eip: 918
title: 採掘可能なトークン標準
author: Jay Logelin <jlogelin@alumni.harvard.edu>、Infernal_toast <admin@0xbitcoin.org>、Michael Seiler <mgs33@cornell.edu>、Brandon Grill <bg2655@columbia.edu>
type: Standards Track
category: ERC
status: Stagnant
created: 2018-03-07
---

### 簡単な要約

Proof of Work アルゴリズムを使用した分配のための標準化された採掘可能なトークンの仕様。

### 概要

この仕様は、トークン契約内のトークンを最初にロックし、mint() 関数を使用して徐々に分配する方法を説明しています。この mint() 関数は、ガス代を最小限に抑え、分配率を制御するために Proof of Work アルゴリズムを使用します。さらに、採掘可能なトークンの標準化により、CPU およびGPUのトークン採掘ソフトウェア、トークン採掘プール、およびトークン採掘エコシステムの他の外部ツールが登場することになります。

### 動機

ICO モデルとその派生物によるトークン分配は、人間の行為者による不正行為の対象となりやすくなっています。さらに、新しいトークンプロジェクトは中央集権的であり、単一のエンティティが初期のコインとICOで集めたすべての資金を処理および管理しなければなりません。「Initial Mining Offering」(IMO)を介してトークンを分配することで、トークン契約の所有権がデプロイヤーに属さなくなり、デプロイヤーは「ただの別のユーザー」になります。その結果、採掘されたトークン分配モデルを使用する際の投資家のリスク露出が大幅に減少します。この標準は、ERC20、ERC721、およびその他との最大限の相互運用性を可能にするために、単独で機能するように設計されています。

### 仕様

#### インターフェース
一般的な行動仕様には、トークンのミンティング操作を定義する主要な関数、複数のトークンを発行するための任意の統合ミンティング操作、チャレンジ番号、ミニング難易度、ミニング ターゲット、および現在の報酬の取得関数、最後にトークンの発行が成功した際に発行される Mint イベントが含まれます。最小限、契約はこのインターフェースに準拠する必要があります(統合操作を除く)。より定義された構造を活用し、オーバーライドされたフェーズ関数を介した外部実装を容易にするために、以下に説明する抽象契約と連携することをお勧めします。

``` solidity
interface ERC918  {
   
   function mint(uint256 nonce) public returns (bool success);

   function getAdjustmentInterval() public view returns (uint);

   function getChallengeNumber() public view returns (bytes32);

   function getMiningDifficulty() public view returns (uint);

   function getMiningTarget() public view returns (uint);

   function getMiningReward() public view returns (uint);
   
   function decimals() public view returns (uint8);

   event Mint(address indexed from, uint rewardAmount, uint epochCount, bytes32 newChallengeNumber);
}
```

#### 抽象契約(オプション)

抽象契約は EIP918 インターフェースに準拠し、トークンのミンティングとミンティングの4つの内部フェーズ(ハッシュ、報酬、エポック、難易度調整)の導入によって、行動定義を拡張しています。この構造は、使用に適しすぎるほど一般的ではなく、複数のミニング実装タイプに対応できるだけの十分な余地を提供しています。

### フィールド

#### adjustmentInterval
難易度調整間の時間(秒単位)。

``` solidity
bytes32 public adjustmentInterval;
```

#### challengeNumber
現在のチャレンジ番号。新しい報酬がミントされた後、新しいチャレンジ番号が生成されることが期待されます。

``` solidity
bytes32 public challengeNumber;
```

#### difficulty
現在のミニング難易度で、\_adjustDifficulty ミンティング フェーズを介して調整する必要があります。

``` solidity
uint public difficulty;
```

#### tokensMinted
ミントされた合計トークンの累積カウンター。通常、\_reward フェーズ中に変更されます。

``` solidity
uint public tokensMinted;
```

#### epochCount
「ブロック」の数

``` solidity
uint public epochCount;
```

### ミニング操作

#### mint

ハッシュ ダイジェストの検証と、msg.sender への報酬割り当ての成功を示すフラグを返します。中間者攻撃を防ぐために、ダイジェストには最近の Ethereum ブロックハッシュと msg.sender のアドレスを含めることをお勧めします。検証後、mint 関数は送信者に対するマイニング報酬を計算して配布し、契約の供給に関する内部アカウンティング操作を実行します。

mint 操作は、ハッシュ、\_reward、\_newEpoch、および \_adjustDifficulty の4つの個別のフェーズを呼び出す public 関数として存在します。最も柔軟な実装を作成しながら必要なコントラクト プロトコルに準拠するために、トークン実装者がこれらの内部メソッドをオーバーライドすることをお勧めします。これにより、基本契約がそれらの実行を処理できるようになります。

このエクスポーズされた関数は、ミナーがチャレンジ ダイジェストを検証し、報酬を計算し、統計情報を入力し、エポック変数を変更し、必要に応じて解決策の難易度を調整するために呼び出されます。完了すると、Mint イベントが発行され、成功フラグが返されます。

``` solidity
contract AbstractERC918 is EIP918Interface {

    // 難易度調整間の時間
    uint public adjustmentInterval;
     
    // 新しい報酬がミントされた後、新しいチャレンジ番号を生成する
    bytes32 public challengeNumber;
    
    // 現在のミニング ターゲット
    uint public miningTarget;

    // ミントされた合計トークンの累積カウンター
    uint public tokensMinted;

    // 難易度の再調整ごとのブロック数
    uint public blocksPerReadjustment;

    // 「ブロック」の数
    uint public epochCount;
   
    /*
     * ミナーがチャレンジ ダイジェストを検証し、報酬を計算し、統計情報を入力し、エポック変数を変更し、解決策の難易度を調整するために呼び出される、外部公開の mint 関数。
     * 完了すると、Mint イベントが発行され、成功インジケーターが返されます。
     **/
    function mint(uint256 nonce) public returns (bool success) {
        require(msg.sender != address(0));

        // ハッシュ関数の検証を実行する
        hash(nonce);
        
        // 現在の報酬を計算する
        uint rewardAmount = _reward();
        
        // ミントされたトークンの量を増やす
        tokensMinted += rewardAmount;
        
        epochCount = _epoch();

        //時折、難易度を再調整する。デプロイ時は再調整しない
        if(epochCount % blocksPerReadjustment == 0){
            _adjustDifficulty();
        }
       
        // 成功した実装を示す Mint イベントを送信する
        emit Mint(msg.sender, rewardAmount, epochCount, challengeNumber);
        
        return true;
    }
}
```

##### *Mint イベント*

検証と報酬が成功すると、mint メソッドは報酬アドレス、報酬額、エポック カウント、および最新のチャレンジ番号を示す Mint イベントをディスパッチします。

``` solidity
event Mint(address indexed from, uint reward_amount, uint epochCount, bytes32 newChallengeNumber);
```

#### hash

実装でオーバーライドされるべき public インターフェース関数 hash。検証済みのダイジェストを返します。

``` solidity
function hash(uint256 nonce) public returns (bytes32 digest);
```

#### \_reward

実装でオーバーライドされるべき内部インターフェース関数 \_reward。報酬額を計算して割り当てます。この関数は報酬額を返す必要があります。

``` solidity
function _reward() internal returns (uint);
```

#### \_newEpoch

実装でオーバーライドされるべき内部インターフェース関数 \_newEpoch。次のマイニング フェーズの準備として、マイニング変数を変更するためのカットポイントを定義します。

``` solidity
function _newEpoch(uint256 nonce) internal returns (uint);
```
 
#### \_adjustDifficulty
 
実装でオーバーライドされるべき内部インターフェース関数 \_adjustDifficulty。必要に応じて、ミニングの難易度(difficulty フィールドを介して)を調整します。

``` solidity
function _adjustDifficulty() internal returns (uint);
```

#### getAdjustmentInterval

難易度調整操作間の時間(秒単位)。

``` solidity
function getAdjustmentInterval() public view returns (uint);
```

#### getChallengeNumber

最近の Ethereum ブロックハッシュ。将来のブロックのプリマイニングを防ぐために使用されます。

``` solidity
function getChallengeNumber() public view returns (bytes32);
```

#### getMiningDifficulty

通常は報酬生成中に自動調整される、PoW ソリューションのダイジェストが必要とする桁数。

``` solidity
function getMiningDifficulty() public view returns (uint)
```

#### getMiningReward

現在の報酬額を返します。アルゴリズムによっては、トークンが採掘されるにつれて、希少性を提供するために報酬が定期的に分割されます。

``` solidity
function getMiningReward() public view returns (uint)
```

### 採掘関数の例
keccak256 ミニング トークンの有効なノンスを見つけるための一般的な Python 採掘関数は次のとおりです:
``` python
def generate_nonce():
  myhex =  b'%064x' % getrandbits(32*8)
  return codecs.decode(myhex, 'hex_codec')
  
def mine(challenge, public_address, difficulty):
  while True:
    nonce = generate_nonce()
    hash1 = int(sha3.keccak_256(challenge+public_address+nonce).hexdigest(), 16)
    if hash1 < difficulty:
      return nonce, hash1
```

ノンスとhash1が見つかったら、これらを使ってスマート コントラクトの mint() 関数を呼び出して、トークンの報酬を受け取ります。

### 統合採掘拡張機能(オプション)
複数のトークンの統合採掘をサポートするために、ERC918 標準の一部として、オプションの統合採掘拡張機能を実装できます。基本契約が tx.origin ではなく msg.sender を使用してリワードを適用する場合にのみ、次の関数が適切に機能することに注意してください。そうでない場合、報酬されたトークンは最終ユーザーではなく呼び出し契約に送信されます。

``` solidity
/**
 * @title ERC-918 採掘可能なトークン標準、オプションの統合採掘機能
 * @dev https://github.com/ethereum/EIPs/blob/master/EIPS/eip-918.md を参照
 * 
 */
contract ERC918Merged is AbstractERC918 {
    /*
     * @notice ミナーがチャレンジ ダイジェストを検証し、報酬を計算し、統計情報を入力し、状態変数を変更し、解決策の難易度を調整するために呼び出される、外部公開の merge 関数。さらに、merge 関数は統合報酬に使用されるターゲット トークン アドレスの配列を受け取ります。完了すると、Mint イベントが発行され、成功インジケーターが返されます。
     *
     * @param _nonce 解決策のノンス
     **/
    function merge(uint256 _nonce, address[] _mineTokens) public returns (bool) {
      for (uint i = 0; i < _mineTokens.length; i++) {
        address tokenAddress = _mineTokens[i];
        ERC918Interface(tokenAddress).mint(_nonce);
      }
    }

    /*
     * @notice 以前の定義との下位互換性のために保持された外部公開の merge 関数
     *
     * @param _nonce 解決策のノンス
     * @param _challenge_digest チャレンジ番号 + メッセージ送信者 + 解決策ノンスのkeccak256エンコード
     **/
     function merge(uint256 _nonce, bytes32 _challenge_digest, address[] _mineTokens) public returns (bool) {
       //チャレンジ ダイジェストが予期されるものと一致する必要がある
       bytes32 digest = keccak256( abi.encodePacked(challengeNumber, msg.sender, _nonce) );
       require(digest == _challenge_digest, "Challenge digest does not match expected digest on token contract [ ERC918Merged.mint() ]");
       return merge(_nonce, _mineTokens);
     }
}
```

### 委任
ミンティング拡張機能(オプション)
ミナーがプールオペレーターに解決策を提出し、ミナーが直接イーサリアムのトランザクション コストを支払う必要がないような第三者ミンティング提出パラダイムを容易にするために、委任ミンティング拡張機能を使用できます。これは、オフチェーンのマイニング アカウントが標準化されたミント ソリューション パケットをパッケージ化して署名し、プールまたは第三者に送信することで実行されます。

ERC918 ミニング可能なミント パケット メタデータは、次のスキーマを使用して準備する必要があります:
``` solidity
{
    "title": "ミニング可能なミント パケット メタデータ",
    "type": "object",
    "properties": {
        "nonce": {
            "type": "string",
            "description": "ターゲットの解決策ノンスを識別する",
        },
        "origin": {
            "type": "string",
            "description": "ノンスを採掘した元のユーザーを識別する",
        },
        "signature": {
            "type": "string",
            "description": "タイトに詰め込まれた変数のハッシュ sha3('delegatedMintHashing(uint256,address)')+nonce+origin_accountの署名",
        }
    }
}
```
JavaScript クライアントでミニング可能なミント パケットを準備する方法は次のようになります:

``` solidity
function prepareDelegatedMintTxn(nonce, account) {
  var functionSig = web3.utils.sha3("delegatedMintHashing(uint256,address)").substring(0,10)
  var data = web3.utils.soliditySha3( functionSig, nonce, account.address )
  var sig = web3.eth.accounts.sign(web3.utils.toHex(data), account.privateKey )
  // ミント パケットを準備する
  var packet = {}
  packet.nonce = nonce
  packet.origin = account.address
  packet.signature = sig.signature
  // 結果のJSONパケットをプールまたは第三者に配信する
  var mineableMintPacket = JSON.stringify(packet, null, 4)
  /* todo: mineableMintPacketをサブミッターに送信する */
  ...
}
```
パケットが準備され書式設定されたら、トランザクションのガスを支払い、結果のトークンを受け取る第三者にルーティングできます。プール/第三者は手動で、手数料を差し引いた上で、元のマイナーにミントされたトークンを払い戻す必要があります。

パケットのリレーを実装する第三者のコード サンプルは次のとおりです:
``` solidity
//ミナーが受け取る
var mineableMintPacket = ...
var packet = JSON.parse(mineableMintPacket)
erc918MineableToken.delegatedMint(packet.nonce, packet.origin, packet.signature)
```
委任ミント拡張機能は、ERC918 を次のようなサブコントラクトとして拡張します:
``` js
import 'openzeppelin-solidity/contracts/contracts/cryptography/ECDSA.sol';

contract ERC918DelegatedMint is AbstractERC918, ECDSA {
   /**
     * @notice delegatedMint で使用されるペイロードのハッシュ (keccak256)
     * @param _nonce ゴールデンノンス
     * @param _origin 元のマイナー
     * @param _signature 元のマイナーの楕円曲線署名
     */
    function delegatedMint(uint256 _nonce, address _origin, bytes _signature) public returns (bool success) {
        bytes32 hashedTx = delegatedMintHashing(_nonce, _origin);
        address minter = recover(hashedTx, _signature);
        require(minter == _origin, "Origin minter address does not match recovered signature address [ AbstractERC918.delegatedMint() ]");
        require(minter != address(0), "Invalid minter address recovered from signature [ ERC918DelegatedMint.delegatedMint() ]");
        success = mintInternal(_nonce, minter);
    }

    /**
     * @notice delegatedMint で使用されるペイロードのハッシュ (keccak256)
     * @param _nonce ゴールデンノンス
     * @param _origin 元のマイナー
     */
    function delegatedMintHashing(uint256 _nonce, address _origin) public pure returns (bytes32) {
        /* "0x7b36737a": delegatedMintHashing(uint256,address) */
        return toEthSignedMessageHash(keccak256(abi.encodePacked( bytes4(0x7b36737a), _nonce, _origin)));
    }
}
```

### ミニング可能なトークンのメタデータ(オプション)
特定のミニング可能なトークンのリッチで潜在的に変更可能なメタデータを提供するために、オフチェーンのリファレンスを提供することがより実現可能です。これには、JSON 文字列フィールド symbol、name、description、website、image、および type でエンコードされたメタデータを返す 'metadataURI()' というインターフェース メソッドの実装が必要です。

ミニング可能なトークンのメタデータに関するSolidity インターフェース:
``` solidity
/**
 * @title ERC-918 ミニング可能なトークン標準、オプションのメタデータ拡張機能
 * @dev https://github.com/ethereum/EIPs/blob/master/EIPS/eip-918.md を参照
 * 
 */
interface ERC918Metadata is AbstractERC918 {
    /**
     * @notice ミニング可能な資産の明確な Uniform Resource Identifier (URI)。
     */
    function metadataURI() external view returns (string);
}
```

ミニング可能なトークンのメタデータ JSON スキーマ定義:
``` solidity
{
    "title": "ミニング可能なトークンのメタデータ",
    "type": "object",
    "properties": {
        "symbol": {
            "type": "string",
            "description": "ミニング可能なトークンのシンボルを識別する",
        },
        "name": {
            "type": "string",
            "description": "ミニング可能なトークンの名前を識別する",
        },
        "description": {
            "type": "string",
            "description": "ミニング可能なトークンの長い説明を識別する",
        },
        "website": {
            "type": "string",
            "description": "ミニング可能なトークンのホームページ URI を識別する",
        },
        "image": {
            "type": "string",
            "description": "ミニング可能なトークンの画像 URI を識別する",
        },
        "type": {
            "type": "string",
            "description": "ミニング可能なトークンのソリューションをエンコードするハッシュ アルゴリズム (例: keccak256) を識別する",
        }
    }
}
```

### 根拠

solidity の keccak256 アルゴリズムを使う必要はありませんが、EVM で効率的な一方向のアルゴリズムであり、solidity で簡単に実行できるため、推奨されています。ノンスは、ミナーが見つけようとするソリューションであり、ハッシュ アルゴリズムの一部です。チャレンジ番号も、ハッシュの一部であり、将来のブロックをマイニングできないようにするためのランダムなデータとして機能します。msg.sender アドレスもハッシュの一部であるため、ノンス ソリューションは特定の Ethereum アカウントにのみ有効であり、中間者攻撃の対象にはなりません。これにより、プールがミナーを簡単に欺くことができないため、プールを運営することもできます。

電力と ハードウェアをミニング トークン資産に転換する経済性は、分散型ミナーのコミュニティに、直接イーサリアム トークン経済に関与するオプションを提供します。ハッシュ パワーで投票することで、現実世界のリソースに経済的に連動した資産により、ミナーは初期のトークン取引に参加するインセンティブを持つことになります。

ミニング トークンに対するコミュニティの懸念の1つは、ネットワークのセキュリティを確保する機能がないことでした。トークンのマイニングはネットワークのセキュリティを確保しませんが、中央集権的な ICO に対する代替手段を提供することで、コミュニティを腐敗から守る機能を果たします。さらに、初期のマイニング オファリングは1週間、1日、または1時間しか続かない可能性があり、その時点ですべてのトークンがミントされます。

### 下位互換性
この標準の以前のバージョンには、challengeNumber、msg.sender、およびnonce をハッシュエンコードした冗長な 'challenge_digest' パラメーターが mint() 関数に組み込まれていました。これをミント操作中の処理とガス使用量を最小限に抑えるために標準から削除することが決定されました。ただし、既存のマイニング プログラムおよびプール ソフトウェアとの相互運用性のために、次の契約を継承ツリーに追加できます:

``` solidity
/**
 * @title ERC-918 ミニング可能なトークン標準、オプションの下位互換性関数
 * @dev https://github.com/ethereum/EIPs/blob/master/EIPS/eip-918.md を参照
 * 
 */
contract ERC918BackwardsCompatible is AbstractERC918 {

    /*
     * @notice 以前のmint()定義との下位互換性のために保持された外部公開のmint関数
     * @param _nonce 解決策のノンス
     * @param _challenge_digest チャレンジ番号 + メッセージ送信者 + 解決策ノンスのkeccak256エンコード
     **/
    function mint(uint256 _nonce, bytes32 _challenge_digest) public returns (bool success) {
        //チャレンジ ダイジェストが予期されるものと一致する必要がある
        bytes32 digest = keccak256( abi.encodePacked(challengeNumber, msg.sender, _nonce) );
        require(digest == _challenge_digest, "Challenge digest does not match expected digest on token contract [ AbstractERC918.mint() ]");
        success = mint(_nonce);
    }
}
```

### テストケース
(コンセンサスの変更に影響するEIPのテストケースは必須です。その他のEIPは該当する場合、テストケースへのリンクを含めることができます。)

### 実装

シンプルな例:
https://github.com/0xbitcoin/EIP918-Mineable-Token/blob/master/contracts/SimpleERC918.sol

複雑な例:

https://github.com/0xbitcoin/EIP918-Mineable-Token/blob/master/contracts/0xdogeExample.sol
https://github.com/0xbitcoin/EIP918-Mineable-Token/blob/master/contracts/0xdogeExample2.sol
https://github.com/0xbitcoin/EIP918-Mineable-Token/blob/master/contracts/0xBitcoinBase.sol

0xBitcoin トークン契約: 
https://etherscan.io/address/0xb6ed7644c69416d67b522e20bc294a9a9b405b31

MVI OpenCL トークン マイナー 
https://github.com/mining-visualizer/MVis-tokenminer/releases

PoWAdv トークン契約:
https://etherscan.io/address/0x1a136ae98b49b92841562b6574d1f3f5b0044e4c

### 著作権
著作権およびその関連権利は [CC0](../LICENSE.md) で放棄されています。