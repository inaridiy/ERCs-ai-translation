---
original: b0a6ff5220c6910acd8718cd52358d4e9f6d6cbb1aebfedb36f7bb613eeb308d
---

---
eip: 1444
title: シグナルツーテキストによるローカライズされたメッセージング
author: Brooklyn Zelenka (@expede), Jennifer Cooper (@jenncoop)
discussions-to: https://ethereum-magicians.org/t/eip-1444-localized-messaging-with-signal-to-text/
status: 停滞
type: Standards Track
category: ERC
created: 2018-09-23
---

## 簡単な要約

機械コードを任意の言語と表現で人間が読める文字列に変換する方法。

## 概要

ユーザーフィードバックを提供するためのオンチェーンシステム。機械効率的なコードを任意の言語や表現の人間が読める文字列に変換する。このシステムは言語のリストを強制しないが、ユーザーが自分の選択した翻訳文を作成、共有、使用できるようにする。

## 動機

エンドユーザーがスマートコントラクトからフィードバックや指示を必要とする多くのケースがある。数値コードを直接公開しても、UXやDXが良くない。イーサリアムがエキスパートと一般ユーザーの両方が使える本当のグローバルシステムになるためには、トランザクション中に何が起こったかについてできるだけ多くの言語でフィードバックを提供するシステムが必要である。

英語で書かれた固定文字列を返すだけでは、世界人口の一部しか対象にできない。この規格は、翻訳の分散コレクションを作成、登録、共有、使用できる方法を提案し、より文化的および言語的に多様なメッセージングを可能にする。

意図、ステータス、状態遷移、その他のセマンティックシグナルを表現する機械効率的な方法には、ブール値、列挙型、[ERC-1066 コード](./eip-1066.md)などがある。これらのシグナルに人間が読める文字列を提供することで、開発者エクスペリエンスが向上し (`revert` など)、エンドユーザーエクスペリエンスも向上する。

## 仕様

### コントラクトアーキテクチャ

2種類のコントラクト: `LocalizationPreferences`と`Localization`。

`LocalizationPreferences`コントラクトは`tx.origin`のプロキシとして機能する。

```diagram
                                                                   +--------------+
                                                                   |              |
                                                          +------> | Localization |
                                                          |        |              |
                                                          |        +--------------+
                                                          |
                                                          |
+-----------+          +-------------------------+        |        +--------------+
|           |          |                         | <------+        |              |
| Requestor | <------> | LocalizationPreferences | <-------------> | Localization |
|           |          |                         | <------+        |              |
+-----------+          +-------------------------+        |        +--------------+
                                                          |
                                                          |
                                                          |        +--------------+
                                                          |        |              |
                                                          +------> | Localization |
                                                                   |              |
                                                                   +--------------+
```

### `Localization`

コードとそのテキスト表現のマッピングを保持するコントラクト。

```solidity
interface Localization {
  function textFor(bytes32 _code) external view returns (string _text);
}
```

#### `textFor`

ローカライズされたテキスト表現を取得する。

```solidity
function textFor(bytes32 _code) external view returns (string _text);
```

### `LocalizationPreferences`

ユーザーの好みの`Localization`を設定できるプロキシコントラクト。テキストの検索は、ユーザーの好みのコントラクトにデリゲートされる。

すべてのキーが埋められた fallback `Localization`が利用可能でなければならない。ユーザー指定の`Localization`に明示的に設定されたローカライゼーションがない場合 (つまり、`textFor`が空文字列を返す)、`LocalizationPreferences`は fallback `Localization`に再デリゲートしなければならない。

```solidity
interface LocalizationPreferences {
  function set(Localization _localization) external returns (bool);
  function textFor(bytes32 _code) external view returns (bool _wasFound, string _text);
}
```

#### `set`

ユーザーの好みの`Localization`を登録する。登録するユーザーは`tx.origin`と見なされるべきである。

```solidity
function set(Localization _localization) external;
```

#### `textFor`

ユーザーの好みの`Localization`コントラクトからコードのテキストを取得する。

最初の戻り値(`bool _wasFound`)は、そのLocalizationからテキストが利用可能かどうか、またはフォールバックが使用されたかどうかを表す。このコンテキストでフォールバックが使用された場合、`textFor`の最初の戻り値は`false`に設定され、それ以外の場合は`true`に設定される。

```solidity
function textFor(bytes32 _code) external view returns (bool _wasFound, string _text);
```

### 文字列フォーマット

すべての文字列は[UTF-8](https://www.ietf.org/rfc/rfc3629.txt)でエンコーディングされなければならない。

```solidity
"Špeĉiäl chârãçtérs are permitted"
"As are non-Latin characters: アルミ缶の上にあるみかん。"
"Emoji are legal: 🙈🙉🙊🎉"
"Feel free to be creative: (ﾉ◕ヮ◕)ﾉ*:･ﾟ✧"
```

### テンプレート

テンプレート文字列は許可され、[ANSI C `printf`](https://pubs.opengroup.org/onlinepubs/009696799/utilities/printf.html)の規則に従わなければならない。

```solidity
"Satoshi's true identity is %s"
```

2つ以上の引数を持つテキストは、POSIX パラメータフィールド拡張を使用するべきである。

```solidity
"Knock knock. Who's there? %1$s. %1$s who? %2$s!"
```

## 根拠

### `bytes32`キー

`bytes32`は非常に効率的であり、EVMの基本ワードサイズです。(card(A) > 1.1579 × 10<sup>77</sup>)という膨大な要素数を考えると、ほぼ任意の実用的なシグナル、列挙型、状態を埋め込むことができます。アプリケーションのキーが`bytes32`より長い場合は、その長いキーをハッシュすることで正しい幅にマッピングできます。

`bytes1`のようなより小さな幅のデータ型を使用するデザイン([ERC-1066](./eip-1066.md)など)は、より大きな幅に直接埋め込むことができます。これは小さなセットを大きなセットにマッピングする単純な1対1の変換です。

### ローカルvsグローバルとシングルトン

この仕様では、単一のグローバルレジストリを強制するのではなく、任意のコントラクトとユースケースが独自のシステムをデプロイできるようにしています。これにより、より柔軟性が高くなり、一般的なユースケースのための単一の`LocalizationPreference`コントラクトを使用したり、`Localization`を異なるプロキシ間で共有したり、`Localization`間で翻訳をデリゲートしたりするなど、コミュニティの選択を制限しません。

合意された単一のシングルトンには多くの実用的な用途があります。例えば、かなり普遍的で、より広範なエコシステム(ウォレット、フレームワーク、デバッガーなど)に直接統合されるコードを翻訳する場合は、単一の`LocalizationPreference`が望ましいでしょう。

さまざまなユースケースやコードのための複数の`LocalizationPreference`を分散させるのではなく、「レジストリのレジストリ」のようなグローバルなものを想像することができます。このアプローチでは、すべての翻訳をすべてのユースケースで統一的に検索できますが、分散化の精神と自由に反するものです。このようなシステムはまた、検索の複雑さを増大させ、最初からコードを正しく書く(またはアップグレード可能なコントラクトのオーバーヘッドを追加する)必要があり、「統一された」または中央集権的な番号付けシステムでユースケースの競合に対処する必要があります。さらに、特にrevertテキストを検索する場合、検索は軽量である必要があります。

これらの理由から、この仕様は、オンチェーンでの検出可能性を犠牲にしつつ、より分散化された軽量なアプローチを選択しています。レジストリは依然として編集可能ですが、強制するのは難しく、この仕様の範囲外です。

### オフチェーンストレージ

別の選択肢は、オフチェーンでテキストを保存し、オンチェーンにポインタを持つことです。`bytes32`コードを返し、別のシステム(Webサーバーなど)にコード-テキストのマッチングを行わせます。オフチェーンのリソースの可用性を保証するのは難しく、`revert`メッセージとも互換性がありません。

### ASCII vs UTF-8 vs UTF-16

UTF-8は現時点で最も広く使用されているエンコーディングです。ASCIIを直接埋め込みながら、ほとんどの自然言語、絵文字、特殊文字をサポートしています。

詳細は[UTF-8 Everywhere Manifesto](https://utf8everywhere.org/)を参照してください。

### テキストが見つからない場合

 requestorに空の文字列を返すと、ローカライゼーションシステムの目的を完全に逸脱してしまいます。見つからないテキストを処理する2つのオプションは:

1. 好みの言語で「テキストが見つかりませんでした」というメッセージを返す
2. 別の言語で実際のメッセージを返す

#### 一般的なオプション

このデザインでは、一般的なフォールバックテキストを使用しないことにしました。ユーザーに有用な情報を提供せず、`Localization`メンテナー(存在する場合)に連絡する必要があるだけです。

#### フォールバックオプション

このプロポーザルで概説したデザインは、一般的に使用される言語(英語や中国語など)でテキストを提供することです。まず、ユーザーが好みを設定していない場合にルーティングされる言語です。次に、ユーザーがその言語のある程度の習熟度を持っているか、少なくとも自動翻訳サービスを使用できる可能性が高いです。

`textFor`の最初の戻り値ブール値を使って、フォールバックが発生したことを知ることができるのは、事後の言語検出を試みるよりもはるかに簡単です。この情報は、ローカライゼーションがフォールバックした理由を説明する必要がある特定のUIケースで有用です。

### 分散型テキストクラウドソーシング

イーサリアムが大規模に普及するためには、ユーザーが最も快適な言語、表現、詳細レベルでインタラクションできるようにする必要があります。従来の中央集権型アプリケーションのように固定された翻訳セットを課すのではなく、この EIP は誰もが翻訳を作成、キュレーション、使用できる方法を提供します。これにより、文化的および言語的に多様なメッセージングを供給する群衆が後押しされ、情報へのより広範囲で分散したアクセスが実現されます。

### `printf`スタイルのフォーマット文字列

C言語の`printf`テンプレートは長年の事実上の標準です。ほとんどの言語(標準ライブラリまたは第三者ライブラリ)で広く互換性があります。これにより、消費プログラムが低開発者オーバーヘッドで文字列を補間できるようになります。

#### パラメータフィールド

POSIX パラメータフィールド拡張は重要です。言語には共通の語順がないためです。パラメータフィールドにより、引数の再利用と並べ替えが可能になります。

```solidity
("%1$s is an element with the atomic number %2$d!", "Mercury", 80);
// => "Mercury is an element with the atomic number 80!"
```

#### 簡略化された翻訳

翻訳テキストはすべてのパラメータを必要とせず、値を無視することができます。これにより、ユーザーに技術的な情報を露出させずに済みます。

```ruby
#!/usr/bin/env ruby

sprintf("%1$s é um elemento", "Mercurio", 80)
# => "Mercurio é um elemento"
```

```clojure
#!/usr/bin/env clojure

(format "Element #%2$s" "Mercury" 80)
;; => Element #80
```

### 補間戦略

テンプレート文字列をそのまま返し、引数を別の戻り値やイ
ベントのフィールドとして提供することが強く推奨されます。実際の補間はオフチェーンで行うようにしてください。

```solidity
event AtomMessage {
  bytes32 templateCode;
  bytes32 atomCode;
  uint256 atomicNumber;
}
```

```javascript
#!/usr/bin/env node

var printf = require('printf');

const { returnValues: { templateCode, atomCode, atomicNumber } } = eventResponse;

const template = await AppText.textFor(templateCode);
// => "%1$s ist ein Element mit der Ordnungszahl %2$d!"

const atomName = await PeriodicTableText.textFor(atomCode);
// => "Merkur"

printf(template, atomName, 80);
// => "Merkur ist ein Element mit der Ordnungszahl 80!"
```

### 未指定の動作

この仕様では以下を指定していません:

* デフォルトの`Localization`へのパブリックまたはプライベートアクセス
* だれがテキストを設定できるか
  * デプロイヤー
  * `onlyOwner`
  * 誰でも
  * ホワイトリストのユーザー
  * その他
* いつテキストが設定されるか
  * `constructor`
  * いつでも
  * 空のスロットに書き込むが、既存のテキストは上書きしない
  * その他

これらは意図的に未指定のままにしています。これらにはそれぞれ多くのケースがあり、いずれかを制限することはこの提案の範囲を超えています。

## 実装

```solidity
pragma solidity ^0.4.25;

contract Localization {
  mapping(bytes32 => string) private dictionary_;

  constructor() public {}

  // 現在は上書きする
  function set(bytes32 _code, string _message) external {
    dictionary_[_code] = _message;
  }

  function textFor(bytes32 _code) external view returns (string _message) {
    return dictionary_[_code];
  }
}

contract LocalizationPreference {
  mapping(address => Localization) private registry_;
  Localization public defaultLocalization;

  bytes32 private empty_ = keccak256(abi.encodePacked(""));

  constructor(Localization _defaultLocalization) public {
    defaultLocalization = _defaultLocalization;
  }

  function set(Localization _localization) external returns (bool) {
    registry_[tx.origin] = _localization;
    return true;
  }

  function get(bytes32 _code) external view returns (bool, string) {
    return get(_code, tx.origin);
  }

  // 主にテストのために
  function get(bytes32 _code, address _who) public view returns (bool, string) {
    string memory text = getLocalizationFor(_who).textFor(_code);

    if (keccak256(abi.encodePacked(text)) != empty_) {
      return (true, text);
    } else {
      return (false, defaultLocalization.textFor(_code));
    }
  }

  function getLocalizationFor(address _who) internal view returns (Localization) {
    if (Localization(registry_[_who]) == Localization(0)) {
      return Localization(defaultLocalization);
    } else {
      return Localization(registry_[tx.origin]);
    }
  }
}
```

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。