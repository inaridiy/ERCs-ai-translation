---
original: 99641cf0c535eed30843e61e3202bd8824b9d50004abe147532400b3f4f632ea
---

---
eip: 1271
title: コントラクトの標準署名検証メソッド
description: コントラクトがアカウントの場合の署名検証の標準的な方法
author: Francisco Giordano (@frangio)、Matt Condon (@shrugs)、Philippe Castonguay (@PhABC)、Amir Bandeali (@abandeali1)、Jorge Izquierdo (@izqui)、Bertrand Masius (@catageek)
discussions-to: https://github.com/ethereum/EIPs/issues/1271
status: Final
type: Standards Track
category: ERC
created: 2018-07-25
---

## 概要
外部所有アカウント(EOA)は、関連する秘密鍵を使ってメッセージに署名できますが、現在のところコントラクトはできません。我々は、任意のコントラクトが、自身に代わって署名が有効かどうかを検証する標準的な方法を提案します。これは、署名コントラクトに `isValidSignature(hash, signature)` 関数を実装することで可能になります。この関数を呼び出して、署名の有効性を検証できます。

## 動機

資産の移動権限の検証など、署名メッセージを利用したいコントラクトが多数存在し、今後も増えていくでしょう。これらのコントラクトが、外部所有アカウント以外(つまりコントラクトオーナーなど)をサポートできるようにするには、コントラクトが自身に代わっての署名の有効性を示す標準的なメカニズムが必要です。

署名の提供が必要な1つの例が、オフチェーンの注文帳を持つ分散型取引所です。ここでは、EOAが注文に署名し、取引所のスマートコントラクトに取引の実行を許可します。しかし、コントラクトの場合、通常の署名は不可能です。なぜなら、コントラクトには秘密鍵がないためです。そこで本提案が必要となります。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」は、[RFC 2119](https://www.ietf.org/rfc/rfc2119.txt)の定義に従って解釈されるものとします。

```javascript
pragma solidity ^0.5.0;

contract ERC1271 {

  // bytes4(keccak256("isValidSignature(bytes32,bytes)")
  bytes4 constant internal MAGICVALUE = 0x1626ba7e;

  /**
   * @dev 提供された署名が、提供されたハッシュに対して有効かどうかを返す必要がある
   * @param _hash      署名対象のデータのハッシュ
   * @param _signature 関連付けられた署名のバイト配列
   *
   * 関数が成功した場合は、必ず bytes4 の magic value 0x1626ba7e を返す必要がある。
   * 状態を変更してはいけない(solc < 0.5の場合はSTATICCALL、solc > 0.5の場合はview修飾子を使用)
   * 外部からの呼び出しを許可する必要がある
   */ 
  function isValidSignature(
    bytes32 _hash, 
    bytes memory _signature)
    public
    view 
    returns (bytes4 magicValue);
}
```

`isValidSignature` は、署名の有効性を検証するために任意のメソッドを呼び出すことができます。この検証は、状況依存(時間ベースや状態ベースなど)、EOA依存(スマートウォレット内の署名者の承認レベルなど)、署名方式依存(ECDSA、マルチシグ、BLSなど)などさまざまな要因に基づいて行うことができます。

この関数は、メッセージに署名したいコントラクト(スマートコントラクトウォレット、DAO、マルチシグウォレットなど)によって実装される必要があります。署名者がコントラクトの場合、署名を受け入れるアプリケーションはこのメソッドを呼び出すべきです。

## 根拠
提案された関数名は、適切であると考えています。なぜなら、ある*承認された*署名者が、特定のデータに対して適切な署名を提供した場合、その署名は署名コントラクトによって「有効」と見なされるからです。つまり、署名されたアクションメッセージは、署名者がスマートウォレットに代わって行うアクションを承認されている場合にのみ有効となります。

2つの引数を提供するのは、署名対象のハッシュと署名を分離するためのシンプルさのためです。bytes32のハッシュを使うのは、コントラクトが標準ではないハッシュ関数(例えば[EIP-712](./eip-712.md)のようなもの)を期待する可能性があるためです。

`isValidSignature()` は状態を変更できないようにする必要があります。これは、GasTokenのマイニングや同様の攻撃ベクトルを防ぐためです。また、この制限により、関数の実装面が単純化され、より良い標準化と、オフチェーンからのコントラクト照会が可能になります。

特定の返り値を期待するのは、署名の検証をより厳密かつシンプルにするためです。

## 下位互換性

このEIPは、コントラクトベースの署名検証に特化しているため、EOAの署名検証に関する従来の取り組みとの下位互換性があります。

## 参考実装

署名コントラクトの実装例:

```solidity

  /**
   * @notice 署名者がこのコントラクトのオーナーであることを検証する。
   */
  function isValidSignature(
    bytes32 _hash,
    bytes calldata _signature
  ) external override view returns (bytes4) {
    // 署名を検証する
    if (recoverSigner(_hash, _signature) == owner) {
      return 0x1626ba7e;
    } else {
      return 0xffffffff;
    }
  }

 /**
   * @notice ハッシュの署名者を回復する。署名者はEOAアカウントであると仮定する。
   * @dev EthSignの署名にのみ対応
   * @param _hash       署名対象のハッシュ
   * @param _signature  (bytes32 r, bytes32 s, uint8 v)としてエンコードされた署名
   */
  function recoverSigner(
    bytes32 _hash,
    bytes memory _signature
  ) internal pure returns (address signer) {
    require(_signature.length == 65, "SignatureValidator#recoverSigner: 署名の長さが不正");

    // Solidityでは変数のスコープが制限されない。
    uint8 v = uint8(_signature[64]);
    bytes32 r = _signature.readBytes32(0);
    bytes32 s = _signature.readBytes32(32);

    // EIP-2ではecrecover()の署名の可変性が許可されている。これを排除し、署名を一意のものにする。
    // Ethereum Yellow Paperの付録Fでは、sの有効範囲は0 < s < secp256k1n ÷ 2 + 1、vの有効範囲は{27, 28}と定義されている。
    // 現在のライブラリの多くは、sの値が下位オーダーの範囲内にある一意の署名を生成する。
    //
    // ライブラリが可変的な署名(sの値が上位範囲にあるなど)を生成する場合は、
    // 0xFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFEBAAEDCE6AF48A03BBFD25E8CD0364141 - s1として新しいsの値を計算し、
    // vを27から28、または28から27に反転させる。
    // vが0/1ではなく27/28を生成するライブラリの場合は、vに27を加える必要がある。
    //
    // OpenZeppelinのソースから引用
    // https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/cryptography/ECDSA.sol

    if (uint256(s) > 0x7FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF5D576E7357A4501DDFE92F46681B20A0) {
      revert("SignatureValidator#recoverSigner: 署名の's'値が不正");
    }

    if (v != 27 && v != 28) {
      revert("SignatureValidator#recoverSigner: 署名の'v'値が不正");
    }

    // ECDSAの署名者を回復
    signer = ecrecover(_hash, v, r, s);
    
    // 署名者が0x0でないことを確認
    require(
      signer != address(0x0),
      "SignatureValidator#recoverSigner: 無効な署名者"
    );

    return signer;
  }
```

外部の署名コントラクトの `isValidSignature()` 関数を呼び出す例:

```solidity
  function callERC1271isValidSignature(
    address _addr,
    bytes32 _hash,
    bytes calldata _signature
  ) external view {
    bytes4 result = IERC1271Wallet(_addr).isValidSignature(_hash, _signature);
    require(result == 0x1626ba7e, "INVALID_SIGNATURE");
  }
```

## セキュリティ上の考慮事項
`isValidSignature()` 関数の呼び出しにガスリミットが設定されていないため、実装によっては大量のガスを消費する可能性があります。したがって、この関数を外部コントラクトに呼び出す際は、ガス量を固定値で指定するのではなく、動的に設定する必要があります。そうしないと、特定の署名の検証ができなくなる可能性があります。

また、この関数を実装するコントラクトは、渡された署名が本当に有効であることを確認する責任があります。そうしないと、深刻な問題が発生する可能性があります。

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。