---
original: f3f674f961468701d1704ddf914ebb7e745fa528fa6e94b9abbb40824c60e164
---

---
eip: 3448
title: MetaProxy Standard
description: プロキシコントラクトに不変のメタデータを添付するための最小限のバイトコード実装
author: pinkiebell (@pinkiebell)
discussions-to: https://ethereum-magicians.org/t/erc-3448-metaproxy-factory/5834
status: Final
type: Standards Track
category: ERC
created: 2021-03-29
---

## 概要
既知の最小限のバイトコードプロキシ実装と不変のメタデータサポートを標準化することで、このスタンダードにより、ユーザーやサードパーティツール(Etherscanなど)が以下のことができるようになります:
(a) 常に既知の方法でリダイレクトされるコントラクトを簡単に発見し、
(b) リダイレクトコントラクトの動作を、そのターゲットコントラクトの動作に依存させ、
(c) 添付されたメタデータを検証/表示する。

ツールは、リダイレクトアドレスのバイトコードを調査して、実行されるコードの場所とそれに関連するメタデータを特定し、そのコードに関する表現(検証されたソース、サードパーティの監査など)に依存することができます。
この実装は、すべての呼び出しを `DELEGATECALL` を介して転送し、(calldata) 入力とバイトコードの最後のメタデータをターゲットコントラクトに渡し、その戻り値をコールする側に中継します。
実装がrevertした場合は、revertメッセージとペイロードデータが返されます。

## 動機
このスタンダードは、別のアドレスで同じ機能を異なるパラメーターでクローンすることが望ましい場合に役立ちます。

## 仕様
MetaProxyコントラクトの正確なバイトコードは次のとおりです:
```
                                              20 bytes target contract address
                                          ----------------------------------------
363d3d373d3d3d3d60368038038091363936013d7300000000000000000000000000000000000000005af43d3d93803e603457fd5bf3
```
ここで、インデックス21 - 41(inclusive)のバイトは、マスター機能コントラクトの20バイトアドレスに置き換えられます。
さらに、MetaProxyバイトコードの後に任意のメタデータを置くことができ、バイトコードの最後の32バイト(1ワード)にメタデータの長さをバイト単位で示す必要があります。

```
<54 bytes metaproxy> <arbitrary data> <length in bytes of arbitrary data (uint256)>
```

## 根拠
このプロジェクトの目標は次のようなものでした:
- 各子コントラクトの不変のメタデータを格納するための安価な方法
- クローンの低コストな展開
- revertメッセージのバブリングを処理する

## 下位互換性
下位互換性の問題はありません。

## テストケース
以下のテストケースでテストされています:
- 引数なしの呼び出し
- 引数付きの呼び出し
- 戻り値付きの呼び出し
- revertを伴う呼び出し(revertされたペイロードが転送されることを確認)

上記のテストケースを含むSolidityコントラクトは、[EIPアセットディレクトリ](../assets/eip-3448/MetaProxyTest.sol)にあります。

## 参考実装
参考実装は[EIPアセットディレクトリ](../assets/eip-3448/MetaProxyFactory.sol)にあります。

### デプロイメントバイトコード
デプロイメントバイトコードの注釈付きバージョン:
```
// PUSH1 11;
// CODESIZE;
// SUB;
// DUP1;
// PUSH1 11;
// RETURNDATASIZE;
// CODECOPY;
// RETURNDATASIZE;
// RETURN;
```

### MetaProxy
MetaProxyバイトコードの注釈付きバージョン:
```
// copy args
// CALLDATASIZE;   calldatasize
// RETURNDATASIZE; 0, calldatasize
// RETURNDATASIZE; 0, 0, calldatasize
// CALLDATACOPY;

// RETURNDATASIZE; 0
// RETURNDATASIZE; 0, 0
// RETURNDATASIZE; 0, 0, 0
// RETURNDATASIZE; 0, 0, 0, 0

// PUSH1 54;       54, 0, 0, 0, 0
// DUP1;           54, 54, 0, 0, 0, 0
// CODESIZE;       codesize, 54, 54, 0, 0, 0, 0
// SUB;            codesize-54, 54, 0, 0, 0, 0
// DUP1;           codesize-54, codesize-54, 54, 0, 0, 0, 0
// SWAP2;          54, codesize-54, codesize-54, 0, 0, 0, 0
// CALLDATASIZE;   calldatasize, 54, codesize-54, codesize-54, 0, 0, 0, 0
// CODECOPY;       codesize-54, 0, 0, 0, 0

// CALLDATASIZE;   calldatasize, codesize-54, 0, 0, 0, 0
// ADD;            calldatasize+codesize-54, 0, 0, 0, 0
// RETURNDATASIZE; 0, calldatasize+codesize-54, 0, 0, 0, 0
// PUSH20 0;       addr, 0, calldatasize+codesize-54, 0, 0, 0, 0 - zero is replaced with shl(96, address())
// GAS;            gas, addr, 0, calldatasize+codesize-54, 0, 0, 0, 0
// DELEGATECALL;   (gas, addr, 0, calldatasize() + metadata, 0, 0) delegatecall to the target contract;
//
// RETURNDATASIZE; returndatasize, retcode, 0, 0
// RETURNDATASIZE; returndatasize, returndatasize, retcode, 0, 0
// SWAP4;          0, returndatasize, retcode, 0, returndatasize
// DUP1;           0, 0, returndatasize, retcode, 0, returndatasize
// RETURNDATACOPY; (0, 0, returndatasize) - Copy everything into memory that the call returned

// stack = retcode, 0, returndatasize # this is for either revert(0, returndatasize()) or return (0, returndatasize())

// PUSH1 _SUCCESS_; push jumpdest of _SUCCESS_
// JUMPI;          jump if delegatecall returned `1`
// REVERT;         (0, returndatasize()) if delegatecall returned `0`
// JUMPDEST _SUCCESS_;
// RETURN;         (0, returndatasize()) if delegatecall returned non-zero (1)
```

### 例
以下のコードスニペットは提案のみを目的としたものであり、このスタンダードの個別の部分ではありません。

#### abi.encodeからのバイトを使ったプロキシの構築
```solidity
/// @notice MetaProxy construction via abi encoded bytes.
function createFromBytes (
  address a,
  uint256 b,
  uint256[] calldata c
) external payable returns (address proxy) {
  // creates a new proxy where the metadata is the result of abi.encode()
  proxy = MetaProxyFactory._metaProxyFromBytes(address(this), abi.encode(a, b, c));
  require(proxy != address(0));
  // optional one-time setup, a constructor() substitute
  MyContract(proxy).init{ value: msg.value }();
}
```

#### calldataからのバイトを使ったプロキシの構築
```solidity
/// @notice MetaProxy construction via calldata.
function createFromCalldata (
  address a,
  uint256 b,
  uint256[] calldata c
) external payable returns (address proxy) {
  // creates a new proxy where the metadata is everything after the 4th byte from calldata.
  proxy = MetaProxyFactory._metaProxyFromCalldata(address(this));
  require(proxy != address(0));
  // optional one-time setup, a constructor() substitute
  MyContract(proxy).init{ value: msg.value }();
}
```

#### calldataとabi.decodeからのメタデータの取得
```solidity
/// @notice Returns the metadata of this (MetaProxy) contract.
/// Only relevant with contracts created via the MetaProxy standard.
/// @dev This function is aimed to be invoked with- & without a call.
function getMetadataWithoutCall () public pure returns (
  address a,
  uint256 b,
  uint256[] memory c
) {
  bytes memory data;
  assembly {
    let posOfMetadataSize := sub(calldatasize(), 32)
    let size := calldataload(posOfMetadataSize)
    let dataPtr := sub(posOfMetadataSize, size)
    data := mload(64)
    // increment free memory pointer by metadata size + 32 bytes (length)
    mstore(64, add(data, add(size, 32)))
    mstore(data, size)
    let memPtr := add(data, 32)
    calldatacopy(memPtr, dataPtr, size)
  }
  return abi.decode(data, (address, uint256, uint256[]));
}
```

#### 自身への呼び出しを介したメタデータの取得
```solidity
/// @notice Returns the metadata of this (MetaProxy) contract.
/// Only relevant with contracts created via the MetaProxy standard.
/// @dev This function is aimed to be invoked via a call.
function getMetadataViaCall () public pure returns (
  address a,
  uint256 b,
  uint256[] memory c
) {
  assembly {
    let posOfMetadataSize := sub(calldatasize(), 32)
    let size := calldataload(posOfMetadataSize)
    let dataPtr := sub(posOfMetadataSize, size)
    calldatacopy(0, dataPtr, size)
    return(0, size)
  }
}
```

上記の例の他にも、Solidityの構造体やカスタムデータエンコーディングを使うことができます。

## セキュリティ上の考慮事項
このスタンダードはバイトコードの実装のみをカバーしており、自体に深刻な副作用は含まれていません。
参考実装は単なる例示にすぎません。機能の使用方法や実装によっては、プロジェクトに応じて副作用を慎重に調査することが強く推奨されます。

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。