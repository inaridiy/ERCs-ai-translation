---
original: 2d1936c18b357a22ff257607f7dd40a299a356f994ea72e4caf84ab796269d82
---

---
eip: 2569
title: 汎用トークンのオンチェーン画像の保存と表示
description: Ethereumにおいて SVG 画像を保存し、汎用トークンから画像ファイルを取得するためのインターフェースのセットです。
author: Hua Zhang (@dgczhh), Yuefei Tan (@whtyfhas), Derek Zhou (@zhous), Ran Xing (@lemontreeran)
discussions-to: https://ethereum-magicians.org/t/erc-2569-saving-and-displaying-image-onchain-for-universal-tokens/4167
status: 停滞
type: Standards Track
category: ERC
created: 2020-03-28
---

## 概要
このインターフェースのセットにより、スマートコントラクトが Ethereum 上に SVG 画像を保存し、fungible トークン、non-fungible トークン、および将来開発される基準に基づくトークンから SVG 画像を取得することができます。

このインターフェースセットには2つのインターフェースがあります。1つは Ethereum に SVG ファイルを保存するためのもの、もう1つは Ethereum から SVG ファイルを取得するためのものです。

典型的な用途には以下のようなものがあります:
* fungible トークンのアイコンの保存ソリューション
* non-fungible トークンのアイコンの保存ソリューション
* DAO の評価トークンのアイコン/ロゴの保存ソリューション

## 動機
ERC-721 トークン標準は、Ethereum における non-fungible トークンを定義する人気の高い標準です。この標準は、暗号ギフト、暗号メダル、暗号コレクティブなどを指定するために広く使用されています。最も有名な用例は [cryptokitty](https://www.cryptokitties.co/) です。

これらのアプリケーションの多くでは、ERC-721 トークンに画像が添付されています。例えば、cryptokitty の場合、各 kitty には固有の画像があります。トークンのコードは Ethereum に永続的に保存されますが、トークンに添付された画像は保存されていません。

既存のソリューションでは、このような画像を依然として集中型サーバーに保存しています。これらのアプリケーションがトークンの画像を表示する際は、Ethereum からトークンの情報を取得し、トークンの情報を使ってその画像を集中型サーバーから検索します。

これは、トークンの画像を表示するための適用可能な方法ですが、集中型サーバーに保存された画像は、損傷や紛失のリスクにさらされています。

そのため、Ethereum 上に画像を永続的かつ改ざん耐性を持って保存し、Ethereum からトークンの画像を取得するためのインターフェースを提案します。

## 仕様

EIP-2569 に準拠したコントラクトには、シグネチャ `getTokenImageSvg(uint256) view returns (string memory)` のメソッドと、シグネチャ `setTokenImageSvg(uint256 tokenId, string memory imagesvg) internal` のメソッドがなければなりません。

これらのメソッドは、スマートコントラクトが汎用トークンの画像を Ethereum に永続的かつ改ざん耐性を持って保存し、スマートコントラクトがトークンの画像を Ethereum から取得する方法を定義しています。

ユーザーは、これらのメソッドを呼び出して SVG 画像にアクセスできます。

* `getTokenImageSvg(uint256 tokenId) external view returns (string memory)`：ERC-721 または ERC-1155 トークン、あるいはトークンタイプまたはトークンインデックスを指定するメンバー "ID" を持つコントラクトで実装されたトークンの場合、トークンの ID 番号を使ってSVG 画像を取得するインターフェースを定義します。ERC-20 トークン、またはトークンタイプやトークンインデックスを指定するメンバー "ID" を持たないコントラクトで実装されたトークンの場合、画像を保存する文字列メンバー変数を持っている場合、その文字列変数から SVG 画像を取得するインターフェースを定義します。

パラメータ:
tokenId: ERC-721 トークンや ERC-1155 トークンのように、トークンタイプやトークンインデックスを指定するメンバー "ID" を持つ non-fungible トークンの場合、提案するインターフェースはSVG 画像のファイルコンテンツをトークンのコントラクトの文字列変数に割り当て、その SVG 画像をこの "ID" 番号に関連付けます。この一意の ID は、"set" 操作と "get" 操作の両方でその SVG 画像にアクセスするために使用されます。ERC-20 トークンのように、そのようなIDを持たない fungible トークンの場合、提案するインターフェースはSVG 画像のファイルコンテンツをトークンのコントラクトの文字列変数に割り当てるだけです。

* `setTokenImageSvg(uint256 tokenId, string memory imagesvg) internal`: ERC-721 やERC-1155 トークン、またはトークンタイプやトークンインデックスを指定するメンバー "ID" を持つコントラクトで実装されたトークンの場合、トークンの ID 番号にSVG 画像を関連付けるインターフェースを定義します。ERC-20 トークン、またはトークンタイプやトークンインデックスを指定するメンバー "ID" を持たないコントラクトで実装されたトークンの場合、このトークンのコントラクトの文字列メンバー変数にSVG 画像を割り当てるインターフェースを定義します。

パラメータ:
tokenId: ERC-721 トークンや ERC-1155 トークンのように、トークンタイプやトークンインデックスを指定するメンバー "ID" を持つ non-fungible トークンの場合、提案するインターフェースはSVG 画像のファイルコンテンツをトークンのコントラクトの文字列変数に割り当て、その SVG 画像をこの "ID" 番号に関連付けます。この一意の ID は、"set" 操作と "get" 操作の両方でその SVG 画像にアクセスするために使用されます。ERC-20 トークンのように、そのようなIDを持たない fungible トークンの場合、提案するインターフェースはSVG 画像のファイルコンテンツをトークンのコントラクトの文字列変数に割り当てるだけです。

imageSvg: SVG 画像ファイルのコンテンツを保存するための文字列変数を使用します。
保存されるSVG 画像には、少なくとも "name"、"desc"(description) の2つの属性が含まれている必要があります。

トークンの画像を Ethereum に保存する手順は以下の通りです:

**ステップ1:** 画像または画像の配列を保持するための文字列変数または文字列の配列を定義します。

**ステップ2:** (SVG) 画像のファイルコンテンツまたは画像ファイルコンテンツの配列を文字列変数または文字列の配列に設定する関数を定義します。

ステップ1: トークンタイプやインデックスを指定するメンバー変数 "ID" を持ち、その "ID" に関連付けられた (SVG) 画像を保持する文字列メンバー変数を持つERC-721 やERC-1155 トークンの場合、提案する "get" インターフェースを呼び出してトークンの ID からSVG 画像を取得します。
トークンタイプやインデックスを指定するメンバー変数 "ID" を持たないが、(SVG) 画像を保持する文字列メンバー変数を持つトークンの場合、"ID" なしで提案する "get" インターフェースを呼び出してSVG 画像を取得します。

## 根拠
ビットコインが作成された後、人々は永続的かつ改ざん耐性のある情報を保持する方法を見出してきました。これらのアプリケーションは、永続的かつ改ざん耐性のあるテキストメッセージをブロックチェーントランザクションにエンコーディングしています。しかし、既存のアプリケーションはテキスト情報のみを対象としており、画像を永続的かつ改ざん耐性を持って保持する解決策はありませんでした。

これを行わない最も重要な理由の1つは、一般的に画像のサイズはテキストファイルのサイズよりはるかに大きいため、Ethereum に画像を保存するためのガスが1ブロックのガス制限を超えてしまうということです。

しかし、1999年にW3Cが SVG(Scalable Vector Graphics) 仕様を開発したことで、この状況は大きく変わりました。

SVG 仕様には、ラスター画像に比べていくつかの利点があります(詳細については参考リンク:https://en.wikipedia.org/wiki/Scalable_Vector_Graphics を参照してください)。その中でも最も重要なのは、ファイルサイズが小さいことです。

"ファイルサイズが小さい - ピクセルベースの画像は最初から大きなサイズで保存されるため、画像を小さくすることはできますが、大きくすることはできません。これはサイトのダウンロード速度に影響を与える可能性があります。SVG は拡張可能なため、最小限のファイルサイズで保存できます。"

この特徴は、Ethereum に画像ファイルを保存する際の痛点を解決するため、Ethereum に SVG 画像を保存することが良い解決策だと考えています。

ほとんどのERC-721関連のDAppsでは、non-fungible トークンの画像を表示しています。一方、ほとんどのERC-20関連のDAppsでは、fungible トークンの画像を表示していません。我々は、既存のトークン標準(ERC-20、ERC-721、ERC-1155など)や将来の標準に基づくトークンの画像を表示することが多くのユースケースで必要だと考えています。したがって、現在トークンの画像を表示していないDAppsも、最終的にはそのような機能を必要とするでしょう。

しかし、トークンの画像を表示できるほとんどの既存のDAppsでは、そのような画像を集中型サーバーに保存しているのが現状です。これは妥協的な解決策だと考えています。SVG 仕様を活用することで、トークンの画像をSVG 画像に変換し、Ethereum に保存することが、DAppsがトークンの画像にアクセスするための better な解決策だと考えています。

この解決策は、ERC-721、ERC-1155、ERC-20ベースのトークンだけでなく、将来の基準に基づくトークンにも適用できます。

## 下位互換性
下位互換性の問題はありません。

## 参考実装
`tokenId`: ERC-721 トークンのトークンインデックス、またはERC-1155 トークンのトークンタイプ/インデックス。uint256 変数です。

`imageSvg`: SVG 画像のファイルコンテンツ。文字列変数です。注意: SVG 画像には少なくとも "name"、"description"、"issuer" の3つの属性が含まれている必要があります。

`setTokenImageSvg`: ID 番号の有無に関わらずトークンにSVG 画像を設定するためのインターフェース。

`getTokenImageSvg`: ID 番号の有無に関わらずトークンのSVG 画像を取得するためのインターフェース。

既存のERC-721実装に3つのsolファイルを追加することを提案します。
提案するsolファイルの詳細は以下の通りです。

```solidity
// ----- IERC721GetImageSvg.sol -------------------------

pragma solidity ^0.5.0;

import "@openzeppelin/contracts/token/ERC721/IERC721.sol";

/**
 * @title ERC-721 Non-Fungible Token Standard, optional retrieving SVG image extension
 * @dev See https://eips.ethereum.org/EIPS/eip-721
 */
contract IERC721GetImageSvg is IERC721 {
    function getTokenImageSvg(uint256 tokenId) external view returns (string memory);
}


// ----- ERC721GetImageSvg.sol -------------------------

pragma solidity ^0.5.0;

import "@openzeppelin/contracts/GSN/Context.sol";
import "@openzeppelin/contracts/token/ERC721/./ERC721.sol";
import "@openzeppelin/contracts/introspection/ERC165.sol";
import "./IERC721GetImageSvg.sol";

contract ERC721GetImageSvg is Context, ERC165, ERC721, IERC721GetImageSvg {
    // Mapping for token Images
    mapping(uint256 => string) private _tokenImageSvgs;

    /*
     *     bytes4(keccak256('getTokenImageSvg(uint256)')) == 0x87d2f48c
     *
     *     => 0x87d2f48c == 0x87d2f48c
     */
    bytes4 private constant _INTERFACE_ID_ERC721_GET_TOKEN_IMAGE_SVG = 0x87d2f48c;
/**
     * @dev Constructor function
     */
    constructor () public {
        // register the supported interfaces to conform to ERC721 via ERC165
        _registerInterface(_INTERFACE_ID_ERC721_GET_TOKEN_IMAGE_SVG);
    }

    /**
     * @dev Returns an SVG Image for a given token ID.
     * Throws if the token ID does not exist. May return an empty string.
     * @param tokenId uint256 ID of the token to query
     */
    function getTokenImageSvg(uint256 tokenId) external view returns (string memory) {
        require(_exists(tokenId), "ERC721GetImageSvg: SVG Image query for nonexistent token");
        return _tokenImageSvgs[tokenId];
    }

    /**
     * @dev Internal function to set the token SVG image for a given token.
     * Reverts if the token ID does not exist.
     * @param tokenId uint256 ID of the token to set its SVG image
     * @param imagesvg string SVG  to assign
     */
    function setTokenImageSvg(uint256 tokenId, string memory imagesvg) internal {
        require(_exists(tokenId), "ERC721GetImageSvg: SVG image set of nonexistent token");
        _tokenImageSvgs[tokenId] = imagesvg;
    }

}


// ----- ERC721ImageSvgMintable.sol -------------------------

pragma solidity ^0.5.0;

import "@openzeppelin/contracts/token/ERC721/ERC721Metadata.sol";
import "@openzeppelin/contracts/access/roles/MinterRole.sol";
import "./ERC721GetImageSvg.sol";

/**
 * @title ERC721ImageSvgMintable
 * @dev ERC721 minting logic with imagesvg.
 */
contract ERC721ImageSvgMintable is ERC721, ERC721Metadata, ERC721GetImageSvg, MinterRole {
    /**
     * @dev Function to mint tokens.
     * @param to The address that will receive the minted tokens.
     * @param tokenId The token id to mint.
     * @param tokenImageSvg The token SVG image of the minted token.
     * @return A boolean that indicates if the operation was successful.
     */
    function mintWithTokenImageSvg(address to, uint256 tokenId, string memory tokenImageSvg) public onlyMinter returns (bool) {
        _mint(to, tokenId);
        setTokenImageSvg(tokenId, tokenImageSvg);
        return true;
    }
}


We propose to add three sol files in the existing ERC-1155 implementation.
Here are the details for the proposed sol files.

// ----- IERC1155GetImageSvg.sol -------------------------

pragma solidity ^0.5.0;

import "./IERC1155.sol";

/**
 * @title ERC-1155 Multi Token Standard, retrieving SVG image for a token
 * @dev See https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1155.md
 */
contract IERC1155GetImageSvg is IERC1155 {
    function getTokenImageSvg(uint256 tokenId) external view returns (string memory);
}


// ----- ERC1155GetImageSvg.sol -------------------------

pragma solidity ^0.5.0;

import "./ERC1155.sol";
import "./IERC1155GetImageSvg.sol";

contract ERC1155GetImageSvg is ERC165, ERC1155, IERC1155GetImageSvg {
    // Mapping for token Images
    mapping(uint256 => string) private _tokenImageSvgs;

    /*
     *     bytes4(keccak256('getTokenImageSvg(uint256)')) == 0x87d2f48c
     *
     *     => 0x87d2f48c == 0x87d2f48c
     */
    bytes4 private constant _INTERFACE_ID_ERC1155_GET_TOKEN_IMAGE_SVG = 0x87d2f48c;

    /**
     * @dev Constructor function
     */
    constructor () public {
        // register the supported interfaces to conform to ERC1155 via ERC165
        _registerInterface(_INTERFACE_ID_ERC1155_GET_TOKEN_IMAGE_SVG);
    }


    /**
     * @dev Returns an SVG Image for a given token ID.
     * Throws if the token ID does not exist. May return an empty string.
     * @param tokenId uint256 ID of the token to query
     */
    function getTokenImageSvg(uint256 tokenId) external view returns (string memory) {
        require(_exists(tokenId), "ERC1155GetImageSvg: SVG Image query for nonexistent token");
        return _tokenImageSvgs[tokenId];
    }

    /**
     * @dev Internal function to set the token SVG image for a given token.
     * Reverts if the token ID does not exist.
     * @param tokenId uint256 ID of the token to set its SVG image
     * @param imagesvg string SVG  to assign
     */
    function setTokenImageSvg(uint256 tokenId, string memory imagesvg) internal {
        require(_exists(tokenId), "ERC1155GetImageSvg: SVG image set of nonexistent token");
        _tokenImageSvgs[tokenId] = imagesvg;
    }

}



// ----- ERC1155MixedFungibleWithSvgMintable.sol -------------------------

pragma solidity ^0.5.0;

import "./ERC1155MixedFungibleMintable.sol";
import "./ERC1155GetImageSvg.sol";

/**
    @dev Mintable form of ERC1155 with SVG images
    Shows how easy it is to mint new items with SVG images
*/

contract ERC1155MixedFungibleWithSvgMintable is ERC1155, ERC1155MixedFungibleMintable, ERC1155GetImageSvg {
    /**
     * @dev Function to mint non-fungible tokens.
     * @param _type The token type to mint.
     * @param _to The address that will receive the minted tokens.
     * @param tokenImageSvg The token SVG image of the minted token.
     */
    function mintNonFungibleWithImageSvg(uint256 _type, address[] calldata _to, string memory tokenImageSvg) external creatorOnly(_type) {
        mintNonFungible(_type, _to);
        setTokenImageSvg(_type, tokenImageSvg);
    }


    /**
     * @dev Function to mint fungible tokens.
     * @param _id The token type to mint.
     * @param _to The address that will receive the minted tokens.
     * @param _quantities The number of tokens for a type to mint.
     * @param tokenImageSvg The token SVG image of the minted token.
     */
    function mintFungibleWithImageSvg(uint256 _id, address[] calldata _to, uint256[] calldata _quantities, string memory tokenImageSvg) external creatorOnly(_id) {
        mintFungible(_id, _to, _quantities, tokenImageSvg)  {
        setTokenImageSvg(_id, tokenImageSvg);
    }
}



We propose to add three sol files in the existing ERC-20 implementation.
Here are the details for the proposed sol files.


// ----- IERC20GetImageSvg.sol -------------------------

pragma solidity ^0.5.0;
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

/**
 * @title ERC-20 Fungible Token Standard, retrieving SVG image for a token
 * @dev See https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol
 */
contract IERC20GetImageSvg is IERC20 {
    function getTokenImageSvg() external view returns (string memory);
}


// ----- ERC20GetImageSvg.sol -------------------------

pragma solidity ^0.5.0;
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "./IERC20GetImageSvg.sol";

contract ERC20GetImageSvg is ERC20, IERC20GetImageSvg {
    string private _tokenImageSvg;
//将图片实现写在构造器中
    constructor(string calldata svgCode) public {
_tokenImageSvg = svgCode
}

    /**
     * @dev Returns an SVG Image.
     */
    function getTokenImageSvg() external view returns (string memory) {
        return _tokenImageSvg;
    }

}


```

## 著作権
著作権およびその関連権利は[CC0](../LICENSE.md)により放棄されています。