---
original: cbd63546b8928b7b3cd4cbba0db9eb07b91ff6764721ebfad679b3556c05f32b
---

---
eip: 1538
title: 透過型コントラクト標準
author: Nick Mudge <nick@perfectabstractions.com>
discussions-to: https://github.com/ethereum/EIPs/issues/1538
status: 取り下げられた
type: Standards Track
category: ERC
created: 2018-10-31
---

[EIP-2535 Diamond Standard](./eip-2535.md)に置き換えられました。

## 簡単な要約
この標準は、アップグレード可能なコントラクトを柔軟で無制限のサイズにし、透明性を持たせるコントラクトアーキテクチャを提供します。

透過型コントラクトは、自身に加えられた変更の完全な履歴を公開文書化します。

透過型コントラクトへの変更はすべて標準形式で報告されます。

## 概要
透過型コントラクトは、プロキシコントラクトのデザインパターンで、以下を提供します:

1. 同時に(アトミックに)複数の関数を追加、置換、削除する方法。
1. 関数の追加、置換、削除を示す標準的なイベント、および変更の理由。
2. コントラクトが公開する関数を発見し取得する標準的な方法。
3. 24KB最大コントラクトサイズの制限を解決し、透過型コントラクトの最大サイズを実質的に無制限にする。このスタンダードにより、コントラクトサイズに関する心配は過去のものとなる。
4. アップグレード可能なコントラクトを、必要に応じて将来的に不変にすることを可能にする。

## 動機
Ethereumコントラクトの根本的な利点は、そのコードが不変であり、信頼なしの信頼性を獲得することです。変更できないコントラクトを信頼する必要がありません。

しかし、変更できない信頼できるコントラクトの根本的な問題は、変更できないことです。

#### バグ

不変のコントラクトにはバグや安全性の脆弱性が無意識に書き込まれ、それが致命的になります。

#### 改善

不変の信頼できるコントラクトは改善できず、時間とともに劣化していきます。

コントラクト標準は進化し、新しいものが登場します。個人、グループ、組織は、人々が何を望み、何が良く、次に何を構築すべきかを学習していきます。改善できないコントラクトは、それを作成した人々だけでなく、それを使う全ての人を遅らせます。

#### アップグレード可能なコントラクトvsセントラライズドプライベートデータベース
なぜアップグレード可能なコントラクトを使うのか、セントラライズドで変更可能なプライベートデータベースを使わないのか?
理由は以下の通りです:
1. ストレージデータと検証済みコードの公開性により、信頼性の証跡を示すことができる。
2. 公開性により、悪質な行為を発見し報告することができる。
3. 独立したセキュリティおよびドメイン専門家がコントラクトの変更履歴を確認し、信頼性の歴史を証明できる。
4. アップグレード可能なコントラクトを不変で信頼できるものにすることができる。
5. アップグレード可能なコントラクトの一部を不変で信頼できるものにすることができる。

#### 不変性

場合によっては、不変で信頼できるコントラクトが適切です。短期的に使用するコントラクト、または変更や改善の必要性がないことが事前に分かっている場合などです。

### 中間地点

透過型コントラクトは、改善できない不変の信頼できるコントラクトと、信頼できないアップグレード可能なコントラクトの中間地点を提供します。

### 目的

1. 信頼性の証跡を示すことで信頼を獲得するアップグレード可能なコントラクトを作成する。
2. コントラクトの開発と変更を公開かつ理解可能にする。
3. 必要に応じて不変になるアップグレード可能なコントラクトを作成する。
4. サイズ制限のないコントラクトを作成する。

### 利点と使用例
このスタンダードは以下の利点を必要とする使用例に適しています:
1. 同時に(アトミックに)複数の関数を追加、置換、削除する機能。
2. 関数の追加、置換、削除のたびにイベントで文書化される。
3. 変更の履歴を示すことで、時間をかけて信頼を築く。
4. 無制限のコントラクトサイズ。
5. コントラクトが現在サポートする関数に関する情報を照会する機能。
6. 機能を提供し続ける単一のコントラクトアドレス、別のコントラクトアドレスに置き換える必要がない。
7. コントラクトをしばらくの間アップグレード可能にし、その後不変にする機能。
8. 「変更不可能な関数」にトラストレスな保証を追加する。

### 新しいソフトウェアの可能性

このスタンダードにより、コントラクトのバージョン管理ソフトウェアが書けるようになります。

ソフトウェアやユーザーインターフェイスは、コントラクトアドレスの `FunctionUpdate` および `CommitMessage` イベントをフィルタリングできます。そのようなソフトウェアは、このスタンダードを実装しているコントラクトアドレスの変更履歴全体を表示できます。

ユーザーインターフェイスやソフトウェアもこのスタンダードを使って、コントラクトの変更を支援または自動化できます。

## 仕様

> **注意:**
Solidity の `delegatecall` オペコードにより、コントラクトは別のコントラクトの関数を実行できますが、呼び出し元のコントラクトのように実行されます。つまり `delegatecall` により、コントラクトは別のコントラクトの関数を「借りる」ことができます。 `delegatecall` で実行された関数は、定義されているコントラクトのストレージ変数ではなく、呼び出し元のコントラクトのストレージ変数に影響します。

### 一般的な概要

透過型コントラクトは、`delegatecall` を使って他のコントラクトにファンクションコールを委譲または転送します。

透過型コントラクトには `updateContract` 関数があり、複数の関数を同時に追加、置換、削除できます。

コントラクトへのすべての変更を標準的な方法で追跡できるよう、関数の追加、置換、削除ごとにイベントが発行されます。

透過型コントラクトは、以下のデザインポイントを実装し、準拠しているコントラクトです。

### 用語

1. このスタンダードでは、**デリゲートコントラクト**は、透過型コントラクトのフォールバック関数が `delegatecall` を使って呼び出すコントラクトです。
2. このスタンダードでは、**変更不可能な関数**は、透過型コントラクトに直接定義された関数で、置換や削除ができない関数です。

### デザインポイント

以下のデザインポイントを実装しているコントラクトが透過型コントラクトです:

1. 透過型コントラクトには、フォールバック関数、コンストラクタ、および0個以上の変更不可能な関数が含まれます。
2. 透過型コントラクトのコンストラクタは、`updateContract` 関数をERC1538インターフェイスを実装するコントラクトに関連付けます。`updateContract` 関数は、透過型コントラクトに直接定義された「変更不可能な関数」であるか、デリゲートコントラクトで定義されている可能性があります。他の関数もコンストラクタで関連付けることができます。
3. 透過型コントラクトがデプロイされた後、`updateContract` 関数を呼び出して関数を追加、置換、削除します。
4. `updateContract` 関数は関数をコントラクトに関連付け、変更を文書化する `CommitMessage` および `FunctionUpdate` イベントを発行します。
5. `FunctionUpdate` イベントは、関数が追加、置換、削除されるたびに発行されます。`CommitMessage` イベントは、`updateContract` 関数が呼び出されるたびに1回発行され、`FunctionUpdate` イベントの後に発行されます。
6. `updateContract` 関数の `_functionSignatures` パラメータには複数の関数シグネチャのリストを渡すことができ、同時に複数の関数を追加/置換/削除できます。
7. 透過型コントラクトで関数が呼び出された場合、それが「変更不可能な関数」であれば即座に実行されます。そうでない場合はフォールバック関数が実行されます。フォールバック関数は関数に関連付けられたデリゲートコントラクトを見つけ、`delegatecall` を使って関数を実行します。関数に関連付けられたデリゲートコントラクトがない場合は実行がリバートします。
8. 透過型コントラクトとそれが使用するすべてのデリゲートコントラクトのソースコードは公開され、検証されています。

透過型コントラクトのアドレスがユーザーが操作するアドレスです。透過型コントラクトのアドレスは変更されません。デリゲートアドレスのみが `updateContracts` 関数を使って変更できます。

通常、透過型コントラクトに関数を追加/置換/削除するには何らかの認証が必要ですが、**認証方式や所有権のスキームはこのスタンダードの一部ではありません**。

### 例

以下は透過型コントラクトの実装例です。ただし、以下の例は**例のみ**であり、標準ではありません。コントラクトが透過型コントラクトであるためには、上記のデザインポイントを実装し、準拠している必要があります。

```solidity
pragma solidity ^0.5.7;

contract ExampleTransparentContract {
  // コントラクトの所有者
  address internal contractOwner;
  event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

  // 関数をデリゲートコントラクトにマッピング
  // funcId => デリゲートコントラクト
  mapping(bytes4 => address) internal delegates;

  // 各関数シグネチャをfuncSignatures配列の位置にマッピング
  // シグネチャ => インデックス+1
  mapping(bytes => uint256) internal funcSignatureToIndex;
    
  event CommitMessage(string message);
  event FunctionUpdate(bytes4 indexed functionId, address indexed oldDelegate, address indexed newDelegate, string functionSignature);
  
  // これは「変更不可能な関数」の例
  // 指定された関数シグネチャのデリゲートコントラクトアドレスを返す
  function delegateAddress(string calldata _functionSignature) external view returns(address) {
    require(funcSignatureToIndex[bytes(_functionSignature)] != 0, "Function signature not found.");
    return delegates[bytes4(keccak256(bytes(_functionSignature)))];
  }
  
  // updateContract関数を使って関数を追加する
  // これは内部ヘルパー関数
  function addFunction(address _erc1538Delegate, address contractAddress, string memory _functionSignatures, string memory _commitMessage) internal {    
    // 0x03A9BCCF == bytes4(keccak256("updateContract(address,string,string)"))
    bytes memory funcdata = abi.encodeWithSelector(0x03A9BCCF, contractAddress, _functionSignatures, _commitMessage);
    bool success;
    assembly {
      success := delegatecall(gas, _erc1538Delegate, add(funcdata, 0x20), mload(funcdata), funcdata, 0)
    }
    require(success, "Adding a function failed");   
  }

  constructor(address _erc1538Delegate) public {
    contractOwner = msg.sender;
    emit OwnershipTransferred(address(0), msg.sender);

    // ERC1538 updateContract関数の追加
    bytes memory signature = "updateContract(address,string,string)";
    bytes4 funcId = bytes4(keccak256(signature));
    delegates[funcId] = _erc1538Delegate;
    emit FunctionUpdate(funcId, address(0), _erc1538Delegate, string(signature));
    emit CommitMessage("コントラクト作成時にERC1538 updateContract関数を追加");
	
    // この透過型コントラクトアドレスに「変更不可能な関数」を関連付ける
    // デリゲートコントラクトの関数セレクタとの衝突を防ぐ
    // updateContract関数を使用
    string memory functions = "delegateAddress(string)";
    addFunction(_erc1538Delegate, address(this), functions, "変更不可能な関数の関連付け");
	
    // ERC1538Queryインターフェイス関数の追加
    functions = "functionByIndex(uint256)functionExists(string)delegateAddresses()delegateFunctionSignatures(address)functionById(bytes4
)functionBySignature(string)functionSignatures()totalFunctions()";    
    // "0x01234567891011121314" は例のERC1538Queryデリゲートコントラクトアドレス
    addFunction(_erc1538Delegate, 0x01234567891011121314, functions, "ERC1538Query関数の追加");
    
    // その他の関数をここで追加できる
  }

  // フォールバック関数をpayableにすることで、デリゲートコントラクト関数のpayableとnon-payableの両方に対応できる
  function() external payable {
    // すべての関数呼び出しをデリゲートコントラクトに委譲
    address delegate = delegates[msg.sig];
    require(delegate != address(0), "Function does not exist.");
    assembly {
      let ptr := mload(0x40)
      calldatacopy(ptr, 0, calldatasize)
      let result := delegatecall(gas, delegate, ptr, calldatasize, 0, 0)
      let size := returndatasize
      returndatacopy(ptr, 0, size)
      switch result
      case 0 {revert(ptr, size)}
      default {return (ptr, size)}
    }
  }
}
```

上記の例のように、透過型コントラクトでは、関数が直接定義されていない限り、すべての関数呼び出しがデリゲートコントラクトに委譲されます。

コンストラクタ関数は `updateContract` 関数をトランスペアレントコントラクトに追加し、それを使って他の関数をトランスペアレントコントラクトに追加します。

関数が透過型コントラクトに追加されるたびに、`CommitMessage` および `FunctionUpdate` イベントが発行され、正確に何の関数が追加または置換されたか、なぜ変更されたかが文書化されます。

`updateContract` 関数を実装するデリゲートコントラクトは、以下のインターフェイスを実装します:

### ERC1538 インターフェイス

```solidity
pragma solidity ^0.5.7;

/// @title ERC1538 Transparent Contract Standard
/// @dev Required interface
///  Note: the ERC-165 identifier for this interface is 0x61455567
interface ERC1538 {
  /// @dev 透過型コントラクトで1つ以上の関数が更新されたときに発行されます。
  ///  メッセージ文字列は変更の概要と理由を示す短い説明を提供する必要があります。
  event CommitMessage(string message);
  
  /// @dev 透過型コントラクトで関数が更新されるたびに発行されます。
  ///  functionIdは関数シグネチャのkeccak256ハッシュのbytes4です。
  ///  oldDelegateは、関数が置換または削除される場合の古いデリゲートコントラクトアドレスです。
  ///  関数が初めて追加される場合、oldDelegateはゼロ値のaddress(0)です。
  ///  newDelegateは、関数が初めて追加される場合や関数が置換される場合の新しいデリゲートコントラクトアドレスです。
  ///  関数が削除される場合、newDelegateはゼロ値のaddress(0)です。
  event FunctionUpdate(
    bytes4 indexed functionId, 
    address indexed oldDelegate, 
    address indexed newDelegate, 
    string functionSignature
  );

  /// @notice 透過型コントラクトの関数を更新する
  /// @dev _delegateの値がゼロの場合、_functionSignaturesで指定された関数が削除されます。
  ///  _delegateの値がデリゲートコントラクトアドレスの場合、_functionSignaturesで指定された関数がそのアドレスにデリゲートされます。
  /// @param _delegate 関数をデリゲートするデリゲートコントラクトのアドレス、または関数を削除する場合はゼロ
  /// @param _functionSignatures 関数シグネチャのリスト(連続)
  /// @param _commitMessage 変更の概要と理由を示す短い説明
  ///        このメッセージはCommitMessageイベントに渡されます。          
  function updateContract(address _delegate, string calldata _functionSignatures, string calldata _commitMessage) external;  
}
```

### 関数シグネチャ文字列フォーマット

`_functionSignatures` パラメータのテキストフォーマットは、単に関数シグネチャの文字列です。例: `"myFirstFunction()mySecondFunction(string)"`このフォーマットは解析が簡単で簡潔です。

ERC721標準関数をトランスペアレントコントラクトに追加する `updateContract` 関数の呼び出し例:
```javascript
functionSignatures = "approve(address,uint256)balanceOf(address)getApproved(uint256)isApprovedForAll(address,address)ownerOf(uint256)safeTransferFrom(address,address,uint256)safeTransferFrom(address,address,uint256,bytes)setApprovalForAll(address,bool)transferFrom(address,address,uint256)"
tx = await transparentContract.updateContract(erc721Delegate.address, functionSignatures, "ERC721関数の追加");
```

### 関数の削除

関数は `updateContract` 関数の最初の引数に `address(0)` を渡すことで削除できます。渡されたリストの関数が削除されます。

### ソースコードの検証

透過型コントラクトのソースコードとデリゲートコントラクトのソースコードは、etherscan.ioなどの第三者ソースによって検証される必要があります。

### 関数セレクタの衝突
関数セレクタの衝突は、既存の関数と同じ4バイトハッシュになる関数がコントラクトに追加された場合に発生します。これはまれに発生しますが、`updateContract` 関数の実装で防ぐ必要があります。[ERC1538の参考実装](https://github.com/mudgen/transparent-contracts-erc1538)を参照して、関数の衝突を防ぐ方法の例を確認してください。

### ERC1538Query

オプションで、透過型コントラクトの関数シグネチャを透過型コントラクト内の配列に格納し、コントラクトがサポートする関数とそのデリゲートコントラクトアドレスを照会できるようにすることができます。

以下は、透過型コントラクトから関数情報を照会するためのオプションのインターフェイスです:

```solidity
pragma solidity ^0.5.7;

interface ERC1538Query {
    
  /// @notice 透過型コントラクトが持つ関数の総数を取得する
  /// @return 透過型コントラクトが持つ関数の数(フォールバック関数を除く)
  function totalFunctions() external view returns(uint256);
	
  /// @notice 特定の関数に関する情報を取得する
  /// @dev `_index` >= `totalFunctions()`の場合はスローされる
  /// @param _index 配列に格納された関数シグネチャの位置
  /// @return 関数シグネチャ、関数セレクタ、デリゲートコントラクトアドレス
  function functionByIndex(uint256 _index) 
    external 
    view 
    returns(
      string memory functionSignature, 
      bytes4 functionId, 
      address delegate
    );
	
  /// @notice 関数が存在するかチェックする
  /// @param 確認する関数シグネチャ
  /// @return 関数が存在する場合はtrue、そうでない場合はfalse
  function functionExists(string calldata _functionSignature) external view returns(bool);
	
  /// @notice 透過型コントラクトがサポートする関数シグネチャをすべて取得する
  /// @return 関数シグネチャのリストを含む文字列
  function functionSignatures() external view returns(string memory);
	
  /// @notice 特定のデリゲートコントラクトがサポートする関数シグネチャをすべて取得する
  /// @param _delegate デリゲートコントラクトアドレス
  /// @return 関数シグネチャのリストを含む文字列
  function delegateFunctionSignatures(address _delegate) external view returns(string memory);
	
  /// @notice 指定された関数シグネチャをサポートするデリゲートコントラクトアドレスを取得する
  /// @param 関数シグネチャ
  /// @return デリゲートコントラクトアドレス
  function delegateAddress(string calldata _functionSignature) external view returns(address);
	
  /// @notice 関数に関する情報を取得する
  /// @dev 関数が見つからない場合はスローされる
  /// @param _functionId 情報を取得する関数のID
  /// @return 関数シグネチャとコントラクトアドレス
  function functionById(bytes4 _functionId) 
    external 
    view 
    returns(
      string memory signature, 
      address delegate
    );
	
  /// @notice 透過型コントラクトが使用するすべてのデリゲートコントラクトアドレスを取得する
  /// @return すべてのデリゲートコントラクトアドレスの配列
  function delegateAddresses() external view returns(address[] memory);
}
```

[ERC1538の参考実装](https://github.com/mudgen/transparent-contracts-erc1538)を参照して、これがどのように実装されているかを確認してください。

`delegateFunctionSignatures` および `functionSignatures` 関数から返される関数シグネチャのリストのテキストフォーマットは、単に関数シグネチャの文字列です。以下は、そのような文字列の例: `"approve(address,uint256)balanceOf(address)getApproved(uint256)isApprovedForAll(address,address)ownerOf(uint256)safeTransferFrom(address,address,uint256)safeTransferFrom(address,address,uint256,bytes)setApprovalForAll(address,bool)transferFrom(address,address,uint256)"`

### 透過型コントラクトのデプロイ方法
1. ERC1538インターフェイスを実装するコントラクトを作成し、ブロックチェーンにデプロイします。このステップは、すでにそのようなコントラクトがブロックチェーンにデプロイされている場合は省略できます。
2. 上記のようなフォールバック関数を持つ透過型コントラクトを作成します。透過型コントラクトにはまた、`updateContract` 関数を追加するコンストラクタも必要です。
3. 透過型コントラクトをブロックチェーンにデプロイします。コンストラクタにERC1538デリゲートコントラクトのアドレスを渡す必要がある場合は、それも渡します。

これらのコントラクトの例については、[参考実装](https://github.com/mudgen/transparent-contracts-erc1538)を参照してください。

### 他のデリゲートコントラクトに依存するデリゲートコントラクトのためのラッパーコントラクト
場合によっては、一部のデリゲートコントラクトが他のデリゲートコントラクトの外部/公開関数を呼び出す必要があります。この問題を解決する便利な方法は、必要な関数の空の実装を含むコントラクトを作成し、それをデリゲートコントラクトで継承することです。これにより、デリゲートコントラクトは、他のデリゲートコントラクトで提供されている関数の実装を提供する必要がなくなり、ガス消費の削減、最大コントラクトサイズ制限の回避、コードの重複防止ができます。この戦略は@amiromayerによって提案されました。[彼のコメントでより詳しい情報を確認できます。](https://github.com/ethereum/EIPs/issues/1538#issuecomment-451985155)この問題を解決する別の方法は、アセンブリを使用して他のデリゲートコントラクトが提供する関数を呼び出すことです。

### 分散型の権限
このスタンダードを拡張して、複数の異なる人が変更を承認する承認機能を追加することができます。変更は完全に承認された後にのみ `updateContract` 関数で適用されます。`CommitMessage` および `FunctionUpdate` イベントは、変更が有効になったときにのみ発行されるべきです。

## セキュリティ
> このスタンダードでは、**オーナー**を、アップグレード可能なコントラクトの関数の追加/置換/削除の権限を持つ1人以上の個人と定義しています。

### 一般的な事項

アップグレード可能なコントラクトのオーナーは、コントラクトのデータストレージのデータを変更、追加、削除する能力を持っています。コントラクトのオーナーは、任意のアドレスに代わって任意のコードを実行する関数をコントラクトに追加することもできます。これは、アップグレード可能なコントラクトに一般的な問題であり、透過型コントラクトに固有のものではありません。

>**注意:** コントラクトの所有権の設計と実装は、**このスタンダードの一部ではありません**。このスタンダードで示された例は、それがどのように行われる可能性があるかの**例**にすぎません。

### 変更不可能な関数

「変更不可能な関数」は、透過型コントラクト自体に定義された関数で、デリゲートコントラクトに定義されたものではありません。透過型コントラク
トのオーナーはこれらの関数を置換することはできません。変更不可能な関数の使用は限定的です。なぜなら、場合によっては、それらが透過型コントラクトのストレージデータを読み書きする場合、オーナーによって操作される可能性があるためです。透過型コントラクトのストレージから読み取ったデータは、コントラクトのオーナーによって変更された可能性があります。透過型コントラクトのストレージに書き込まれたデータは、コントラクトのオーナーによって元に戻されたり変更されたりする可能性があります。

ある場合には、変更不可能な関数が透過型コントラクトにトラストレスな保証を追加することができます。

### 透明性

このスタンダードを実装するコントラクトは、関数が追加、置換、削除されるたびにイベントを発行します。これにより、人やソフトウェアがコントラクトの変更を監視できます。悪意のある関数がコントラクトに追加された場合、それを発見できます。このスタンダードに準拠するには、透過型コントラクトとデリゲートコントラクトのすべてのソースコードを公開し、検証する必要があります。

セキュリティおよびドメイン専門家は、透過型コントラクトの変更履歴を確認して、悪意のある行為の履歴がないかを検出できます。

## 根拠

### 関数シグネチャの文字列 vs bytes4[]の関数セレクタの配列

`updateContract` 関数は、関数セレクタの `bytes4[]` 配列ではなく、関数シグネチャの `string` リストを引数として受け取るようになっています。その理由は以下の3つです:

1. 関数シグネチャを渡すことで、`updateContract` の実装でセレクタの衝突を防ぐことができます。
2. このスタンダードの主要な部分は、アップグレード可能なコントラクトをより透明にすることです。時間の経過とともに何が変更されたかと、なぜ変更されたかを簡単に確認できるようにすることです。関数が追加、置換、削除されるときに、`FunctionUpdate` イベントにその関数シグネチャが含まれます。これにより、ソースコードやABIにアクセスしなくても、コントラクトのイベントをフィルタリングしてコントラクトの変更履歴を表示するソフトウェアを簡単に書くことができます。4バイトの関数セレクタのみが提供された場合、これは不可能です。
3. 透過型コントラクトのソースコードを見ただけでは、そのコントラクトがサポートするすべての関数を確認することはできません。これが、ERC1538Queryインターフェイスが存在する理由です。人やソフトウェアが、透過型コントラクトが現在サポートしているすべての関数を調べたり表示したりできるようにするためです。関数シグネチャが使用されるのは、ERC1538Query関数がそれらを表示できるようにするためです。

### ガスの考慮事項

関数呼び出しの委譲にはガスのオーバーヘッドがあります。これは2つの方法で軽減されます:
1. デリゲートコントラクトを小さくすることで、ガスコストを削減できます。関数が多数あるコントラクトを呼び出すよりも、関数が少ないコントラクトを呼び出す方がガスが少なくなります。
2. 透過型コントラクトには最大サイズの制限がないため、ユースケースに合わせてガス最適化関数を追加できます。例えば、誰かがERC721標準を実装する透過型コントラクトを使用し、[ERC1412標準](https://github.com/ethereum/EIPs/issues/1412)のバッチ転送関数を実装して、ガスを削減(およびバッチ転送をより便利にする)することができます。

### ストレージ

このスタンダードはデータの保存や整理方法を指定していませんが、以下のようなアプローチが考えられます:

**継承ストレージ**

1. 透過型コントラクトのストレージ変数は、透過型コントラクトのソースコードで定義されたものと、追加されたデリゲートコントラクトのソースコードで定義されたもので構成されます。

2. デリゲートコントラクトは、透過型コントラクトに存在するすべてのストレージ変数を、それらが存在する順序で定義していれば、透過型コントラクトのストレージ変数を使用できます。

3. デリゲートコントラクトは、透過型コントラクトに存在するすべてのストレージ変数を同じ順序で定義していれば、新しいストレージ変数を作成できます。

継承ストレージを実装する簡単な方法は以下の通りです:

1. 透過型コントラクトとデリゲートコントラクトが使用するストレージ変数を含むストレージコントラクトを作成する。
2. デリゲートコントラクトがストレージコントラクトを継承する。
3. 新しいデリゲートコントラクトを追加して新しいストレージ変数を追加する場合は、古いストレージコントラクトを継承した新しいストレージコントラクトを作成する。新しいストレージコントラクトを新しいデリゲートコントラクトで使用する。
4. 新しいデリゲートコントラクトごとに、ステップ2または3を繰り返す。

**非構造化ストレージ**

アセンブリを使用して、特定のストレージ位置にデータを保存および読み取る。このアプローチの利点は、デリゲートコントラクトが使用していないストレージ位置を定義する必要がないことです。

**永続ストレージ**

データの型に基づいた汎用APIを使ってデータを保存できる。[ERC930の詳細を参照](https://github.com/ethereum/EIPs/issues/930)

### 不変化
透過型コントラクトを不変にすることができます。これは、`updateContract` 関数を呼び出して `updateContract` 関数自体を削除することで行います。この関数がなくなれば、関数の追加、置換、削除はもはや不可能になります。

### 関数のバージョン

ソフトウェアやユーザーは、ERC1538Queryインターフェイスの `delegateAddress` 関数を呼び出すことで、呼び出された関数のバージョンを確認できます。この関数は関数シグネチャを引数として受け取り、その関数が実装されているデリゲートコントラクトアドレスを返します。

### ベストプラクティス、ツール、その他の情報

> 透過型コントラクトに関するベストプラクティス、ツール、チュートリアルなどをさらに開発・公開する必要があります。

以下は、透過型コントラクトに関する記事の一覧です。他の記事を共有したい場合は、このissueにコメントを追加してください。

[ERC1538: Future Proofing Smart Contracts and Tokens](https://coinjournal.net/erc1538-future-proofing-smart-contacts-and-tokens/)

[The ERC1538 improving towards the "transparent contract" standard](https://www.crypto-economy.net/en/ethereum-eth-erc1538-transparent-contract-standard/)

### 着想

このスタンダードは、ZeppelinOSの[Upgradeability with vtables](https://github.com/zeppelinos/labs/tree/master/upgradeability_with_vtable)の実装に着想を得ています。

また、[Mokensプロジェクト](https://github.com/Mokens/MIPs/blob/master/MIPS/mip-2-Goals-and-Objectives.md)の[Mokensコントラクト](https://etherscan.io/address/0xc1eab49cf9d2e23e43bcf23b36b2be14fc2f8838#code)の設計と実装にも着想を得ています。Mokensコントラクトは[このスタンダードを実装するように更新されています](https://etherscan.io/address/0x0ac5637fe62ec14fd9e237a81a9679d4adef701f#code)。

## 下位互換性
このスタンダードにより、新しい機能を追加したり既存の機能を置換/削除したりできるため、コントラクトは将来の標準や機能と互換性を持つことができます。

このスタンダードはコントラクトの将来を保証します。

## 実装
このスタンダードの参考実装は、[transparent-contracts-erc1538](https://github.com/mudgen/transparent-contracts-erc1538)リポジトリにあります。

## 著作権
著作権およびその関連権利は[CC0](../LICENSE.md)で放棄されています。