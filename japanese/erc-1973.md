---
original: d024c746cf2e6157385cbc2f761d69dcf9f26abe9d58ee42e56e269079710259
---

---
eip: 1973
title: スケーラブルな報酬
author: Lee Raj (@lerajk), Qin Jian (@qinjian)
type: Standards Track
category: ERC
status: Stagnant
created: 2019-04-01
---

## 簡単な要約

 DAPPのエコシステムの中で、「n」個のトークンがブロックごとにマイントされ、「m」人の参加者に均等に分配されるミンタブルなトークン報酬インターフェイス。

## 概要

 ミンタブルなトークン報酬インターフェイスにより、DAppはトークン報酬が参加者に均等に分配されるトークン経済を構築できます。トークンはブロックごとにマイントされ、設定可能です(例: 1ブロックあたり10.2356トークン、0.1トークン、1350トークン)。マイント機能は、アクティブな参加者が開始できます。各参加者に分配されるトークン報酬は、ネットワークの参加者数に依存します。ネットワークの初期段階では参加者が少ないため、1人あたりの報酬が高くなりますが、ネットワークが拡大するにつれて報酬は動的に減少していきます。

## 動機

プッシュシステムでトークンを大量の参加者に分配すると、ブロックガスリミットの問題が発生します。参加者が数万人に達すると、参加者レジストリとその対応する報酬を追跡するのが非常に困難になります。例えば、5,000人のアドレスをループして0.0000001の報酬トークンを配布するのは非常に非効率的です。さらに、これらのトランザクションのガス代は高く、DAppの開発者や企業が負担する必要があり、中央集権化の懸念につながります。

完全に分散化されたアプリケーションを維持し、ブロックガスリミットの問題を回避するには、プルシステムが必要です。しかし、数万人の参加者にスケーラブルな報酬を分配するための標準的なソリューションはまだ提案されていません。これがTPP、ラウンドマスク、参加者マスクなどの概念を使ってこのEIPで提案するものです。

## 仕様

### 定義

 `参加者あたりのトークン量(TPP)`: TPP = (マイントされたトークン量 / 総アクティブ参加者数)

 `ラウンドマスク`: トークンコントラクトのTPPの累積スナップショット。例えば、トランザクション1では100人の参加者に10トークンがマイントされ(TPP = 10 / 100)、トランザクション2では95人の参加者に12トークンがマイントされた(TPP = 12 / 95)場合、

 ラウンドマスク = (10/100) + (12/95)

 `参加者マスク`: `msg.sender`(参加者)の報酬の推移を追跡するために使用されます。参加者がエコシステムに参加または退出すると、プレイヤーマスクが更新されます。

 参加者マスク = 前のラウンドマスク OR (現在のラウンドマスク - TPP)

 `msg.senderの報酬`: ラウンドマスク - 参加者マスク

 例えば、合計6つのトランザクション(スマートコントラクトのトリガーや関数呼び出し)があり、10人の既存の参加者(分母)と20トークン(分子)がトランザクションごとにマイントされているとします。2番目のトランザクションで11番目の参加者が参加し、5番目のトランザクション前に退出した場合、11番目の参加者の残高は以下のようになります:

 ```
 t1 ラウンドマスク = (20/10)
 t2 ラウンドマスク = (20/10) + (20/11)
 t3 ラウンドマスク = (20/10) + (20/11) + (20/11)
 t4 ラウンドマスク = (20/10) + (20/11) + (20/11) + (20/11)
 t5 ラウンドマスク = (20/10) + (20/11) + (20/11) + (20/11)+ (20/10)
 t6 ラウンドマスク = (20/10) + (20/11) + (20/11) + (20/11)+ (20/10) + (20/10)
 ```

 6つのトランザクションで合計60トークンがリリースされました。

 参加者がt2で参加し、t5の前に退出した場合、t2からt4の間の報酬を受け取る権利があります。t2で参加したときの'参加者マスク = (20/10)'、t5の前に退出したときの累積報酬トークンは:

 msg.senderの報酬: `[t4 ラウンドマスク = (20/10) + (20/11)+ (20/11) + (20/11)] - [参加者マスク = (20/10)] = [報酬 = (20/11)+ (20/11) + (20/11)]`

 同じ参加者がより後の時点(t27やt35)でエコシステムに参加した場合、新しい'参加者マスク'が与えられ、参加者が退出するときの新しい報酬トークンが計算されます。この処理は各参加者について動的に継続されます。

 `tokensPerBlock`: ブロックごとにリリースされるトークンの量

 `blockFreezeInterval`: 次のマイントまでに必要なブロック数。例えば50に設定されていて、'n'トークンがブロック'b'でマイントされた場合、次の'n'トークンはブロック'b + 50'が経過するまでマイントされません。

 `lastMintedBlockNumber`: 最後に'n'トークンがマイントされたブロック番号

 `totalParticipants`: DAppネットワークの総参加者数

 `tokencontractAddress`: トークンがマイントされるコントラクトアドレス。デフォルトはaddress(this)

```solidity

pragma solidity ^0.5.2;

import "openzeppelin-solidity/contracts/token/ERC20/ERC20Mintable.sol";
import "openzeppelin-solidity/contracts/token/ERC20/ERC20Detailed.sol";

contract Rewards is ERC20Mintable, ERC20Detailed {

using SafeMath for uint256;

uint256 public roundMask;
uint256 public lastMintedBlockNumber;
uint256 public totalParticipants = 0;
uint256 public tokensPerBlock; 
uint256 public blockFreezeInterval; 
address public tokencontractAddress = address(this);
mapping(address => uint256) public participantMask; 

/**
 * @dev コンストラクタ、変数を初期化します。
 * @param _tokensPerBlock ブロックごとにリリースされるトークンの量(wei形式で入力、例: 1000000000000000000)
 * @param _blockFreezeInterval トークンがさらにマイントされるまでに必要なブロック数(例: 1, 10, 100)
 */
 constructor(uint256 _tokensPerBlock, uint256 _blockFreezeInterval) public ERC20Detailed("Simple Token", "SIM", 18){ 
lastMintedBlockNumber = block.number;
tokensPerBlock = _tokensPerBlock;
blockFreezeInterval = _blockFreezeInterval;
}

/**
 * @dev msg.senderがマインター(ホワイトリスト)に登録されているかチェックするモディファイア。
 */
modifier isAuthorized() {
require(isMinter(msg.sender));
_;
}

/**
 * @dev ネットワークに参加者を追加する関数。
 * @param _minter トークンをマイントできるアドレス。
 * @return 操作が成功したかどうかを示すブール値。
 */
function addMinters(address _minter) external returns (bool) {
_addMinter(_minter);
totalParticipants = totalParticipants.add(1);
updateParticipantMask(_minter);
return true;
}


/**
 * @dev ネットワークから参加者を削除する関数。
 * @param _minter トークンをマイントできなくなるアドレス。
 * @return 操作が成功したかどうかを示すブール値。
 */
function removeMinters(address _minter) external returns (bool) {
totalParticipants = totalParticipants.sub(1);
_removeMinter(_minter); 
return true;
}


/**
 * @dev ネットワークに新しいトークンを導入する関数。
 * @return 操作が成功したかどうかを示すブール値。
 */
function trigger() external isAuthorized returns (bool) {
bool res = readyToMint();
if(res == false) {
return false;
} else {
mintTokens();
return true;
}
}

/**
 * @dev 参加者が報酬トークンを引き出す関数。
 * @return 操作が成功したかどうかを示すブール値。
 */
function withdraw() external isAuthorized returns (bool) {
uint256 amount = calculateRewards();
require(amount >0);
ERC20(tokencontractAddress).transfer(msg.sender, amount);
}

/**
 * @dev 新しいトークンがマイントできるかどうかをチェックする関数。
 * @return 操作が成功したかどうかを示すブール値。
 */
function readyToMint() public view returns (bool) {
uint256 currentBlockNumber = block.number;
uint256 lastBlockNumber = lastMintedBlockNumber;
if(currentBlockNumber > lastBlockNumber + blockFreezeInterval) { 
return true;
} else {
return false;
}
}

/**
 * @dev 参加者の現在の報酬を計算する関数。
 * @return 計算された報酬額を示すuint。
 */
function calculateRewards() private returns (uint256) {
uint256 playerMask = participantMask[msg.sender];
uint256 rewards = roundMask.sub(playerMask);
updateParticipantMask(msg.sender);
return rewards;
}

/**
 * @dev 新しいトークンをエコノミーにマイントする関数。
 * @return 操作が成功したかどうかを示すブール値。
 */
function mintTokens() private returns (bool) {
uint256 currentBlockNumber = block.number;
uint256 tokenReleaseAmount = (currentBlockNumber.sub(lastMintedBlockNumber)).mul(tokensPerBlock);
lastMintedBlockNumber = currentBlockNumber;
mint(tokencontractAddress, tokenReleaseAmount);
calculateTPP(tokenReleaseAmount);
return true;
}

 /**
* @dev TPP(参加者あたりのトークン量)を計算する関数。
* @return 操作が成功したかどうかを示すブール値。
*/
function calculateTPP(uint256 tokens) private returns (bool) {
uint256 tpp = tokens.div(totalParticipants);
updateRoundMask(tpp);
return true;
}

 /**
* @dev ラウンドマスクを更新する関数。
* @return 操作が成功したかどうかを示すブール値。
*/
function updateRoundMask(uint256 tpp) private returns (bool) {
roundMask = roundMask.add(tpp);
return true;
}

 /**
* @dev 参加者マスク(前のラウンドマスクを保存)を更新する関数
* @return 操作が成功したかどうかを示すブール値。
*/
function updateParticipantMask(address participant) private returns (bool) {
uint256 previousRoundMask = roundMask;
participantMask[participant] = previousRoundMask;
return true;
}

}
```

## 根拠

現在、スケーラブルな報酬分配メカニズムの標準はありません。DAPPの中で持続可能な暗号経済環境を作るには、インセンティブが大きな役割を果たします。しかし、数万人の参加者に報酬を分配する方法がスケーラブルでないと、ほとんどのDAPPには良好なインセンティブ構造がありません。持続可能な暗号経済環境を持つものは、中央集権的なサーバーやセレクトノードのグループに大きく依存しています。しかし、アプリケーションを完全に分散化するためには、報酬分配メカニズムが参加者自身に依存し、参加者数が増えてもスケールできる必要があります。これがこのEIPが目指すものです。

## 下位互換性

該当なし。

## テストケース

作業中、後で追加します。

## 実装

作業中、後で適切な実装を追加します。以下はサンプル例です:

`etherscanの報酬コントラクト`: https://ropsten.etherscan.io/address/0x8b0abfc541ab7558857816a67e186221adf887bc#tokentxns

`ステップ1`: 以下のパラメータでRewardsコントラクトをデプロイ_tokensPerBlock = 1e18, _blockFreezeInterval = 1

`ステップ2`: Alice(0x123)とBob(0x456)をマインターとして追加する、addMinters(address _minter)

`ステップ3`: AliceまたはBobのアカウントからtrigger()を呼び出す。65ブロックが経過したため、65 SIMトークンがマイントされる。RMは32500000000000000000

`ステップ4`: Aliceが引き出し、32.5 SIMトークンを受け取る(65トークン / 2参加者)。彼女のPMは32500000000000000000


`ステップ5`: Satoshi(0x321)とVitalik(0x654)をマインターとして追加する、addMinters(address _minter)

`ステップ6`: Alice / Bob / Satoshi / Vitalikのいずれかのアカウントからtrigger()を呼び出す。101ブロックが経過したため、101 SIMトークンがマイントされる。RMは57750000000000000000

`ステップ7`: Aliceが引き出し、25.25 SIMトークンを受け取る(101トークン / 4参加者)。彼女のPMは57750000000000000000

`ステップ8`: Bobが引き出し、57.75 SIMトークンを受け取る((65トークン / 2参加者) + (101トークン / 4参加者))。Bobの PM = 57750000000000000000

## 著作権

CC0でライセンスされています。

## 参考文献

1. Scalable Reward Distribution on the Ethereum Blockchain by Bogdan Batog, Lucian Boca and Nick Johnson

2. Fomo3d DApp, https://fomo3d.hostedwiki.co/