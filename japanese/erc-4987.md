---
original: b458c5672146f880a72d5bf0903114a5235e7a6c9f1d804eec008391a938fd75
---

---
eip: 4987
title: 保有トークンインターフェース
description: 保有トークンの所有権とバランスを照会するためのインターフェース
author: Devin Conley (@devinaconley)
discussions-to: https://ethereum-magicians.org/t/eip-4987-held-token-standard-nfts-defi/7117
status: 停滞
type: Standards Track
category: ERC
created: 2021-09-21
requires: 20, 165, 721, 1155
---

## 概要

提案された標準は、保有トークンの機能的な所有権とバランスを公開するための軽量なインターフェースを定義しています。保有トークンとは、コントラクトが所有するトークンです。この標準は、[EIP-20](./eip-20.md)、[EIP-721](./eip-721.md)、または[EIP-1155](./eip-1155.md)トークンを保有するスマートコントラクトによって実装される可能性があり、所有権とバランスの検証に依存する、オンチェーンおよびオフチェーンのシステムによって利用されることを目的としています。

## 動機

暗号通貨の異なる分野(DeFi、NFT など)が収束し、合成性が向上するにつれ、実際の所有者(おそらくコントラクト)と機能的な所有者(おそらくユーザー)の区別がより一般的になります。現在、これにより、トークンの預金を必要とするメカニズムと、それらのトークンの所有権やバランスの検証に依存するシステムの間に矛盾が生じています。

この提案は、トークンホルダーがオーナーシップとバランス情報を公開するための標準インターフェースを提供することで、この矛盾に対処することを目的としています。これにより、ユーザーは既存のトークンの効用を放棄することなく、これらの DeFi メカニズムに参加できるようになります。全体として、これにより、ユーザーとプロトコル開発者の両方に利益をもたらす、システム間の相互運用性が大幅に向上します。

この ERC 標準の実装者の例には以下が含まれます:

- ステーキングまたはファーミングコントラクト
- レンディングプール
- タイムロックまたはベスティングボールト
- 分割可能な NFT コントラクト
- スマートコントラクトウォレット

この ERC 標準の消費者の例には以下が含まれます:

- ガバナンスシステム
- ゲーミング
- PFP 検証
- アートギャラリーまたはショーケース
- トークンベースのメンバーシッププログラム

## 仕様

`ERC20` 保有トークン標準を実装するスマートコントラクトは、`IERC20Holder` インターフェースのすべての関数を実装しなければなりません。

`ERC20` 保有トークン標準を実装するスマートコントラクトは、また `ERC165` も実装し、インターフェース ID `0x74c89d54` が渡されたときに true を返さなければなりません。

```solidity
/**
 * @notice the ERC20 holder standard provides a common interface to query
 * token balance information
 */
interface IERC20Holder is IERC165 {
  /**
   * @notice emitted when the token is transferred to the contract
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenAmount held token amount
   */
  event Hold(
    address indexed owner,
    address indexed tokenAddress,
    uint256 tokenAmount
  );

  /**
   * @notice emitted when the token is released back to the user
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenAmount held token amount
   */
  event Release(
    address indexed owner,
    address indexed tokenAddress,
    uint256 tokenAmount
  );

  /**
   * @notice get the held balance of the token owner
   * @dev should throw for invalid queries and return zero for no balance
   * @param tokenAddress held token address
   * @param owner functional token owner
   * @return held token balance
   */
  function heldBalanceOf(address tokenAddress, address owner)
    external
    view
    returns (uint256);
}
```

`ERC721` 保有トークン標準を実装するスマートコントラクトは、`IERC721Holder` インターフェースのすべての関数を実装しなければなりません。

`ERC721` 保有トークン標準を実装するスマートコントラクトは、また `ERC165` も実装し、インターフェース ID `0x16b900ff` が渡されたときに true を返さなければなりません。

```solidity
/**
 * @notice the ERC721 holder standard provides a common interface to query
 * token ownership and balance information
 */
interface IERC721Holder is IERC165 {
  /**
   * @notice emitted when the token is transferred to the contract
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenId held token ID
   */
  event Hold(
    address indexed owner,
    address indexed tokenAddress,
    uint256 indexed tokenId
  );

  /**
   * @notice emitted when the token is released back to the user
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenId held token ID
   */
  event Release(
    address indexed owner,
    address indexed tokenAddress,
    uint256 indexed tokenId
  );

  /**
   * @notice get the functional owner of a held token
   * @dev should throw for invalid queries and return zero for a token ID that is not held
   * @param tokenAddress held token address
   * @param tokenId held token ID
   * @return functional token owner
   */
  function heldOwnerOf(address tokenAddress, uint256 tokenId)
    external
    view
    returns (address);

  /**
   * @notice get the held balance of the token owner
   * @dev should throw for invalid queries and return zero for no balance
   * @param tokenAddress held token address
   * @param owner functional token owner
   * @return held token balance
   */
  function heldBalanceOf(address tokenAddress, address owner)
    external
    view
    returns (uint256);
}
```

`ERC1155` 保有トークン標準を実装するスマートコントラクトは、`IERC1155Holder` インターフェースのすべての関数を実装しなければなりません。

`ERC1155` 保有トークン標準を実装するスマートコントラクトは、また `ERC165` も実装し、インターフェース ID `0xced24c37` が渡されたときに true を返さなければなりません。

```solidity
/**
 * @notice the ERC1155 holder standard provides a common interface to query
 * token balance information
 */
interface IERC1155Holder is IERC165 {
  /**
   * @notice emitted when the token is transferred to the contract
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenId held token ID
   * @param tokenAmount held token amount
   */
  event Hold(
    address indexed owner,
    address indexed tokenAddress,
    uint256 indexed tokenId,
    uint256 tokenAmount
  );

  /**
   * @notice emitted when the token is released back to the user
   * @param owner functional token owner
   * @param tokenAddress held token address
   * @param tokenId held token ID
   * @param tokenAmount held token amount
   */
  event Release(
    address indexed owner,
    address indexed tokenAddress,
    uint256 indexed tokenId,
    uint256 tokenAmount
  );

  /**
   * @notice get the held balance of the token owner
   * @dev should throw for invalid queries and return zero for no balance
   * @param tokenAddress held token address
   * @param owner functional token owner
   * @param tokenId held token ID
   * @return held token balance
   */
  function heldBalanceOf(
    address tokenAddress,
    address owner,
    uint256 tokenId
  ) external view returns (uint256);
}
```

## 根拠

このインターフェースは、極めて軽量で、既存のすべてのトークンコントラクトと互換性があるように設計されています。トークンホルダーコントラクトは、おそらく既に関連情報をすべて保存しているため、この標準は、その情報を公開するための共通インターフェースを追加するだけです。

トークンアドレスパラメーターは、コントラクトが複数のトークンコントラクトを同時に保有できるようサポートするために含まれています。一部のコントラクトは単一のトークンアドレスのみを保有する可能性がありますが、これはそのような場合も含めてより一般的です。

トークンの種類ごとに別のインターフェース(EIP-20、EIP-721、EIP-1155)が提案されているのは、これらの異なるトークンを保有するためのコントラクトロジックが独立している可能性が高いためです。単一のコントラクトが複数のトークンタイプを保有する場合は、適切な保有トークンインターフェースをそれぞれ実装できます。

## 下位互換性

重要なことに、提案された仕様は、既存のすべての EIP-20、EIP-721、EIP-1155 トークンコントラクトと完全に互換性があります。

トークンホルダーコントラクトは、この軽量なインターフェースを実装するように更新する必要があります。

この標準の消費者は、所有権ロジックで、このインターフェースを尊重するように更新する必要があります。

## 参考実装

[インターフェース](../assets/eip-4987/IERC721Holder.sol)、ボールト[トークンホルダー](../assets/eip-4987/Vault.sol)、および[消費者](../assets/eip-4987/Consumer.sol)を含む完全な例実装は、`assets/eip-4987/` にあります。

特に、`IERC721Holder` インターフェースの消費者は、次のようなロジックを使用して、任意の特定のトークン ID の所有者を連鎖的に検索できます。

```solidity
  /**
   * @notice get the functional owner of a token
   * @param tokenId token id of interest
   */
  function getOwner(uint256 tokenId) external view returns (address) {
    // get raw owner
    address owner = token.ownerOf(tokenId);

    // if owner is not contract, return
    if (!owner.isContract()) {
      return owner;
    }

    // check for token holder interface support
    try IERC165(owner).supportsInterface(0x16b900ff) returns (bool ret) {
      if (!ret) return owner;
    } catch {
      return owner;
    }

    // check for held owner
    try IERC721Holder(owner).heldOwnerOf(address(token), tokenId) returns (address user) {
      if (user != address(0)) return user;
    } catch {}

    return owner;
  }
```

## セキュリティ上の考慮事項

この標準の消費者は、不明なコントラクトからの所有権情報を慎重に使用する必要があります。悪意のある行為者が、ガバナンスシステム、ゲーム、メンバーシッププログラムなどを操作する目的で、インターフェースを実装し、無効または悪意のある情報を報告する可能性があります。

消費者は、サニティチェックとして、ホルダーコントラクトの全体的なトークンバランスと所有権も確認する必要があります。

## 著作権

著作権およびその関連権利は[CC0](../LICENSE.md)によって放棄されています。