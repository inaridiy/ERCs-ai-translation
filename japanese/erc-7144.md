---
original: 49353fda98f144bb52dd421bc3376ec411247298ec645f26ac8ba6b4f5830561
---

---
eip: 7144
title: トランザクション検証ステップを持つERC-20
description: 盗難ウォレットの場合のセキュリティステップを実現するための、transfer()とapprove()呼び出しの新しい検証ステップ
author: Eduard López i Fina (@eduardfina)
discussions-to: https://ethereum-magicians.org/t/erc721-with-a-validation-step/14071
status: Review
type: Standards Track
category: ERC
created: 2023-05-07
requires: 20
---

## 概要

このスタンダードは[ERC-20](./eip-20.md)の拡張です。ウォレットの空き巣を避けるための新しい検証機能を定義しています: `transfer`または`approve`はすべて検証を待つようにロックされます。

## 動機

ブロックチェーンの力は同時にその弱点でもあります: ユーザーにデータの完全な責任を負わせること。

現在、多くのトークン盗難の事例が存在し、トークンを冷たいウォレットに転送するなどの現在のトークン盗難対策では、トークンを使いづらくなります。

`transfer`と`approve`の前に検証ステップを設けることで、スマートコントラクト開発者にセキュアなトークン盗難対策を作成する機会を与えます。

実装例としては、バリデーター・アドレスがすべてのスマートコントラクト・トランザクションの検証を担当するシステムが考えられます。

このアドレスはユーザーがトークンの検証リクエストを確認し、正しいものを受け入れるdAppに接続されています。

このアドレスに検証トランザクションのみの権限を与えることで、ユーザーのアドレスとバリデーターのアドレスの両方を同時に持っていない限り、トークンを盗むことができない、より安全なシステムを実現できます。

## 仕様

この文書の中の "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" および "OPTIONAL" というキーワードは、RFC 2119に記載されているように解釈されるものとします。

[ERC-20](./eip-20.md)準拠のコントラクトはこのEIPを実装してもよい(MAY)。

トークンの所有権を変更するすべての操作(例えば`transfer`/`transferFrom`)は、検証待ちの`TransferValidation`を作成し、`ValidateTransfer`を発行しなければならず(SHALL)、トークンは移転されません。

トークンの承認を可能にするすべての操作(例えば`approve`)は、検証待ちの`ApprovalValidation`を作成し、`ValidateApproval`を発行しなければならず(SHALL)、承認は有効化されません。

承認されたアカウントが所有者ではなく呼び出した場合の`transfer`は、検証の必要なく直接実行されなければなります。これは、トークンを直接移動するために`approve`を要求するすべての現在のプロジェクトに適応するためです。

`TransferValidation`または`ApprovalValidation`を検証する際は、`valid`フィールドをtrueに設定し、二重検証してはいけません(MUST NOT)。

`TransferValidation`を検証する操作はトークンの所有権を変更しなければなりません(SHALL)。

`ApprovalValidation`を検証する操作は承認を有効化しなければなりません(SHALL)。

### コントラクト・インターフェース

```solidity
interface IERC7144 {

    struct TransferValidation {
        // 所有者のアドレス
        address from;
        // 受取人のアドレス
        address to;
        // トークン量
        uint256 amount;
        // 有効な転送かどうか
        bool valid;
    }

    struct ApprovalValidation {
        // 所有者のアドレス
        address owner;
        // スペンダーのアドレス
        address spender;
        // 承認されたトークン量
        uint256 amount;
        // 有効な承認かどうか
        bool valid;
    }

    /**
     * @dev 新しい転送検証が要求されたときに発行されるイベント
     */
    event ValidateTransfer(address indexed from, address indexed to, uint256 amount, uint256 indexed transferValidationId);

    /**
    * @dev 新しい承認検証が要求されたときに発行されるイベント
    */
    event ValidateApproval(address indexed owner, address indexed spender, uint256 amount, uint256 indexed approvalValidationId);

    /**
     * @dev このコントラクトがバリデーターERC20であるかどうかを返す
     */
    function isValidatorContract() external view returns (bool);

    /**
     * @dev 転送IDを使って転送検証構造体を返す
     *
     */
    function transferValidation(uint256 transferId) external view returns (TransferValidation memory);

    /**
    * @dev 承認IDを使って承認検証構造体を返す
    *
    */
    function approvalValidation(uint256 approvalId) external view returns (ApprovalValidation memory);

    /**
     * @dev 作成された転送検証の総数を返す
     *
     */
    function totalTransferValidations() external view returns (uint256);

    /**
     * @dev 作成された承認検証の総数を返す
     *
     */
    function totalApprovalValidations() external view returns (uint256);
}
  ```

`isValidatorContract()`関数は`public`として実装されなければなりません(MUST)。

`transferValidation(uint256 transferId)`関数は`public`または`external`として実装してもよい(MAY)。

`approvalValidation(uint256 approveId)`関数は`public`または`external`として実装してもよい(MAY)。

`totalTransferValidations()`関数は`pure`または`view`として実装してもよい(MAY)。

`totalApprovalValidations()`関数は`pure`または`view`として実装してもよい(MAY)。

## 根拠

### 汎用性

このスタンダードは検証機能のみを定義し、それらの使用方法は定義していません。検証を内部的に定義し、ユーザーがそれらを管理する方法を決めることができます。

例えば、dAppに接続されたバリデーター・アドレスを持つことで、ユーザーが自分の検証を管理できるようにするなどです。

このバリデーターは、すべてのトークンや特定のユーザーのみに使用できます。

また、既存のERC-20トークンのラップ・スマートコントラクトとしても使用でき、既存のトークンと1:1の変換を可能にします。

### 拡張性

このスタンダードは検証機能のみを定義しており、それをどのように検証するかは定義していません。サードパーティのプロトコルは、これらの関数を自由に呼び出す方法を定義できます。

## 下位互換性

このスタンダードは[ERC-20](./eip-20.md)の拡張であり、`transfer`/`transferFrom`/`approve`以外のすべての操作と互換性があります。

これらの操作は、トークンを転送したり承認を有効化したりするのではなく、検証リクエストを作成するように上書きされます。

## 参考実装

```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "./IERC7144.sol";

/**
 * @dev ERC7144の実装
 */
contract ERC7144 is IERC7144, ERC20 {

    // 転送IDから転送検証への対応付け
    mapping(uint256 => TransferValidation) private _transferValidations;

    // 承認IDから承認検証への対応付け
    mapping(uint256 => ApprovalValidation) private _approvalValidations;

    // 作成された転送検証の総数
    uint256 private _totalTransferValidations;

    // 作成された承認検証の総数
    uint256 private _totalApprovalValidations;

    /**
     * @dev トークンコレクションの`name`と`symbol`を設定して初期化
     */
    constructor(string memory name_, string memory symbol_) ERC20(name_, symbol_){
    }

    /**
    * @dev このコントラクトがバリデーターERC721であるかどうかを返す
    */
    function isValidatorContract() public pure returns (bool) {
        return true;
    }

    /**
     * @dev 転送IDを使って転送検証構造体を返す
     *
     */
    function transferValidation(uint256 transferId) public view override returns (TransferValidation memory) {
        require(transferId < _totalTransferValidations, "ERC7144: invalid transfer ID");
        TransferValidation memory v = _transferValidation(transferId);

        return v;
    }

    /**
     * @dev 承認IDを使って承認検証構造体を返す
     *
     */
    function approvalValidation(uint256 approvalId) public view override returns (ApprovalValidation memory) {
        require(approvalId < _totalApprovalValidations, "ERC7144: invalid approval ID");
        ApprovalValidation memory v = _approvalValidation(approvalId);

        return v;
    }

    /**
     * @dev 作成された転送検証の総数を返す
     *
     */
    function totalTransferValidations() public view override returns (uint256) {
        return _totalTransferValidations;
    }

    /**
     * @dev 作成された承認検証の総数を返す
     *
     */
    function totalApprovalValidations() public view override returns (uint256) {
        return _totalApprovalValidations;
    }

    /**
     * @dev `transferId`の転送検証を返す。転送が存在しない場合は例外を投げない
     */
    function _transferValidation(uint256 transferId) internal view virtual returns (TransferValidation memory) {
        return _transferValidations[transferId];
    }

    /**
     * @dev `approvalId`の承認検証を返す。承認が存在しない場合は例外を投げない
     */
    function _approvalValidation(uint256 approvalId) internal view virtual returns (ApprovalValidation memory) {
        return _approvalValidations[approvalId];
    }

    /**
     * @dev 転送IDを使って転送を検証する
     *
     */
    function _validateTransfer(uint256 transferId) internal virtual {
        TransferValidation memory v = transferValidation(transferId);
        require(!v.valid, "ERC721V: the transfer is already validated");

        super._transfer(v.from, v.to, v.amount);

        _transferValidations[transferId].valid = true;
    }

    /**
     * @dev 承認IDを使って承認を検証する
     *
     */
    function _validateApproval(uint256 approvalId) internal virtual {
        ApprovalValidation memory v = approvalValidation(approvalId);
        require(!v.valid, "ERC7144: the approval is already validated");

        super._approve(v.owner, v.spender, v.amount);

        _approvalValidations[approvalId].valid = true;
    }

    /**
     * @dev `from`から`to`に`tokenId`の転送リクエストを作成する
     *
     * 要件:
     *
     * - `from`はゼロアドレスであってはならない
     * - `to`はゼロアドレスであってはならない
     *
     * {ValidateTransfer}イベントを発行する
     */
    function _transfer(
        address from,
        address to,
        uint256 amount
    ) internal virtual override {
        require(from != address(0), "ERC7144: transfer from the zero address");
        require(to != address(0), "ERC7144: transfer to the zero address");

        if(_msgSender() == from) {
            TransferValidation memory v;

            v.from = from;
            v.to = to;
            v.amount = amount;

            _transferValidations[_totalTransferValidations] = v;

            emit ValidateTransfer(from, to, amount, _totalTransferValidations);

            _totalTransferValidations++;
        } else {
            super._transfer(from, to, amount);
        }
    }

    /**
     * @dev `owner`から`amount`の操作を可能にする承認リクエストを作成する
     *
     * {ValidateApproval}イベントを発行する
     */
    function _approve(
        address owner,
        address spender,
        uint256 amount
    ) internal virtual override {
        require(owner != address(0), "ERC7144: approve from the zero address");
        require(spender != address(0), "ERC7144: approve to the zero address");

        ApprovalValidation memory v;

        v.owner = owner;
        v.spender = spender;
        v.amount = amount;

        _approvalValidations[_totalApprovalValidations] = v;

        emit ValidateApproval(v.owner, spender, amount, _totalApprovalValidations);

        _totalApprovalValidations++;
    }
}
```

## セキュリティ上の考慮事項

仕様に定義されているように、トークンの所有権を変更したり、トークンの管理を可能にしたりする操作は、検証待ちの`TransferValidation`または`ApprovalValidation`を作成し、トークンを移転したり承認を有効化したりしてはいけません(SHALL NOT)。

この前提に基づいて、`TransferValidation`または`ApprovalValidation`を検証する操作は、適用されるシステムで要求される最大限のセキュリティで保護されなければなりません。

例えば、トランザクションの検証を担当するバリデーター・アドレスがあるシステムが有効です。

別の例として、各ユーザーがバリデーター・アドレスを選択できるシステムも正しいでしょう。

いずれ
の場合も、`TransferValidation`または`ApprovalValidation`を検証できるアドレスを、選択されたシステムの許可なく検証できないことが重要です。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)により放棄されています。