---
original: 5905d37e6c80e8dc0431ee4e4a0dbe07d698c22edd5492985403345d2f11926b
---

---
eip: 6682
title: NFT フラッシュローン
description: ERC-721 NFT フラッシュローンのための最小限のインターフェース
author: out.eth (@outdoteth)
discussions-to: https://ethereum-magicians.org/t/eip-6682-nft-flashloans/13294
status: Draft
type: Standards Track
category: ERC
created: 2023-03-12
requires: 20, 721, 3156
---

## 概要

この規格は、[ERC-3156](./eip-3156.md) フラッシュローン規格の拡張版で、[ERC-721](./eip-721.md) NFT フラッシュローンをサポートします。フラッシュローン提供者がNFTを貸し出し、同一のトランザクション内で返済と手数料の支払いを行う方法を提案しています。

## 動機

現行のフラッシュローン規格 [ERC-3156](./eip-3156.md) は [ERC-20](./eip-20.md) トークンのみをサポートしています。ERC-721 トークンはERC-20 トークンとは十分に異なるため、既存の規格を拡張してNFTをサポートする必要があります。

NFTフラッシュローンは、NFTの所有権が確認される必要のある様々なアクション、例えばエアドロップの受け取り、ステーキング報酬の受け取り、ゲーム内リソースの獲得などで有用です。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」は、RFC 2119およびRFC 8174に記載されている通りに解釈されるものとします。

### コントラクトインターフェース

```solidity
pragma solidity ^0.8.19;

interface IERC6682 {
    /// @dev フラッシュローン手数料の支払いに使用されるトークンのアドレス。
    function flashFeeToken() external view returns (address);

    /// @dev NFTがフラッシュローンに利用可能かどうか。
    /// @param token NFTコントラクトのアドレス。
    /// @param tokenId NFTのID。
    function availableForFlashLoan(address token, uint256 tokenId) external view returns (bool);
}
```

`flashFeeToken` 関数は、フラッシュローン手数料の支払いに使用されるトークンのアドレスを返さなければなりません。

手数料の支払いにETHを使用する場合、`flashFeeToken` は `address(0)` を返さなければなりません。

`availableForFlashLoan` 関数は、`tokenId` の `token` がフラッシュローンに利用可能かどうかを返さなければなりません。`tokenId` がフラッシュローンに利用できない場合、`availableForFlashLoan` は `false` を返さなければならず、リバートしてはいけません。

実装者は `IERC3156FlashLender` も実装しなければなりません。

## 根拠

上記の変更は、既存のフラッシュローン規格 ([ERC-3156](./eip-3156.md)) をサポートするために必要な最小限の追加です。

NFTをサポートするために、完全に新しい規格を作成するのではなく、既に広く採用されているフラッシュローン規格 ([ERC-3156](./eip-3156.md)) を可能な限り拡張することを選択しました。

ほとんどの場合、手数料の支払いは、NFT自体を分割できないため、NFTとは別の通貨で行うことが望ましいでしょう。例えば、フラッシュローン提供者が各NFTに対して0.1 ETHの手数料を課す場合、このインターフェースは、借り手が各NFTの手数料率と手数料の支払い通貨を判断できるようにする必要があります。

## 下位互換性

この EIP は [ERC-3156](./eip-3156.md) と完全に下位互換性があります。ただし、`maxFlashLoan` メソッドは例外です。このメソッドはNFTの文脈では意味を成さないため、下位互換性を損なわずに削除することはできません。このEIPを実装する際、ERC-20 フラッシュローンをサポートする意図がない場合は、常に `maxFlashLoan` から `1` を返すことをお勧めします。この `1` は、`flashLoan` コールあたり1つのNFTしかフラッシュローンできないことを示しています。例:

```solidity
function maxFlashLoan(address token) public pure override returns (uint256) {
    // ERC20 トークンのフラッシュローンもサポートする場合は、
    // ここで別の値を返すことができます
    return 1;
}
```

## 参考実装

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.19;

import "../interfaces/IERC20.sol";
import "../interfaces/IERC721.sol";
import "../interfaces/IERC3156FlashBorrower.sol";
import "../interfaces/IERC3156FlashLender.sol";
import "../interfaces/IERC6682.sol";

contract ExampleFlashLender is IERC6682, IERC3156FlashLender {
    uint256 internal _feePerNFT;
    address internal _flashFeeToken;

    constructor(uint256 feePerNFT_, address flashFeeToken_) {
        _feePerNFT = feePerNFT_;
        _flashFeeToken = flashFeeToken_;
    }

    function flashFeeToken() public view returns (address) {
        return _flashFeeToken;
    }

    function availableForFlashLoan(address token, uint256 tokenId) public view returns (bool) {
        // このコントラクトが所有しているNFTかどうかを返す
        try IERC721(token).ownerOf(tokenId) returns (address result) {
            return result == address(this);
        } catch {
            return false;
        }
    }

    function flashFee(address token, uint256 tokenId) public view returns (uint256) {
        return _feePerNFT;
    }

    function flashLoan(IERC3156FlashBorrower receiver, address token, uint256 tokenId, bytes calldata data)
        public
        returns (bool)
    {
        // NFTがフラッシュローンに利用可能であることを確認
        require(availableForFlashLoan(token, tokenId), "IERC6682: NFTがフラッシュローンに利用できません");

        // NFTを借り手に転送
        IERC721(token).safeTransferFrom(address(this), address(receiver), tokenId);

        // 手数料を計算
        uint256 fee = flashFee(token, tokenId);

        // 借り手を呼び出す
        bool success =
            receiver.onFlashLoan(msg.sender, token, tokenId, fee, data) == keccak256("ERC3156FlashBorrower.onFlashLoan");

        // フラッシュローンが成功したことを確認
        require(success, "IERC6682: フラッシュローンに失敗しました");
        
        // 借り手がNFTを返済したことを確認
        require(IERC721(token).ownerOf(tokenId) == address(this), "IERC6682: 借り手がNFTを返済していません");

        // 借り手から手数料を転送
        IERC20(flashFeeToken()).transferFrom(msg.sender, address(this), fee);

        return success;
    }

    function maxFlashLoan(address token) public pure override returns (uint256) {
        // ERC20 トークンのフラッシュローンもサポートする場合は、
        // ここで別の値を返すことができます
        return 1;
    }

    function onERC721Received(address, address, uint256, bytes memory) public returns (bytes4) {
        return this.onERC721Received.selector;
    }
}
```

## セキュリティ上の考慮事項

`flashFeeToken` メソッドが悪意のあるコントラクトを返す可能性があります。`flashFeeToken` メソッドから返されたアドレスを呼び出すつもりの借り手は、そのコントラクトが悪意のあるものではないことを確認する必要があります。ユーザー入力のアドレスと `flashFeeToken` から返されたアドレスが一致することを確認するのが1つの方法です。

議論が必要です。

## 著作権

著作権およびそれに関連する権利は [CC0](../LICENSE.md) により放棄されています。