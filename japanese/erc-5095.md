---
original: 521e4a040c4cfb6c83b62cd4703d523176483af5c8b686d5db0e115dfeafb484
---

---
eip: 5095
title: プリンシパル・トークン
description: プリンシパル・トークン(ゼロクーポン・トークン)は、将来の時間に単一の基礎となる[EIP-20](./eip-20.md)トークンと引き換えることができます。
author: Julian Traversa (@JTraversa)、Robert Robbins (@robrobbins)、Alberto Cuesta Cañada (@alcueca)
discussions-to: https://ethereum-magicians.org/t/eip-5095-principal-token-standard/9259
status: 停滞
type: Standards Track
category: ERC
created: 2022-05-01
requires: 20, 2612
---

## 概要

プリンシパル・トークンは、将来の時間に基礎となるトークンの所有権を表します。

この仕様は、[EIP-20](./eip-20.md)トークンの拡張であり、トークンの預金と引き出し、残高の読み取り、および[EIP-2612](./eip-2612.md)仕様が提供する[EIP-712](./eip-712.md)シグネチャベースの承認を可能にします。

## 動機

プリンシパル・トークンには標準化がなく、開発スペースが難しく、実装方式が多様になっています。

主な例としては、将来の収益を剥奪してプリンシパル・トークンを残す収益トークン化プラットフォームや、プリンシパル・トークンを貸借の媒体として使用する固定金利のマネーマーケットなどがあります。

この実装の不一致により、アプリケーション層やウォレット層での統合が困難になっています。開発者は現在、個々のプリンシパル・トークンのアダプターや、プールコントラクト、時には保管契約のアダプターも実装する必要があり、開発リソースが大幅に浪費されています。

## 仕様

すべてのプリンシパル・トークン(PT)は、将来の基礎となるトークンの所有権を表すために[EIP-20](./eip-20.md)を実装しなければなりません。
PTが譲渡不可能な場合は、`transfer`または`transferFrom`の呼び出しを拒否することができます。
[EIP-20](./eip-20.md)の操作`balanceOf`、`transfer`、`totalSupply`などは、プリンシパル・トークンの残高に対して機能します。

すべてのプリンシパル・トークンは、[EIP-20](./eip-20.md)のオプションのメタデータ拡張を実装しなければなりません。
`name`および`symbol`関数は、基礎となるトークンの`name`および`symbol`、発行プロトコル、および収益トークン化プロトコルの場合はオリジネーションマネーマーケットを反映するべきです。

すべてのプリンシパル・トークンは、[EIP-2612](./eip-2612.md)を実装して、さまざまな統合でのPTの承認のUXを改善することができます。

### 定義:

- 基礎となるトークン: プリンシパル・トークンが満期時に引き換えられるトークン。
  対応する[EIP-20](./eip-20.md)コントラクトによって定義された単位を持ちます。
- 満期: プリンシパル・トークンが満期になる時間(unix)。プリンシパル・トークンは、この時間以降に基礎となるトークンと引き換えることができます。
- 手数料: ユーザーがプリンシパル・トークンに課される基礎となるトークンまたはプリンシパル・トークンの金額。手数料は、償還時または満期後の収益に存在する可能性があります。
- スリッページ: 広告された償還価値と PT 償還の経済的現実との間の差異で、手数料では説明されないもの。

### メソッド

#### `underlying`

プリンシパル・トークンが会計と償還に使用する基礎となるトークンのアドレス。

EIP-20トークンコントラクトでなければなりません。

_NOT_リバートしてはいけません。

```yaml
- name: underlying
  type: function
  stateMutability: view

  inputs: []

  outputs:
    - name: underlyingAddress
      type: address
```

#### `maturity`

プリンシパル・トークンが基礎となるトークンと引き換えられる可能性のある、またはその時間以降のunixタイムスタンプ(uint256)。

_NOT_リバートしてはいけません。

```yaml
- name: maturity
  type: function
  stateMutability: view

  inputs: []

  outputs:
    - name: timestamp
      type: uint256
```

#### `convertToUnderlying`

提供されたPTの量と引き換えられる基礎となるトークンの量。

満期前は、PTがまだ満期に達していないと仮定して計算されます。

手数料は含まれません。

呼び出し元によって変化してはいけません。

スリッページや他のオンチェーンの条件は反映されません。

整数オーバーフローによる場合を除いて、リバートしてはいけません。

0に向けて切り下げます。

この計算は、"ユーザー単位"のプリンシパル・トークン単価ではなく、"平均ユーザー"のプリンシパル・トークン単価を反映する可能性があります。

```yaml
- name: convertToUnderlying
  type: function
  stateMutability: view

  inputs:
    - name: principalAmount
      type: uint256

  outputs:
    - name: underlyingAmount
      type: uint256
```

#### `convertToPrincipal`

プリンシパル・トークンコントラクトが、指定された基礎となるトークンの量を提供するために要求するプリンシパル・トークンの量。

手数料は含まれません。

呼び出し元によって変化してはいけません。

スリッページや他のオンチェーンの条件は反映されません。

整数オーバーフローによる場合を除いて、リバートしてはいけません。

0に向けて切り下げます。

この計算は、"ユーザー単位"のプリンシパル・トークン単価ではなく、"平均ユーザー"のプリンシパル・トークン単価を反映する可能性があります。

```yaml
- name: convertToPrincipal
  type: function
  stateMutability: view

  inputs:
    - name: underlyingAmount
      type: uint256

  outputs:
    - name: principalAmount
      type: uint256
```

#### `maxRedeem`

`holder`の残高から`redeem`呼び出しを通して償還できるプリンシパル・トークンの最大量。

`holder`から`redeem`を通して転送でき、リバートを引き起こさない可能性のある最大のプリンシパル・トークンの量を返さなければなりません(必要に応じて過小評価する必要があります)。

グローバルおよびユーザー固有の制限を考慮しなければなりません。償還が完全に無効になっている(一時的でも)場合は、0を返さなければなりません。

_NOT_リバートしてはいけません。

```yaml
- name: maxRedeem
  type: function
  stateMutability: view

  inputs:
    - name: holder
      type: address

  outputs:
    - name: maxPrincipalAmount
      type: uint256
```

#### `previewRedeem`

オンチェーンまたはオフチェーンのユーザーが、現在のブロックでの自分の償還の影響をシミュレートできるようにします。

同じトランザクションで`redeem`が返す`underlyingAmount`と同じか、それ以上の正確な量の基礎となるトークンを返さなければなりません。

償還制限(maxRedeemから返されるものなど)は考慮せず、ユーザーが十分なプリンシパル・トークンを持っているかどうかに関係なく、償還が受け入れられるものとして常に機能する必要があります。

償還手数料を含める必要があります。インテグレーターは償還手数料の存在を認識する必要があります。

ユーザー/グローバルの制限によってリバートしてはいけません。他の条件によってリバートする可能性があります。

`convertToUnderlying`と`previewRedeem`の間の不利な差異は、プリンシパル・トークン単価のスリッページや他の種類の条件と見なされるべきです。

```yaml
- name: previewRedeem
  type: function
  stateMutability: view

  inputs:
    - name: principalAmount
      type: uint256

  outputs:
    - name: underlyingAmount
      type: uint256
```

#### `redeem`

満期時または満期以降に、正確に`principalAmount`のプリンシパル・トークンを`from`から焼却し、`to`に`underlyingAmount`の基礎となるトークンを送信します。

インターフェースやその他のコントラクトは、資金の保管が存在することを期待してはいけません。プリンシパル・トークン自体がカストディを持つことは非常に便利なインテグレーターにとって有用ですが、一部のプロトコルではこれが後方互換性を損なう可能性があります。

`Redeem`イベントを発行しなければなりません。

`holder`から直接プリンシパル・トークンが焼却される償還フローをサポートしなければなりません。ここで`holder`は`msg.sender`であるか、`msg.sender`が`holder`のプリンシパル・トークンに対して[EIP-20](./eip-20.md)の承認を持っている必要があります。
プリンシパル・トークンがプリンシパル・トークンコントラクトに転送された後に`redeem`実行が行われ、`redeem`中に考慮される追加のフローをサポートすることができます。

`principalAmount`のすべてが償還できない場合(引き出し制限に達した、スリッページがある、保有者が十分なプリンシパル・トークンを持っていないなど)、リバートしなければなりません。

一部の実装では、引き出しを実行する前にプリンシパル・トークンに事前に要求する必要があります。これらのメソッドは別途実行する必要があります。

```yaml
- name: redeem
  type: function
  stateMutability: nonpayable

  inputs:
    - name: principalAmount
      type: uint256
    - name: to
      type: address
    - name: from
      type: address

  outputs:
    - name: underlyingAmount
      type: uint256
```

#### `maxWithdraw`

`holder`のプリンシパル・トークンの残高から、`withdraw`呼び出しを通して償還できる基礎となるアセットの最大量。

`holder`から`withdraw`を通して償還でき、リバートを引き起こさない可能性のある基礎となるトークンの最大量を返さなければなりません(必要に応じて過小評価する必要があります)。

グローバルおよびユーザー固有の制限を考慮しなければなりません。引き出しが完全に無効になっている(一時的でも)場合は、0を返さなければなりません。

_NOT_リバートしてはいけません。

```yaml
- name: maxWithdraw
  type: function
  stateMutability: view

  inputs:
    - name: holder
      type: address

  outputs:
    - name: maxUnderlyingAmount
      type: uint256
```

#### `previewWithdraw`

オンチェーンまたはオフチェーンのユーザーが、現在のブロックでの自分の引き出しの影響をシミュレートできるようにします。

同じトランザクションで`withdraw`が返す`principalAmount`と同じか、それ以下の正確な量のプリンシパル・トークンを返さなければなりません。

引き出し制限(maxWithdrawから返されるものなど)は考慮せず、ユーザーが十分なプリンシパル・トークンを持っているかどうかに関係なく、引き出しが受け入れられるものとして常に機能する必要があります。

引き出し手数料を含める必要があります。インテグレーターは引き出し手数料の存在を認識する必要があります。

ユーザー/グローバルの制限によってリバートしてはいけません。他の条件によってリバートする可能性があります。

`convertToPrincipal`と`previewWithdraw`の間の不利な差異は、プリンシパル・トークン単価のスリッページや他の種類の条件と見なされるべきです。

```yaml
- name: previewWithdraw
  type: function
  stateMutability: view

  inputs:
    - name: underlyingAmount
      type: uint256

  outputs:
    - name: principalAmount
      type: uint256
```

#### `withdraw`

`holder`から`principalAmount`を焼却し、正確に`underlyingAmount`の基礎となるトークンを`receiver`に送信します。

`Redeem`イベントを発行しなければなりません。

`holder`から直接プリンシパル・トークンが焼却される引き出しフローをサポートしなければなりません。ここで`holder`は`msg.sender`であるか、`msg.sender`が`holder`のプリンシパル・トークンに対して[EIP-20](./eip-20.md)の承認を持っている必要があります。
プリンシパル・トークンがプリンシ
パル・トークンコントラクトに転送された後に`withdraw`実行が行われ、`withdraw`中に考慮される追加のフローをサポートすることができます。

`underlyingAmount`のすべてが引き出せない場合(引き出し制限に達した、スリッページがある、保有者が十分なプリンシパル・トークンを持っていないなど)、リバートしなければなりません。

一部の実装では、引き出しを実行する前にプリンシパル・トークンに事前に要求する必要があります。これらのメソッドは別途実行する必要があります。

```yaml
- name: withdraw
  type: function
  stateMutability: nonpayable

  inputs:
    - name: underlyingAmount
      type: uint256
    - name: receiver
      type: address
    - name: holder
      type: address

  outputs:
    - name: principalAmount
      type: uint256
```

### イベント

#### Redeem

`from`が`principalAmount`のプリンシパル・トークンを交換し、`underlyingAmount`の基礎となるトークンを`to`に転送しました。

`EIP5095.redeem`メソッドでプリンシパル・トークンが焼却され、基礎となるトークンがコントラクトから引き出されたときに、必ず発行されなければなりません。

```yaml
- name: Redeem
  type: event

  inputs:
    - name: from
      indexed: true
      type: address
    - name: to
      indexed: true
      type: address
    - name: amount
      indexed: false
      type: uint256
```

## 根拠

プリンシパル・トークンのインターフェースは、コア最小限のインターフェースと、後方互換性を有効にするためのオプションのインターフェースを最適化するように設計されています。基礎となるトークンの会計管理の詳細は意図的に指定されていません。プリンシパル・トークンはオンチェーンでブラックボックスとして扱われ、使用前にオフチェーンで検査されることが期待されています。

[EIP-20](./eip-20.md)は、トークンの承認と残高計算などの実装の詳細が直接引き継がれるため、強制されています。この標準化により、プリンシパル・トークンは[EIP-20](./eip-20.md)のすべての使用例に加えて、EIP-5095とも即座に互換性を持つようになります。

すべてのプリンシパル・トークンは満期時に償還可能ですが、満期後にさらに収益が生成されるかどうかが唯一の違いです。償還の普遍性を考えると、`redeem`の存在により、インテグレーターはオープン市場でプリンシパル・トークンを購入し、プリンシパル・トークン自体のアドレスを知るだけで後で償還することができます。

この EIP は [EIP-4626](./eip-4626.md) の設計に大きく依存しています。なぜなら、技術的にはプリンシパル・トークンは収益ベアリングボールトのサブセットと記述できるからです。ただし、`maturity`変数と実装の制限が追加されています。[EIP-4626](./eip-4626.md)を拡張すると、PT実装に、PTビジネスケースが解決する必要のない(`mint`と`deposit`)メソッドを含める必要があります。また、部分的な引き出し(`withdraw`を介して実装)がPTでは珍しいと主張することもできます。

PTは正確な秒単位で満期を迎えますが、スマートコントラクトの反応的な性質を考えると、満期を示すイベントを発行することはできません。満期後の最初のトランザクションでイベントを発行すると、不正確で高価になります。代わりに、インテグレーターは最初の`Redeem`イベントを使用するか、自分で各PTの予想満期を追跡することをお勧めします。

## 後方互換性

この EIP は [EIP-20](./eip-20.md) 仕様と完全に後方互換性があり、他の標準との既知の互換性の問題はありません。
EIP-5095を使用しないプリンシパル・トークンの本番実装については、ラッパーアダプターを開発して使用するか、ラップトークンを実装することができます。

## 参考実装

```
// SPDX-License-Identifier: MIT
pragma solidity 0.8.14;

import {ERC20} from "yield-utils-v2/contracts/token/ERC20.sol";
import {MinimalTransferHelper} from "yield-utils-v2/contracts/token/MinimalTransferHelper.sol";

contract ERC5095 is ERC20 {
    using MinimalTransferHelper for ERC20;

    /* EVENTS
     *****************************************************************************************************************/

    event Redeem(address indexed from, address indexed to, uint256 underlyingAmount);

    /* MODIFIERS
     *****************************************************************************************************************/

    /// @notice 現在のブロックタイムスタンプが満期以降であることを確認するモディファイア。
    modifier afterMaturity() virtual {
        require(block.timestamp >= maturity, "BEFORE_MATURITY");
        _;
    }

    /* IMMUTABLES
     *****************************************************************************************************************/

    ERC20 public immutable underlying;
    uint256 public immutable maturity;

    /* CONSTRUCTOR
     *****************************************************************************************************************/

    constructor(
        string memory name_,
        string memory symbol_,
        uint8 decimals_,
        ERC20 underlying_,
        uint256 maturity_
    ) ERC20(name_, symbol_, decimals_) {
        underlying = underlying_;
        maturity = maturity_;
    }

    /* CORE FUNCTIONS
     *****************************************************************************************************************/

    /// @notice 正確な量のプリンシパル・トークンを焼却し、基礎となるトークンの量を交換します。
    /// @dev これは満期前にリバートします。
    /// @param principalAmount 焼却されるプリンシパル・トークンの正確な量。
    /// @param from プリンシパル・トークンの所有者。msg.senderではない場合は、事前の承認が必要です。
    /// @param to 基礎となるトークンを送信するアドレス。
    /// @return underlyingAmount 送信された基礎となるトークンの総量。
    function redeem(
        uint256 principalAmount,
        address from,
        address to
    ) public virtual afterMaturity returns (uint256 underlyingAmount) {
        _decreaseAllowance(from, principalAmount);

        // previewRedeemでは丸め誤差が発生するため、チェックする。
        require((underlyingAmount = _previewRedeem(principalAmount)) != 0, "ZERO_ASSETS");

        _burn(from, principalAmount);

        emit Redeem(from, to, principalAmount);

        _transferOut(to, underlyingAmount);
    }

    /// @notice 正確な量の基礎となるトークンを受け取るために、計算された量のプリンシパル・トークンを焼却します。
    /// @dev これは満期前にリバートします。
    /// @param underlyingAmount 受け取る基礎となるトークンの正確な量。
    /// @param from プリンシパル・トークンの所有者。msg.senderではない場合は、事前の承認が必要です。
    /// @param to 基礎となるトークンを送信するアドレス。
    /// @return principalAmount 償還された基礎となるトークンの総量。
    function withdraw(
        uint256 underlyingAmount,
        address from,
        address to
    ) public virtual afterMaturity returns (uint256 principalAmount) {
        principalAmount = _previewWithdraw(underlyingAmount); // previewWithdrawは切り上げるので、丸め誤差チェックは不要。

        _decreaseAllowance(from, principalAmount);

        _burn(from, principalAmount);

        emit Redeem(from, to, principalAmount);

        _transferOut(to, underlyingAmount);
    }

    /// @notice 内部的で上書き可能な転送関数。
    /// @dev 転送に失敗するとリバートします。
    /// @param to 転送の受信者。
    /// @param amount 転送量。
    function _transferOut(address to, uint256 amount) internal virtual {
        underlying.safeTransfer(to, amount);
    }

    /* ACCOUNTING FUNCTIONS
     *****************************************************************************************************************/

    /// @notice 指定された量のプリンシパル・トークンと交換される基礎となるトークンの量を計算します。
    /// @dev 満期前は、満期時のように換算します。
    /// @param principalAmount 換算の対象となるプリンシパル量。
    /// @return underlyingAmount 指定のプリンシパル量に対して受け取る基礎となるトークンの総量。
    function convertToUnderlying(uint256 principalAmount) external view returns (uint256 underlyingAmount) {
        return _convertToUnderlying(principalAmount);
    }

    function _convertToUnderlying(uint256 principalAmount) internal view virtual returns (uint256 underlyingAmount) {
        return principalAmount;
    }

    /// @notice 指定された量の基礎となるトークンをプリンシパルに換算します(手数料は含まれません)。
    /// @dev 満期前は、満期時のように換算します。
    /// @param underlyingAmount 換算の対象となる基礎となるトークンの総量。
    /// @return principalAmount 指定の基礎となるトークン量を提供するために必要なプリンシパル・トークンの量。
    function convertToPrincipal(uint256 underlyingAmount) external view returns (uint256 principalAmount) {
        return _convertToPrincipal(underlyingAmount);
    }

    function _convertToPrincipal(uint256 underlyingAmount) internal view virtual returns (uint256 principalAmount) {
        return underlyingAmount;
    }

    /// @notice ユーザーが指定量のプリンシパル・トークンの償還をシミュレーションできるようにします(手数料やその他の現在のブロックの条件を含む)。
    /// @dev これは満期前にリバートします。
    /// @param principalAmount 償還されるプリンシパル・トークンの量。
    /// @return underlyingAmount 受け取る基礎となるトークンの量。
    function previewRedeem(uint256 principalAmount) external view afterMaturity returns (uint256 underlyingAmount) {
        return _previewRedeem(principalAmount);
    }

    function _previewRedeem(uint256 principalAmount) internal view virtual returns (uint256 underlyingAmount) {
        return _convertToUnderlying(principalAmount); // 手数料/スリッページを含める必要がある
    }

    /// @notice 所有者が償還できる最大のプリンシパル・トークンの量を計算します。
    /// @dev これは満期前は0を返します。
    /// @param owner 償還が計算されるアドレス。
    /// @return maxPrincipalAmount 指定の所有者が償還できる最大のプリンシパル・トークンの量。
    function maxRedeem(address owner) public view returns (uint256 maxPrincipalAmount) {
        return block.timestamp >= maturity ? _balanceOf[owner] : 0;
    }

    /// @notice ユーザーが指定量の基礎となるトークンの引き出しをシミュレーションできるようにします。
    /// @dev これは満期前にリバートします。
    /// @param underlyingAmount 引き出される基礎となるトークンの量。
    /// @return principalAmount 償還されるプリンシパル・トークンの量。
    function previewWithdraw(uint256 underlyingAmount) external view afterMaturity returns (uint256 principalAmount) {
        return _previewWithdraw(underlyingAmount);
    }

    function _previewWithdraw(uint256 underlyingAmount) internal view virtual returns (uint256 principalAmount) {
        return _convertToPrincipal(underlyingAmount); // 手数料/スリッページを含める必要がある
    }

    /// @notice 指定の所有者が引き出せる最大の基礎となるトークンの量を計算します。
    /// @dev これは満期前は0を返します。
    /// @param owner 引き出しが計算されるアドレス。
    /// @return maxUnderlyingAmount 指定の所有者が引き出せる最大の基礎となるトークンの量。
    function maxWithdraw(address owner) public view returns (uint256 maxUnderlyingAmount) {
        return _previewWithdraw(maxRedeem(owner));
    }
}

```

## セキュリティ上の考慮事項

完全に許可なしの使用例では、このEIPのインターフェースだけに準拠し、適切な保管機能を実装しないが、セカンダリ市場でプリンシパル・トークンを購入する機能を提供する悪意のある実装に陥る可能性があります。

すべてのインテグレーターは、統合する前に各実装を確認し、ユーザーの預金を失う可能性のある方法を確認することをお勧めします。

`convertToUnderlying`メソッドは表示目的に役立つ推定値であり、その文脈が示唆するような基礎となるアセットの_正確_な量を保証する必要はありません。

多くの標準と同様に、可能な限り基礎となるトークンの`decimals`を反映することを強くお勧めします。これにより、フロントエンドや他のオフチェーンユーザーの統合が簡素化されます。

## 著作権
著作権およびその関連権利は[CC0](../LICENSE.md)によって放棄されています。