---
original: 0e30fb8de7e28c12243764f9e50f3c3f4a536ad42bef0a6eb1d93f04dfd755cf
---

---
eip: 6224
title: コントラクトの依存関係レジストリ
description: スマートコントラクトとその依存関係を管理するためのレジストリ。
author: Artem Chystiakov (@arvolear)
discussions-to: https://ethereum-magicians.org/t/eip-6224-contracts-dependencies-registry/12316
status: Review
type: Standards Track
category: ERC
created: 2022-12-27
requires: 1967, 5750
---

## 概要

このEIPは、分散型プロトコルがそのスマートコントラクトを管理するために使用できるオンチェーンのレジストリシステムを導入します。

提案されるシステムは2つのコンポーネントから成り立ちます: `ContractsRegistry`と`Dependant`。`ContractsRegistry`コントラクトは、プロトコル内で使用されるすべてのスマートコントラクトの参照を保持し、必要に応じて自己管理プロキシを展開することで、それらを更新可能にします。また、`Dependant`コントラクトがその必要な依存関係を取得するためのハブとしても機能します。

## 動機

拡大し続けるイーサリアムエコシステムの中で、プロジェクトはますます複雑になっています。現代のプロトコルには、顧客のニーズに応えるために新機能を継続的に提供し、業界のペースに合わせ続けるための可搬性と機敏性が求められます。しかし、ブロックチェーンやスマートコントラクトの不変性により、この要求を満たすことは困難です。さらに、複雑性の増大と継続的な提供により、バグが発生し、コントラクト間の依存関係が絡み合い、システムの保守性が低下します。

明確な建築的ファサードを持ち、前方互換性を念頭に設計され、依存関係が透明で整理されたアプリケーションは、開発と保守が容易です。本EIPは、`ContractsRegistry`と`Dependant`という2つのスマートコントラクトを提示することで、上述の問題を解決しようとするものです。

提供されるシステムを使用することの利点は以下の通りです:

- 専用のコントラクトによるスマートコントラクトの構造化された管理。
- プロトコルの必要に応じた更新可能性の提供。
- スマートコントラクトの動的な追加、削除、置換。
- 依存関係の管理を保持するための依存性注入メカニズム。
- プロトコルを維持するためのカスタムアクセス制御ルールの設定。

## 仕様

本書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」のキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

### 概要

このシステムは2つのスマートコントラクトで構成されています:

- プロトコルのスマートコントラクトを管理および更新するシングルトンレジストリである`ContractsRegistry`。
- 依存性注入メカニズムを可能にする`Dependant`のミックスイン。

以下の図は、レジストリとその依存関係にあるコントラクトの関係を示しています:

![](../assets/eip-6224/diagram.svg)

### ContractsRegistry

`ContractsRegistry`は提案されるシステムの中心的なコントラクトです。プロトコル内で使用される個別のすべてのコントラクトの参照を保持しなければなりません(`MUST`)。`ContractRegistry`は、登録されたコントラクトの上にプロキシコントラクトを展開するように設定することができます(`MAY`)。

さらに、`ContractsRegistry`はゼロアドレスの登録を拒否しなければなりません(`MUST`)。

`ContractsRegistry`は以下のインターフェースを実装しなければなりません(`MUST`):

```solidity
pragma solidity ^0.8.0;

interface IContractsRegistry {
    /**
     * @notice コントラクトがレジストリに追加されたときに発行されるイベント
     * @param name コントラクトの名称
     * @param contractAddress 追加されたコントラクトのアドレス
     */
    event ContractAdded(string name, address contractAddress);
 
    /**
     * @notice プロキシコントラクトがレジストリに追加されたときに発行されるイベント
     * @param name コントラクトの名称
     * @param contractAddress プロキシコントラクトのアドレス
     * @param implementation 実装コントラクトのアドレス
     */
    event ProxyContractAdded(string name, address contractAddress, address implementation);
 
    /**
     * @notice レジストリを通してプロキシコントラクトが更新されたときに発行されるイベント
     * @param name コントラクトの名称
     * @param newImplementation 新しい実装コントラクトのアドレス
     */
    event ProxyContractUpgraded(string name, address newImplementation);
 
    /**
     * @notice コントラクトがレジストリから削除されたときに発行されるイベント
     * @param name 削除されたコントラクトの名称
     */
    event ContractRemoved(string name);
 
    /**
     * @notice 名称によって関連付けられたコントラクトを返す関数。
     *
     * 要求されたコントラクトが`address(0)`の場合は、必ず例外を発生させなければならない(`MUST`)。
     *
     * @param name コントラクトの名称
     * @return コントラクトのアドレス
     */
    function getContract(string memory name) external view returns (address);
 
    /**
     * @notice 指定された名称のコントラクトがレジストリに追加されているかどうかを確認する関数
     * @param name コントラクトの名称
     * @return コントラクトがレジストリに存在する場合はtrue
     */
    function hasContract(string memory name) external view returns (bool);
 
    /**
     * @notice 指定されたコントラクトに依存関係を注入する関数。
     *
     * `setDependencies()`を`address(this)`と`bytes("")`の引数で呼び出さなければならない(`MUST`)。
     *
     * @param name コントラクトの名称
     */
    function injectDependencies(string memory name) external;
 
    /**
     * @notice 指定されたコントラクトに依存関係を注入する関数。追加のデータを渡すことができる。
     *
     * `setDependencies()`を`address(this)`と`data`の引数で呼び出さなければならない(`MUST`)。
     *
     * @param name コントラクトの名称
     * @param data 依存関係コントラクトに渡される追加のコンテキストデータ
     */
    function injectDependenciesWithData(
        string memory name,
        bytes memory data
    ) external;
 
    /**
     * @notice 追加されたプロキシコントラクトを新しい実装で更新する関数。
     *
     * 実装間の互換性を確保するのは所有者の責任です。
     *
     * `ProxyContractUpgraded`イベントを発行しなければならない(`MUST`)。
     *
     * @param name プロキシコントラクトの名称
     * @param newImplementation プロキシがアップグレードされる新しい実装
     */
    function upgradeContract(string memory name, address newImplementation) external;
 
    /**
     * @notice 追加されたプロキシコントラクトを新しい実装で更新する関数。データを渡すことができる。
     *
     * 実装間の互換性を確保するのは所有者の責任です。
     *
     * `ProxyContractUpgraded`イベントを発行しなければならない(`MUST`)。
     *
     * @param name プロキシコントラクトの名称
     * @param newImplementation プロキシがアップグレードされる新しい実装
     * @param data プロキシがアップグレード後に呼び出される際のデータ。これはABIエンコーディングされた関数呼び出しになる可能性がある
     */
    function upgradeContractAndCall(
        string memory name,
        address newImplementation,
        bytes memory data
    ) external;
 
    /**
     * @notice `ContractsRegistry`が直接的な更新可能性の制御を持たないか、設計上更新可能ではないピュア(非プロキシ)のコントラクトを追加する関数。
     *
     * `ContractAdded`イベントを発行しなければならない(`MUST`)。提供されたアドレスが`address(0)`の場合は例外を発生させる
     *
     * @param name コントラクトに関連付ける名称
     * @param contractAddress 追加するコントラクトのアドレス
     */
    function addContract(string memory name, address contractAddress) external;
 
    /**
     * @notice 提供された実装の上にプロキシコントラクトをデプロイすることで、レジストリにプロキシコントラクトを追加する関数。
     *
     * この関数は、`ContractsRegistry`が更新可能にする必要があるコントラクトを追加するために使用できる。
     *
     * `ProxyContractAdded`イベントを発行しなければならない(`MUST`)。実装アドレスが`address(0)`の場合は例外を発生させる
     *
     * @param name コントラクトに関連付ける名称
     * @param contractAddress 実装をポイントするプロキシのアドレス
     */
    function addProxyContract(string memory name, address contractAddress) external;
 
    /**
     * @notice 提供された実装の上にプロキシコントラクトをデプロイすることで、レジストリにプロキシコントラクトを追加する関数。データを渡すことができる。
     *
     * この関数は、`ContractsRegistry`が更新可能にする必要があるコントラクトを追加するために使用できる。
     *
     * `ProxyContractAdded`イベントを発行しなければならない(`MUST`)。実装アドレスが`address(0)`の場合は例外を発生させる
     *
     * @param name コントラクトに関連付ける名称
     * @param contractAddress 実装のアドレス
     * @param data プロキシが呼び出される際のデータ。これはABIエンコーディングされた初期化呼び出しになる可能性がある
     */
    function addProxyContractAndCall(
        string memory name,
        address contractAddress,
        bytes memory data
    ) external;
 
    /**
     * @notice すでにデプロイされたプロキシを`ContractsRegistry`に追加する関数。新しい`ContractRegistry`に移行する際に使用できる。
     * その場合、新しいレジストリは追加された新しいプロキシを更新する権限を持っている必要がある。
     *
     * `ProxyContractAdded`イベントを発行しなければならない(`MUST`)。実装アドレスが`address(0)`の場合は例外を発生させる
     *
     * @param name コントラクトに関連付ける名称
     * @param contractAddress プロキシのアドレス
     */
    function justAddProxyContract(string memory name, address contractAddress) external;
 
    /**
     * @notice ContractsRegistryからコントラクトを削除する関数。
     *
     * `ContractRemoved`イベントを発行しなければならない(`MUST`)。コントラクトがすでに削除されている場合は例外を発生させる
     *
     * @param name コントラクトに関連付けられた名称
     */
    function removeContract(string memory name) external;
}
```

### Dependant

`ContractsRegistry`は`Dependant`コントラクトと連携して動作します。プロトコルの個別のすべてのコントラクトは、依存性注入メカニズムをサポートするために`Dependant`を継承しなければなりません(`MUST`)。

必要な依存関係は、`constructor`や`initializer`メソッドではなく、オーバーライドされた`setDependencies`メソッドで設定しなければなりません。

`setDependencies`と`setInjector`メソッドを呼び出せるのは注入者のみです。初期の注入者はゼロアドレスになりますが、その場合アクセス制御チェックで例外を発生させてはいけません(`MUST NOT`)。

`Dependant`コントラクトは以下のインターフェースを実装しなければなりません(`MUST`):

```solidity
pragma solidity ^0.8.0;

interface IDependant {
    /**
     * @notice `ContractsRegistry`から呼び出されて依存関係を注入する関数。
     *
     * コントラクトは`msg.sender`の適切なアクセスチェックを行わなければならない。呼び出しは`ContractsRegistry`からのみ可能でなければならない
     *
     * @param contractsRegistry 依存関係を取得するレジストリ
     * @param data 追加のアプリケーション固有のコンテキストを提供する可能性のあるデータ
     */
    function setDependencies(address contractsRegistry, bytes memory data) external;
 
    /**
     * @notice 新しい依存関係注入者を設定する関数。
     *
     * コントラクトは`msg.sender`の適切なアクセスチェックを行わなければならない
     *
     * @param injector 新しい依存関
係注入者
     */
    function setInjector(address injector) external;
 
    /**
     * @notice 現在の依存関係注入者を取得する関数
     * @return 現在の依存関係注入者
     */
    function getInjector() external view returns (address);
}

- `Dependant`コントラクトは、特殊なスロット`0x3d1f25f1ac447e55e7fec744471c4dab1c6a2b6ffb897825f9ea3d2e8c9be583`(
`bytes32(uint256(keccak256("eip6224.dependant.slot")) - 1)`として取得される)に依存関係注入者(通常は`ContractsRegistry`)のアドレスを保存することができる(`MAY`)。

## 根拠

明示的に指定する必要がある設計上の決定事項がいくつかあります:

### ContractsRegistryの根拠

#### コントラクト識別子

コントラクト識別子には`string`が選択されています。これは、可読性を維持し、`ContractsRegistry`とのやり取りの際の人的ミスを減らすためです。プロトコルの最上位のスマートコントラクトとして、ユーザーがブロックエクスプローラーやDAOを介して直接やり取りする可能性があるため、明確さが優先されています。

`string`識別子のため、イベントパラメータはインデックス化されません。`string indexed`パラメータは、32バイトを超える場合は`keccak256`ハッシュになります。これにより可読性が低下しますが、可読性が優先されています。

#### 例外

`getContract`ビュー関数は、要求されたコントラクトが`address(0)`の場合に例外を発生させます。これは、プロトコルの誤初期化リスクを最小限に抑えるために不可欠です。正しいコントラクトは、依存関係注入アクションの前にレジストリに追加されるべきです。

`addContract`、`addProxyContract`、`addProxyContractAndCall`、`justAddProxyContract`メソッドは、提供されたアドレスが`address(0)`の場合に例外を発生させます。同様のリスク最小化の理由からです。

### Dependantの根拠

#### 依存関係

`data`パラメータは、追加のアプリケーション固有のコンテキストを伝えるために提供されます。メソッドの動作を拡張するために使用できます。

#### 注入者

`setInjector`関数を`external`にすることで、ファクトリ作成コントラクトの依存性注入メカニズムをサポートできます。ただし、このメソッドは慎重に使用する必要があります。

## 参考実装

> 参考実装は OpenZeppelin contracts 4.9.2 に依存しています。

### ContractsRegistry実装

```solidity
pragma solidity ^0.8.0;

import {Address} from "@openzeppelin/contracts/utils/Address.sol";
import {TransparentUpgradeableProxy} from "@openzeppelin/contracts/proxy/transparent/TransparentUpgradeableProxy.sol";
import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";

import {Dependant} from "./Dependant.sol";

interface IContractsRegistry {
    event ContractAdded(string name, address contractAddress);
    event ProxyContractAdded(
        string name,
        address contractAddress,
        address implementation
    );
    event ProxyContractUpgraded(string name, address newImplementation);
    event ContractRemoved(string name);

    function getContract(string memory name) external view returns (address);

    function hasContract(string memory name) external view returns (bool);

    function injectDependencies(string memory name) external;

    function injectDependenciesWithData(string memory name, bytes memory data)
        external;

    function upgradeContract(string memory name, address newImplementation)
        external;

    function upgradeContractAndCall(
        string memory name,
        address newImplementation,
        bytes memory data
    ) external;

    function addContract(string memory name, address contractAddress) external;

    function addProxyContract(string memory name, address contractAddress)
        external;

    function addProxyContractAndCall(
        string memory name,
        address contractAddress,
        bytes memory data
    ) external;

    function justAddProxyContract(string memory name, address contractAddress)
        external;

    function removeContract(string memory name) external;
}

contract ProxyUpgrader {
    using Address for address;

    address private immutable _OWNER;

    modifier onlyOwner() {
        _onlyOwner();
        _;
    }

    constructor() {
        _OWNER = msg.sender;
    }

    function upgrade(address what_, address to_, bytes calldata data_) external onlyOwner {
        if (data_.length > 0) {
            TransparentUpgradeableProxy(payable(what_)).upgradeToAndCall(to_, data_);
        } else {
            TransparentUpgradeableProxy(payable(what_)).upgradeTo(to_);
        }
    }

    function getImplementation(address what_) external view onlyOwner returns (address) {
        // bytes4(keccak256("implementation()")) == 0x5c60da1b
        (bool success_, bytes memory returndata_) = address(what_).staticcall(hex"5c60da1b");

        require(success_, "ProxyUpgrader: not a proxy");

        return abi.decode(returndata_, (address));
    }

    function _onlyOwner() internal view {
        require(_OWNER == msg.sender, "ProxyUpgrader: not an owner");
    }
}

contract ContractsRegistry is IContractsRegistry, OwnableUpgradeable {
    ProxyUpgrader private _proxyUpgrader;

    mapping(string => address) private _contracts;
    mapping(address => bool) private _isProxy;

    function __ContractsRegistry_init() public initializer {
        _proxyUpgrader = new ProxyUpgrader();

        __Ownable_init();
    }

    function getContract(string memory name_) public view returns (address) {
        address contractAddress_ = _contracts[name_];

        require(
            contractAddress_ != address(0),
            "ContractsRegistry: this mapping doesn't exist"
        );

        return contractAddress_;
    }

    function hasContract(string memory name_) public view returns (bool) {
        return _contracts[name_] != address(0);
    }

    function getProxyUpgrader() external view returns (address) {
        return address(_proxyUpgrader);
    }

    function injectDependencies(string memory name_) public virtual onlyOwner {
        injectDependenciesWithData(name_, bytes(""));
    }

    function injectDependenciesWithData(string memory name_, bytes memory data_)
        public
        virtual
        onlyOwner
    {
        address contractAddress_ = _contracts[name_];

        require(
            contractAddress_ != address(0),
            "ContractsRegistry: this mapping doesn't exist"
        );

        Dependant dependant_ = Dependant(contractAddress_);
        dependant_.setDependencies(address(this), data_);
    }

    function upgradeContract(string memory name_, address newImplementation_)
        public
        virtual
        onlyOwner
    {
        upgradeContractAndCall(name_, newImplementation_, bytes(""));
    }

    function upgradeContractAndCall(
        string memory name_,
        address newImplementation_,
        bytes memory data_
    ) public virtual onlyOwner {
        address contractToUpgrade_ = _contracts[name_];

        require(
            contractToUpgrade_ != address(0),
            "ContractsRegistry: this mapping doesn't exist"
        );
        require(
            _isProxy[contractToUpgrade_],
            "ContractsRegistry: not a proxy contract"
        );

        _proxyUpgrader.upgrade(contractToUpgrade_, newImplementation_, data_);

        emit ProxyContractUpgraded(name_, newImplementation_);
    }

    function addContract(string memory name_, address contractAddress_)
        public
        virtual
        onlyOwner
    {
        require(
            contractAddress_ != address(0),
            "ContractsRegistry: zero address is forbidden"
        );

        _contracts[name_] = contractAddress_;

        emit ContractAdded(name_, contractAddress_);
    }

    function addProxyContract(string memory name_, address contractAddress_)
        public
        virtual
        onlyOwner
    {
        addProxyContractAndCall(name_, contractAddress_, bytes(""));
    }

    function addProxyContractAndCall(
        string memory name_,
        address contractAddress_,
        bytes memory data_
    ) public virtual onlyOwner {
        require(
            contractAddress_ != address(0),
            "ContractsRegistry: zero address is forbidden"
        );

        address proxyAddr_ = _deployProxy(
            contractAddress_,
            address(_proxyUpgrader),
            data_
        );

        _contracts[name_] = proxyAddr_;
        _isProxy[proxyAddr_] = true;

        emit ProxyContractAdded(name_, proxyAddr_, contractAddress_);
    }

    function justAddProxyContract(string memory name_, address contractAddress_)
        public
        virtual
        onlyOwner
    {
        require(
            contractAddress_ != address(0),
            "ContractsRegistry: zero address is forbidden"
        );

        _contracts[name_] = contractAddress_;
        _isProxy[contractAddress_] = true;

        emit ProxyContractAdded(
            name_,
            contractAddress_,
            _proxyUpgrader.getImplementation(contractAddress_)
        );
    }

    function removeContract(string memory name_) public virtual onlyOwner {
        address contractAddress_ = _contracts[name_];

        require(
            contractAddress_ != address(0),
            "ContractsRegistry: this mapping doesn't exist"
        );

        delete _isProxy[contractAddress_];
        delete _contracts[name_];

        emit ContractRemoved(name_);
    }

    function _deployProxy(
        address contractAddress_,
        address admin_,
        bytes memory data_
    ) internal virtual returns (address) {
        return
            address(
                new TransparentUpgradeableProxy(contractAddress_, admin_, data_)
            );
    }
}
```

### Dependant実装

```solidity
pragma solidity ^0.8.0;

interface IDependant {
    function setDependencies(address contractsRegistry, bytes memory data) external;
 
    function setInjector(address injector) external;
 
    function getInjector() external view returns (address);
}

abstract contract Dependant is IDependant {
    /**
     * @dev bytes32(uint256(keccak256("eip6224.dependant.slot")) - 1)
     */
    bytes32 private constant _INJECTOR_SLOT =
        0x3d1f25f1ac447e55e7fec744471c4dab1c6a2b6ffb897825f9ea3d2e8c9be583;

    modifier dependant() {
        _checkInjector();
        _;
        _setInjector(msg.sender);
    }

    function setDependencies(address contractsRegistry_, bytes memory data_) public virtual;

    function setInjector(address injector_) external {
        _checkInjector();
        _setInjector(injector_);
    }

    function getInjector() public view returns (address injector_) {
        bytes32 slot_ = _INJECTOR_SLOT;

        assembly {
            injector_ := sload(slot_)
        }
    }

    function _setInjector(address injector_) internal {
        bytes32 slot_ = _INJECTOR_SLOT;

        assembly {
            sstore(slot_, injector_)
        }
    }

    function _checkInjector() internal view {
        address injector_ = getInjector();

        require(injector_ == address(0) || injector_ == msg.sender, "Dependant: not an injector");
    }
}
```

## セキュリティ上の考慮事項

`ContractsRegistry`の所有者が鍵を安全な場所に保管することが非常に重要です。`ContractsRegistry`の資格情報の紛失/漏洩は、アプリケーションの回復不能な状態につながります。`ContractRegistry`はプロトコルの基幹部分であり、アクセス権限は信頼できる当事者にのみ付与されるべきです。

### ContractsRegistryのセキュリティ

- `ContractsRegistry`はプロキシアップグレード間の互換性チェックを行いません。新しい実装が古いものと互換性があることを確認するのは、ユーザーの責任です。

### Dependantのセキュリティ

- `Dependant`コントラクトは、`setDependencies`関数への最初の呼び出しが行われる前に、依存関係注入者を設定しなければなりません。つまり、最初の依存関係注入をフロントランニングすることが可能です。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。