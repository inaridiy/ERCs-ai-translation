---
original: f431a42854927b82c309060d017c5bd4f61abf8dc4679e466369e698a2d52396
---

---
eip: 165
title: 標準インターフェース検出
author: Christian Reitwießner <chris@ethereum.org>、Nick Johnson <nick@ethereum.org>、Fabian Vogelsteller <fabian@lukso.network>、Jordi Baylina <jordi@baylina.cat>、Konrad Feldmeier <konrad.feldmeier@brainbot.com>、William Entriken <github.com@phor.net>
type: Standards Track
category: ERC
status: Final
created: 2018-01-23
requires: 214
---

## 簡単な要約

スマートコントラクトが実装するインターフェースを公開および検出するための標準的な方法を作成します。

## 概要

ここでは、以下のことを標準化します:

1. インターフェースの識別方法
2. コントラクトがどのようにインターフェースの実装を公開するか
3. ERC-165 の実装を検出する方法
4. 任意のインターフェースの実装を検出する方法

## 動機

[ERC-20トークンインターフェース](./eip-20.md)などの「標準インターフェース」の場合、コントラクトがインターフェースをサポートしているかどうか、およびサポートしている場合のバージョンを確認することが時に有用です。これにより、コントラクトとの対話方法を適応させることができます。特にERC-20の場合、バージョン識別子がすでに提案されています。この提案では、インターフェースの概念を標準化し、インターフェースの識別(命名)を標準化します。

## 仕様

### インターフェースの識別方法

この標準では、*インターフェース*は[Ethereum ABIで定義されるファンクションセレクター](https://solidity.readthedocs.io/en/develop/abi-spec.html#function-selector)のセットです。これは[Solidityのインターフェースの概念](https://solidity.readthedocs.io/en/develop/abi-spec.html)およびreturn types、mutability、eventsを定義する `interface` キーワードの定義の一部分です。

インターフェース識別子を、インターフェース内のすべてのファンクションセレクターのXORとして定義します。次のコードサンプルは、インターフェース識別子の計算方法を示しています:

```solidity
pragma solidity ^0.4.20;

interface Solidity101 {
    function hello() external pure;
    function world(int) external pure;
}

contract Selector {
    function calculateSelector() public pure returns (bytes4) {
        Solidity101 i;
        return i.hello.selector ^ i.world.selector;
    }
}
```

注: インターフェースには任意のファンクションを許可しないため、インターフェースの識別には含まれません。

### コントラクトがどのようにインターフェースの実装を公開するか

ERC-165 に準拠するコントラクトは、次のインターフェース(`ERC165.sol`)を実装する必要があります:

```solidity
pragma solidity ^0.4.20;

interface ERC165 {
    /// @notice コントラクトがインターフェースを実装しているかどうかを問い合わせる
    /// @param interfaceID ERC-165で指定されたインターフェース識別子
    /// @dev インターフェースの識別はERC-165で指定されています。この関数は30,000ガス以下で実行されます。
    /// @return `interfaceID`がコントラクトで実装されている場合は`true`、`interfaceID`が0xffffffffでない場合は`false`
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```

このインターフェースの識別子は `0x01ffc9a7` です。これは `bytes4(keccak256('supportsInterface(bytes4)'));` を実行するか、上記の `Selector` コントラクトを使って計算できます。

したがって、実装コントラクトには以下のような `supportsInterface` 関数があります:

- `interfaceID` が `0x01ffc9a7` (EIP165インターフェース)の場合は `true` を返す
- `interfaceID` が `0xffffffff` の場合は `false` を返す
- このコントラクトが実装するその他の `interfaceID` の場合は `true` を返す
- その他の `interfaceID` の場合は `false` を返す

この関数はboolを返し、最大30,000ガスを使用する必要があります。

実装上の注意点として、この関数を実装する方法はいくつかの論理的な方法があります。ガス使用量に関する議論と例示的な実装を参照してください。

### ERC-165 の実装を検出する方法

1. ソースコントラクトが宛先アドレスに対して `STATICCALL` を行い、入力データ `0x01ffc9a701ffc9a700000000000000000000000000000000000000000000000000000000` とガス30,000を使用します。これは `contract.supportsInterface(0x01ffc9a7)` に相当します。
2. 呼び出しが失敗するか `false` を返す場合、宛先コントラクトはERC-165を実装していません。
3. 呼び出しが `true` を返す場合、2つ目の呼び出しを行います。入力データは `0x01ffc9a7ffffffff00000000000000000000000000000000000000000000000000000000` です。
4. 2つ目の呼び出しが失敗するか `true` を返す場合、宛先コントラクトはERC-165を実装していません。
5. それ以外の場合、宛先コントラクトはERC-165を実装しています。

### 任意のインターフェースの実装を検出する方法

1. コントラクトがERC-165を実装しているかどうかが不明な場合は、上記の手順を使って確認してください。
2. ERC-165を実装していない場合は、従来の方法でそのメソッドを確認する必要があります。
3. ERC-165を実装している場合は、単に `supportsInterface(interfaceID)` を呼び出して、使用可能なインターフェースを実装しているかどうかを確認できます。

## 根拠

可能な限り簡単な仕様にしようとしました。この実装は現在のSolidityバージョンとも互換性があります。

## 下位互換性

上記のメカニズム(`0xffffffff`を使用)は、ERC-165を実装していないほとんどのコントラクトで、それらがERC-165を実装していないことを判断するために機能するはずです。

また、[ENS](./eip-137.md)もすでにこのEIPを実装しています。

## テストケース

他のコントラクトが実装するインターフェースを検出するコントラクトです。@fulldecent と @jbaylina によるものです。

```solidity
pragma solidity ^0.4.20;

contract ERC165Query {
    bytes4 constant InvalidID = 0xffffffff;
    bytes4 constant ERC165ID = 0x01ffc9a7;

    function doesContractImplementInterface(address _contract, bytes4 _interfaceId) external view returns (bool) {
        uint256 success;
        uint256 result;

        (success, result) = noThrowCall(_contract, ERC165ID);
        if ((success==0)||(result==0)) {
            return false;
        }

        (success, result) = noThrowCall(_contract, InvalidID);
        if ((success==0)||(result!=0)) {
            return false;
        }

        (success, result) = noThrowCall(_contract, _interfaceId);
        if ((success==1)&&(result==1)) {
            return true;
        }
        return false;
    }

    function noThrowCall(address _contract, bytes4 _interfaceId) constant internal returns (uint256 success, uint256 result) {
        bytes4 erc165ID = ERC165ID;

        assembly {
                let x := mload(0x40)               // 空の記憶域の場所を見つける "free memory pointer"を使用
                mstore(x, erc165ID)                // 署名を空の記憶域の先頭に置く
                mstore(add(x, 0x04), _interfaceId) // 引数を署名の直後に置く

                success := staticcall(
                                    30000,         // 30kガス
                                    _contract,     // 宛先アドレス
                                    x,             // 入力は位置xに格納されている
                                    0x24,          // 入力は36バイト長
                                    x,             // 出力を入力の上に格納する(スペースを節約)
                                    0x20)          // 出力は32バイト長

                result := mload(x)                 // 結果をロードする
        }
    }
}
```

## 実装

このアプローチでは、`view` 関数の `supportsInterface` 実装を使用します。実行コストは入力に関係なく586ガスです。ただし、コントラクトの初期化時にはそれぞれのインターフェースを格納する必要があり(`SSTORE`は20,000ガス)、`ERC165MappingImplementation` コントラクトは汎用的で再利用可能です。

```solidity
pragma solidity ^0.4.20;

import "./ERC165.sol";

contract ERC165MappingImplementation is ERC165 {
    /// @dev 要素0xffffffffをtrueに設定してはいけません
    mapping(bytes4 => bool) internal supportedInterfaces;

    function ERC165MappingImplementation() internal {
        supportedInterfaces[this.supportsInterface.selector] = true;
    }

    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
        return supportedInterfaces[interfaceID];
    }
}

interface Simpson {
    function is2D() external returns (bool);
    function skinColor() external returns (string);
}

contract Lisa is ERC165MappingImplementation, Simpson {
    function Lisa() public {
        supportedInterfaces[this.is2D.selector ^ this.skinColor.selector] = true;
    }

    function is2D() external returns (bool){}
    function skinColor() external returns (string){}
}
```

次は `pure` 関数の `supportsInterface` 実装です。最悪のケースの実行コストは236ガスですが、サポートするインターフェースの数が増えるとリニアに増加します。

```solidity
pragma solidity ^0.4.20;

import "./ERC165.sol";

interface Simpson {
    function is2D() external returns (bool);
    function skinColor() external returns (string);
}

contract Homer is ERC165, Simpson {
    function supportsInterface(bytes4 interfaceID) external view returns (bool) {
        return
          interfaceID == this.supportsInterface.selector || // ERC165
          interfaceID == this.is2D.selector
                         ^ this.skinColor.selector; // Simpson
    }

    function is2D() external returns (bool){}
    function skinColor() external returns (string){}
}
```

サポートするインターフェースが3つ以上(ERC165自体を必須サポートインターフェースとして含む)の場合、マッピングアプローチ(すべての場合)はpureアプローチよりもガス消費が少なくなります。

## バージョン履歴
* PR 1640、2019-01-23 finalizeド -- これは、以前のnoThrowCallテストケースを36バイトではなく32バイトを使うように修正しています。以前のコードはエラーでしたが、Solidity 0.4.xでは静かに動作していましたが、Solidity 0.5.0で導入された新しい動作によって壊れていました。この変更は[#1640](https://github.com/ethereum/EIPs/pull/1640)で議論されました。

* EIP 165、2018-04-20 finalizeド -- 元の公開バージョン。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。