---
original: 7a846d9f9b1b5d6bc5f08e148be2bb90dacc3976ee827eb6b43c349bda2a25ae
---

---
eip: 137
title: Ethereum Domain Name Service - 仕様
author: Nick Johnson <arachnid@notdot.net>
status: 最終版
type: Standards Track
category: ERC
created: 2016-04-04
---

# 概要

このEIP草案は、Ethereum Name Service (ENS)の詳細を説明しています。ENSは、短くて人間に読みやすい名前をサービスやリソースの識別子にマッピングする提案されたプロトコルとABI定義です。これにより、ユーザーや開発者は人間に読みやすく覚えやすい名前を使用でき、基礎となるリソース (コントラクト、コンテンツアドレス指定データなど) が変更された場合でも、その名前を更新することができます。

ドメイン名の目的は、ネットワークリソースを指定するための安定した、人間に読みやすい識別子を提供することです。このように、ユーザーは'vitalik.wallet'や'www.mysite.swarm'などの覚えやすい文字列を入力し、適切なリソースに接続されます。名前とリソースのマッピングは時間とともに変更される可能性があるため、ユーザーがウォレットを変更したり、ウェブサイトがホストを変更したり、Swarmドキュメントが新しいバージョンに更新されたりしても、ドメイン名は変更されません。さらに、ドメインは単一のリソースを指定する必要はありません。異なるレコードタイプにより、同じドメインが異なるリソースを参照できます。たとえば、ブラウザーは'mysite.swarm'のAレコード (アドレス) を取得してサーバーのIPアドレスを解決できますが、メールクライアントは同じアドレスのMXレコード (メールエクスチェンジャー) を取得してメールサーバーを解決できます。

# 動機

Ethereumにおける名前解決の[仕様](https://github.com/ethereum/wiki/wiki/Registrar-ABI)と[実装](https://ethereum.gitbooks.io/frontier-guide/content/registrar_services.html)は基本的な機能を提供していますが、長期的な有用性を大幅に制限する次のような欠点があります:
- すべての名前に対する単一のグローバル名前空間と単一の「中央集権的」リゾルバー。
- 委任とサブ名/サブドメインのサポートが限定的または皆無。
- 1つのレコードタイプしかなく、ドメインに複数のレコードを関連付けるサポートがない。
- 単一のグローバル実装のため、複数の異なる名前割り当てシステムのサポートがない。
- 責任の混同: 名前解決、登録、whois情報。

これらの機能が可能になれば、次のようなユースケースが実現できます:
- サブ名/サブドメインのサポート - 例: live.mysite.tld、forum.mysite.tld。
- Swarmでホストされたdapp、Whisperアドレス、メールサーバーなど、単一の名前の下で複数のサービス。
- DNSレコードタイプのサポート。これにより、ブロックチェーン上のホスティングの「レガシー」名を、Ethereum クライアントのようなものが解決できるようになります。
- DNSゲートウェイ。ENSドメインをDomain Name Serviceに公開し、レガシークライアントがブロックチェーンサービスを解決して接続できるようにする。

特に最初の2つのユースケースは、現在のインターネットのDNSでよく見られるものであり、Ethereumプラットフォームの発展と成熟に伴って引き続き有用であると考えられます。

この文書の規範的な部分では、提案されるシステムの実装を指定していません。その目的は、名前解決の一貫性を促進するために、異なるリゾルバー実装が準拠できるプロトコルを文書化することです。付録では、リゾルバーコントラクトとライブラリの参考実装を提供していますが、これらは例示的なものとして扱う必要があります。

また、このドキュメントでは、ドメインの登録や更新、ドメインの所有者を見つける方法を指定することも試みていません。登録はレジストラーの責任であり、トップレベルドメインによって必然的に異なるガバナンス問題です。

ドメインレコードの更新も、解決とは別に処理できます。Swarmなどのシステムでは、ドメインを更新するための明確なインターフェイスが必要になる可能性があり、その場合はこれに対する標準の開発が予想されます。

# 仕様
## 概要

ENSシステムは3つの主要な部分で構成されています:
- ENSレジストリ
- リゾルバー
- レジストラー

レジストリは、登録された任意の名前とその名前の担当リゾルバーのアドレスのマッピングを提供する単一のコントラクトです。また、名前の所有者がリゾルバーアドレスを設定したり、親ドメインとは異なる所有者を持つサブドメインを作成したりすることができます。

リゾルバーは、名前に対するリソースの検索を担当します - 例えば、コントラクトアドレス、コンテンツハッシュ、IPアドレスなどを適切に返します。リゾルバー仕様 (ここで定義され、他のEIPで拡張される) は、リゾルバーが実装できる関数を定義しています。

レジストラーは、ユーザーにドメイン名を割り当てる責任があり、ENSレジストリ内のノードの所有者です。レジストラーはコントラクトまたは外部所有アカウントである可能性がありますが、少なくともルートおよびトップレベルのレジストラーはコントラクトで実装されると予想されます。

ENSでの名前解決は2段階のプロセスです。まず、解決する名前をハッシュ化して、ENSレジストリに問い合わせます。レコードが存在する場合、レジストリはそのリゾルバーのアドレスを返します。次に、リクエストされたリソースに適したメソッドを使ってリゾルバーを呼び出します。リゾルバーはその後、目的の結果を返します。

例えば、'beercoin.eth'に関連付けられたトークンコントラクトのアドレスを見つけたい場合は、まず次のようにしてリゾルバーを取得します:

```javascript
var node = namehash("beercoin.eth");
var resolver = ens.resolver(node);
```

次に、リゾルバーにアドレスを問い合わせます:

```javascript
var address = resolver.addr(node);
```

`namehash`手順は名前自体にのみ依存するため、事前に計算してコントラクトに挿入できます。これにより、生の名前の構成要素の数に関係なく、O(1)でENSレコードを検索できます。

## 名前の構文

ENS名は次の構文に準拠する必要があります:

<pre>&lt;domain> ::= &lt;label> | &lt;domain> "." &lt;label>
&lt;label> ::= [UTS46](https://unicode.org/reports/tr46/)に準拠する有効な文字列ラベル
</pre>

要するに、名前は「.」で区切られたラベルのシーケンスで構成されます。各ラベルは[UTS46](https://unicode.org/reports/tr46/)で説明されている正規化されたラベルでなければなりません。ここでは`transitional=false`と`useSTD3AsciiRules=true`のオプションを使用します。Javascriptの実装では、名前を正規化およびチェックする[ライブラリ](https://www.npmjs.com/package/idna-uts46)が利用可能です。

大文字と小文字は名前に使用できますが、UTS46の正規化プロセスによってラベルが大文字小文字を無視して正規化されるため、スペルが同じで大文字小文字が異なる2つの名前は同じnamehashを生成します。

ラベルとドメインの長さに制限はありませんが、レガシーDNSとの互換性のために、ラベルは64文字以内、完全なENS名は255文字以内に制限することをお勧めします。同じ理由から、ラベルが先頭または末尾にハイフンを含まず、先頭に数字を含まないことをお勧めします。

## namehash アルゴリズム

ENSで使用される前に、名前は'namehash'アルゴリズムでハッシュ化されます。このアルゴリズムは名前の各コンポーネントを再帰的にハッシュし、任意の有効な入力ドメインに対して一意の固定長の文字列を生成します。namehashの出力は'ノード'と呼ばれます。

namehashアルゴリズムのPseudocodeは次のとおりです:

```
def namehash(name):
  if name == '':
    return '\0' * 32
  else:
    label, _, remainder = name.partition('.')
    return sha3(namehash(remainder) + sha3(label))
```

簡単に説明すると、名前はラベルに分割され、各ラベルがハッシュされます。その後、最後のコンポーネントから始めて、前の出力にラベルハッシュを連結し、再度ハッシュします。最初のコンポーネントは32個の'0'バイトと連結されます。したがって、'mysite.swarm'は次のように処理されます:

```
node = '\0' * 32
node = sha3(node + sha3('swarm'))
node = sha3(node + sha3('mysite'))
```

実装は次のnamehashのテストベクトルに準拠する必要があります:

    namehash('') = 0x0000000000000000000000000000000000000000000000000000000000000000
    namehash('eth') = 0x93cdeb708b7545dc668eb9280176169d1c33cfd8ed6f04690a0bcc88a93fc4ae
    namehash('foo.eth') = 0xde9b09fd7c5f901e23a3f19fecc54828e9c848539801e86591bd9801b019f84f

## レジストリ仕様

ENSレジストリコントラクトは次の関数を公開しています:

```solidity
function owner(bytes32 node) constant returns (address);
```

指定されたノードの所有者 (レジストラー) を返します。

```solidity
function resolver(bytes32 node) constant returns (address);
```

指定されたノードのリゾルバーを返します。

```solidity
function ttl(bytes32 node) constant returns (uint64);
```

ノードのTTL (Time-To-Live) を返します。つまり、ノードの情報がキャッシュされる最大期間です。

```solidity
function setOwner(bytes32 node, address owner);
```

ノードの所有権を別のレジストラーに移転します。この関数は`node`の現在の所有者のみが呼び出せます。この関数の呼び出しが成功すると、`Transfer(bytes32 indexed, address)`イベントがログに記録されます。

```solidity
function setSubnodeOwner(bytes32 node, bytes32 label, address owner);
```

新しいノード`sha3(node, label)`を作成し、その所有者を`owner`に設定します。または、既存のノードの所有者を更新します。この関数は`node`の現在の所有者のみが呼び出せます。この関数の呼び出しが成功すると、`NewOwner(bytes32 indexed, bytes32 indexed, address)`イベントがログに記録されます。

```solidity
function setResolver(bytes32 node, address resolver);
```

`node`のリゾルバーアドレスを設定します。この関数は`node`の所有者のみが呼び出せます。この関数の呼び出しが成功すると、`NewResolver(bytes32 indexed, address)`イベントがログに記録されます。

```solidity
function setTTL(bytes32 node, uint64 ttl);
```

ノードのTTLを設定します。ノードのTTLは、レジストリ内の'owner'と'resolver'レコードに適用され、関連するリゾルバーによって返される情報にも適用されます。

## リゾルバー仕様

リゾルバーは、ここで指定されたレコードタイプのサブセットを実装できます。レコードタイプの仕様がリゾルバーに複数の関数を提供することを要求する場合、リゾルバーはそれらすべてまたはどれも実装しなければなりません。リゾルバーはスローする fallback 関数を指定しなければなりません。

リゾルバーには1つの必須関数があります:

```solidity
function supportsInterface(bytes4 interfaceID) constant returns (bool)
```

`supportsInterface`関数は[EIP-165](./eip-165.md)で文書化されており、提供された4バイトの識別子で指定されたインターフェイスを実装している場合にtrueを返します。インターフェイス識別子は、そのインターフェイスが提供する関数のシグネチャハッシュのXORです。単一関数
インターフェイスの場合は、単純にその関数のシグネチャハッシュと等しくなります。リゾルバーが`supportsInterface()`に対してtrueを返す場合、そのインターフェイスで指定された関数を実装しなければなりません。

`supportsInterface`は常に`0x01ffc9a7`(つまり`supportsInterface`自体の関数シグネチャハッシュ)に対してtrueを返さなければなりません。

現在標準化されているリゾルバーインターフェイスは次の表に示されています。

以下のインターフェイスが定義されています:

| インターフェイス名 | インターフェイスハッシュ | 仕様 |
| --- | --- | --- |
| `addr` | 0x3b3b57de | [コントラクトアドレス](#addr) |
| `name`      | 0x691f3431   | #181    |
| `ABI`       | 0x2203ab56   | #205    |
| `pubkey`    | 0xc8690233   | #619    |

EIPでは、このレジストリに追加する新しいインターフェイスを定義できます。

### <a name="addr"></a>コントラクトアドレスインターフェイス

コントラクトアドレスリソースをサポートするリゾルバーは、次の関数を提供する必要があります:

```solidity
function addr(bytes32 node) constant returns (address);
```

リゾルバーが`addr`検索をサポートしているが、要求されたノードにaddrレコードがない場合、リゾルバーはゼロアドレスを返さなければなりません。

`addr`レコードを解決するクライアントは、ゼロ返値をチェックし、名前にリゾルバーが指定されていない場合と同様に扱わなければなりません - つまり、アドレスに資金を送信したり、アドレスと対話したりすることを拒否しなければなりません。これを行わないと、ユーザーが誤ってゼロアドレスに資金を送信する可能性があります。

アドレスの変更は次のイベントをトリガーする必要があります:

```solidity
event AddrChanged(bytes32 indexed node, address a);
```

# 付録A: レジストリ実装

```solidity
contract ENS {
    struct Record {
        address owner;
        address resolver;
        uint64 ttl;
    }

    mapping(bytes32=>Record) records;

    event NewOwner(bytes32 indexed node, bytes32 indexed label, address owner);
    event Transfer(bytes32 indexed node, address owner);
    event NewResolver(bytes32 indexed node, address resolver);

    modifier only_owner(bytes32 node) {
        if(records[node].owner != msg.sender) throw;
        _
    }

    function ENS(address owner) {
        records[0].owner = owner;
    }

    function owner(bytes32 node) constant returns (address) {
        return records[node].owner;
    }

    function resolver(bytes32 node) constant returns (address) {
        return records[node].resolver;
    }

    function ttl(bytes32 node) constant returns (uint64) {
        return records[node].ttl;
    }

    function setOwner(bytes32 node, address owner) only_owner(node) {
        Transfer(node, owner);
        records[node].owner = owner;
    }

    function setSubnodeOwner(bytes32 node, bytes32 label, address owner) only_owner(node) {
        var subnode = sha3(node, label);
        NewOwner(node, label, owner);
        records[subnode].owner = owner;
    }

    function setResolver(bytes32 node, address resolver) only_owner(node) {
        NewResolver(node, resolver);
        records[node].resolver = resolver;
    }

    function setTTL(bytes32 node, uint64 ttl) only_owner(node) {
        NewTTL(node, ttl);
        records[node].ttl = ttl;
    }
}
```

# 付録B: サンプルリゾルバー実装
### 組み込みリゾルバー

最も単純なリゾルバーは、コントラクトアドレスリソースプロファイルを実装することで自身の名前リゾルバーとして機能するコントラクトです:

```solidity
contract DoSomethingUseful {
    // その他のコード

    function addr(bytes32 node) constant returns (address) {
        return this;
    }

    function supportsInterface(bytes4 interfaceID) constant returns (bool) {
        return interfaceID == 0x3b3b57de || interfaceID == 0x01ffc9a7;
    }

    function() {
        throw;
    }
}
```

このようなコントラクトをENSレジストリに直接挿入することで、単純な使用例でリゾルバーコントラクトを別途用意する必要がなくなります。ただし、未知の関数呼び出しに対して'throw'する要件は、一部のタイプのコントラクトの通常の動作を妨害する可能性があります。

### スタンドアローンリゾルバー

コントラクトアドレスプロファイルを実装し、所有者のみがレコードを更新できるようにする基本的なリゾルバー:

```solidity
contract Resolver {
    event AddrChanged(bytes32 indexed node, address a);

    address owner;
    mapping(bytes32=>address) addresses;

    modifier only_owner() {
        if(msg.sender != owner) throw;
        _
    }

    function Resolver() {
        owner = msg.sender;
    }

    function addr(bytes32 node) constant returns(address) {
        return addresses[node];    
    }

    function setAddr(bytes32 node, address addr) only_owner {
        addresses[node] = addr;
        AddrChanged(node, addr);
    }

    function supportsInterface(bytes4 interfaceID) constant returns (bool) {
        return interfaceID == 0x3b3b57de || interfaceID == 0x01ffc9a7;
    }

    function() {
        throw;
    }
}
```

このコントラクトをデプロイした後、ENSレジストリを更新してこのコントラクトを名前のリゾルバーとして参照し、同じノードに対して`setAddr()`を呼び出してそのコントラクトアドレスを設定します。

### パブリックリゾルバー

上記のリゾルバーと似ていますが、コントラクトアドレスプロファイルのみをサポートし、ENSレジストリを使用してエントリを更新できるユーザーを決定します:

```solidity
contract PublicResolver {
    event AddrChanged(bytes32 indexed node, address a);
    event ContentChanged(bytes32 indexed node, bytes32 hash);

    ENS ens;
    mapping(bytes32=>address) addresses;

    modifier only_owner(bytes32 node) {
        if(ens.owner(node) != msg.sender) throw;
        _
    }

    function PublicResolver(address ensAddr) {
        ens = ENS(ensAddr);
    }

    function addr(bytes32 node) constant returns (address ret) {
        ret = addresses[node];
    }

    function setAddr(bytes32 node, address addr) only_owner(node) {
        addresses[node] = addr;
        AddrChanged(node, addr);
    }

    function supportsInterface(bytes4 interfaceID) constant returns (bool) {
        return interfaceID == 0x3b3b57de || interfaceID == 0x01ffc9a7;
    }

    function() {
        throw;
    }
}
```

# 付録C: サンプルレジストラー実装

このレジストラーでは、ユーザーが最初に要求した場合、無料でドメイン名を登録できます。

```solidity
contract FIFSRegistrar {
    ENS ens;
    bytes32 rootNode;

    function FIFSRegistrar(address ensAddr, bytes32 node) {
        ens = ENS(ensAddr);
        rootNode = node;
    }

    function register(bytes32 subnode, address owner) {
        var node = sha3(rootNode, subnode);
        var currentOwner = ens.owner(node);
        if(currentOwner != 0 && currentOwner != msg.sender)
            throw;

        ens.setSubnodeOwner(rootNode, subnode, owner);
    }
}
```