---
original: 315a9adb3d0b441b54ef16af31cd48aa1e35ce4824d5d187dc84df2dabd704db
---

---
eip: 5131
title: ENSのためのSAFE認証
description: ENSのテキストレコードを使用して、より安全で便利な署名操作を実現する。
author: Wilkins Chung (@wwhchung) <wilkins@manifold.xyz>、Jalil Wahdatehagh (@jwahdatehagh)、Cry (@crydoteth)、Sillytuna (@sillytuna)、Cyberpnk (@CyberpnkWin)
discussions-to: https://ethereum-magicians.org/t/eip-5131-ens-subdomain-authentication/9458
status: 停滞
type: Standards Track
category: ERC
created: 2022-06-03
requires: 137, 181, 634
---

## 概要
このEIPは、Ethereum Name Service仕様([EIP-137](./eip-137.md))を介して1つ以上の署名ウォレットをリンクし、メインウォレットの管理と資産所有権を証明します。

## 動機
Ethereum生態系の第三者アプリケーションに資産の所有権を証明することは一般的です。ユーザーは、ある操作を実行する前に自分を認証するためにデータのペイロードに署名することが多いです。しかし、この方法は - メインウォレットへのroot権限を与えるようなものです - 安全性が低く、利便性も低いです。

***例:***
 1. OpenSeaでプロフィールを編集するには、ウォレットで署名する必要があります。
 2. NFTゲートコンテンツにアクセスするには、NFTを所有するウォレットで署名する必要があります。
 3. イベントにアクセスするには、必要なNFTを所有するウォレットで署名する必要があります。
 4. エアドロップを受け取るには、適格なウォレットでスマートコントラクトと対話する必要があります。
 5. NFTの所有権を証明するには、そのNFTを所有するウォレットで署名する必要があります。

上記の例すべてにおいて、ユーザーはウォレット自体を使ってdAppやスマートコントラクトと対話しています。これは
 - 不便(ハードウェアウォレットやマルチシグで管理されている場合)
 - 安全ではない(上記の操作は読み取り専用ですが、書き込みアクセス権を持つウォレットで署名/対話するため)

代わりに、特定のウォレットの代わりに認証するためのウォレットを複数承認できるようにすべきです。

### 既存の方法と解決策の問題点
残念ながら、ユーザーが悪意のあるペイロードに署名してしまった事例が多数ありました。その結果、ほとんどの場合、署名アドレスに関連する資産が大幅に失われています。

さらに、多くのユーザーは資産の大部分を「コールドストレージ」に保管しています。「コールドストレージ」ソリューションによる高いセキュリティにより、通常、アクセシビリティが低下します。

一部のソリューションでは、リンクを作成するための専用のレジストリスマートコントラクトを提案したり、新しいプロトコルをサポートしたりしています。これは採用の観点から問題があり、それらの標準化もされていません。

### 提案: Ethereum Name Service (EIP-137)の使用
「車輪の再発明」をするのではなく、この提案では、広く採用されているEthereum Name Serviceと、ENSのテキストレコード機能([EIP-634](./eip-634.md))を組み合わせて、より安全で便利な署名と認証、およびメインウォレットへの「読み取り専用」アクセスを実現することを目的としています。

その結果、2つの利点があります。このEIPにより、ユーザーは、頻繁に署名操作に使用されない(コールドウォレット)ウォレットのセキュリティ性を維持しつつ、より頻繁に使用される(ホットウォレット)ウォレットに潜在的な悪意のある署名操作を委託することで、セキュリティが向上します。

#### dAppとの対話のセキュリティの向上
多くのdAppでは、ウォレットの管理権限を証明してアクセスを得る必要があります。現時点では、ウォレット自体を使ってdAppと対話する必要があります。これはセキュリティ上の問題があり、悪意のあるdAppやフィッシングサイトが、悪意のあるペイロードに署名させることで、ウォレットの資産を危険にさらす可能性があります。

ただし、この危険は、これらの対話に2次ウォレットを使用することで軽減できます。悪意のある対話は、ほとんど価値のない2次ウォレットの資産に限定されるため、被害は最小限に抑えられます。

#### 複数デバイスアクセスのセキュリティの向上
ハードウェアウォレット以外のウォレットを複数のデバイスで使用するには、各デバイスにシードフレーズをインポートする必要があります。新しいデバイスにシードフレーズを入力するたびに、そのデバイスがシードフレーズを知ることになるため、ウォレットが危険にさらされるリスクが高まります。

代わりに、各デバイスに固有のウォレットを持ち、それがメインウォレルトの承認された2次ウォレットになるようにできます。デバイス固有のウォレットが危険にさらされたり失われた場合は、認証権限を取り消すことができます。

さらに、ウォレット認証をチェーン化して、2次ウォレットが自身の3次ウォレットを1つ以上承認できるようにすることで、チームメンバーそれぞれに独自のサイナーを持たせつつ、メインウォレットが根元のステムの権限を取り消すだけで、全体のツリーを無効化できるようにすることができます。

#### 利便性の向上
多くの個人がハードウェアウォレットを最大のセキュリティのために使用しています。しかし、これは非常に不便で、ハードウェアウォレットを常に携帯したくないことが多いです。

代わりに、認証アクティビティ(モバイルデバイスなど)に非ハードウェアウォレットを承認した場合、ハードウェアウォレットを持ち歩く必要なく、ほとんどのdAppを使用できるようになります。

## 仕様
この文書の中の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

以下の用語を使用します:
 - `mainAddress`は、認証または資産所有権を証明しようとしているウォレットアドレスを表します。
 - `mainENS`は、`mainAddress`の逆引きENS文字列を表します。
 - `authAddress`は、`mainAddress`の代わりに署名に使用したいアドレスを表します。
 - `authENS`は、`authAddress`の逆引きENS文字列を表します。
 - `authKey`は、`[0-9A-Za-z]+`の形式の文字列を表します。

`mainAddress`の管理権限と`mainAddress`資産の所有権が`authAddress`によって証明されるのは、以下の条件がすべて満たされる場合です:
 - `mainAddress`にENSレゾルバーレコードと逆引きレコードが設定されている。
 - `authAddress`にENSレゾルバーレコードと逆引きレコードが設定されている。
 - `authENS`に`eip5131:vault`というフォーマット`<authKey>:<mainAddress>`のENSテキストレコードがある。
 - `mainENS`に`eip5131:<authKey>`というENSテキストレコードがある。

### 単一のENSドメインに1つ以上の`authAddress`レコードを設定する
`mainAddress`にはENSレゾルバーレコードと逆引きレコードを設定する必要があります。
リンクされたアカウントを自動的に検出するために、`authAddress`にもENSレゾルバーレコードと逆引きレコードを設定することが望ましいです。

1. 未使用の`<authKey>`を選択します。これは`[0-0A-Za-z]+`の形式の任意の文字列です。
2. `mainENS`に`eip5131:<authKey>`というテキストレコードを設定し、値を希望する`authAddress`に設定します。
3. `authENS`に`eip5131:vault`というテキストレコードを設定し、値を`<authKey>:mainAddress`に設定します。

現時点では、このEIPでは`authAddress`エントリの上限を強制していません。ユーザーは好きなだけアドレスを繰り返して設定できます。

### `authAddress`を使って`mainAddress`を認証する
`mainAddress`の管理権限と`mainAddress`資産の所有権は、関連する`authAddress`が`msg.sender`であるか、メッセージに署名している場合に証明されます。

実際には、以下の操作を行うことで機能します:
1. `authENS`のレゾルバーを取得する
2. `authENS`の`eip5131:vault`テキストレコードを取得する
3. `<authKey>:<mainAddress>`を解析して、`authKey`と`mainAddress`を特定する
4. `mainAddress`の逆引きENSレコードを必ず取得し、それが`<mainENS>`と一致することを確認する
    - そうでないと、`mainAddress`を指すその他のENSノード(認証付き)を設定して、それらを介して認証できてしまう
5. `mainENS`の`eip5131:<authKey>`テキストレコードを取得し、それが`authAddress`と一致することを確認する

この仕様では、コントラクトレベルとクライアント/サーバー側の両方でシグネチャの検証が可能です。スマートコントラクトに限定されるわけではないため、外部インターフェイスの定義は提案されていません。

### `authAddress`の取り消し
`authAddress`の権限を取り消すには、`mainENS`の`eip5131:<authKey>`テキストレコードを削除するか、新しい`authAddress`を指すように更新します。

## 根拠

### EIP-137の使用
提案された仕様では、別のレジストリパラダイムを導入するのではなく、EIP-137とENSを使用しています。これは、EIP-137とENSがすでに広く採用されているためです。

ただし、EIP-137の欠点は、リンクされた`authAddress`にETHが必要であり、`authENS`の逆引きレコードと`eip5131:vault`テキストレコードを設定できることです。これは、`mainAddress`が`authAddress`の署名付きメッセージでリバースレコードとテキストレコードを設定できる別のリバースルックアップレジストリによって解決できます。

L2とENSのL2機能の登場により、異なるチェーン上で管理されるドメインでも、オフチェーンでリンクされたアドレスを検証することができます。

### 1対多の認証関係
この提案仕様では、1(`mainAddress`)対多(`authAddress`)の認証関係を許可しています。つまり、1つの`mainAddress`が複数の`authAddress`を認証できますが、`authAddress`は自身または単一の`mainAddress`しか認証できません。

この設計の理由は、クライアントとスマートコントラクトのコードでの認証の簡単さにあります。`authAddress`が何の`mainAddress`に署名しているかを判断できるため、ユーザー入力は不要です。

さらに、ユーザーが認証しようとしている資産に応じて適切なアドレスを使用するように、UXを設計できます。`authAddress`と`mainAddress`が所有する資産を表示し、使用するアドレスを決定できます。

## 参考実装

### クライアント/サーバー側
TypeScriptでは、ethers.jsを使った検証関数は以下のようになります:
```
export interface LinkedAddress {
  ens: string,
  address: string,
}

export async function getLinkedAddress(
  provider: ethers.providers.EnsProvider, address: string
): Promise<LinkedAddress | null> {
  const addressENS = await provider.lookupAddress(address);
  if (!addressENS) return null;

  const vaultInfo = await (await provider.getResolver(addressENS))?.getText('eip5131:vault');
  if (!vaultInfo) return null;

  const vaultInfoArray = vaultInfo.split(':');
  if (vaultInfoArray.length !== 2) {
    throw new Error('EIP5131: Authkeyとvaultアドレスが正しく設定されていません。');
  }

  const [ authKey, vaultAddress ] = vaultInfoArray;

  const vaultENS = await provider.lookupAddress(vaultAddress);
  if (!vaultENS) {
    throw new Error(`EIP5131: vaultのENSドメインが逆引きレコードを持っていません。`);
  };

  const expectedSigningAddress = await (
    await provider.getResolver(vaultEN
S)
  )?.getText(`eip5131:${authKey}`);

  if (expectedSigningAddress?.toLowerCase() !== address.toLowerCase()) {
    throw new Error(`EIP5131: 認証が一致しません。`);
  };

  return {
    ens: vaultENS,
    address: vaultAddress
  };
}
```

### コントラクト側

#### バックエンド有り
アプリケーションがセキュアなバックエンドサーバーを運営している場合、上記のクライアント/サーバーコードを実行し、その結果を[EIP-1271](./eip-1271.md)のような仕様と組み合わせて、メインアドレスの署名者が本当に認証されていることを安価かつ安全に検証できます。

#### バックエンド無し (JavaScriptのみ)
メインアドレスの認証リンクを持つメッセージ送信者を検証するための内部関数の参考実装を以下に示します。

```
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

/// @author: manifold.xyz

/**
 * ENSレジストリインターフェイス
 */
interface ENS {
    function resolver(bytes32 node) external view returns (address);
}

/**
 * ENSレゾルバーインターフェイス
 */
interface Resolver {
    function addr(bytes32 node) external view returns (address);
    function name(bytes32 node) external view returns (string memory);
    function text(bytes32 node, string calldata key) external view returns (string memory);
}

/**
 * 署名アドレスがリンクされたアドレスに関連付けられていることを検証する
 */
library LinkedAddress {
    /**
     * メッセージ送信者がmainAddressの認証アドレスであることを検証する
     *
     * @param ensRegistry    ENSレジストリのアドレス
     * @param mainAddress     認証したいメインアドレス
     * @param mainENSNodeHash メインENSノードハッシュ
     * @param authKey         検証に使用するauthKeyのテキストレコード
     * @param authENSNodeHash 認証ENSノードハッシュ
     */
    function validateSender(
        address ensRegistry,
        address mainAddress,
        bytes32 mainENSNodeHash,
        string calldata authKey,
        bytes32 authENSNodeHash
    ) internal view returns (bool) {
        return validate(ensRegistry, mainAddress, mainENSNodeHash, authKey, msg.sender, authENSNodeHash);
    }

    /**
     * authAddressがmainAddressの認証アドレスであることを検証する
     *
     * @param ensRegistry     ENSレジストリのアドレス
     * @param mainAddress     認証したいメインアドレス
     * @param mainENSNodeHash メインENSノードハッシュ
     * @param authAddress     認証ウォレットのアドレス
     * @param authENSNodeHash 認証ENSノードハッシュ
     */
    function validate(
        address ensRegistry,
        address mainAddress,
        bytes32 mainENSNodeHash,
        string calldata authKey,
        address authAddress,
        bytes32 authENSNodeHash
    ) internal view returns (bool) {
        _verifyMainENS(ensRegistry, mainAddress, mainENSNodeHash, authKey, authAddress);
        _verifyAuthENS(ensRegistry, mainAddress, authKey, authAddress, authENSNodeHash);

        return true;
    }

    // *********************
    //   ヘルパー関数
    // *********************
    function _verifyMainENS(
        address ensRegistry,
        address mainAddress,
        bytes32 mainENSNodeHash,
        string calldata authKey,
        address authAddress
    ) private view {
        // 提供されたアドレスにENSノードが正しく解決されることを確認する
        address mainResolver = ENS(ensRegistry).resolver(mainENSNodeHash);
        require(mainResolver != address(0), "メインENSが登録されていません");
        require(mainAddress == Resolver(mainResolver).addr(mainENSNodeHash), "メインアドレスが正しくありません");

        // mainENSによって authAddressに設定されているauthKeyテキストレコードを検証する
        string memory authText = Resolver(mainResolver).text(mainENSNodeHash, string(abi.encodePacked("eip5131:", authKey)));
        require(
            keccak256(bytes(authText)) == keccak256(bytes(_addressToString(authAddress))),
            "無効な認証アドレス"
        );
    }

    function _verifyAuthENS(
        address ensRegistry,
        address mainAddress,
        string memory authKey,
        address authAddress,
        bytes32 authENSNodeHash
    ) private view {
        // 提供されたアドレスにENSノードが正しく解決されることを確認する
        address authResolver = ENS(ensRegistry).resolver(authENSNodeHash);
        require(authResolver != address(0), "認証ENSが登録されていません");
        require(authAddress == Resolver(authResolver).addr(authENSNodeHash), "認証アドレスが正しくありません");

        // authENSによって適切に設定されているテキストレコードを検証する
        string memory vaultText = Resolver(authResolver).text(authENSNodeHash, "eip5131:vault");
        require(
            keccak256(abi.encodePacked(authKey, ":", _addressToString(mainAddress))) ==
                keccak256(bytes(vaultText)),
            "無効な認証テキストレコード"
        );
    }

    bytes16 private constant _HEX_SYMBOLS = "0123456789abcdef";

    function sha3HexAddress(address addr) private pure returns (bytes32 ret) {
        uint256 value = uint256(uint160(addr));
        bytes memory buffer = new bytes(40);
        for (uint256 i = 39; i > 1; --i) {
            buffer[i] = _HEX_SYMBOLS[value & 0xf];
            value >>= 4;
        }
        return keccak256(buffer);
    }

    function _addressToString(address addr) private pure returns (string memory ptr) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            ptr := mload(0x40)

            // メモリポインタを調整し、32バイトアラインを維持する
            // 文字列長を格納する32バイト; アドレスは42バイト長
            mstore(0x40, add(ptr, 96))

            // (文字列長, '0', 'x')を格納 (42, 48, 120)
            // 32バイトの境界をまたいで1回の書き込み
            ptr := add(ptr, 2)
            mstore(ptr, 0x2a3078)

            // 文字列を逆順に書き込む
            for {
                // 'x'が終端、ptrはlsb文字
                let end := add(ptr, 31)
                ptr := add(ptr, 71)
            } gt(ptr, end) {
                ptr := sub(ptr, 1)
                addr := shr(4, addr)
            } {
                let v := and(addr, 0xf)
                // > 9の場合、ascii 'a-f'を使う(条件分岐不要)
                v := add(v, mul(gt(v, 9), 39))
                // '0'のasciiを追加
                v := add(v, 48)
                mstore8(ptr, v)
            }

            // 長さを指すポインタを返す (32 + 2 for '0x' - 1)
            ptr := sub(ptr, 33)
        }

        return string(ptr);
    }
}
```

## セキュリティ上の考慮事項
このEIPの主な目的は、セキュリティを強化し、メインウォレットが必要ない場合や、メインウォレットの資産を移動する必要がない場合に、ウォレットの管理権限と資産の所有権を安全に認証する方法を促進することです。「読み取り専用」の認証を行うための方法と考えてください。

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)により放棄されています。