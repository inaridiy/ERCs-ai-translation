---
original: 8aa92c5686a551f811f9514c12395e5ed35800b1afd89873afb4e2d3d744a00f
---

---
eip: 5727
title: 半ファンジブル・ソウルバウンド・トークン
description: アイデンティティ、資格、所属、評判を表すことができる、譲渡不可能なトークン(ソウルバウンド・トークン)のためのインターフェース。
author: Austin Zhu (@AustinZhu), Terry Chen <terry.chen@phaneroz.io>
discussions-to: https://ethereum-magicians.org/t/eip-5727-semi-fungible-soulbound-token/11086
status: Draft
type: Standards Track
category: ERC
created: 2022-09-28
requires: 165, 712, 721, 3525, 4906, 5192, 5484
---

## 概要

ソウルバウンド・トークン(SBT)のインターフェースです。SBTは個人のアイデンティティ、資格、所属、評判を表す譲渡不可能なトークンです。

このインターフェースは、ファンジブルとノンファンジブルのトークンを組み合わせて管理する方法を提供します。ソウルバウンド・トークンのライフサイクルを管理するための基本メソッドと、DAO ガバナンス、委任、トークン有効期限、アカウント回復などの拡張機能を備えています。

このインターフェースは、ソウルバウンド・トークンシステムの開発に柔軟で拡張性のある枠組みを提供することを目的としています。

## 動機

現在のWeb3エコシステムは、金融化された譲渡可能なトークンに重点が置かれています。しかし、個人の属性や権利を表す譲渡不可能なトークンに対するニーズが高まっています。Ethereumコミュニティ内の既存の試みは、必要な柔軟性と拡張性が不足しています。このインターフェースはこのギャップに取り組み、SBTに対する包括的なソリューションを提供します。

このインターフェースは、譲渡不可能な所有権を表すために使用でき、以下を含む一般的なユースケースに対応する機能を提供します:

- ライフサイクル管理: SBTのミント、取り消し、サブスクリプションと有効期限の管理のための堅牢なツール。
- DAOガバナンスと委任: コミュニティ主導の意思決定と SBT管理の委任を可能にする。
- アカウント回復: アカウント回復とキー交代のための高度なメカニズム、セキュリティと継続性を確保する。
- トークンの多様性: ファンジブルとノンファンジブルの両方のSBTをサポート、会員カードやロイヤルティプログラムなど、幅広いユースケースに対応。
- トークンのグループ化: 複雑な報酬構造(バウチャー、ポイント、バッジなど)に適したスロットベースのシステム。
- クレーマブルSBT: エアドロップ、プレゼント、紹介プログラムなどのためのSBT配布を簡素化。

このインターフェースは、Web3の景観を豊かにするだけでなく、より分散化され個人化されたデジタル社会への道を開きます。

## 仕様

この文書の中の "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY"、"OPTIONAL" というキーワードは、RFC 2119に記載されているように解釈されるものとします。

トークンは `tokenId` によって識別され、これは 256 ビットの符号なし整数です。トークンには、その額面を表す値を持つこともできます。

スロットは `slotId` によって識別され、これも 256 ビットの符号なし整数です。スロットは、ファンジブルとノンファンジブルのトークンをグループ化するために使用され、トークンを半ファンジブルにします。トークンは一度に1つのスロットにしか属することができません。

### コア

コアメソッドは、SBTのライフサイクルを管理するために使用されます。すべての半ファンジブルSBT実装で必須でサポートされなければなりません。

```solidity
/**
 * @title ERC5727 Soulbound Token Interface
 * @dev The core interface of the ERC5727 standard.
 */
interface IERC5727 is IERC3525, IERC5192, IERC5484, IERC4906 {
    /**
     * @dev MUST emit when a token is revoked.
     * @param from The address of the owner
     * @param tokenId The token id
     */
    event Revoked(address indexed from, uint256 indexed tokenId);

    /**
     * @dev MUST emit when a token is verified.
     * @param by The address that initiated the verification
     * @param tokenId The token id
     * @param result The result of the verification
     */
    event Verified(address indexed by, uint256 indexed tokenId, bool result);

    /**
     * @notice Get the verifier of a token.
     * @dev MUST revert if the `tokenId` does not exist
     * @param tokenId the token for which to query the verifier
     * @return The address of the verifier of `tokenId`
     */
    function verifierOf(uint256 tokenId) external view returns (address);

    /**
     * @notice Get the issuer of a token.
     * @dev MUST revert if the `tokenId` does not exist
     * @param tokenId the token for which to query the issuer
     * @return The address of the issuer of `tokenId`
     */
    function issuerOf(uint256 tokenId) external view returns (address);

    /**
     * @notice Issue a token in a specified slot to an address.
     * @dev MUST revert if the `to` address is the zero address.
     *      MUST revert if the `verifier` address is the zero address.
     * @param to The address to issue the token to
     * @param tokenId The token id
     * @param slot The slot to issue the token in
     * @param burnAuth The burn authorization of the token
     * @param verifier The address of the verifier
     * @param data Additional data used to issue the token
     */
    function issue(
        address to,
        uint256 tokenId,
        uint256 slot,
        BurnAuth burnAuth,
        address verifier,
        bytes calldata data
    ) external payable;

    /**
     * @notice Issue credit to a token.
     * @dev MUST revert if the `tokenId` does not exist.
     * @param tokenId The token id
     * @param amount The amount of the credit
     * @param data The additional data used to issue the credit
     */
    function issue(
        uint256 tokenId,
        uint256 amount,
        bytes calldata data
    ) external payable;

    /**
     * @notice Revoke a token from an address.
     * @dev MUST revert if the `tokenId` does not exist.
     * @param tokenId The token id
     * @param data The additional data used to revoke the token
     */
    function revoke(uint256 tokenId, bytes calldata data) external payable;

    /**
     * @notice Revoke credit from a token.
     * @dev MUST revert if the `tokenId` does not exist.
     * @param tokenId The token id
     * @param amount The amount of the credit
     * @param data The additional data used to revoke the credit
     */
    function revoke(
        uint256 tokenId,
        uint256 amount,
        bytes calldata data
    ) external payable;

    /**
     * @notice Verify if a token is valid.
     * @dev MUST revert if the `tokenId` does not exist.
     * @param tokenId The token id
     * @param data The additional data used to verify the token
     * @return A boolean indicating whether the token is successfully verified
     */
    function verify(
        uint256 tokenId,
        bytes calldata data
    ) external returns (bool);
}
```

### 拡張機能

以下のすべての拡張機能は、[ERC-5727](./eip-5727.md)の実装にとってオプショナルです。実装は、それらの一部、全部、または何も実装しないことを選択できます。

#### 列挙可能

この拡張機能は、所有者のトークンを列挙するためのメソッドを提供します。コアインターフェースと一緒に実装することをお勧めします。

```solidity
/**
 * @title ERC5727 Soulbound Token Enumerable Interface
 * @dev This extension allows querying the tokens of a owner.
 */
interface IERC5727Enumerable is IERC3525SlotEnumerable, IERC5727 {
    /**
     * @notice Get the number of slots of a owner.
     * @param owner The owner whose number of slots is queried for
     * @return The number of slots of the `owner`
     */
    function slotCountOfOwner(address owner) external view returns (uint256);

    /**
     * @notice Get the slot with `index` of the `owner`.
     * @dev MUST revert if the `index` exceed the number of slots of the `owner`.
     * @param owner The owner whose slot is queried for.
     * @param index The index of the slot queried for
     * @return The slot is queried for
     */
    function slotOfOwnerByIndex(
        address owner,
        uint256 index
    ) external view returns (uint256);

    /**
     * @notice Get the balance of a owner in a slot.
     * @dev MUST revert if the slot does not exist.
     * @param owner The owner whose balance is queried for
     * @param slot The slot whose balance is queried for
     * @return The balance of the `owner` in the `slot`
     */
    function ownerBalanceInSlot(
        address owner,
        uint256 slot
    ) external view returns (uint256);
}
```

#### メタデータ

この拡張機能は、トークン、スロット、およびコントラクト自体のメタデータを取得するためのメソッドを提供します。トークン、スロット、およびコントラクト(つまりSBTコレクション)の外観とプロパティを指定する必要がある場合は、この拡張機能を実装することをお勧めします。

```solidity
/**
 * @title ERC5727 Soulbound Token Metadata Interface
 * @dev This extension allows querying the metadata of soulbound tokens.
 */
interface IERC5727Metadata is IERC3525Metadata, IERC5727 {

}
```

#### ガバナンス

この拡張機能は、投票を通じてミントおよび取り消しの権限を管理するためのメソッドを提供します。特定のSBTの発行を有権者が決定する必要がある場合に役立ちます。

```solidity
/**
 * @title ERC5727 Soulbound Token Governance Interface
 * @dev This extension allows issuing of tokens by community voting.
 */
interface IERC5727Governance is IERC5727 {
    enum ApprovalStatus {
        Pending,
        Approved,
        Rejected,
        Removed
    }

    /**
     * @notice Emitted when a token issuance approval is changed.
     * @param approvalId The id of the approval
     * @param creator The creator of the approval, zero address if the approval is removed
     * @param status The status of the approval
     */
    event ApprovalUpdate(
        uint256 indexed approvalId,
        address indexed creator,
        ApprovalStatus status
    );

    /**
     * @notice Emitted when a voter approves an approval.
     * @param voter The voter who approves the approval
     * @param approvalId The id of the approval
     */
    event Approve(
        address indexed voter,
        uint256 indexed approvalId,
        bool approve
    );

    /**
     * @notice Create an approval of issuing a token.
     * @dev MUST revert if the caller is not a voter.
     *      MUST revert if the `to` address is the zero address.
     * @param to The owner which the token to mint to
     * @param tokenId The id of the token to mint
     * @param amount The amount of the token to mint
     * @param slot The slot of the token to mint
     * @param burnAuth The burn authorization of the token to mint
     * @param data The additional data used to mint the token
     */
    function requestApproval(
        address to,
        uint256 tokenId,
        uint256 amount,
        uint256 slot,
        BurnAuth burnAuth,
        address verifier,
        bytes calldata data
    ) external;

    /**
     * @notice Remove `approvalId` approval request.
     * @dev MUST revert if the caller is not the creator of the approval request.
     *      MUST revert if the approval request is already approved or rejected or non-existent.
     * @param approvalId The approval to remove
     */
    function removeApprovalRequest(uint256 approvalId) external;

    /**
     * @notice Approve `approvalId` approval request.
     * @dev MUST revert if the caller is not a voter.
     *     MUST revert if the approval request is already approved or rejected or non-existent.
     * @param approvalId The approval to approve
     * @param approve True if the approval is approved, false if the approval is rejected
     * @param data The additional data used to approve the approval (e.g. the signature, voting power)
     */
    function voteApproval(
        uint256 approvalId,
        bool approve,
        bytes calldata data
    ) external;

    /**
     * @notice Get the URI of the approval.
     * @dev MUST revert if the `approvalId` does not exist.
     * @param approvalId The approval whose URI is queried for
     * @return The URI of the approval
     */
    function approvalURI(
        uint256 approvalId
    ) external view returns (string memory);
}
```

#### 委任

この拡張機能は、オペレーターにスロットのミント権を委任(委任解除)するためのメソッドを提供します。特定のスロットでトークンをミントする権限をオペレーターに与えたい場合に役立ちます。

```solidity
/**
 * @title ERC5727 Soulbound Token Delegate Interface
 * @dev This extension allows delegation of issuing and revocation of tokens to an operator.
 */
interface IERC5727Delegate is IERC5727 {
    /**
     * @notice Emitted when a token issuance is delegated to an operator.
     * @param operator The owner to which the issuing right is delegated
     * @param slot
The slot to issue the token in
     */
    event Delegate(address indexed operator, uint256 indexed slot);

    /**
     * @notice Emitted when a token issuance is revoked from an operator.
     * @param operator The owner to which the issuing right is delegated
     * @param slot The slot to issue the token in
     */
    event UnDelegate(address indexed operator, uint256 indexed slot);

    /**
     * @notice Delegate rights to `operator` for a slot.
     * @dev MUST revert if the caller does not have the right to delegate.
     *      MUST revert if the `operator` address is the zero address.
     *      MUST revert if the `slot` is not a valid slot.
     * @param operator The owner to which the issuing right is delegated
     * @param slot The slot to issue the token in
     */
    function delegate(address operator, uint256 slot) external;

    /**
     * @notice Revoke rights from `operator` for a slot.
     * @dev MUST revert if the caller does not have the right to delegate.
     *      MUST revert if the `operator` address is the zero address.
     *      MUST revert if the `slot` is not a valid slot.
     * @param operator The owner to which the issuing right is delegated
     * @param slot The slot to issue the token in
     */

    function undelegate(address operator, uint256 slot) external;

    /**
     * @notice Check if an operator has the permission to issue or revoke tokens in a slot.
     * @param operator The operator to check
     * @param slot The slot to check
     */
    function isOperatorFor(
        address operator,
        uint256 slot
    ) external view returns (bool);
}

```

#### 回復

この拡張機能は、古いオーナーからトークンを回復するためのメソッドを提供します。ユーザーが危険にさらされた古いウォレットからトークンを取り戻せるようにするために、この拡張機能の使用をお勧めします。署名スキームは、可読性とユーザビリティのために[EIP-712](./eip-712.md)と互換性があるものとします。

```solidity
/**
 * @title ERC5727 Soulbound Token Recovery Interface
 * @dev This extension allows recovering soulbound tokens from an address provided its signature.
 */
interface IERC5727Recovery is IERC5727 {
    /**
     * @notice Emitted when the tokens of `owner` are recovered.
     * @param from The owner whose tokens are recovered
     * @param to The new owner of the tokens
     */
    event Recovered(address indexed from, address indexed to);

    /**
     * @notice Recover the tokens of `owner` with `signature`.
     * @dev MUST revert if the signature is invalid.
     * @param owner The owner whose tokens are recovered
     * @param signature The signature signed by the `owner`
     */
    function recover(address owner, bytes memory signature) external;
}
```

#### 有効期限

この拡張機能は、トークンの有効期限を管理するためのメソッドを提供します。一定期間後にトークンを期限切れ/無効にしたい場合に役立ちます。

```solidity
/**
 * @title ERC5727 Soulbound Token Expirable Interface
 * @dev This extension allows soulbound tokens to be expirable and renewable.
 */
interface IERC5727Expirable is IERC5727, IERC5643 {
    /**
     * @notice Set the expiry date of a token.
     * @dev MUST revert if the `tokenId` token does not exist.
     *      MUST revert if the `date` is in the past.
     * @param tokenId The token whose expiry date is set
     * @param expiration The expire date to set
     * @param isRenewable Whether the token is renewable
     */
    function setExpiration(
        uint256 tokenId,
        uint64 expiration,
        bool isRenewable
    ) external;
}
```

## 根拠

### トークンストレージモデル

我々は、ファンジブルとノンファンジブルのトークンの両方をサポートするために設計された半ファンジブルトークンのストレージモデルを採用しています。これは、[ERC-1155](./eip-1155.md)で使用されているモデルよりもSBTの表現に適していると考えています。

まず、各スロットを使用して、SBTの異なるカテゴリーを表すことができます。例えば、DAOは会員SBT、役割バッジ、評判などを1つのSBTコレクションに持つことができます。

次に、[ERC-1155](./eip-1155.md)では各ファンジブルトークンの単位が完全に同一であるのに対し、このインターフェースは、同様のトークンでも区別することができます。これは、異なる組織から得られた資格スコアは、値だけでなく有効期間、出所などでも異なるため正当化されます。しかし、それらはすべて同じスロットに属し、その人の信頼性、会員資格などに貢献します。

### 回復メカニズム

SBTの損失を防ぐために、所有者アドレスで署名された署名を提供することで、ユーザーがトークンを回復できるメカニズムを提案しています。このメカニズムは[ERC-1271](./eip-1271.md)に着想を得ています。

SBTはアドレスにバインドされ、アドレスのアイデンティティを表すことを意図しているため、各回復は所有者のすべてのトークンの移転と見なされます。これが、`recover`関数を使用する理由です。

## 下位互換性

このEIPは新しいトークンインターフェースを提案しており、[ERC-721](./eip-721.md)、[ERC-3525](./eip-3525.md)、[ERC-4906](./eip-4906.md)、[ERC-5192](./eip-5192.md)、[ERC-5484](./eip-5484.md)と互換性があります。

このEIPは[ERC-165](./eip-165.md)とも互換性があります。

## テストケース

サンプル実装にはHardhatを使用したテストケースが含まれています。

## 参考実装

参考実装は[こちら](../assets/eip-5727/ERC5727.sol)にあります。

## セキュリティ上の考慮事項

このEIPはトークンの一般的な転送を含んでいないため、トランスファー一般に関連するセキュリティ上の問題はありません。

ただし、ユーザーは回復メカニズムのセキュリティリスクに注意する必要があります。ユーザーが秘密鍵を失った場合、すべてのソウルバウンドトークンが盗難の対象となる可能性があります。攻撃者は署名を作成し、被害者のすべてのSBTを回復することができます。したがって、ユーザーは常に秘密鍵を安全に保管する必要があります。開発者は、複数の署名が必要なSBT回復メカニズムを実装することをお勧めします。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。