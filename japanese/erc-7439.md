---
original: 4a804116cd9c001ab748e8558e5e97b306da03e2fb24680ad7e82b0dca33871b
---

---
eip: 7439
title: チケットの転売防止
description: 認可されたチケット再販業者を通じて顧客がチケットを再販できるインターフェース。
author: LeadBest Consulting Group <service@getoken.io>、Sandy Sung (@sandy-sung-lb)、Mars Peng <mars.peng@getoken.io>、Taien Wang <taien.wang@getoken.io>
discussions-to: https://ethereum-magicians.org/t/prevent-ticket-touting/15269
status: 最終
type: Standards Track
category: ERC
created: 2023-07-28
requires: 165, 721
---
## 概要

このスタンダードは [ERC-721](./eip-721.md) の拡張であり、チケット代理店やイベント主催者が、チケット転売市場での搾取から観客を守り、顧客が認可されたチケット再販業者を通じてチケットを再販できるようにする標準機能を定義しています。

## 動機

大規模なチケット転売は長年の問題で、それに伴う詐欺や犯罪問題により、不幸な事件や社会資源の浪費が生じています。これは、キャリアのあらゆるレベルのアーティストや関連ビジネス全般に大きな打撃を与えています。各国政府がスカルパーの行動を制限する法律を制定し始めましたが、その効果は限定的です。彼らはまだ転売禁止のイベントのチケットを売ったり、まだ所有していないチケットを手に入れて、大幅な違法な利益を上げています。私たちは、消費者に優しい再販インターフェースを提供し、買い手が当初支払った価格以下で再販または再割り当てできるようにすることが、「二次チケット市場」を一掃する効率的な方法だと考えています。

典型的なチケットは「紙片」や電子メールの「バウチャー」の形をしているため、偽造や流通が容易です。これらのチケットの譲渡性を制限するために、私たちは、所有者を含むすべての当事者がチケットを譲渡できるのは、チケット代理店、マネージャー、プロモーター、認可された再販プラットフォームなどの特定のアカウントのみという仕組みを設計しました。したがって、チケット転売屋はチケットを自由に譲渡することができません。さらに、機能を強化するために、各チケットにトークン情報スキーマを実装し、所有者を除く認可されたアカウントのみがこれらのレコードを変更できるようにしました。

このスタンダードは、チケット代理店が [ERC-721](./eip-721.md) トークンをイベントチケットとして利用し、チケット転売を防ぐための制限を定義するフレームワークを提供します。このスタンダードを実装することで、顧客を詐欺や不正行為から保護することを目指しています。

## 仕様

この文書の中の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119 およびRFC 8174に記載されているように解釈されるものとします。

### インターフェース

ここで参照されているインターフェースと構造は以下の通りです:

* TokenInfo
    * `signature`: ユーザーの秘密鍵またはエージェントの秘密鍵を使ってトークンの有効性を証明するために、アダプターが自ら定義することをお勧めします。
    * `status`: トークンの現在のステータスを表します。
    * `expireTime`: イベントの終了時間を設定することをお勧めします。
* TokenStatus
    * `Sold`: トークンが売れた場合、`Sold`に変更する必要があります。このステータスではトークンは有効です。
    * `Resell`: トークンが二次市場にある場合、`Resell`に変更する必要があります。このステータスではトークンは有効です。
    * `Void`: トークン所有者が不正な取引に関与した場合、トークンのステータスを`Void`に設定する必要があります。このステータスではトークンは無効です。
    * `Redeemed`: トークンが使用された場合、トークンのステータスを`Redeemed`に変更することをお勧めします。

```solidity
/// @title IERC7439 Prevent Ticket Touting Interface
interface IERC7439 /* is ERC721 */ {
    /// @dev TokenStatus はトークンの現在のステータスを表し、特定の役割のみがステータスを変更できます。
    enum TokenStatus {
        Sold,    // 0
        Resell,  // 1
        Void,    // 2
        Redeemed // 3
    }

    /// @param signature ユーザーの秘密鍵またはエージェントの秘密鍵で署名されたデータ
    /// @param tokenStatus トークンのステータスを変更する
    /// @param expireTime イベントの終了時間
    struct TokenInfo {
        bytes signature;
        TokenStatus tokenStatus;
        uint256 expireTime;
    }

    /// @notice トークンIDが指定されたトークンのステータスが変更されたことをリスナーに通知するために使用されます
    /// @param tokenId ステータスが変更されたトークン
    /// @param tokenStatus トークンのステータスが変更された
    /// @param signature ユーザーの秘密鍵またはエージェントの秘密鍵で署名されたデータ
    event TokenStatusChanged(
        uint256 indexed tokenId,
        TokenStatus indexed tokenStatus,
        bytes signature
    );

    /// @notice トークンのステータスを付けてトークンをミントするために使用されます
    /// @dev トークンのステータスが変更された場合は、必ず `TokenStatusChanged` イベントを発行する必要があります。
    /// @param to トークンの受取人
    /// @param signature ユーザーの秘密鍵またはエージェントの秘密鍵で署名されたデータ
    function safeMint(address to, bytes memory signature) external;

    /// @notice トークンのステータスを変更するために使用され、特定の役割のみが呼び出すことができます
    /// @dev トークンのステータスが変更された場合は、必ず `TokenStatusChanged` イベントを発行する必要があります。
    /// @param tokenId ステータスを変更するトークン
    /// @param signature ユーザーの秘密鍵またはエージェントの秘密鍵で署名されたデータ
    /// @param tokenStatus 変更するトークンのステータス
    /// @param newExpireTime 新しいイベントの終了時間
    function changeState(
        uint256 tokenId,
        bytes memory signature,
        TokenStatus tokenStatus,
        uint256 newExpireTime
    ) external;
}
```
`supportsInterface` メソッドは、`0x15fbb306`で呼び出された場合、`true`を返さなければなりません。

## 根拠

提案を設計する際、以下の点を考慮しました:

1. チケット代理店、パフォーマー、観客にとって最も重要なことは何ですか?
   * チケット会社にとって、すべてのチケットを売り切ることが最も重要です。時には、活気のある販売環境を作るために、チケット会社がスカルパーと協力することもあります。この慣行は観客とパフォーマーの両方に有害です。このような状況を防ぐには、オープンで透明な一次販売チャネルと公平な二次販売メカニズムが必要です。`safeMint`関数は公開関数であり、`TokenInfo`にはバイヤーアカウントまたはエージェントが解決できる署名を追加して、チケットの有効性を証明します。そしてトークンの`status`は`Sold`になります。それにもかかわらず、チケット会社の圧力も考慮しなければなりません。彼らは有効なチケットの効用を最大化することを目指しており、つまり、1枚1枚を売り切ることを意味します。従来のメカニズムでは、チケット会社は初回の販売からしか利益を得ることができず、二次販売の超過利益を享受することはできません。したがって、チケット会社が管理可能な二次販売プロセスを設計しました。`_beforeTokenTransfer()`関数では、アクセス制御関数であり、`PARTNER_ROLE`の`mint`または`burn`の状況でのみチケットを転送できます。`PARTNER_ROLE`はチケット代理店や合法的な二次チケット販売プラットフォームである可能性があり、これは国の監督機関またはチケット代理店が指定したプラットフォームかもしれません。公正なチケット市場を維持するために、彼ら自身でチケットを転送することはできません。なぜなら、買い手がスカルパーかどうかを区別することはできないからです。

   * パフォーマーやイベント主催者にとって、チケット販売中に悪いニュースが出ることを望んでいません。パフォーマーの評判を損なうかもしれないニュースがないことが彼らが望むことです。それ以外では、本当のファンが来ることが重要です。スカルパーの手に渡ったり、二次市場に混乱が生じたりするチケットは、真のファンには魅力的ではありません。パフォーマーはそのような状況を喜ばないと思います。透明なメカニズムを通じて、パフォーマーやイベント主催者は、トークンのミント量と`TokenInfo`-`TokenStatus`の比較から、常に実際の販売状況を管理できます。
        ```
        enum TokenStatus {
            Sold,    // 0
            Resell,  // 1
            Void,    // 2
            Redeemed // 3
        }
        ```
   * 観客にとって、必要なのは有効なチケットを手に入れることだけです。従来のメカニズムでは、ファンは多くの障害に遭遇します。人気のあるコンサートでは、チケットを手に入れようとするファンは、スカルパーやチケット会社のような敵と戦わなければなりません。これらのスカルパーはプロのようで、チケットを戦略的に奪い取ります。驚いたことに、チケット会社はこれらのスカルパーと組んでいる可能性があります。あるいは、自分で無料やVIPチケットを保持しているかもしれません。透明なメカニズムは観客にとっても重要です。

2. 健全なチケット生態系をどのように構築するか?
   * 明確なチケットルールは、供給と需要のバランスを保つ上で重要です。

   * オープンな価格設定システムは、消費者を保護するために不可欠です。
 
   * 優れた流動性。初期市場では、ユーザー自身がチケットをミントできます。必要であれば、購入したチケットも透明で公開された二次市場で譲渡できます。初回販売でチケットを購入できなかった観客も、合法的な二次市場で安心して購入できます。`changeState`関数は、チケットの流動性を良好に保つためのものです。`PARTNER_ROLE`のみがチケットのステータスを変更できます。売れたチケットを二次市場で売る必要がある場合、二次市場に依頼してリセール状態に変更する必要があります。ステータスを変更するプロセスは、二次市場のチケットの公式な検証です。これは、二次購入者への保護メカニズムです。

3. スムーズなチケット販売プロセスをどのように設計するか?
   * 購入/販売が簡単。観客はNFTとしてチケットを購入できます。これは一般的な慣行です。
   
   * 払い戻しが簡単。何か極端なことが起こり、ショーをキャンセルする必要がある場合。チケットの払い戻しを簡単に処理できます。
 
   * 引き換えが簡単。ショー前に、チケット代理店は署名を確認して、観客が本物かどうかを確認できます。`TokenStatus`は`sold`と等しくなければならず、`expireTime`で観客が正しいセッションに到着したかどうかを区別できます。検証に合格すれば、チケット代理店は`TokenStatus`を`Redeemed`に変更できます。
   
   * 通常のフロー
        ![Alt text](../assets/eip-7439/normal.png)

   * Voidフロー
        ![Alt text](../assets/eip-7439/void.png)

   * Resellフロー
        ![Alt text](../assets/eip-7439/resell.png)

## 下位互換性

このスタンダードは [ERC-721](./eip-721.md) と互換性があります。

## テストケース

```javascript
const { expectRevert } = require("@openzeppelin/test-helpers");
const { expect } = require("chai");
const ERC7439 = artifacts.require("ERC7439");

contract("ERC7439", (accounts) => {
  const [deployer, partner, userA, userB] = accounts;
  const expireTime = 19999999;
  const tokenId = 0;
  const signature = "0x993dab3dd91f5c6dc28e17439
be475478f5635c92a56e17e82349d3fb2f166196f466c0b4e0c146f285204f0dcb13e5ae67bc33f4b888ec32dfe0a063e8f3f781b"
  const zeroHash = "0x";

  beforeEach(async () => {
    this.erc7439 = await ERC7439.new({
      from: deployer,
    });
    await this.erc7439.mint(userA, signature, { from: deployer });
  });

  it("トークンをミントすべき", async () => {
    const tokenInfo = await this.erc7439.tokenInfo(tokenId);

    expect(await this.erc7439.ownerOf(tokenId)).to.equal(userA);
    expect(tokenInfo.signature).equal(signature);
    expect(tokenInfo.status).equal("0"); // Sold
    expect(tokenInfo.expireTime).equal(expireTime);
  });

  it("通常のユーザーは正常に転送できるべきではない", async () => {
    expectRevert(await this.erc7439.transferFrom(userA, userB, tokenId, { from: userA }), "ERC7439: You cannot transfer this NFT!");
  });

  it("パートナーは正常に転送でき、トークン情報をリセール状態に変更できるべき", async () => {
    const tokenStatus = 1; // Resell

    await this.erc7439.changeState(tokenId, zeroHash, tokenStatus, { from: partner });
    await this.erc7439.transferFrom(userA, partner, tokenId, { from: partner });

    expect(tokenInfo.tokenHash).equal(zeroHash);
    expect(tokenInfo.status).equal(tokenStatus); // Resell
    expect(await this.erc7439.ownerOf(tokenId)).to.equal(partner);
  });

  it("パートナーはトークンのステータスをVoidに変更できるべき", async () => {
    const tokenStatus = 2; // Void

    await this.erc7439.changeState(tokenId, zeroHash, tokenStatus, { from: partner });

    expect(tokenInfo.tokenHash).equal(zeroHash);
    expect(tokenInfo.status).equal(tokenStatus); // Void
  });

  it("パートナーはトークンのステータスをRedeemedに変更できるべき", async () => {
    const tokenStatus = 3; // Redeemed

    await this.erc7439.changeState(tokenId, zeroHash, tokenStatus, { from: partner });

    expect(tokenInfo.tokenHash).equal(zeroHash);
    expect(tokenInfo.status).equal(tokenStatus); // Redeemed
  });

  it("パートナーはトークンを再販し、ステータスをResellからSoldに変更できるべき", async () => {    
    let tokenStatus = 1; // Resell
    await this.erc7439.changeState(tokenId, zeroHash, tokenStatus, { from: partner });
    await this.erc7439.transferFrom(userA, partner, tokenId, { from: partner });
    
    expect(tokenInfo.status).equal(tokenStatus); // Resell
    expect(tokenInfo.tokenHash).equal(zeroHash);

    tokenStatus = 0; // Sold
    const newSignature = "0x113hqb3ff45f5c6ec28e17439be475478f5635c92a56e17e82349d3fb2f166196f466c0b4e0c146f285204f0dcb13e5ae67bc33f4b888ec32dfe0a063w7h2f742f";
    await this.erc7439.changeState(tokenId, newSignature, tokenStatus, { from: partner });
    await this.erc7439.transferFrom(partner, userB, tokenId, { from: partner });

    expect(tokenInfo.status).equal(tokenStatus); // Sold
    expect(tokenInfo.tokenHash).equal(newSignature);
  });
});
```

## 参考実装

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.19;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
// 追加のメタデータが必要な場合は、ERC721URIStorageをインポートできます
// import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/AccessControl.sol";
import "@openzeppelin/contracts/utils/Counters.sol";
import "./IERC7439.sol";

contract ERC7439 is ERC721, AccessControl, IERC7439 {
    using Counters for Counters.Counter;

    bytes32 public constant PARTNER_ROLE = keccak256("PARTNER_ROLE");
    Counters.Counter private _tokenIdCounter;

    uint256 public expireTime;

    mapping(uint256 => TokenInfo) public tokenInfo;

    constructor(uint256 _expireTime) ERC721("MyToken", "MTK") {
        _grantRole(DEFAULT_ADMIN_ROLE, msg.sender);
        _grantRole(PARTNER_ROLE, msg.sender);
        expireTime = _expireTime;
    }

    function safeMint(address to, bytes memory signature) public {
        uint256 tokenId = _tokenIdCounter.current();
        _tokenIdCounter.increment();
        _safeMint(to, tokenId);
        tokenInfo[tokenId] = TokenInfo(signature, TokenStatus.Sold, expireTime);
        emit TokenStatusChanged(tokenId, TokenStatus.Sold, signature);
    }

    function changeState(
        uint256 tokenId,
        bytes memory signature,
        TokenStatus tokenStatus,
        uint256 newExpireTime
    ) public onlyRole(PARTNER_ROLE) {
        tokenInfo[tokenId] = TokenInfo(signature, tokenStatus, newExpireTime);
        emit TokenStatusChanged(tokenId, tokenStatus, signature);
    }
    
    function _burn(uint256 tokenId) internal virtual override(ERC721) {
        super._burn(tokenId);

        if (_exists(tokenId)) {
            delete tokenInfo[tokenId];
            // ERC721URIStorageをインポートした場合
            // delete _tokenURIs[tokenId];
        }
    }

    function supportsInterface(
        bytes4 interfaceId
    ) public view virtual override(AccessControl, ERC721) returns (bool) {
        return
            interfaceId == type(IERC7439).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override(ERC721) {
        if (!hasRole(PARTNER_ROLE, _msgSender())) {
            require(
                from == address(0) || to == address(0),
                "ERC7439: You cannot transfer this NFT!"
            );
        }

        super._beforeTokenTransfer(from, to, tokenId);
    }
}