---
original: d9734e03e981d1a2d323fac3dff5508c5107c59d384476d577bfcaa95c27e261
---

---
eip: 7533
title: パブリック クロスポート
description: すべてのEVMチェーンを接続するためのブリッジ
author: George (@JXRow), Zisu (@lazy1523)
discussions-to: https://ethereum-magicians.org/t/connect-all-l2s/15534
status: Draft
type: Standards Track
category: ERC
created: 2023-10-11
---


## 概要

パブリック クロスポート (PCP) の目的は、さまざまなEVMチェーンを安全かつ効率的に接続することです。メッセージを複数のチェーンにプッシュする方法を置き換え、複数のチェーンからメッセージをプルする方法に変更することで、クロスチェーンブリッジの数とガスコストを大幅に削減します。多くのクロスチェーンブリッジプロジェクトがPCPに基づいて構築されるにつれ、全体的なセキュリティが向上します。


## 動機

現在、L1とL2の間には公式のクロスチェーンブリッジがありますが、L2間にはありません。10のL2チェーンが相互にクロスチェーンする必要がある場合、90個のクロスチェーンブリッジが必要になります。しかし、プル機構を使用して他の9つのチェーンからのメッセージを1つのトランザクションにマージし、自身のチェーンと同期させる場合、10個のクロスチェーンブリッジのみが必要になります。これにより、必要なクロスチェーンブリッジの数が大幅に減り、ガスコストを最小限に抑えることができます。

複数のクロスチェーンブリッジプロジェクトの参加により、このような実装によってセキュリティが大幅に向上します。現在、冗長なクロスチェーンブリッジの構築が行われており、セキュリティの向上には寄与していません。標準化された `SendPort` コントラクトを使用することで、同じクロスチェーンメッセージが複数の冗長なブリッジによって転送されている場合、ターゲットチェーンの `IReceivePort` での検証結果が同じになります。この結果が複数のクロスチェーンブリッジプロジェクトによって確認されることで、単一の確認よりもはるかに高いセキュリティが提供されます。この EIP の目的は、より多くのクロスチェーンブリッジプロジェクトに参加を促し、冗長な構築を強化されたセキュリティに変えることです。

クロスチェーンブリッジプロジェクトの参加を呼び込むために、ブリッジの数とガスコストの削減に加えて、`SendPort` でのハッシュ MerkleTree データ構造の使用により、クロスチェーンメッセージを追加してもブリッジのオーバーヘッドが増加しません。クロスチェーンブリッジの転送には、わずかなサイズのルートのみが必要で、さらにガスを節約できます。


### ユースケース

この EIP では、クロスチェーンエコシステムを3つの層に分割し、基盤層で `SendPort` コントラクトと `IReceivePort` インターフェースを定義しています。他の層の実装は、エコシステムのプロジェクト参加者に委ねられています。

![](../assets/eip-7533/0.png)

クロスチェーンメッセージングに加えて、アプリケーションはブリッジをサービスとして使用できます。たとえば、トークンのクロスチェーン。

クロスチェーンメッセージングブリッジは、トークンのクロスチェーン機能と組み合わせることができます。参考実装のコード例を参照してください。あるいは、それらを分離することもできます。NFTのクロスチェーンアプリケーションの例では、トークンのメッセージングブリッジを再利用でき、さらに複数のメッセージングブリッジを活用することもできます。複数のブリッジを利用してメッセージの検証を行うことで、クロスチェーンおよび検証サービスのコストを追加することなく、セキュリティを大幅に強化できます。


## 仕様

この文書の中で使用される "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY"、"OPTIONAL" というキーワードは、RFC 2119およびRFC 8174に記載されている通りに解釈されるものとします。

クロスチェーンの本質は、ソースチェーンで発生したイベントをターゲットチェーンに通知することです。このプロセスは3つのステップに分けることができます。全体的な原則を示す図は以下の通りです:

![](../assets/eip-7533/1.png)

### 1.クロスチェーンメッセージの追加

この EIP に基づき、各チェーンに `SendPort` コントラクトがデプロイされます。これは、そのチェーンでのクロスチェーンメッセージを収集し、パッケージ化する責任を負います。`SendPort` は、パブリック、パーミッションレス、管理者フリー、自動システムとして動作します。クロスチェーンブリッジオペレーターは、`SendPort` からクロスチェーンメッセージを取得し、ターゲットチェーンに転送することでクロスチェーンメッセージングプロセスを完了します。

`SendPort` コントラクトは複数のブリッジに対応でき、そのチェーンで発生したイベント (つまり、クロスチェーンメッセージ) を収集し、MerkleTreeにパッケージ化します。例えば、ブリッジコントラクトがユーザーのUSDTデポジットを受け取った場合、このイベントのハッシュとターゲットチェーンのIDを `SendPort` コントラクトに送信できます。`SendPort` は、送信者のアドレス (つまりブリッジコントラクトのアドレス) のハッシュとともに、この情報をリーフとして配列に追加します。一定期間 (例: 1分) にわたって一定数のリーフを収集した後、`SendPort` は自動的にそれらをMerkleTreeにパッケージ化し、次の収集フェーズを開始します。`SendPort` の役割は、イベントの収集とパッケージ化に焦点を当てています。管理なしで自律的に動作するため、各チェーンに `SendPort` を再デプロイする必要はありません。**推奨**は、1チェーン1 `SendPort` です。

`SendPort.addMsgHash()` 関数は、さまざまなクロスチェーンブリッジプロジェクトや他のコントラクトから呼び出すことができます。この関数は許可を必要としないため、不正なメッセージが送信される可能性があります。不正行為を防ぐため、`SendPort` はパッケージ化プロセスに送信者のアドレスを含めます。これは、`sender` が `toChainId` チェーンに `msgHash` 情報を送信しようとしていることを示しています。ターゲットチェーンでこの情報が解読される際に、不正行為を防ぐのに役立ちます。

### 2.ルートの取得と設定

新しいMerkleTreeのパッケージング完了後、パッケージキャリア (通常はクロスチェーンブリッジプロジェクト) は複数のチェーンからルートを取得し、各チェーンの `IReceivePort` コントラクトに格納します。

ルートには、1つのソースチェーンから複数のターゲットチェーンへのメッセージが含まれています。パッケージキャリアにとって、ルートに関連するメッセージが含まれていない場合や、特定のターゲットチェーンに対するメッセージが含まれていない場合があります。したがって、パッケージキャリアは、関連性に基づいて、ルートを特定のターゲットチェーンに転送するかどうかを判断する裁量を持っています。

したがって、`IReceivePort` コントラクトは一意ではなく、`IReceivePort` インターフェースに基づいてパッケージキャリーによって実装されます。複数のパッケージキャリーがいる場合、複数の `IReceivePort` コントラクトが存在することになります。

### 3.クロスチェーンメッセージの検証

`IReceivePort` コントラクトは各チェーンのルートを格納しており、完全なメッセージが提供された場合にその真正性を検証できます。ルート自体では、メッセージを解読することはできず、真正性の検証にのみ使用できることに注意が必要です。完全なメッセージは、ソースチェーンの `SendPort` コントラクトから取得できます。

ルートは同じ `SendPort` から発信されるため、異なる `IReceivePort` コントラクトのルートは**同一**である**べき**です。つまり、メッセージが真正である場合、異なる `IReceivePort` コントラクトで真正と検証**されるべき**です。これにより、セキュリティが大幅に向上します。マルチシグネチャの原則と同様に、多数の `IReceivePort` コントラクトがメッセージを真正と検証した場合、それが真実である可能性が高くなります。一方、メッセージを偽物と検証する `IReceivePort` コントラクトがある場合は、ハッカー攻撃の可能性や、対応するクロスチェーンブリッジの障害を示している可能性があります。このようなデcentralized参加モデルにより、単一障害点によってシステムのセキュリティが損なわれることはありません。冗長な構築を強化されたセキュリティに変えることができます。

データ整合性に関して:

`SendPort` は、すべてのルートと連続インデックス番号を削除や変更せずに保持します。各クロスチェーンブリッジの `IReceivePort` コントラクトも**同様のアプローチ**を取る**べき**です。

### `ISendPort` インターフェース

```solidity
pragma solidity ^0.8.0;

interface ISendPort {
    event MsgHashAdded(uint indexed packageIndex, address sender, bytes32 msgHash, uint toChainId, bytes32 leaf);

    event Packed(uint indexed packageIndex, uint indexed packTime, bytes32 root);

    struct Package {
        uint packageIndex;
        bytes32 root;
        bytes32[] leaves;
        uint createTime;
        uint packTime;
    }

    function addMsgHash(bytes32 msgHash, uint toChainId) external;

    function pack() external;

    function getPackage(uint packageIndex) external view returns (Package memory);

    function getPendingPackage() external view returns (Package memory);
}
```

用語説明:

- `Package`: 一定期間内にクロスチェーンメッセージを収集し、単一のパッケージにパッケージ化します。
  - `packageIndex`: パッケージのインデックス。0から始まります。
  - `root`: `leaves` から生成されたMerkleTreeのルート。パッケージ化されたパッケージを表します。
  - `leaves`: 各リーフはクロスチェーンメッセージを表し、`msgHash`、`sender`、`toChainId` から計算されたハッシュです。
    - `msgHash`: メッセージのハッシュ。外部コントラクトから渡されます。
    - `sender`: 外部コントラクトのアドレス。明示的に渡す必要はありません。
    - `toChainId`: ターゲットチェーンのチェーンID。外部コントラクトから渡されます。
  - `createTime`: パッケージがメッセージの収集を開始した時刻。前のパッケージがパッケージ化された時刻でもあります。
  - `packTime`: パッケージがパッケージ化された時刻。パッケージ化後は、これ以上リーフを追加できません。
- `addMsgHash()`: 外部コントラクトがクロスチェーンメッセージのハッシュを SendPort に送信します。
- `pack()`: パッケージ化プロセスを手動でトリガーします。通常、最後の送信者が自身のメッセージを送信したときに自動的にトリガーされます。最後の送信者を待つのに時間がかかりすぎる場合は、手動でパッケージ化プロセスをトリガーできます。
- `getPackage()`: SendPort 内の各パッケージ (パッケージ化済みおよび保留中) を取得します。
- `getPendingPackage()`: SendPort 内の保留中のパッケージを取得します。


### `IReceivePort` インターフェース

```solidity
pragma solidity ^0.8.0;

interface IReceivePort {
    event PackageReceived(uint indexed fromChainId, uint indexed packageIndex, bytes32 root);

    struct Package {
        uint fromChainId;
        uint packageIndex;
        bytes32 root;
    }

    function receivePackages(Package[] calldata packages) external;

    function getRoot(uint fromChainId, uint packageIndex) external view returns (bytes32);

    function verify(
        uint fromChainId,
        uint packageIndex,
        bytes32[] memory proof,
        bytes
hash msgHash,
        address sender
    ) external view returns (bool);
}
```

用語説明:

- `Package`: 一定期間内にクロスチェーンメッセージを収集し、単一のパッケージにバンドルします。
  - `fromChainId`: パッケージの発信元チェーン。
  - `packageIndex`: パッケージのインデックス。0から始まります。
  - `root`: `leaves` から生成されたMerkleTreeのルート。パッケージ化されたパッケージを表します。
- `receivePackages()`: 異なるソースチェーンの SendPort から複数のルートを受け取ります。
- `getRoot()`: 特定のチェーンからルートを取得します。
- `verify()`: ソースチェーンでのメッセージが送信者によって送信されたことを検証します。


## 根拠

従来のアプローチでは、以下の図に示すようにプッシュ方式が使用されていました:

![](../assets/eip-7533/2.png)

6つのチェーンがある場合、各チェーンは他の5つのチェーンにプッシュする必要があり、以下の図に示すように30個のクロスチェーンブリッジが必要になります:

![](../assets/eip-7533/3.png)

N個のチェーンがお互いにクロスチェーン通信を必要とする場合、必要なクロスチェーンブリッジの数は、num = N * (N - 1) と計算されます。

プル方式を使用すると、5つのチェーンからのクロスチェーンメッセージを1つのトランザクションにバッチ処理できるため、必要なクロスチェーンブリッジの数を大幅に削減できます。以下の図に示します:

![](../assets/eip-7533/4.png)

各チェーンが他の5つのチェーンからメッセージをプルする場合、必要なクロスチェーンブリッジは6つだけです。N個のチェーンがクロスチェーン通信を必要とする場合、必要なクロスチェーンブリッジの数は、num = N となります。

したがって、プル方式によってクロスチェーンブリッジの数を大幅に削減できます。

MerkleTreeデータ構造は、クロスチェーンメッセージのサイズを効率的に圧縮します。クロスチェーンメッセージの数に関係なく、それらを単一のルートに圧縮できます。ルートは bytes32 値で表されます。パッケージキャリーはルートのみを転送すれば良いため、ガスコストが低くなります。


## 下位互換性

このEIPはコンセンサスレイヤーを変更しないため、Ethereumの全体としての下位互換性の問題はありません。

このEIPは他のERC標準を変更しないため、Ethereumアプリケーションの下位互換性の問題もありません。


## 参考実装

以下は、クロスチェーンブリッジのサンプルコントラクトです:

### `SendPort.sol`

```solidity
pragma solidity ^0.8.0;

import "./ISendPort.sol";

contract SendPort is ISendPort {
    uint public constant PACK_INTERVAL = 6000;
    uint public constant MAX_PACKAGE_MESSAGES = 100;

    uint public pendingIndex = 0;

    mapping(uint => Package) public packages;

    constructor() {
        packages[0] = Package(0, bytes32(0), new bytes32[](0), block.timestamp, 0);
    }

    function addMsgHash(bytes32 msgHash, uint toChainId) public {
        bytes32 leaf = keccak256(
            abi.encodePacked(msgHash, msg.sender, toChainId)
        );
        Package storage pendingPackage = packages[pendingIndex];
        pendingPackage.leaves.push(leaf);

        emit MsgHashAdded(pendingPackage.packageIndex, msg.sender, msgHash, toChainId, leaf);

        if (pendingPackage.leaves.length >= MAX_PACKAGE_MESSAGES) {
            console.log("MAX_PACKAGE_MESSAGES", pendingPackage.leaves.length);
            _pack();
            return;
        }

        // console.log("block.timestamp", block.timestamp);
        if (pendingPackage.createTime + PACK_INTERVAL <= block.timestamp) {
            console.log("PACK_INTERVAL", pendingPackage.createTime, block.timestamp);
            _pack();
        }
    }

    function pack() public {
        require(packages[pendingIndex].createTime + PACK_INTERVAL <= block.timestamp, "SendPort::pack: pack interval too short");

       _pack();
    }

    function getPackage(uint packageIndex) public view returns (Package memory) {
        return packages[packageIndex];
    }

    function getPendingPackage() public view returns (Package memory) {
        return packages[pendingIndex];
    }

    function _pack() internal {
        Package storage pendingPackage = packages[pendingIndex];
        bytes32[] memory _leaves = pendingPackage.leaves;
        while (_leaves.length > 1) {
            _leaves = _computeLeaves(_leaves);
        }
        pendingPackage.root = _leaves[0];
        pendingPackage.packTime = block.timestamp;

        emit Packed(pendingPackage.packageIndex, pendingPackage.packTime, pendingPackage.root);

        pendingIndex = pendingPackage.packageIndex + 1;
        packages[pendingIndex] = Package(pendingIndex, bytes32(0), new bytes32[](0), pendingPackage.packTime, 0);
    }

    function _computeLeaves(bytes32[] memory _leaves) pure internal returns (bytes32[] memory _nextLeaves) {
        if (_leaves.length % 2 == 0) {
            _nextLeaves = new bytes32[](_leaves.length / 2);
            bytes32 computedHash;
            for (uint i = 0; i + 1 < _leaves.length; i += 2) {
                computedHash = _hashPair(_leaves[i], _leaves[i + 1]);
                _nextLeaves[i / 2] = computedHash;
            }

        } else {
            bytes32 lastLeaf = _leaves[_leaves.length - 1];
            _nextLeaves = new bytes32[]((_leaves.length / 2 + 1));
            bytes32 computedHash;
            for (uint i = 0; i + 1 < _leaves.length; i += 2) {
                computedHash = _hashPair(_leaves[i], _leaves[i + 1]);
                _nextLeaves[i / 2] = computedHash;
            }
            _nextLeaves[_nextLeaves.length - 1] = lastLeaf;
        }
    }

    function _hashPair(bytes32 a, bytes32 b) private pure returns (bytes32) {
        return a < b ? _efficientHash(a, b) : _efficientHash(b, a);
    }

    function _efficientHash(bytes32 a, bytes32 b) private pure returns (bytes32 value) {
        /// @solidity memory-safe-assembly
        assembly {
            mstore(0x00, a)
            mstore(0x20, b)
            value := keccak256(0x00, 0x40)
        }
    }
}
```

外部機能:

- `PACK_INTERVAL`: 2つの連続するパッケージ化操作の最小時間間隔。この間隔を超えると、新しいパッケージ化操作を開始できます。
- `MAX_PACKAGE_MESSAGES`: `MAX_PACKAGE_MESSAGES` メッセージが収集されると、直ちにパッケージ化操作がトリガーされます。これは `PACK_INTERVAL` 設定よりも優先されます。

### `ReceivePort.sol`

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "./IReceivePort.sol";

abstract contract ReceivePort is IReceivePort, Ownable {

    //fromChainId => packageIndex => root
    mapping(uint => mapping(uint => bytes32)) public roots;

    constructor() {}

    function receivePackages(Package[] calldata packages) public onlyOwner {
        for (uint i = 0; i < packages.length; i++) {
            Package calldata p = packages[i];
            require(roots[p.fromChainId][p.packageIndex] == bytes32(0), "ReceivePort::receivePackages: package already exist");
            roots[p.fromChainId][p.packageIndex] = p.root;

            emit PackageReceived(p.fromChainId, p.packageIndex, p.root);
        }
    }

    function getRoot(uint fromChainId, uint packageIndex) public view returns (bytes32) {
        return roots[fromChainId][packageIndex];
    }

    function verify(
        uint fromChainId,
        uint packageIndex,
        bytes32[] memory proof,
        bytes32 msgHash,
        address sender
    ) public view returns (bool) {
        bytes32 leaf = keccak256(
            abi.encodePacked(msgHash, sender, block.chainid)
        );
        return _processProof(proof, leaf) == roots[fromChainId][packageIndex];
    }

    function _processProof(bytes32[] memory proof, bytes32 leaf) internal pure returns (bytes32) {
        bytes32 computedHash = leaf;
        for (uint256 i = 0; i < proof.length; i++) {
            computedHash = _hashPair(computedHash, proof[i]);
        }
        return computedHash;
    }

    function _hashPair(bytes32 a, bytes32 b) private pure returns (bytes32) {
        return a < b ? _efficientHash(a, b) : _efficientHash(b, a);
    }

    function _efficientHash(bytes32 a, bytes32 b) private pure returns (bytes32 value) {
        /// @solidity memory-safe-assembly
        assembly {
            mstore(0x00, a)
            mstore(0x20, b)
            value := keccak256(0x00, 0x40)
        }
    }
}
```

### `BridgeExample.sol`

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "./ISendPort.sol";
import "./ReceivePort.sol";

contract BridgeExample is ReceivePort {
    using SafeERC20 for IERC20;

    ISendPort public sendPort;

    mapping(bytes32 => bool) public usedMsgHashes;

    mapping(uint => address) public trustBridges;

    mapping(address => address) public crossPairs;

    constructor(address sendPortAddr) {
        sendPort = ISendPort(sendPortAddr);
    }

    function setTrustBridge(uint chainId, address bridge) public onlyOwner {
        trustBridges[chainId] = bridge;
    }

    function setCrossPair(address fromTokenAddr, address toTokenAddr) public onlyOwner {
        crossPairs[fromTokenAddr] = toTokenAddr;
    }

    function getLeaves(uint packageIndex, uint start, uint num) view public returns(bytes32[] memory) {
        ISendPort.Package memory p = sendPort.getPackage(packageIndex);
        bytes32[] memory result = new bytes32[](num);
        for (uint i = 0; i < p.leaves.length && i < num; i++) {
            result[i] = p.leaves[i + start];
        }
        return result;
    }

    function transferTo(
        uint toChainId,
        address fromTokenAddr,
        uint amount,
        address receiver
    ) public {
        bytes32 msgHash = keccak256(
            abi.encodePacked(toChainId, fromTokenAddr, amount, receiver)
        );
        sendPort.addMsgHash(msgHash, toChainId);

        IERC20(fromTokenAddr).safeTransferFrom(msg.sender, address(this), amount);
    }

    function transferFrom(
        uint fromChainId,
        uint packageIndex,
        bytes32[] memory proof,
        address fromTokenAddr,
        uint amount,
        address receiver
    ) public {
        bytes32 msgHash = keccak256(
            abi.encodePacked(block.chainid, fromTokenAddr, amount, receiver)
        );

        require(!usedMsgHashes[msgHash], "transferFrom: Used msgHash");

        require(
            verify(
                fromChainId,
                packageIndex,
                proof,
                msgHash,
                trustBridges[fromChainId]
            ),
            "transferFrom: verify failed"
        );

        usedMsgHashes[msgHash] = true;

        address toTokenAddr = crossPairs[fromTokenAddr];
        require(toTokenAddr != address(0), "transferFrom: fromTokenAddr is not crossPair");
        IERC20(toTokenAddr).safeTransfer(receiver, amount);
    }
}
```


## セキュリティ上の考慮事項

クロスチェーンブリッジ間の競争と二重支払いについて:

`SendPort` は1つのタスクを担当しています: クロスチェーン転送対象のメッセージをパッケージ化すること。メッセージの送信と検証は、各クロスチェーンブリッジプロジェクトによって独立して実装されます。目的は、ソースチェーンで異なるクロスチェーンブリッジが取得したクロスチェーンメッセージが一致することを確保することです。したがって、クロスチェーンブリッジ間で、メッセージの転送や検証の権利を競う必要はありません。各ブリッジは独立して動作します。クロスチェーンブリッジの実装にバグがある場合、それ自体にリスクがありますが、他のクロスチェーンブリッジには影響しません。

**提案**:

1. `IReceivePort.receivePackages()` を誰でも呼び出せないようにする。
2. 検証を行う際は、二重支払いを防ぐために検証済みの `msgHash` を保存する。
3. MerkleTreeのすべての送信者を信頼しない。

クロスチェーンメッセージの偽造について:

`SendPort` は使用制限のないパブリックコントラクトであるため、誰でも任意のクロスチェーンメッセージを送信できます。`SendPort` は `msg.sender` をパッケージ化プロセスに含めます。ハッカーがクロスチェーンメッセージを偽造しようとした場合、ハッカーのアドレスがメッセ
ージとともにパッケージ化されます。検証時に、ハッカーのアドレスを特定できます。これが、MerkleTreeのすべての送信者を信頼しないことを提案する理由です。

メッセージの順序について:

`SendPort` は受信したクロスチェーンメッセージを時間順にソートしますが、検証時の順序は保証されません。例えば、ユーザーが10 ETHと20 USDTのクロスチェーン転送を行った場合、ターゲットチェーンでは20 USDTを先に引き出し、次に10 ETHを引き出す、あるいはその逆の順序になる可能性があります。具体的な順序は `IReceivePort` の実装によって異なります。


## 著作権

著作権およびその関連権利は [CC0](../LICENSE.md) により放棄されています。