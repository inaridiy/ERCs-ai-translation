---
original: 76be72d8057f4f8e971fe0d61afcae0997e37b56cbbc45002a8220186203cdaf
---

---
eip: 7092
title: 金融債券
description: 事業体が投資家に発行する債務を表します。
author: Samuel Gwlanold Edoumou (@Edoumou)
discussions-to: https://ethereum-magicians.org/t/financial-bonds/14461
status: 最終
type: Standards Track
category: ERC
created: 2023-05-28
requires: 165
---

## 概要

本提案では、一定収入金融債券の主要な特性を定義し、一次市場での債券発行と二次市場での債券の売買を容易にします。また、この規格は複数のブロックチェーンにまたがる債券の運用と管理のためのクロスチェーン機能も提供します。

## 動機

固定利付き金融商品は、企業や他の事業体が資金調達に広く利用されている資産クラスです。しかし、トークン化された債券への移行は、[ERC-3475](./eip-3475.md)のような既存の規格が馴染みのない概念を導入し、不必要なガス消費につながるため、課題があります。さらに、クーポン、満期日、元本といった名称付き変数がないため、開発者がどのメタデータがどのパラメータに割り当てられているかを覚える必要があり、ERC-3475の実装が困難です。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

**このERCに準拠するすべてのコントラクトは、以下のトークンインターフェースと[ERC-165](./eip-165.md)インターフェースを実装しなければなりません:**

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

/**
* @title ERC-7092 金融債券標準
*/
interface IERC7092 /** is ERC165 */ {
    // イベント
    /**
    * @notice コントラクト作成時を除き、債券トークンが移転、発行、償還されたときに必ず発行されなければならない
    * @param _from 債券を所有するアカウント
    * @param _to 債券を受け取るアカウント
    * @param _amount 移転する債券トークンの量
    */
    event Transfer(address indexed _from, address indexed _to, uint256 _amount);

    /**
    * @notice アカウントが承認されたとき、または許可が減少したときに必ず発行されなければならない
    * @param _owner 債券トークンの所有者
    * @param _spender 債券を使用することが許可されたアカウント
    * @param _amount `_owner`が`_spender`に許可した債券トークンの量
    *        または`_spender`の許可を減少させる債券トークンの量
    */
    event Approval(address indexed _owner, address indexed _spender, uint256 _amount);

    /**
    * @notice コントラクト作成時を除き、複数の債券トークンが移転、発行、償還されたときに必ず発行されなければならない
    * @param _from 債券保有者のアカウントの配列
    * @param _to 債券を移転するアカウントの配列
    * @param _amount 移転する債券トークンの量の配列
    *
    ** OPTIONAL - インターフェースやその他のコントラクトはこの関数が存在することを期待してはいけません。 `batchTransfer`および`batchTransferFrom`関数で発行されなければなりません
    */
    event TransferBatch(address[] _from, address[] _to, uint256[] _amount);

    /**
    * @notice 複数のアカウントが承認されたとき、または複数のアカウントの許可が減少したときに必ず発行されなければならない
    * @param _owner 債券保有者のアカウント
    * @param _spender 債券を使用することが許可された、または許可が減少したアカウントの配列
    * @param _amount `_owner`が複数のアカウントに`_spender`に許可した、または許可を減少させた債券トークンの量の配列
    *
    ** OPTIONAL - インターフェースやその他のコントラクトはこの関数が存在することを期待してはいけません。 `batchApprove`および`batchDecreaseAllowance`関数で発行されなければなりません
    */
    event ApprovalBatch(address indexed _owner, address[] _spender, uint256[] _amount);

    // ゲッター関数
    /**
    *  @notice 債券のISINを返す
    */
    function isin() external view returns(string memory);

    /**
    * @notice 債券の名称を返す
    */
    function name() external view returns(string memory);

    /**
    * @notice 債券のシンボルを返す
    *         シンボルは発行体の略称と満期日の組み合わせが推奨されます
    *         例: 「Green Energy」という会社が2030年10月25日に満期の債券を発行する場合、債券シンボルは「GE30」、「GE2030」、または「GE102530」などが考えられます
    */
    function symbol() external view returns(string memory);

    /**
    * @notice 債券の通貨を返す。これは債券の元本を返済するために使用されるトークンのコントラクトアドレスです
    */
    function currency() external view returns(address);

    /**
    * @notice 債券の額面を返す。これは債券が発行される最小単位です。元本通貨単位で表現されなければなりません
    *         例: 額面が1,000で通貨がUSDCの場合、債券の額面は1,000 USDCになります
    */
    function denomination() external view returns(uint256);

    /**
    * @notice 発行額(総債務額)を返す。発行額は額面単位で表現することが推奨されます。
    */
    function issueVolume() external view returns(uint256);

    /**
    * @notice 債券の利率を返す。利率はベーシスポイント単位で表現することが推奨されます。
    *         1ベーシスポイント = 0.01% = 0.0001
    *         例: 利率が5%の場合、coupon() => 500ベーシスポイント
    */
    function couponRate() external view returns(uint256);

    /**
    * @notice 債券が投資家に発行された日付を返す。これはblock.timestampのようなUnixタイムスタンプです
    */
    function issueDate() external view returns(uint256);

    /**
    * @notice 債券の満期日、つまり元本が返済される日付を返す。これはblock.timestampのようなUnixタイムスタンプです
    *         満期日は発行日より後の日付でなければなりません
    */
    function maturityDate() external view returns(uint256);

    /**
    * @notice アカウントの元本を返す。元本は債券通貨単位(USDC、DAI等)で表現することが推奨されます
    * @param _account アカウントアドレス
    */
    function principalOf(address _account) external view returns(uint256);

    /**
    * @notice `_owner`アカウントが`_spender`アカウントに債券の管理を許可した量を返す
    * @param _owner 債券保有者のアドレス
    * @param _spender 債券保有者に許可されたアドレス
    */
    function allowance(address _owner, address _spender) external view returns(uint256);

    // セッター関数
    /**
    * @notice `_spender`アカウントに`_amount`の債券トークンの管理を許可する
    * @param _spender 債券保有者に許可されるアドレス
    * @param _amount 承認する債券トークンの量
    */
    function approve(address _spender, uint256 _amount) external returns(bool);

    /**
    * @notice `_spender`の許可を`_amount`減少させる
    * @param _spender 許可を減少させるアドレス
    * @param _amount 許可から減少させる債券トークンの量
    */
    function decreaseAllowance(address _spender, uint256 _amount) external returns(bool);

    /**
    * @notice `_amount`の債券を`_to`アドレスに移転する。このメソッドでは、移転される債券にデータを添付することもできる
    * @param _to 債券を送信するアドレス
    * @param _amount 移転する債券トークンの量
    * @param _data トークン保有者が提供する追加情報
    */
    function transfer(address _to, uint256 _amount, bytes calldata _data) external returns(bool);

    /**
    * @notice `_from`アカウントから`_to`アカウントに`_amount`の債券を移転する。呼び出し側は`_from`アカウントによって承認されている必要がある
    *         このメソッドでは、移転される債券にデータを添付することもできる
    * @param _from 債券保有者のアドレス
    * @param _to 債券を移転するアドレス
    * @param _amount 移転する債券トークンの量
    * @param _data トークン保有者が提供する追加情報
    */
    function transferFrom(address _from, address _to, uint256 _amount, bytes calldata _data) external returns(bool);

    // バッチ関数
    /**
    * @notice 複数の`_spender`アカウントに指定の`_amount`の債券トークンの管理を許可する
    * @param _spender 債券保有者に許可されるアカウントの配列
    * @param _amount 各アカウントに許可する債券トークンの量の配列
    *
    * OPTIONAL - インターフェースやその他のコントラクトはこれらの値が存在することを期待してはいけません。ユーザビリティを向上させるために使用されるメソッドです。
    */
    function batchApprove(address[] calldata _spender, uint256[] calldata _amount) external returns(bool);

    /**
    * @notice 複数の`_spender`の許可を`_amount`の対応する量だけ減少させる
    * @param _spender 許可を減少させるアカウントの配列
    * @param _amount 各アカウントの許可から減少させる債券トークンの量の配列
    *
    * OPTIONAL - インターフェースやその他のコントラクトはこの関数が存在することを期待してはいけません。トークンの許可を減少させるために使用されるメソッドです。
    */
    function batchDecreaseAllowance(address[] calldata _spender, uint256[] calldata _amount) external returns(bool);

    /**
    * @notice `_amount`の配列の量の債券を、`_to`の配列のアカウントに移転する。追加情報を添付することもできる
    * @param _to 債券を送信するアカウントの配列
    * @param _amount 各アカウントに移転する債券トークンの量の配列
    * @param _data トークン保有者が提供する追加情報の配列
    *
    * OPTIONAL - インターフェースやその他のコントラクトはこの関数が存在することを期待してはいけません。
    */
    function batchTransfer(address[] calldata _to, uint256[] calldata _amount, bytes[] calldata _data) external returns(bool);

    /**
    * @notice `_from`アカウントから`_to`アカウントに`_amount`の配列の量の債券を移転する。追加情報を添付することもできる
    * @param _from 債券保有者のアカウントの配列
    * @param _to 債券を移転するアカウントの配列
    * @param _amount 各アカウントに移転する債券トークンの量の配列
    * @param _data トークン保有者が提供する追加情報の配列
    *
    ** OPTIONAL - インターフェースやその他のコントラクトはこの関数が存在することを期待してはいけません。
    */
    function batchTransferFrom(address[] calldata _from, address[] calldata _to, uint256[] calldata _amount, bytes[] calldata _data) external returns(bool);
}
```

### 追加の債券パラメータインターフェース

`IERC7092ESG`インターフェースは、この提案を実装するコントラクトにとってオプションです。このインターフェースは、標準の使いやすさを向上させるために使用される可能性があります。

- `currencyOfCoupon`クーポン支払いに使用される通貨は、元本返済に使用される通貨と異なる場合があります
- `couponType`は、発行体が投資家に支払うことを約束した利率の種類を示すために使用される可能性があります。これには、ゼロクーポン、固定レート、変動レートなどがあります。
- `couponFrequency`は、債券がその債券保有者に利息を支払う頻度を指し、期間(年間、半年間、四半期、月間など)で表されます。
- `dayCountBasis`は、2つのクーポン支払日または特定の期間の間の債券の発生利息を計算するために使用されます。一部の日数計算基準には、Actual/Actual、30/360、Actual/360、Actual/365、30/365などがあります

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

interface IERC7092ESG /** is ERC165 */ {
    /**
    * @notice トークンの小数点以下の桁数を返す。例えば10を返す場合、トークンの量は10000000000倍する必要があることを意味する
*/
    function decimals() external view returns(uint8);

    /**
    * @notice クーポン支払いに使用されるトークンのコントラクトアドレスを返す。元本に使用されるものと同じ場合もある
    */
    function currencyOfCoupon() external view returns(address);

    /**
    * @notice クーポンの種類を返す
    *         例えば、0はゼロクーポン、1は固定レート、2は変動レートなどを表す
    */
    function couponType() external view returns(uint8);

    /**
    * @notice クーポンの支払い頻度、つまり1年間のクーポン支払い回数を返す
    */
    function couponFrequency() external view returns(uint256);

    /**
    * @notice 日数計算基準を返す
    *         例えば、0はactual/actual、1はactual/360などを表す
    */
    function dayCountBasis() external view returns(uint8);
}
```

### クロスチェーンインターフェース

この規格では、債券トークンのクロスチェーン管理のために`IERC7092CrossChain`インターフェースの実装を許可しています。このインターフェースはオプションであり、アプリケーションがクロスチェーンのトランザクションを可能にするために使用される可能性があります。クロスチェーンのトランザクションを開始する関数は、明示的に宛先チェーンID `destinationChainID`を定義し、ターゲットのスマートコントラクト `destinationContract`を指定しなければなりません。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

interface IERC7092CrossChain /** is ERC165 */ {
    // イベント
    /**
    * @notice クロスチェーンのトランザクションで債券トークンが移転または償還されたときに必ず発行されなければならない
    * @param _from 債券保有者のアカウント
    * @param _to 債券を受け取るアカウント
    * @param _amount 移転する債券トークンの量
    * @param _destinationChainID 宛先チェーンを識別する一意のID
    */
    event CrossChainTransfer(address indexed _from, address indexed _to, uint256 _amount, bytes32 _destinationChainID);

    /**
    * @notice 複数の債券トークンがクロスチェーンのトランザクションで移転または償還されたときに必ず発行されなければならない
    * @param _from 債券保有者のアカウントの配列
    * @param _to 債券を受け取るアカウントの配列
    * @param _amount 移転する債券トークンの量の配列
    * @param _destinationChainID 宛先チェーンを識別する一意のIDの配列
    */
    event CrossChainTransferBatch(address[] _from, address[] _to, uint256[] _amount, bytes32[] _destinationChainID);

    /**
    * @notice 現在のチェーンとは異なる宛先チェーンで、アカウントが債券保有者のトークンを使用することが承認されたときに必ず発行されなければならない
    * @param _owner 債券保有者のアカウント
    * @param _spender 債券を使用することが許可されたアカウント
    * @param _amount `_owner`が`_spender`に許可した債券トークンの量
    * @param _destinationChainID 宛先チェーンを識別する一意のID
    */
    event CrossChainApproval(address indexed _owner, address indexed _spender, uint256 _amount, bytes32 _destinationChainID);

    /**
    * @notice 現在のチェーンとは異なる宛先チェーンで、`_spender`の配列のアカウントが承認されたか、または`_spender`の配列のアカウントの許可が減少したときに必ず発行されなければならない
    * @param _owner 債券トークンの所有者
    * @param _spender 債券を使用することが許可されたアカウントの配列
    * @param _amount `_owner`が複数のアカウントに`_spender`に許可した、または許可を減少させた債券トークンの量の配列
    * @param _destinationChainID 宛先チェーンを識別する一意のIDの配列
    */
    event CrossChainApprovalBatch(address indexed _owner, address[] _spender, uint256[] _amount, bytes32[] _destinationChainID);

    // 関数
    /**
    * @notice `_spender`アカウントに、宛先チェーンで`_amount`の債券トークンの管理を許可する
    * @param _spender 債券保有者に許可されるアカウント
    * @param _amount 承認する債券トークンの量
    * @param _destinationChainID 宛先チェーンを識別する一意のID
    * @param _destinationContract 宛先チェーンで相互作用するスマートコントラクト
    */
    function crossChainApprove(address _spender, uint256 _amount, bytes32 _destinationChainID, address _destinationContract) external returns(bool);

    /**
    * @notice `_spender`の配列のアカウントに、`_amount`の配列の量の債券トークンの管理を、宛先チェーンで許可する
    * @param _spender 債券保有者に許可されるアカウントの配列
    * @param _amount 各アカウントに許可する債券トークンの量の配列
    * @param _destinationChainID 宛先チェーンを識別する一意のIDの配列
    * @param _destinationContract 宛先チェーンで相互作用するスマートコントラクトの配列
    */
    function crossChainBatchApprove(address[] calldata _spender, uint256[] calldata _amount, bytes32[] calldata _destinationChainID, address[] calldata _destinationContract) external returns(bool);

    /**
    * @notice `_spender`の許可を、宛先チェーンで`_amount`減少させる
    * @param _spender 許可を減少させるアドレス
    * @param _amount 許可から減少させる債券トークンの量
    * @param _destinationChainID 宛先チェーンを識別する一意のID
    * @param _destinationContract 宛先チェーンで相互作用するスマートコントラクト
    */
    function crossChainDecreaseAllowance(address _spender, uint256 _amount, bytes32 _destinationChainID, address _destinationContract) external returns(bool);

    /**
    * @notice `_spender`の配列のアカウントの許可を、`_amount`の配列の対応する量だけ、宛先チェーンで減少させる
    * @param _spender 許可を減少させるアカウントの配列
    * @param _amount 各アカウントの許可から減少させる債券トークンの量の配列
    * @param _destinationChainID 宛先チェーンを識別する一意のIDの配列
    * @param _destinationContract 宛先チェーンで相互作用するスマートコントラクトの配列
    */
    function crossChainBatchDecreaseAllowance(address[] calldata _spender, uint256[] calldata _amount, bytes32[] calldata _destinationChainID, address[] calldata _destinationContract) external returns(bool);

    /**
    * @notice `_amount`の債券トークンを`_to`アカウントに、現在のチェーンから別のチェーンに移転する(例: EthereumからPolygonに移転)。
    *         このメソッドでは、移転される債券にデータを添付することもできる
    * @param _to 債券を送信するアカウント
    * @param _amount 移転する債券トークンの量
    * @param _data トークン保有者が提供する追加情報
    * @param _destinationChainID 宛先チェーンを識別する一意のID
    * @param _destinationContract 宛先チェーンで相互作用し、移転された債券トークンをデポジットまたはミントするスマートコントラクト
    */
    function crossChainTransfer(address _to, uint256 _amount, bytes calldata _data, bytes32 _destinationChainID, address _destinationContract) external returns(bool);

    /**
    * @notice `_amount`の配列の量の債券トークンを、`_to`の配列のアカウントに、現在のチェーンから別のチェーンに移転する(例: EthereumからPolygonに移転)。
    *         このメソッドでは、移転される債券にデータを添付することもできる
    * @param _to 債券を送信するアカウントの配列
    * @param _amount 各アカウントに移転する債券トークンの量の配列
    * @param _data トークン保有者が提供する追加情報の配列
    * @param _destinationChainID 宛先チェーンを識別する一意のIDの配列
    * @param _destinationContract 宛先チェーンで相互作用し、移転された債券トークンをデポジットまたはミントするスマートコントラクトの配列
    */
    function crossChainBatchTransfer(address[] calldata _to, uint256[] calldata _amount, bytes[] calldata _data, bytes32[] calldata _destinationChainID, address[] calldata _destinationContract) external returns(bool);

    /**
    * @notice `_from`アカウントから`_to`アカウントに`_amount`の債券トークンを、現在のチェーンから別のチェーンに移転する。呼び出し側は`_from`アカウントによって承認されている必要がある
    *         このメソッドでは、移転される債券にデータを添付することもできる
    * @param _from 債券保有者のアドレス
    * @param _to 債券を移転するアドレス
    * @param _amount 移転する債券トークンの量
    * @param _data トークン保有者が提供する追加情報
    * @param _destinationChainID 宛先チェーンを識別する一意のID
    * @param _destinationContract 宛先チェーンで相互作用し、移転されたトークンをデポジットまたはミントするスマートコントラクト
    */
    function crossChainTransferFrom(address _from, address _to, uint256 _amount, bytes calldata _data, bytes32 _destinationChainID, address _destinationContract) external returns(bool);

    /**
    * @notice `_amount`の配列の量の債券トークンを、`_from`の配列のアカウントから`_to`の配列のアカウントに、現在のチェーンから別のチェーンに移転する。
    *         呼び出し側は、`_from`アカウントによって`_amount`の対応する量の支出が承認されている必要がある
    *         このメソッドでは、移転される債券にデータを添付することもできる
    * @param _from 債券保有者のアドレスの配列
    * @param _to 債券を移転するアドレスの配列
    * @param _amount 各アカウントに移転する債券トークンの量の配列
    * @param _data トークン保有者が提供する追加情報の配列
    * @param _destinationChainID 宛先チェーンを識別する一意のIDの配列
    * @param _destinationContract 宛先チェーンで相互作用し、移転されたトークンをデポジットまたはミントするスマートコントラクトの配列
    */
    function crossChainBatchTransferFrom(address[] calldata _from, address[] calldata _to, uint256[] calldata _amount, bytes[] calldata _data, bytes32[] calldata _destinationChainID, address[] calldata _destinationContract) external returns(bool);
}
```

## 根拠

このERCの設計は、従来の債券基準との一貫性を維持することで、トークン化された債券への移行を簡素化することを目的としています。このアプローチにより、固定利付き金融商品をオンチェーントークンとして表現し、ウォレットで管理し、分散型取引所などのアプリケーションで利用することができます。一方で、他の基準に関連する複雑性と非効率性を回避することができます。このERCは、従来の債券に似た特性を持つ新しい債券トークンの作成を容易にし、債券取引と管理の利便性、流動性、コスト効率を向上させます。

「発行額」や「元本」といった伝統的な金融用語の使用は、従来の債券の用語との一貫性を維持することを目的としており、従来の事業体の適応を容易にします。

### 総供給量とアカウントの残高

`totalSupply`と`balanceOf`関数は定義されていませんが、`issueVolume`、`principalOf`、`denomination`から導出できます。ただし、これらの関数は、この基準を実装するあらゆるコントラクトに追加できるため、これらの値の適切な関係が確保されます。

```solidity
    function totalSupply() external view returns(uint256) {
        return issueVolume() / denomination();
    }

    function balance0f(account) external view returns(uint256) {
        return principal(account) / denomination();
    }
```

## 下位互換性

このERCは、[ERC-20](./eip-20.md)や[ERC-1155](./eip-1155.md)などの既存の基準とは下位互換性がありません。これは、`totalSupply`や`balanceOf`などの機能がないためです。このスタンダードの純粋な実装は、トークン化された債券の発行に推奨されます。他の上記の基準との混合ソリューションは失敗する可能性があります。

## リファレンス実装

完全なリファレンス実装は[こちら](../assets/eip-7092/ERC7092.sol)
## リファレンス実装

完全なリファレンス実装は[こちら](../assets/eip-7092/ERC7092.sol)にあります。

コール可能、プット可能、転換可能などのオプション付き債券は、提案された[`ERC7092.sol`](../assets/eip-7092/ERC7092.sol)インターフェースを継承することで作成できます。

### コール可能債券:

```solidity
pragma solidity ^0.8.0;

import 'ERC7092.sol';

contract ERC7092Callable is ERC7092 {
    // 発行体がコールできるようにするロジックを記述する
    // 必要な状態変数と関数

    /**
    * @notice `_investor`が所有する債券をコールする
    *         発行体のみが呼び出すことができる
    */
    function call(address _investor) public {
        require(msg.sender == _issuer[bondISIN].issuerAddress, "ERC7092Callable: ONLY_ISSUER");
        require(_principals[_investor] > 0, "ERC7092Callable: NO_BONDS");
        require(block.timestamp < _bond[bondISIN].maturityDate, "ERC7092Callable: BOND_MATURED");
        
        uint256 principal =  _principals[_investor];
        _principals[_investor] = 0;
        
        // ここにロジックを追加する
    }
}
```

### プット可能債券:

```solidity
pragma solidity ^0.8.0;

import 'ERC7092.sol';

contract ERC7092Puttable is ERC7092 {
    // 投資家がプットできるようにするロジックを記述する
    // 必要な状態変数と関数
    
    /**
    * @notice 債券をプットする
    *         債券を所有する投資家のみが呼び出すことができる
    */
    function put() public {
        require(_principals[msg.sender] > 0, "ERC7092Puttable: ONLY_INVESTORS");
        require(block.timestamp < _bond[bondISIN].maturityDate, "ERC7092Puttable: BOND_MATURED");
        
        uint256 principal =  _principals[msg.sender];
        _principals[msg.sender] = 0;
        
        // ここにロジックを追加する
    }
}
```

### 転換可能債券:

```solidity
pragma solidity ^0.8.0;

import 'ERC7092.sol';

contract ERC7092Convertible is ERC7092 {
    // 投資家または発行体が債券を株式に転換できるようにするロジックを記述する
    // 必要な状態変数と関数
    
    /**
    * @notice 債券を株式に転換する。ここでは、投資家が自身の債券を株式に転換しなければならないと仮定している
    *         発行体も投資家の債券を株式に転換できる
    */
    function convert() public {
        require(_principals[msg.sender] > 0, "ERC7092Convertible: ONLY_INVESTORS");
        require(block.timestamp < _bond[bondISIN].maturityDate, "ERC7092Convertible: BOND_MATURED");
        
        uint256 principal =  _principals[msg.sender];
        _principals[msg.sender] = 0;
        
        // ここにロジックを追加する
    }
}
```

### 身元登録

この基準は、債券をトークン化することを目的としています。債券保有者の身元に関する情報を本質的に管理するものではありません。ただし、規制要件への準拠を強化し、透明性を向上させるために、この基準の上にアイデンティティレジストリを追加することができます。

身元登録を維持することで、発行体は`ERC7092`基準の下で発行された債券トークンが、登録済みで承認された事業体にのみ移転されるようにすることができます。この慣行は規制コンプライアンスの要件に沿っており、債券保有者の身元を管理および検証する構造化された方法を提供します。また、債券トークンの無許可または非準拠の移転を防ぐことにも役立ちます。

## セキュリティに関する考慮事項

このERCを実装するには、オーナーの債券を管理するオペレーターを承認する関数や、債券の移転を可能にする関数に関連するセキュリティリスクを慎重に検討する必要があります。これらの関数の使用には、債券の所有者または承認されたアカウントのみが呼び出せるよう、堅牢な検証が必要です。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。