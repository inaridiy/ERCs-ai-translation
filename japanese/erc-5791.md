---
original: ea825f06a52733bf06e067c1e66f17e9a56a130d33ae3055705ab472f7096da9
---

---
eip: 5791
title: 物理的に裏付けられたトークン
description: ERC-721 NFTの所有権を物理的なチップにリンクするための最小限のインターフェース
author: 2pmflow (@2pmflow)、locationtba (@locationtba)、Cameron Robertson (@ccamrobertson)、cygaar (@cygaar)、Brian Weick (@bweick)
discussions-to: https://ethereum-magicians.org/t/physical-backed-tokens/11350
status: ドラフト
type: Standards Track
category: ERC
created: 2022-10-17
requires: 191, 721
---

## 概要

このスタンダードは[ERC-721](./eip-721.md)の拡張です。[ERC-721](./eip-721.md) NFTを「物理的に裏付けられた」ものとし、NFTの物理的な対応物の所有者が所有するための最小限のインターフェースを提案しています。

## 動機

NFTコレクターは、デジタルアセットを収集し、オンラインで他の人と共有することを楽しんでいます。しかし、物理的なアセットをNFTとして認証された所有権とともに表示するための標準はまだありません。既存のソリューションは断片化されており、少なくとも以下のいずれかの問題を抱えています:

- 物理的なアイテムの所有権とNFTの所有権が切り離されている。
- 物理的なアイテムの真正性を検証するには、信頼できる第三者の関与が必要である(例: StockX)。

## 仕様

この文書の中の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

### 要件

このアプローチでは、物理的なアイテムに以下の要件を満たすチップが取り付けられている必要があります:

- チップは、ECDSA secp256k1の非対称鍵ペアを安全に生成および保存できること。
- チップは、生成された非対称鍵ペアの秘密鍵を使ってメッセージに署名できること。
- チップは公開鍵を公開すること。
- 秘密鍵を抽出することはできないこと。

このアプローチでは、また、コントラクトが[ERC-721](./eip-721.md)のアカウントバウンド実装(すべての[ERC-721](./eip-721.md)転送機能がスローする、例えば[ERC-721](./eip-721.md)で参照されている「読み取り専用NFTレジストリ」の実装)を使用することも要求しています。これにより、物理的なアイテムの所有権がNFTの所有権の管理と転送を開始するために必要となり、このインターフェースで導入された新しい機能を通じて行われるようになります。

### アプローチ

各NFTは概念的に物理的なチップにリンクされています。

NFTがミントされる際、対応するチップアドレス(チップの公開鍵から派生した20バイトのアドレス)を含むイベントも発行されなければなりません。これにより、ダウンストリームのインデクサーがNFTコレクションのどのチップアドレスがどのトークンにマッピングされているかを知ることができます。NFTをミントするには、そのトークンIDをある特定のチップにリンクさせる必要があります。

このインターフェースには、有効な署名がチップから渡された場合に、NFTを呼び出し元に転送する`transferTokenWithChip`という関数が含まれています。有効な署名は、[ERC-191](./eip-191.md)および[EIP-2](./eip-2.md)(s値の制限)に定められた方式に従わなければなりません。署名するデータは、宛先アドレス(呼び出し元)と最近のブロックハッシュ(最新性のレベルは実装次第)で構成されます。

このインターフェースには、物理的なアイテムのチップがコレクション内の既存のNFTをバックアップしているかどうかを検証できるその他の関数も含まれています。

### インターフェース

```solidity
interface IERC5791 {
    /// @notice チップアドレスに対応するトークンIDを返します。
    /// @dev チップがコレクション内に存在しない場合はエラーをスローします。
    /// @param chipAddress 物理的なアイテムに埋め込まれたチップのアドレス(チップの公開鍵から計算されたもの)。
    /// @return 渡されたチップアドレスに対応するトークンID。
    function tokenIdFor(address chipAddress) external view returns (uint256);

    /// @notice 指定したトークンIDのチップが、ペイロードの署名の署名者であるかどうかを返します。
    /// @dev トークンIDがコレクション内に存在しない場合はエラーをスローします。
    /// @param tokenId トークンID。
    /// @param payload チップが署名するための任意のデータ。
    /// @param signature ペイロードに対するチップの署名。
    /// @return ペイロードの署名がトークンIDにリンクされたチップによって署名されたかどうか。
    function isChipSignatureForToken(uint256 tokenId, bytes calldata payload, bytes calldata signature)
        external
        view
        returns (bool);

    /// @notice トークンをメッセージ送信者のウォレットに転送します。
    /// @param signatureFromChip (msgSender, blockhash)のEIP-191署名、ここでblockhashはblockNumberUsedInSigのブロックハッシュです。
    /// @param blockNumberUsedInSig signatureFromChipで使用されたブロック番号。最近のブロック番号である必要があります。
    /// @param useSafeTransferFrom 実装ではEIP-721のsafeTransferFromを使用するかどうか、それともtransferFromを使用するか。
    ///
    /// @dev 実装では、ブロック番号が十分に最近のものであることを確認し、リプレイ攻撃を回避する必要があります。
    /// また、署名から復元されたアドレスがmsgSenderと一致することも確認する必要があります。
    /// 署名から復元されたアドレスが既存のトークンにリンクされたチップアドレスと一致する場合、トークンをmsgSenderに転送する必要があります。
    /// チップにリンクされたトークンが存在しない場合は、関数をエラーにする必要があります。
    function transferTokenWithChip(
        bytes calldata signatureFromChip,
        uint256 blockNumberUsedInSig,
        bool useSafeTransferFrom
    ) external;

    /// @notice 上記のtransferTokenWithChipを呼び出しますが、useSafeTransferFromをfalseに設定します。
    function transferTokenWithChip(bytes calldata signatureFromChip, uint256 blockNumberUsedInSig) external;

    /// @notice トークンがミントされたときに発行されるイベント
    event PBTMint(uint256 indexed tokenId, address indexed chipAddress);

    /// @notice トークンがチップに再マッピングされたときに発行されるイベント
    /// チップの交換は特定のシナリオ(例:チップの不具合)で有用な場合があります。
    event PBTChipRemapping(uint256 indexed tokenId, address indexed oldChipAddress, address indexed newChipAddress);
}
```

この EIP を実装するERC-721トークンを認識するために、[ERC-165](./eip-165.md)の`function supportsInterface(bytes4 interfaceID) external view returns (bool)`を呼び出し、`interfaceID=0x4901df9f`を渡すと、true を返す必要があります。

ミントのインターフェースは実装次第です。ミントされたNFTの所有者は、物理的なチップの所有者(この認証は`transferTokenWithChip`で定義された署名スキームを使って実装できる)である必要があります。

## 根拠

このソリューションの目的は、中央集権的な権威なしで物理的なアイテムをデジタルNFTにリンクするための最も単純な道筋を提供することです。

インターフェースには、署名スキームに関して意見のある`transferTokenWithChip`関数が含まれています。これにより、この EIP を実装するあらゆるNFTの転送をサポートする、集約型のようなプロダクトを将来的に構築できるようになります。

### 範囲外

以下は、意図的にこの EIP の範囲外とされている周辺的な問題です:

- 特定のNFTコレクションのチップアドレスが実際に物理的なチップに埋め込まれたものであり、任意のEOAではないことを信頼すること
- チップの劣化や損傷を防ぐこと
- チップが物理的なアイテムに付着し続けることを保証すること
- など

これらの課題については並行して取り組んでいます。

トークンIDとチップアドレスのマッピングも範囲外です。これは複数の方法で行うことができます。例えば、コントラクトの所有者がミント前にこのマッピングをプリセットする、ミント関数にトークンID とチップアドレスのタプルを渡し、それをコントラクトに信頼されるアドレスが事前に署名する、信頼できるレジストリでルックアップする、トークンIDをミント時に先着順で割り当てるなどです。

さらに、物理的なアイテムの所有者は、チップの署名を他の人に送ることで、NFTを別のウォレットに転送することができます。これでも、所有権の管理が物理的なアイテムに紐付けられているため、NFTは物理的に裏付けられたものと見なされます。これは、(1)アイテムの所有者がチップに署名する必要があり、(2)アイテムの所有者がいつでもNFTの所有権を取り戻せるため、アイテムの所有者が一時的にアイテムを他の人に貸し出しているものと解釈できます。

## 下位互換性

この提案は、APIレベルでは[ERC-721](./eip-721.md)と下位互換性があります。上述のように、トークンが物理的に裏付けられるためには、コントラクトが[ERC-721](./eip-721.md)のアカウントバウンド実装(すべての[ERC-721](./eip-721.md)転送機能がスローする)を使用する必要があります。そうすることで、転送はここで導入された新しい関数を通じて行われ、チップの署名が必要となります。

## 参考実装

チップの署名からチップアドレスを復元する方法の例を以下に示します。

```solidity
import '@openzeppelin/contracts/utils/cryptography/ECDSA.sol';

function getChipAddressFromChipSignature(
  bytes calldata signatureFromChip,
  uint256 blockNumberUsedInSig
) internal returns (TokenData memory) {
  if (block.number <= blockNumberUsedInSig) {
    revert InvalidBlockNumber();
  }
  unchecked {
    if (block.number - blockNumberUsedInSig > getMaxBlockhashValidWindow()) {
      revert BlockNumberTooOld();
    }
  }
  bytes32 blockHash = blockhash(blockNumberUsedInSig);
  bytes32 signedHash = keccak256(abi.encodePacked(_msgSender(), blockHash))
    .toEthSignedMessageHash();
  address chipAddr = signedHash.recover(signatureFromChip);
}
```

## セキュリティ上の考慮事項

`transferTokenWithChip`に渡される[ERC-191](./eip-191.md)の署名には、関数呼び出し元のアドレスが署名データに含まれているため、署名を再利用したリプレイ攻撃を防ぐことができます。また、最近のブロックハッシュも要求されるため、悪意のあるチップの所有者が短期間のウィンドウ内で事前に署名を生成して使うことを防ぐことができます。

さらに、トークンが物理的に裏付けられていると見なされる信頼レベルは、物理的なチップのセキュリティに依存しますが、これはこの EIP の範囲外です。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。