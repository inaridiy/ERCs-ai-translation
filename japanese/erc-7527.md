---
original: 1be8fad617fe55ce06d3173614719f4762b720467f6d685d84a95629cac0d70f
---

---
eip: 7527
title: トークンバウンド関数オラクルAMM
description: FTをNFTにラップし、NFTをFTにアンラップするインターフェースは、組み込まれた関数オラクルAMMに基づいています
author: Elaine Zhang (@lanyinzly) <lz8aj@virginia.edu>、Jerry <jerrymindflow@gmail.com>、Amandafanny <amandafanny200@gmail.com>、Shouhao Wong (@wangshouh) <wongshouhao@outlook.com>、0xPoet <0xpoets@gmail.com>
discussions-to: https://ethereum-magicians.org/t/eip-7527-token-bound-function-oracle-amm-contract/15950
status: Draft
type: Standards Track
category: ERC
created: 2023-09-03
requires: 165, 721
---


## 概要

このプロポーザルでは、[ERC-20](./eip-20.md)またはETHを[ERC-721](./eip-721.md)にラップし、ERC-721をERC-20またはETHにアンラップするためのインターフェースを概説しています。関数オラクルは、組み込まれた関数オラクルの自動化された市場メーカー(FOAMM)の方程式に基づいて、NFTのミントとバーンの価格を提供します。

## 動機

分散型システムでは、特に一意のトークンやあまり取引されていないトークンであるNFTの流動性が大きな課題となることがあります。信頼できるNFTエコシステムを育成するために、FOAMMの動機は、透明で自動化されたスマートコントラクトメカニズムを通じて、NFTの流動性のための自動価格発見ソリューションを提供することです。

このERCは以下の側面に対して革新的なソリューションを提供します:

- 自動価格発見
- 流動性の向上

### 自動価格発見

FOAMMの取引は、マッチングカウンターパーティを必要とせずに行うことができます。プールに直接アクセスする際、FOAMMは事前に定義された関数に基づいてオラクルから価格を自動的に提供します。

### 流動性の向上

従来のDEXモデルでは、流動性はリクイディティプロバイダー(LP)によって供給されます。これらのLPはトークンをリクイディティプールに預け入れ、取引を容易にします。LPの除去や引き出しは、市場の利用可能な流動性に直接影響を与えるため、大きな変動性をもたらす可能性があります。

FOAMMシステムでは、流動性は`wrap`または`unwrap`を通じて内部的に追加または削除されます。FOAMMは外部LPへの依存を減らし、突然の引き出しによって引き起こされる変動性のリスクを軽減します。なぜなら、流動性は参加者の継続的な相互作用によって絶えず補充され、維持されるからです。

## 仕様

この文書の中の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

コントラクトインターフェース:

ここでは3つのインターフェース、`Agency`、`App`、`Factory`が含まれています。

`Agency`と`App`は同じコントラクトで実装されるか、別々に実装されるかもしれません。別々に実装される場合、相互にバウンドされ、初期化後にアップグレードできないものとします。

`Agency`と`App`は`iconstructor`インターフェースを実装し、コントラクト内のパラメータを初期化し、構成パラメータを検証する必要があります。ファクトリーを使ってAgencyとAppをデプロイする場合、ファクトリーは2つの関数を呼び出すときにも自動的に呼び出します。

`App`は`onlyAgency()`修飾子を実装し、`mint`と`burn`は`onlyAgency()`を修飾子として適用する必要があります。これにより、`Mint`と`Burn`の呼び出しは、対応する`Agency`を通じて行われた場合にのみ効果を持つことが保証されます。

`Agency`は`onlyApp()`を実装することが`OPTIONAL`です。

`Factory`インターフェースは`OPTIONAL`です。`Agency`と`App`を繰り返しデプロイする必要がある場合に最も有用です。

関数オラクルは`getWrapOracle`と`getUnwrapOracle`を通じて実装されており、パラメータと関数で定義された数学的方程式に基づいて価格を提供します。

FOAMMは`wrap`と`unwrap`を通じて実装されており、`getWrapOracle`と`getUnwrapOracle`を呼び出して価格を取得し、自動的にクリアします。`wrap`を実行するには、FOAMMはプレミアムを受け取り、`App`内の`mint`を開始します。`unwrap`を実行するには、FOAMMはプレミアムを転送し、`App`内の`burn`を開始します。

`Agency`は、すべての`mint`と`burn`転送の単一のエントリーポイントとして機能します。

### Agencyインターフェース

```
pragma solidity ^0.8.20;

/**
 * @dev The settings of the agency.
 * @param currency The address of the currency. If `currency` is 0, the currency is Ether.
 * @param basePremium The base premium of the currency.
 * @param feeRecipient The address of the fee recipient.
 * @param mintFeePercent The fee of minting.
 * @param burnFeePercent The fee of burning.
 */

struct Asset {
    address currency;
    uint256 basePremium;
    address feeRecipient;
    uint16 mintFeePercent;
    uint16 burnFeePercent;
}

interface IERC7527Agency {
    /**
     * @dev Allows the account to receive Ether
     *
     * Accounts MUST implement a `receive` function.
     *
     * Accounts MAY perform arbitrary logic to restrict conditions
     * under which Ether can be received.
     */
    receive() external payable;

    /**
     * @dev Emitted when `tokenId` token is wrapped.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param tokenId The identifier of the newly created non-fungible token.
     * @param premium The premium of wrapping.
     * @param fee The fee of wrapping.
     */
    event Wrap(address indexed to, uint256 indexed tokenId, uint256 premium, uint256 fee);

    /**
     * @dev Emitted when `tokenId` token is unwrapped.
     * @param to The address of the recipient of the currency.
     * @param tokenId The identifier of the non-fungible token to unwrap.
     * @param premium The premium of unwrapping.
     * @param fee The fee of unwrapping.
     */
    event Unwrap(address indexed to, uint256 indexed tokenId, uint256 premium, uint256 fee);

    /**
     * @dev Constructor of the instance contract.
     */
    function iconstructor() external;

    /**
     * @dev Wrap some amount of currency into a non-fungible token.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param data The data to encode into ifself and the newly created non-fungible token.
     * @return The identifier of the newly created non-fungible token.
     */
    function wrap(address to, bytes calldata data) external payable returns (uint256);

    /**
     * @dev Unwrap a non-fungible token into some amount of currency.
     *
     * Todo: event
     *
     * @param to The address of the recipient of the currency.
     * @param tokenId The identifier of the non-fungible token to unwrap.
     * @param data The data to encode into ifself and the non-fungible token with identifier `tokenId`.
     */
    function unwrap(address to, uint256 tokenId, bytes calldata data) external payable;

    /**
     * @dev Returns the strategy of the agency.
     * @return app The address of the app.
     * @return asset The asset of the agency.
     * @return attributeData The attributeData of the agency.
     */
    function getStrategy() external view returns (address app, Asset memory asset, bytes memory attributeData);

    /**
     * @dev Returns the premium and fee of unwrapping.
     * @param data The data to encode to calculate the premium and fee of unwrapping.
     * @return premium The premium of wrapping.
     * @return fee The fee of wrapping.
     */
    function getUnwrapOracle(bytes memory data) external view returns (uint256 premium, uint256 fee);

    /**
     * @dev Returns the premium and fee of wrapping.
     * @param data The data to encode to calculate the premium and fee of wrapping.
     * @return premium The premium of wrapping.
     * @return fee The fee of wrapping.
     */
    function getWrapOracle(bytes memory data) external view returns (uint256 premium, uint256 fee);
}
```

### Appインターフェース

`ERC7527App`は`ERC721Metadata`インターフェースの`name`を継承する必要があります。

```
pragma solidity ^0.8.20;

interface IERC7527App {
    /**
     * @dev Returns the maximum supply of the non-fungible token.
     */
    function getMaxSupply() external view returns (uint256);

    /**
     * @dev Returns the name of the non-fungible token with identifier `id`.
     * @param id The identifier of the non-fungible token.
     */
    function getName(uint256 id) external view returns (string memory);

    /**
     * @dev Returns the agency of the non-fungible token.
     */
    function getAgency() external view returns (address payable);

    /**
     * @dev Constructor of the instance contract.
     */
    function iconstructor() external;

    /**
     * @dev Sets the agency of the non-fungible token.
     * @param agency The agency of the non-fungible token.
     */
    function setAgency(address payable agency) external;

    /**
     * @dev Mints a non-fungible token to `to`.
     * @param to The address of the recipient of the newly created non-fungible token.
     * @param data The data to encode into the newly created non-fungible token.
     */
    function mint(address to, bytes calldata data) external returns (uint256);

    /**
     * @dev Burns a non-fungible token with identifier `tokenId`.
     * @param tokenId The identifier of the non-fungible token to burn.
     * @param data The data to encode into the non-fungible token with identifier `tokenId`.
     */
    function burn(uint256 tokenId, bytes calldata data) external;
}
```

トークンIDは、`mint`関数の`data`パラメータで指定できます。

### ファクトリーインターフェース

バウンドされたAppとAgencyをデプロイするためにファクトリーが必要な場合、ファクトリーは以下のインターフェースを実装する必要があります:

```
pragma solidity ^0.8.20;

import {Asset} from "./IERC7527Agency.sol";

/**
 * @dev The settings of the agency.
 * @param implementation The address of the agency implementation.
 * @param asset The parameter of asset of the agency.
 * @param immutableData The immutable data are stored in the code region of the created proxy contract of agencyImplementation.
 * @param initData If init data is not empty, calls proxy contract of agencyImplementation with this data.
 */
struct AgencySettings {
    address payable implementation;
    Asset asset;
    bytes immutableData;
    bytes initData;
}

/**
 * @dev The settings of the app.
 * @param implementation The address of the app implementation.
 * @param immutableData The immutable data are stored in the code region of the created proxy contract of appImplementation.
 * @param initData If init data is not empty, calls proxy contract of appImplementation with this data.
 */
struct AppSettings {
    address implementation;
    bytes immutableData;
    bytes initData;
}

interface IERC7527Factory {
    /**
     * @dev Deploys a new agency and app clone and initializes both.
     * @param agencySettings The settings of the agency.
     * @param appSettings The settings of the app.
     * @param data The data is additional data, it has no specified format and it is sent in call to `factory`.
     * @return appInstance The address of the created proxy contract of appImplementation.
     * @return agencyInstance The address of the created proxy contract of agencyImplementation.
     */
    function deployWrap(AgencySettings calldata agencySettings, AppSettings calldata appSettings, bytes calldata data)
        external
        returns (address, address);
}
```

## 根拠

### 以前のインターフェース

[ERC-5679](./eip-5679.md)は`IERC5679Ext721`インターフェースを提案し、[ERC-721](./eip-721.md)トークン標準にミントとバーンを一貫した方法で導入しました。後方互換性を確保するため、`ERC721TokenReceiver`を実装していないいくつかのコントラクトを考慮し、`IERC7527App`は`safeMint`ではなく`mint`関数を使用しています。相互バウンドの安全性と一意性を確保するため、`IERC5679Ext721`の`burn`関数の`_from`パラメータは、バウンドされたエージェンシーのコントラクトアドレスでなければなりません。したがって、`IERC7527App`の`burn`関数には`_from`パラメータが含まれていません。

### 相互バウンド

`IERC7527App`と`IERC7527Agency`のコントラクトを実装し、お互いの唯一の所有者になるようにします。ラッププロセスは、受け取った fungible トークンの保険料額をチェックし、その後 App でNFTをミントします。NFTの所有者またはアプルーバーのみがそれをアンラップできます。

### 実装の多様性

ユーザーは、Agencyとアプリのインターフェースを実装するときに、関数とフィー率をカスタマイズできます。

異なる
Agency実装には、異なるラップ、アンラップ関数ロジック、および異なるオラクル関数があります。ユーザーは、通貨、初期価格、手数料受取アドレス、手数料率などをカスタマイズして、Agency コントラクトを初期化できます。

異なるApp実装は、さまざまなユースケースに対応しています。ユーザーはAppコントラクトを初期化できます。

ファクトリーは必須ではありません。ファクトリーの実装はニーズに応じたものです。ユーザーは、ファクトリーを通じて異なるAgency実装と異なるApp実装を選択し、それらを組み合わせて様々な製品を作成できます。

### 通貨の種類

`IERC7527Agency`の`currency`は fungible トークンのアドレスです。`Asset`は`currency`として1種類の fungible トークンしか定義できません。`currency`は、ETHや[ERC-20](./eip-20.md)を含む様々な種類の fungible トークンをサポートします。

### トークンID

各ラッププロセスで、一意の`tokenId`を生成する必要があります。この`tokenId`は、アンラッププロセス中の検証に不可欠です。また、トークンの排他的な資格証明としても機能します。この仕組みにより、コントラクト内の資産の安全性が保証されます。

### ラップとミント

`strategy`はAgencyインターフェースの実装時に設定され、一度デプロイされたら変更できないことを確認する必要があります。

`wrap`関数を実行する際、予め定められた戦略パラメータを`getWrapOracle`関数に渡して、現在のプレミアムと手数料を取得します。対応するプレミアムはAgency インスタンスに転送され、`mintFeePercent`に従って手数料は`feeRecipient`に転送されます。その後、AppがユーザーのアドレスにNFTをミントします。

Agencyに転送されたプレミアム(トークン)は、アンラッププロセス以外では移動できません。ラップを実行することがミントプロセスの唯一のトリガーとなります。

### アンラップとバーン

`unwrap`関数を実行する際、予め定められた戦略パラメータを`getUnwrapOracle`関数に渡して、現在のプレミアムと手数料を取得します。Appがそのnon-fungible tokenをバーンします。その後、`burnFeePercent`に従って手数料を差し引いたプレミアムがユーザーのアドレスに転送され、手数料は`feeRecipient`に転送されます。`unwrap`を実行することが`burn`プロセスの唯一のトリガーとなります。

### 2つのインターフェースを一緒に使う

`IERC7527App`と`IERC7527Agency`は安全性のために一緒に実装できますが、柔軟性のために初期化前に独立して実装することもできます。

### 価格設定

`getWrapOracle`と`getUnwrapOracle`は、現在のプレミアムと手数料を取得するために使用されます。オラクル関数を通じて、オンチェーンの価格取得を実装しています。ラップとアンラッププロセス中の価格とフィーの取得をサポートするだけでなく、レンディングコントラクトなどの他のコントラクトがそれらを呼び出してプレミアムとフィーを取得することもサポートします。

オンチェーンおよびオフチェーンのパラメータに基づく関数オラクルをサポートできますが、オンチェーンのパラメータのみがオンチェーンの現実の合意をサポートすることが推奨されます。

### `initData`と`iconstructor`

ファクトリーによってAppとAgencyをデプロイする際、ファクトリーは`initData`をCallDataとして使ってAgencyとAppコントラクトを呼び出し、また`App`と`Agency`内の`iconstructor`関数を呼び出します。

`initData`は主にパラメータ化された初期化関数を呼び出すために使用されますが、`iconstructor`は多くの場合、構成パラメータの検証や非パラメータ化された初期化関数に使用されます。

## 後方互換性

後方互換性の問題は見つかりませんでした。

## 参考実装

```
pragma solidity ^0.8.20;

import {
    ERC721Enumerable,
    ERC721,
    IERC721Enumerable
} from "@openzeppelin/contracts/token/ERC721/extensions/ERC721Enumerable.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {Address} from "@openzeppelin/contracts/utils/Address.sol";
import {ClonesWithImmutableArgs} from "clones-with-immutable-args/ClonesWithImmutableArgs.sol";
import {IERC7527App} from "./interfaces/IERC7527App.sol";
import {IERC7527Agency, Asset} from "./interfaces/IERC7527Agency.sol";
import {IERC7527Factory, AgencySettings, AppSettings} from "./interfaces/IERC7527Factory.sol";

contract ERC7527Agency is IERC7527Agency {
    using Address for address payable;

    receive() external payable {}

    function iconstructor() external override pure {
        (, Asset memory _asset,) = getStrategy();
        require(_asset.basePremium != 0, "LnModule: zero basePremium");
    }

    function unwrap(address to, uint256 tokenId, bytes calldata data) external payable override {
        (address _app, Asset memory _asset,) = getStrategy();
        require(_isApprovedOrOwner(_app, msg.sender, tokenId), "LnModule: not owner");
        IERC7527App(_app).burn(tokenId, data);
        uint256 _sold = IERC721Enumerable(_app).totalSupply();
        (uint256 premium, uint256 burnFee) = getUnwrapOracle(abi.encode(_sold));
        _transfer(address(0), payable(to), premium - burnFee);
        _transfer(address(0), _asset.feeRecipient, burnFee);
        emit Unwrap(to, tokenId, premium, burnFee);
    }

    function wrap(address to, bytes calldata data) external payable override returns (uint256) {
        (address _app, Asset memory _asset,) = getStrategy();
        uint256 _sold = IERC721Enumerable(_app).totalSupply();
        (uint256 premium, uint256 mintFee) = getWrapOracle(abi.encode(_sold));
        require(msg.value >= premium + mintFee, "ERC7527Agency: insufficient funds");
        _transfer(address(0), _asset.feeRecipient, mintFee);
        if (msg.value > premium + mintFee) {
            _transfer(address(0), payable(msg.sender), msg.value - premium - mintFee);
        }
        uint256 id_ = IERC7527App(_app).mint(to, data);
        require(_sold + 1 == IERC721Enumerable(_app).totalSupply(), "ERC7527Agency: Reentrancy");
        emit Wrap(to, id_, premium, mintFee);
        return id_;
    }

    function getStrategy() public pure override returns (address app, Asset memory asset, bytes memory attributeData) {
        uint256 offset = _getImmutableArgsOffset();
        address currency;
        uint256 basePremium;
        address payable feeRecipient;
        uint16 mintFeePercent;
        uint16 burnFeePercent;
        assembly {
            app := shr(0x60, calldataload(add(offset, 0)))
            currency := shr(0x60, calldataload(add(offset, 20)))
            basePremium := calldataload(add(offset, 40))
            feeRecipient := shr(0x60, calldataload(add(offset, 72)))
            mintFeePercent := shr(0xf0, calldataload(add(offset, 92)))
            burnFeePercent := shr(0xf0, calldataload(add(offset, 94)))
        }
        asset = Asset(currency, basePremium, feeRecipient, mintFeePercent, burnFeePercent);
        attributeData = "";
    }

    function getUnwrapOracle(bytes memory data) public pure override returns (uint256 premium, uint256 fee) {
        uint256 input = abi.decode(data, (uint256));
        (, Asset memory _asset,) = getStrategy();
        premium = _asset.basePremium + input * _asset.basePremium / 100;
        fee = premium * _asset.burnFeePercent / 10000;
    }

    function getWrapOracle(bytes memory data) public pure override returns (uint256 premium, uint256 fee) {
        uint256 input = abi.decode(data, (uint256));
        (, Asset memory _asset,) = getStrategy();
        premium = _asset.basePremium + input * _asset.basePremium / 100;
        fee = premium * _asset.mintFeePercent / 10000;
    }

    function _transfer(address currency, address recipient, uint256 premium) internal {
        if (currency == address(0)) {
            payable(recipient).sendValue(premium);
        } else {
            IERC20(currency).transfer(recipient, premium);
        }
    }

    function _isApprovedOrOwner(address app, address spender, uint256 tokenId) internal view virtual returns (bool) {
        IERC721Enumerable _app = IERC721Enumerable(app);
        address _owner = _app.ownerOf(tokenId);
        return (spender == _owner || _app.isApprovedForAll(_owner, spender) || _app.getApproved(tokenId) == spender);
    }
    /// @return offset The offset of the packed immutable args in calldata

    function _getImmutableArgsOffset() internal pure returns (uint256 offset) {
        // solhint-disable-next-line no-inline-assembly
        assembly {
            offset := sub(calldatasize(), add(shr(240, calldataload(sub(calldatasize(), 2))), 2))
        }
    }
}

contract ERC7527App is ERC721Enumerable, IERC7527App {
    constructor() ERC721("ERC7527App", "EA") {}

    address payable private _oracle;

    modifier onlyAgency() {
        require(msg.sender == _getAgency(), "only agency");
        _;
    }

    function iconstructor() external {}

    function getName(uint256) external pure returns (string memory) {
        return "App";
    }

    function getMaxSupply() public pure override returns (uint256) {
        return 100;
    }

    function getAgency() external view override returns (address payable) {
        return _getAgency();
    }

    function setAgency(address payable oracle) external override {
        require(_getAgency() == address(0), "already set");
        _oracle = oracle;
    }

    function mint(address to, bytes calldata data) external override onlyAgency returns (uint256 tokenId) {
        require(totalSupply() < getMaxSupply(), "max supply reached");
        tokenId = abi.decode(data, (uint256));
        _mint(to, tokenId);
    }

    function burn(uint256 tokenId, bytes calldata) external override onlyAgency {
        _burn(tokenId);
    }

    function _getAgency() internal view returns (address payable) {
        return _oracle;
    }
}

contract ERC7527Factory is IERC7527Factory {
    using ClonesWithImmutableArgs for address;

    function deployWrap(AgencySettings calldata agencySettings, AppSettings calldata appSettings, bytes calldata)
        external
        override
        returns (address appInstance, address agencyInstance)
    {
        appInstance = appSettings.implementation.clone(appSettings.immutableData);
        {
            agencyInstance = address(agencySettings.implementation).clone(
                abi.encodePacked(
                    appInstance,
                    agencySettings.asset.currency,
                    agencySettings.asset.basePremium,
                    agencySettings.asset.feeRecipient,
                    agencySettings.asset.mintFeePercent,
                    agencySettings.asset.burnFeePercent,
                    agencySettings.immutableData
                )
            );
        }

        IERC7527App(appInstance).setAgency(payable(agencyInstance));

        IERC7527Agency(payable(agencyInstance)).iconstructor();
        IERC7527App(appInstance).iconstructor();

        if (agencySettings.initData.length != 0) {
            (bool success, bytes memory result) = agencyInstance.call(agencySettings.initData);

            if (!success) {
                assembly {
                    revert(add(result, 32), mload(result))
                }
            }
        }

        if (appSettings.initData.length != 0) {
            (bool success, bytes memory result) = appInstance.call(appSettings.initData);

            if (!success) {
                assembly {
                    revert(add(result, 32), mload(result))
                }
            }
        }
    }
}