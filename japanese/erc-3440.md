---
original: d2da3b89fd6eedebd14bf93a572a7e50f1bdb09598855fc00426d910422d2f5a
---

---
eip: 3440
title: ERC-721 Editions Standard
author: Nathan Ginnever (@nginnever)
discussions-to: https://ethereum-magicians.org/t/eip-3340-nft-editions-standard-extension/6044
status: Stagnant
type: Standards Track
category: ERC
created: 2021-04-20
requires: 712, 721
---

## 簡単な要約

この規格は、NFTに署名を付けることで、[ERC-721仕様](./eip-721.md)を拡張するものです。これにより、アーティストが作品の原本と限定版プリントを指定できるようになり、作品の出自を改善することができます。

## 概要

ERC-3440は、アートワークのためのNFTをより堅牢にするために設計されたERC-721の拡張機能です。これは、[元のERC-721拡張機能](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/extensions/ERC721Enumerable.sol)に似た専用の列挙拡張機能を提供することで、オリジナルと限定版プリントを指定する機能を追加したものです。この拡張機能の主な改善点は、アーティストが自身の作品の限定性を指定し、特定のトークンIDに対するユニークな署名データを提供できるようにすることです。これは、アーティストが自身の作品のプリントに署名するのと同様の機能です。

## 動機
現在、NFTと作品の関係は、NFTの共有`tokenURI`状態に保存されたトークンメタデータでのみ強制されています。ブロックチェーンは、NFTの起源までの不変の履歴を提供しますが、その起源は、アーティストが手書きの署名ほど密接に管理しているキーではありません。

版は、オリジナルの作品の複製プリントです。ERC-721は、デジタルアートや音楽などの作品のために設計されたものではありません。ERC-721(NFT)は元々、権利証書やその他の契約を扱うために作られました。やがてERC-721は、ゲームのトークンに進化しましたが、そこでは、サーバーでホストされるメタデータで十分かもしれません。この提案は、NFT、デジタルアート、所有者、アーティストの間により具体的なつながりを作ることができると考えています。アートのための明確な規格を作ることで、アーティストがイーサリアムブロックチェーンとファンとの関係を維持するのが容易になります。

NFTの用途はデジタルアートに進化しており、オリジナルのNFTと印刷版に署名を付ける必要があります。ERC-721コントラクトはアーティストが展開するかもしれませんが、現在のところ、何かがアーティストによって固有に触れられたことを理解する唯一の方法は、サーバー外のブロックチェーンにあるメタデータを前提とする第三者アプリケーションに表示することです。この提案は、アーティストが自身の作品に署名できる機能を提供し、購入者にその固有性を表示するための規格を提供することで、その距離を取り除きます。限定版の指定と不変の署名の組み合わせにより、信頼できる形で関係性が強化されます。この署名は、アプリケーションがこれらの署名と限定版を、購入者に対する固有性の証拠として簡単に表示できるよう、ビュー関数に付随します。

## 仕様
この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

ERC-721準拠のコントラクトは、版権を提供するためにこのERCを実装してもよいものとします。これにより、アーティストが自身の作品の原本と限定版プリントに署名する標準的な方法が提供されます。

ERC-3440の実装では、どのトークンIDが原本のNFTであるか(デフォルトでは0番)、どのトークンIDが固有のレプリカであるかを指定しなければなりません(MUST)。原本のプリントは0番のトークンIDであるべきですが(SHOULD)、別のIDに割り当てることもできます(MAY)。原本のプリントは1つしか指定できません(MUST)。実装では、最大の発行数を指定しなければならず(MUST)、その数を超えてはいけません(MUST NOT)。

アーティストは署名機能を使って、原本または限定版プリントに署名することができますが(MAY)、これは任意です(OPTIONAL)。単純にトークンIDの整数のハッシュを署名するメッセージが推奨されます(RECOMMENDED)。

署名メッセージは[EIP-712](https://eips.ethereum.org/EIPS/eip-712)標準を使用しなければなりません(MUST)。

ERC-3440に準拠するコントラクトは、以下の抽象コントラクト(ERC3440.solと呼ばれる)を実装しなければなりません:

```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/utils/cryptography/ECDSA.sol";

/**
 * @dev 版権拡張機能付きのERC721トークン。
 */
abstract contract ERC3440 is ERC721URIStorage {

    // eip-712
    struct EIP712Domain {
        string  name;
        string  version;
        uint256 chainId;
        address verifyingContract;
    }
    
    // 署名されるメッセージの内容
    struct Signature {
        address verificationAddress; // アーティストが各作品に対してaddress(this)のみ署名することを保証する
        string artist;
        address wallet;
        string contents;
    }

    // タイプハッシュ
    bytes32 constant EIP712DOMAIN_TYPEHASH = keccak256(
        "EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"
    );

    bytes32 constant SIGNATURE_TYPEHASH = keccak256(
        "Signature(address verifyAddress,string artist,address wallet, string contents)"
    );

    bytes32 public DOMAIN_SEPARATOR;
    
    // 署名のオプションのマッピング
    mapping (uint256 => bytes) private _signatures;
    
    // アーティストのアドレスを表示するためのビュー
    address public artist;

    // 作成された全プリントの数を表示するためのビュー
    uint public editionSupply = 0;
    
    // オリジナルコピーのIDを表示するためのビュー
    uint public originalId = 0;
    
    // 署名されたトークンのイベント
    event Signed(address indexed from, uint256 indexed tokenId);

    /**
     * @dev `_artist`をオリジナルのアーティストに設定する。
     * @param `address _artist` 署名アーティストのウォレット(TODO: 複数の署名者とコントラクト署名者(非EOA)を検討する)
     */
    function _designateArtist(address _artist) internal virtual {
        require(artist == address(0), "ERC721Extensions: アーティストはすでに設定されています");

        // アーティストの特別な指定がない場合は、それを設定する。
        artist = _artist;
    }
    
    /**
     * @dev `tokenId`をオリジナルプリントとして設定する。
     * @param `uint256 tokenId` オリジナルプリントのnftID
     */
    function _designateOriginal(uint256 _tokenId) internal virtual {
        require(msg.sender == artist, "ERC721Extensions: オリジナルの指定はアーティストのみが行えます");
        require(_exists(_tokenId), "ERC721Extensions: 存在しないトークンのオリジナルを問い合わせることはできません");
        require(originalId == 0, "ERC721Extensions: オリジナルプリントは既に別のIDに指定されています");

        // オリジナルの特別な指定がない場合は、それを設定する。
        originalId = _tokenId;
    }
    

    /**
     * @dev 印刷版の最大数を設定する。
     * @param `uint256 _maxEditionSupply` 最大発行数
     */
    function _setLimitedEditions(uint256 _maxEditionSupply) internal virtual {
        require(msg.sender == artist, "ERC721Extensions: 最大発行数の指定はアーティストのみが行えます");
        require(editionSupply == 0, "ERC721Extensions: 既に最大発行数が設定されています");

        // 印刷版の最大数の指定がない場合は、それを設定する。0の場合はオリジナルの印刷版がないことを示す
        editionSupply = _maxEditionSupply;
    }

    /**
     * @dev 印刷版のトークンを作成する。
     * @param `string memory _tokenURI` 各NFTに付与されるメタデータ
     */
    function _createEditions(string memory _tokenURI) internal virtual {
        require(msg.sender == artist, "ERC721Extensions: 印刷版の作成はアーティストのみが行えます");
        require(editionSupply > 0, "ERC721Extensions: 版数が0より大きく設定されていません");
        for(uint i=0; i < editionSupply; i++) {
            _mint(msg.sender, i);
            _setTokenURI(i, _tokenURI);
        }
    }

    /**
     * @dev ハッシュユーティリティ内部関数
     * @param `Signature memory _message` 署名されるメッセージ構造体
     * このコントラクトのアドレスが強制的に組み込まれる
     */
    function _hash(Signature memory _message) internal view returns (bytes32) {
        return keccak256(abi.encodePacked(
            "\x19\x01",
            DOMAIN_SEPARATOR,
            keccak256(abi.encode(
                SIGNATURE_TYPEHASH,
                address(this),
                _message.artist,
                _message.wallet,
                _message.contents
            ))
        ));
    }

    /**
     * @dev プリントを表す`tokenId`に署名する。
     * @param `uint256 _tokenId` 署名されるNFTのID
     * @param `Signature memory _message` 署名されるメッセージ
     * @param `bytes memory _signature` オフチェーンで作成された署名バイト列
     *
     * 要件:
     *
     * - `tokenId`が存在する必要がある。
     *
     * {Signed}イベントを発行する。
     */
    function _signEdition(uint256 _tokenId, Signature memory _message, bytes memory _signature) internal virtual {
        require(msg.sender == artist, "ERC721Extensions: 作品の署名はアーティストのみが行えます");
        require(_signatures[_tokenId].length == 0, "ERC721Extensions: このトークンはすでに署名されています");
        bytes32 digest = hash(_message);
        address recovered = ECDSA.recover(digest, _signature);
        require(recovered == artist, "ERC721Extensions: アーティストの署名が一致しません");
        _signatures[_tokenId] = _signature;
        emit Signed(artist, _tokenId);
    }

    
    /**
     * @dev アーティストの署名を表示する。
     * @param `uint256 _tokenId` 署名の有無を確認するNFTのID
     * @returns `bytes` トークンに保存された署名
     */
    function getSignature(uint256 _tokenId) external view virtual returns (bytes memory) {
        require(_signatures[_tokenId].length != 0, "ERC721Extensions: このIDには署名がありません");
        return _signatures[_tokenId];
    }
    
    /**
     * @dev メッセージがアーティストによって署名されているかどうかを返す。
     * @param `Signature memory _message` アーティストが外部で公開した署名付きメッセージ
     * @param `bytes memory _signature` メッセージに付与された署名
     * @param `uint _tokenId` 署名されたものとして検証するトークンのID
     * @returns `bool` アーティストによって署名されている場合はtrue
     * アーティストは、NFT上で検証できる署名をアウトオブバンドで配信することができる
     */
    function isSigned(Signature memory _message, bytes memory _signature, uint _tokenId) external view virtual returns (bool) {
        bytes32 messageHash = hash(_message);
        address _artist = ECDSA.recover(messageHash, _signature);
        return (_artist == artist && _equals(_signatures[_tokenId], _signature));
    }

    /**
    * @dev 2つの`bytes memory`変数が等しいかどうかを確認するユーティリティ関数。
    * ハッシュを使うことで、個々のバイトを比較するよりもガスを節約できる。
    * 等しいとは以下のことを意味する:
    *  - 'self.length == other.length'
    *  - 'n' in '[0, self.length)', 'self[n] == other[n]'
    */
    function _equals(bytes memory _self, bytes memory _other) internal pure returns (bool equal) {
        if (_self.length != _other.length) {
            return false;
        }
        uint addr;
        uint addr2;
        uint len = _self.length;
        assembly {
            addr := add(_self, /*BYTES_HEADER_SIZE*/32)
            addr2 := add(_other, /*BYTES_HEADER_SIZE*/32)
        }
        assembly {
            equal := eq(keccak256(addr, len), keccak
## 根拠

NFTの主要な役割の1つは、デジタルアートの独自性を表示することです。作品の出自は美術作品にとって望ましい機能であり、この規格は、署名によって一意性を検証できるようにすることで、NFTをより良いものにします。アーティストが明示的にトークンに署名することで、ブロックチェーン上のアーティストと作品の関係がより強化されます。アーティストは今後も自身の秘密鍵を保持し、同じ署名がユニークなNFTに存在することを示すメッセージに署名することができます。

## 下位互換性

この提案は既存の721拡張機能を組み合わせたものであり、ERC-721標準との下位互換性があります。

## テストケース
実装例とテストは[こちら](https://github.com/nginnever/NFT-editions)にあります。

## 参考実装
```solidity
// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./ERC3440.sol";

/**
 * @dev 版権拡張機能付きのERC721トークン。
 */
contract ArtToken is ERC3440 {

    /**
     * @dev NFTをデプロイしたアカウントを`address artist`としてオリジナルのアーティストに設定する。
     */
     constructor (
        string memory _name, 
        string memory _symbol,
        uint _numberOfEditions,
        string memory tokenURI,
        uint _originalId
    ) ERC721(_name, _symbol) {
        _designateArtist(msg.sender);
        _setLimitedEditions(_numberOfEditions);
        _createEditions(tokenURI);
        _designateOriginal(_originalId);

        DOMAIN_SEPARATOR = keccak256(abi.encode(
            EIP712DOMAIN_TYPEHASH,
            keccak256(bytes("Artist's Editions")),
            keccak256(bytes("1")),
            1,
            address(this)
        ));
    }
    
    /**
     * @dev `tokenId`を表すプリントに署名する。
     */
    function sign(uint256 _tokenId, Signature memory _message, bytes memory _signature) public {
        _signEdition(_tokenId, _message, _signature);
    }
}

```

## セキュリティ上の考慮事項
この拡張機能により、アーティストは原本版、最大発行数、発行版を指定する機能を持つことができます。また、`tokenURI`拡張機能を使って作品へのリンクを提供します。アーティストが原本を販売した後にこれらの値を変更するリスクを最小限に抑えるため、これらの機能は1回しか実行できません。これらの機能を1回しか実行できないようにすることで、一意性と検証可能性の一貫性が確保されます。そのため、参考実装ではこれらの機能をコンストラクタ関数で処理しています。版は1回しか署名できず、トークンの公開前に版が正しく署名されていることを確認する必要があります。

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)により放棄されています。