---
original: ee60acd0db5e3be1ecadb69e66ddc82a4c747c5e09ac1fd63e431ae9baa127d3
---

---
eip: 7246
title: Encumber - 所有権と保証の分割
description: トークンの所有権を移転することなく、トークンをプレッジできるようにするトークンインターフェース。
author: Coburn Berry (@coburncoburn), Mykel Pereira (@mykelp), Scott Silver (@scott-silver)
discussions-to: https://ethereum-magicians.org/t/encumber-extending-the-erc-20-token-standard-to-allow-pledging-tokens-without-giving-up-ownership/14849
status: Draft
type: Standards Track
category: ERC
created: 2023-06-27
requires: 20
---

## 概要

このERCは、Encumber - アカウントが別のアカウントに自分のバランスの一部を独占的に移動する権利を付与する機能 - を追加することで、[ERC-20](./eip-20.md)トークン標準を拡張するものです。Encumberは[ERC-20](./eip-20.md)の承認よりも強力なバージョンです。[ERC-20](./eip-20.md)の承認では、別のアカウントに指定したトークン量を転送する許可を与えますが、Encumberでは同じ許可を与えつつ、トークンが必要なときに利用可能であることを保証します。

## 動機

この拡張機能は[ERC-20](./eip-20.md)トークン標準に柔軟性を加え、トークンのロックが必要だが、実際の所有権を維持することが好ましい使用例に対応します。このインターフェースは、[ERC-721](./eip-721.md)などの他のトークン標準にも簡単に適応できます。

トークン保有者は一般的に、特定の条件下でトークンを返却するスマートコントラクトにトークンを転送します。場合によっては、スマートコントラクトが実際にトークンを保持する必要はなく、必要に応じてトークンが利用可能であることを保証するだけでよい場合があります。承認では十分な保証が得られないため、現時点では、トークンの利用可能性を保証する唯一の方法は、トークンをスマートコントラクトに転送することです。トークンを移動せずにロックすることで、トークンの権利と所有権がより明確になります。これにより、エアドロップやトークンの所有に関連するその他の副次的な利益を、真の所有者に届けることができます。また、[ERC-20](./eip-20.md)トークンのプールを一度に引き出すことができるのに対し、エンカンバードトークンを引き出すには複数のアカウントを反復する必要があるため、ガス使用量が大幅に抑えられるという安全性の層も追加されます。

## 仕様

この文書の中の "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY"、"OPTIONAL" というキーワードは、RFC 2119およびRFC 8174に記載されている通り解釈されるものとします。

準拠したトークンは、以下のインターフェースを実装しなければなりません。

```solidity
/**
 * @dev ERC-7246標準のインターフェース。
 */
interface IERC7246{
    /**
     * @dev `owner`から`taker`に`amount`トークンがエンカンバーされたときに発行されるイベント。
     */
    event Encumber(address indexed owner, address indexed taker, uint amount);

    /**
     * @dev `owner`から`taker`へのエンカンバーが`amount`減少したときに発行されるイベント。
     */
    event Release(address indexed owner, address indexed taker, uint amount);

    /**
     * @dev `owner`が現在エンカンバーしているトークルの合計量を返します。
     * `balanceOf(owner)`を超えてはいけません。
     *
     * `balanceOf(owner)`を`encumberedBalanceOf(owner)`以下に減らすような関数は、必ず失敗するようにしなければなりません。
     */
    function encumberedBalanceOf(address owner) external returns (uint);

    /**
     * @dev `owner`が`taker`にエンカンバーしているトークンの数を返します。
     *
     * この値は、`owner`または許可されたアカウントが`encumber`または`encumberFrom`を呼び出すと増加し、
     * `taker`が`release`や`transferFrom`を呼び出すと減少します。
     */
    function encumbrances(address owner, address taker) external returns (uint);

    /**
     * @dev 呼び出し元が`taker`に`amount`トークンをエンカンバーします。
     * これにより、`taker`は呼び出し元のバランスから`amount`トークンを`transferFrom`で移動する権利を得ます。
     *
     * 呼び出し元が`amount`トークンを持っていない場合(例えば`balanceOf(caller) - encumbrances(caller) < amount`)、必ず失敗するようにしなければなりません。
     *
     * {Encumber}イベントを発行します。
     */
    function encumber(address taker, uint amount) external;

    /**
     * @dev `owner`が`taker`に`amount`トークンをエンカンバーします。
     * これにより、`taker`は`owner`のバランスから`amount`トークンを`transferFrom`で移動する権利を得ます。
     *
     * 送信者が何らかの方法で`owner`から明示的に承認されていない限り、この関数は失敗するべきです。
     *
     * `owner`が`amount`トークンを持っていない場合(例えば`balanceOf(owner) - encumbrances(owner) < amount`)、必ず失敗するようにしなければなりません。
     *
     * {Encumber}イベントを発行します。
     */
    function encumberFrom(address owner, address taker, uint amount) external;

    /**
     * @dev `owner`から呼び出し元に対するエンカンバーを`amount`減少させます。
     *
     * {Release}イベントを発行します。
     */
    function release(address owner, uint amount) external;


    /**
     * @dev アドレスの未エンカンバーバランスを読み取るための便利な関数。
     * `balanceOf(owner) - encumberedBalanceOf(owner)`として簡単に実装できます。
     */
    function availableBalanceOf(address owner) public view returns (uint);
}
```

## 根拠
この仕様は、ERC-20仕様を補完し、鏡像するように設計されており、採用と理解を容易にすることを意図しています。この仕様は、所有者がエンカンバードトークンを転送できないという唯一の真の要件を除いて、あえて最小限の規定的なものになっています。ただし、標準的なERC-20定義への侵入を最小限に抑えるために、接続方法について注目に値する決定が例示実装に含まれています。
    - 例では、`encumberFrom`を容易にするために`approve`への依存関係がありますが、この提案では、`approveEncumber`のように、ERC-20の承認をミラーするメカニズムを定義することも可能です。
    - `transferFrom(src, dst, amount)`は、まず`encumbrances(src, amount)`を減らし、次に`allowance(src, msg.sender)`から支出するように記述されています。あるいは、`transferFrom`を、承認のバランスと同時にエンカンバードバランスから支出するように実装することもできます。その場合、`approve`は、承認されたバランスがエンカンバードバランスの必要量を下回らないことを確認する必要があり、また`encumber`の呼び出しの前提条件になります。

Encumberインターフェースは、`amount`パラメータの代わりに`tokenId`を使うことで、ERC-721トークンにも拡張できます。このインターフェースは、最も一般的な使用例(ERC-20)の明確さを優先していますが、他のフォーマットとも互換性があります。

## 下位互換性

このEIPは、既存の[ERC-20](./eip-20.md)標準との下位互換性を持っています。実装では、別のアカウントにエンカンバーされたトークンの転送をブロックする機能を追加する必要があります。

## 参考実装

これは、[ERC-20](./eip-20.md)ベースのコントラクトを拡張することで実装できます。transferメソッドを変更して、エンカンバードトークンの転送をブロックし、transferFromを使ってスペントされたときにエンカンバーを解除するようにします。

``` solidity
// エンカンバー機能を実装したERC-20トークン

pragma solidity ^0.8.0;
import {ERC20} from "../lib/openzeppelin-contracts/contracts/token/ERC20/ERC20.sol";
import { IERC7246 } from "./IERC7246.sol";

contract EncumberableERC20 is ERC20, IERC7246 {
    // 所有者 -> テイカー -> 引き出し可能な金額
    mapping (address => mapping (address => uint)) public encumbrances;

    // トークン所有者のエンカンバードバランス。encumberedBalanceはbalanceOfを超えてはいけない
    // トークンがリベースされると、この規約に違反するリスクがあることに注意
    mapping (address => uint) public encumberedBalanceOf;

    address public minter;

    constructor(string memory name, string memory symbol) ERC20(name, symbol) {
        minter = msg.sender;
    }

    function mint(address recipient, uint amount) public {
        require(msg.sender == minter, "only minter");
        _mint(recipient, amount);
    }

    function encumber(address taker, uint amount) external {
        _encumber(msg.sender, taker, amount);
    }

    function encumberFrom(address owner, address taker, uint amount) external {
        require(allowance(owner, msg.sender) >= amount);
       _encumber(owner, taker, amount);
    }

    function release(address owner, uint amount) external {
        _release(owner, msg.sender, amount);
    }

    // バランスとエンカンバーを近づけるときは、必ずチェックする
    function availableBalanceOf(address a) public view returns (uint) {
        return (balanceOf(a) - encumberedBalanceOf[a]);
    }

    function _encumber(address owner, address taker, uint amount) private {
        require(availableBalanceOf(owner) >= amount, "insufficient balance");
        encumbrances[owner][taker] += amount;
        encumberedBalanceOf[owner] += amount;
        emit Encumber(owner, taker, amount);
    }

    function _release(address owner, address taker, uint amount) private {
        if (encumbrances[owner][taker] < amount) {
          amount = encumbrances[owner][taker];
        }
        encumbrances[owner][taker] -= amount;
        encumberedBalanceOf[owner] -= amount;
        emit Release(owner, taker, amount);
    }

    function transfer(address dst, uint amount) public override returns (bool) {
        // エンカンバーをチェックするが、使用はしない
        require(availableBalanceOf(msg.sender) >= amount, "insufficient balance");
        _transfer(msg.sender, dst, amount);
        return true;
    }

    function transferFrom(address src, address dst, uint amount) public override returns (bool) {
        uint encumberedToTaker = encumbrances[src][msg.sender];
        bool exceedsEncumbrance = amount > encumberedToTaker;
        if (exceedsEncumbrance)  {
            uint excessAmount = amount - encumberedToTaker;

            // 承認から支出するのに十分なエンカンバードトークンがあることを確認する
           require(availableBalanceOf(src) >= excessAmount, "insufficient balance");

           // エンカンバーを超えているので、全額使用する
            _spendEncumbrance(src, msg.sender, encumberedToTaker);

            _spendAllowance(src, dst, excessAmount);
        } else {
            _spendEncumbrance(src, msg.sender, amount);
        }

        _transfer(src, dst, amount);
        return true;
    }

    function _spendEncumbrance(address owner, address taker, uint256 amount) internal virtual {
        uint256 currentEncumbrance = encumbrances[owner][taker];
        require(currentEncumbrance >= amount, "insufficient encumbrance");
        uint newEncumbrance = currentEncumbrance - amount;
        encumbrances[owner][taker] = newEncumbrance;
        encumberedBalanceOf[owner] -= amount;
    }
}
```

## セキュリティ上の考慮事項

`balanceOf`を使ってトランスファー可能な金額を判断する当事者は、代わりに`balanceOf(account) - encumberedBalance(account)`、または実装されていれば`availableBalanceOf(account)`を使うべきです。

エンカンバードバランスは常にトークンバランスによってバックアップされているという性質は、`transfer`と`transferFrom`を変更してエンカンバードトークンの転送をブロックすることで、簡単に実現できます。ユーザーのバランスを変更する他の関数(リベーストークンや管理者バーン関数など)がある場合、実装者は、それらの関数でも`balanceOf(account)`を`encumberedBalanceOf(account)`以下に減らさないようにする追加の保護措置を講じる必要があります。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。