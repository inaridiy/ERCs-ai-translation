---
original: 31f92113b9460566a9ba172f5550ed705ced4f97f7931b2f531062cf9f0ce99a
---

---
eip: 4799
title: 非代替性トークン所有権指定標準
description: NFTの所有権を指定するための標準化されたインターフェース
author: David Buckman (@davidbuckman), Isaac Buckman (@isaacbuckman)
discussions-to: https://ethereum-magicians.org/t/erc-4799-non-fungible-token-wrapping-standard/8396
status: 停滞
type: Standards Track
category: ERC
created: 2022-02-13
requires: 165
---

## 概要

以下では、NFTをスマートコントラクトによるエスクローで保持している間に、NFTの所有権を誰かに指定するための標準インターフェースを定義しています。この標準により、与えられたチェーンの最終アドレスが各NFTの指定所有者となる、NFTの有向非循環グラフを構築できます。これにより、オリジナルの真正性を失うことなく、既存のNFTに追加の機能を導入することができます。つまり、すべてのNFTが合成可能で、レンタル、担保、分割などが可能になります。

## 動機

多くのNFTは、所有者に何らかの効用を提供することを目的としています。この効用は様々な形態を取ることができます。例えば、アパートに住む権利、イベントのチケットへのアクセス、トークンのエアドロップ、その他無限の可能性のある用途などです。しかし、現在の形式では、NFTに関連付けられている唯一の検証可能なウォレットは所有者のみであるため、効用を配布したいクライアントは、NFTの所有者に対してのみ配布せざるを得ません。つまり、複雑な所有権契約をオリジナルのNFTコントラクトに組み込む必要があり、オリジナルのNFTの真正性を外部のコントラクトにリンクさせる仕組みがありません。

この標準の目的は、すでにミントされたNFTに対して、任意の複雑な所有権契約を定義できるようにすることです。これにより、新しい所有権構造を持つ契約を展開できますが、確立されたNFTコントラクトが提供する真正性も活用できます。従来のラッピングでは、新しいNFTを作成し、確立された真正性を失うことになりました。

この標準が登場する前は、NFTコントラクトがデプロイされた後に機能を追加するには、NFTをラッピングする以外の方法がありませんでした。しかし、これではオリジナルのNFTの効用にアクセスできなくなってしまいます。NFTの所有者を照会するアプリケーションは、ラッピングスマートコントラクトを所有者と判断してしまいます。この標準を使えば、NFTがラッピングされている場合でも、アプリケーションが効用を利用者に提供する標準化された方法があります。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

```solidity
import "@openzeppelin/contracts/utils/introspection/IERC165.sol";

interface IERC4799NFT is IERC165 {
    /// @dev NFTの所有権が何らかの方法で変更されたときに発行されます。
    ///  このイベントは、NFTが作成された(`from` == 0)場合や破棄された(`to` == 0)場合に発行されます。
    ///  例外: コントラクト作成時には、Transfer イベントを発行せずに任意の数のNFTを作成および割り当てることができます。
    ///  いつでも転送が行われると、その NFT の承認アドレス(ある場合)はリセットされます。
    event Transfer(
        address indexed from,
        address indexed to,
        uint256 indexed tokenId
    );

    /// @notice NFTの所有者を見つける
    /// @dev ゼロアドレスに割り当てられたNFTは無効とみなされ、それらについての問い合わせは失敗します
    /// @param tokenId 対象のNFTの識別子
    /// @return NFTの所有者のアドレス
    function ownerOf(uint256 tokenId) external view returns (address);
}
```
```solidity
/// @title ERC-4799 非代替性トークン所有権指定標準
/// @dev https://eips.ethereum.org/EIPS/eip-4799 を参照
/// 注: このインターフェースのERC-165識別子は[TODO]です。

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import "./IERC4799NFT.sol";

interface IERC4799 is IERC165 {
    /// @dev ソーストークンがターゲットトークンの所有者に所有権を指定したときに発行されます
    event OwnershipDesignation(
        IERC4799NFT indexed sourceContract,
        uint256 sourceTokenId,
        IERC4799NFT indexed targetContract,
        uint256 targetTokenId
    );

    /// @notice 指定されたNFTを見つける
    /// @param sourceContract ソースNFTのコントラクトアドレス
    /// @param sourceTokenId ソースNFTのトークンID
    /// @return (targetContract, targetTokenId) 親NFTのコントラクトアドレスとトークンID
    function designatedTokenOf(IERC4799NFT sourceContract, uint256 sourceTokenId)
        external
        view
        returns (IERC4799NFT, uint256);
}
```

NFTの指定された所有権の真正性は、ソースコントラクトに従ってERC-4799コントラクトがオリジナルのNFTを所有していることによって保証されます。これはクライアントが必ずソースコントラクトに問い合わせて確認する必要があります。

この仕様を尊重するクライアントは、ERC-4799コントラクトのアドレスに効用を配布してはいけません。代わりに、ERC-4799コントラクトが指し示す指定トークンの所有者に配布しなければなりません。

## 根拠

ラッピングコントラクトの将来的な互換性を最大限に高めるために、まずキャノニカルなNFTインターフェースを定義しました。`IERC4799NFT`というインターフェースを作成し、これは事実上すべての人気のあるNFTコントラクト、つまり[ERC-721](./eip-721.md)準拠の全デプロイ済みコントラクトによって暗黙的に実装されます。このインターフェースは、NFTの本質を表しています: トークン識別子からユニークな所有者のアドレスへのマッピングで、`ownerOf`関数によって表現されます。

提案の核心は`IERC4799`インターフェースで、NFT所有権指定コントラクト(ODC)の標準インターフェースです。ERC4799は、`designatedTokenOf`関数の実装を要求します。この関数は、ソースNFTを正確に1つのターゲットNFTにマッピングします。この関数を通じて、ODCは指定された所有権を表明します。この指定された所有権は、ODCがオリジナルのNFTの所有者として記載されている場合にのみ真正であり、したがって、すべてのNFTには正確に1人の指定所有者がいるという不変条件が維持されます。

## 下位互換性

`IERC4799NFT`インターフェースは`IERC721`と下位互換性があります。なぜなら、`IERC721`は暗黙的に`IERC4799NFT`を拡張しているからです。つまり、`ERC4799NFT`を実装するERC-4799標準は、ERC-721と完全に下位互換性があります。

## 参考実装

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.8.0 <0.9.0;

import "./IERC4799.sol";
import "./IERC4799NFT.sol";
import "./ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";

contract ERC721Composable is IERC4799, IERC721Receiver {
    mapping(IERC4799NFT => mapping(uint256 => IERC4799NFT)) private _targetContracts;
    mapping(IERC4799NFT => mapping(uint256 => uint256)) private _targetTokenIds;

    function designatedTokenOf(IERC4799NFT sourceContract, uint256 sourceTokenId)
        external
        view
        override
        returns (IERC4799NFT, uint256)
    {
        return (
            IERC4799NFT(_targetContracts[sourceContract][sourceTokenId]),
            _targetTokenIds[sourceContract][sourceTokenId]
        );
    }

    function designateToken(
        IERC4799NFT sourceContract,
        uint256 sourceTokenId,
        IERC4799NFT targetContract,
        uint256 targetTokenId
    ) external {
        require(
            ERC721(address(sourceContract)).ownerOf(sourceTokenId) == msg.sender ||
            ERC721(address(sourceContract)).getApproved(sourceTokenId) == msg.sender, 
            "ERC721Composable: Only owner or approved address can set a designate ownership");
        _targetContracts[sourceContract][sourceTokenId] = targetContract;
        _targetTokenIds[sourceContract][sourceTokenId] = targetTokenId;
        emit OwnershipDesignation(
            sourceContract, 
            sourceTokenId,  
            targetContract,
            targetTokenId
        );
    }

    function onERC721Received(
        address,
        address from,
        uint256 sourceTokenId,
        bytes calldata
    ) external override returns (bytes4) {
        ERC721(msg.sender).approve(from, sourceTokenId);
        return IERC721Receiver.onERC721Received.selector;
    }

        function supportsInterface(bytes4 interfaceId)
        public
        view
        virtual
        override
        returns (bool)
    {
        return
            (interfaceId == type(IERC4799).interfaceId ||
            interfaceId == type(IERC721Receiver).interfaceId);
    }
}
```
```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity >=0.8.0 <0.9.0;

import "./IERC4799.sol";
import "./IERC4799NFT.sol";
import "@openzeppelin/contracts/utils/introspection/ERC165Checker.sol";

contract DesignatedOwner {
    function designatedOwnerOf(
        IERC4799NFT tokenContract,
        uint256 tokenId,
        uint256 maxDepth
    ) public view returns (address owner) {
        owner = tokenContract.ownerOf(tokenId);
        if (ERC165Checker.supportsInterface(owner, type(IERC4799).interfaceId)) {
            require(maxDepth > 0, "designatedOwnerOf: depth limit exceeded");
            (tokenContract, tokenId) = IERC4799(owner).designatedTokenOf(
                tokenContract,
                tokenId
            );
            return designatedOwnerOf(tokenContract, tokenId, maxDepth - 1);
        }
    }
}
```

## セキュリティ上の考慮事項

### 長い/循環的な所有権チェーン

主なセキュリティ上の懸念は、悪意のある行為者が過度に長い、または循環的な所有権チェーンを作成し、指定された所有者を照会しようとするアプリケーションがガスを使い果たして機能できなくなることです。これに対処するため、クライアントは常に`maxDepth`パラメータを考慮して照会し、一定数のチェーン遷移後に計算を打ち切るべきです。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。