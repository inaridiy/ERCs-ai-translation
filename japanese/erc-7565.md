---
original: 3014290be859fc89e5aaf6bb1ae4f6beee715857c5df10410eea347ced96514c
---

---
eip: 7565
title: 永続的なコントラクトNFTを担保として
description: 金融資産をNFTとしてロックし、DeFiでの資金調達の担保として使用することで、流動性供給を促進する。
author: Hyoungsung Kim (@HyoungsungKim) <hyougnsung@keti.re.kr>, Yong-Suk Park <yspark@keti.re.kr>, Hyun-Sik Kim <hskim@keti.re.kr>
discussions-to: https://ethereum-magicians.org/t/erc-7565-proposal-perpetual-contract-nft-for-defi-composability/16790
status: Draft
type: Standards Track
category: ERC
created: 2023-11-27
requires: 721, 4907
---

## 概要

このERCは、個人（「資産所有者」と呼ばれる）がNFTを担保に資金を借り入れることができるメカニズムを提案しています。これらのNFTは、事前に定義された満期期間後に、基礎となる資産とそれに付随する利益を請求する権利を表します。[^1]

[^1]:
    ```csl-json
    {
        "container-title": "IEEE Access",
        "author": [
            {
                "given": "Hyoungsung",
                "family": "Kim"
            },
            {
                "given": "Hyun-Sik",
                "family": "Kim"
            },
            {
                "given": "Yong-Suk",
                "family": "Park"
            }
        ],
        "DOI": "10.1109/ACCESS.2022.3225884",
        "URL": "https://ieeexplore.ieee.org/document/9967987",
        "type": "article-journal",
        "id": 9967987,
        "citation-label": "9967987",        
        "issued": {
            "date-parts": [
                [
                    2022
                ]
            ]
        },
        "keyword": "Contracts;Nonfungible tokens;Cryptocurrency;Finance;Smart contracts;Blockchains;Financial services;Automated market maker (AMM);blockchain;decentralized exchange (DEX);decentralized finance (DeFi);Ethereum;liquidity pool (LP);non-fungible token (NFT);uniswap",
        "page": "126802-126814",
        "title": "Perpetual Contract NFT as Collateral for DeFi Composability",
        "volume": 10
    }
    ```

## 動機

急速に進化するDeFiの環境では、利息や議決権などの利点を提供する資産ロックのさまざまなメカニズムが導入されています。しかし、この分野の重要な課題の1つは、これらの資産がロックされている間の流動性を維持することです。このERCは、[ERC-721](./eip-721.md)と[ERC-4907](./eip-4907.md)を使用して、ロックされた資産から利益を生み出す方法を提案することで、この課題に取り組んでいます。

DeFiサービスでは、自動化された市場メーカー(AMM)を運営しており、流動性提供者は資産をプールに提供し、その持分を表すNFTを受け取ります。これらのNFTは資産の権利と関連する利益を表しますが、同時に資産をプールにロックするため、提供者にとって流動性の課題となります。現在の慣行では、緊急の流動性ニーズのために提供者が資産を引き出す必要があり、これがプールの流動性に悪影響を及ぼし、資産スワップ時のスリップページを増加させる可能性があります。

当提案では、流動性プールにロックされた資産を表すこれらのNFTを担保として使用できるようにします。この方法により、流動性提供者は資産を引き出すことなく一時的な流動性を得ることができ、プールの流動性レベルを維持することができます。さらに、貸借や取引など、資産ロックが一般的なDeFiサービスにも拡張されます。ロックされた資産表現をNFTとして担保化することで、DeFiサービス全体にわたる多様な流動性ソリューションを提供することを目指しています。

私たちが導入する永続的なコントラクトNFTの概念は、暗号通貨デリバティブ市場の永続的先物契約のアイデアを活用しています。これらのNFTは永続的な契約の権利と担保を表し、DeFiのコンポーザビリティのための効果的な担保として使用できます。永続的なコントラクトNFTは、ロックされた資産の効用を高める新しい形のNFTを提供し、流動性を提供しながら資産ロックの利点を維持するというDeFiアプリケーションの大きな利点を提供します。

## 仕様

このドキュメントの「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

### コントラクトインターフェース

Solidityインターフェース。

```solidity
    interface IPerpetualContractNFT {

        // NFTが借入の担保として使用された際に発行されるイベント
        event Collateralized(uint256 indexed tokenId, address indexed owner, uint256 loanAmount, uint256 interestRate, uint256 loanDuration);

        // NFTを担保とした借入が完済された際に発行されるイベント
        event LoanRepaid(uint256 indexed tokenId, address indexed owner);

        // 借入が デフォルトした際に、NFTが貸し手に移転した事を示すイベント
        event Defaulted(uint256 indexed tokenId, address indexed lender);

        // NFT所有者がNFTを担保に入れて借入を行えるようにする
        // @param tokenId 担保に入れるNFTのトークンID
        // @param loanAmount 借り入れる資金の額
        // @param interestRate 借入の金利
        // @param loanDuration 借入期間
        function collateralize(uint256 tokenId, uint256 loanAmount, uint256 interestRate, uint64 loanDuration) external;

        // 借り手が借入を返済し、担保に入れたNFTを取り戻せるようにする
        // @param tokenId 担保に入れられたNFTのトークンID
        // @param repayAmount 返済する金額
        function repayLoan(uint256 tokenId, uint256 repayAmount) external;

        // 特定のNFTの借入条件を照会できるようにする
        // @param tokenId 照会するNFTのトークンID
        // @return loanAmount 借入金額
        // @return interestRate 金利
        // @return loanDuration 借入期間
        // @return loanDueDate 返済期限
        function getLoanTerms(uint256 tokenId) external view returns (uint256 loanAmount, uint256 interestRate, uint256 loanDuration, uint256 loanDueDate);

        // 特定のNFTの現在の所有者を照会できるようにする
        // @param tokenId 照会するNFTのトークンID
        // @return 現在の所有者のアドレス
        function currentOwner(uint256 tokenId) external view returns (address);

        // 特定のNFTの借入返済額を表示する
        // @param tokenId 担保に入れられたNFTのトークンID
        // @return 返済に必要な総額(元金+利息)
        function viewRepayAmount(uint256 tokenId) external view returns (uint256);
    }
```

#### イベント `Collateralized`

- `Collateralized`イベントは、`collateralize`関数が正常に実行された際に必ず発行されなければなりません。
- 用途: NFTが借入の担保として使用された際の、借入金額、金利、借入期間などの重要な詳細を記録するためのイベントです。

#### イベント `LoanRepaid`

- `LoanRepaid`イベントは、`repayLoan`関数が正常に実行された際に必ず発行されなければなりません。
- 用途: 借入が返済され、対応するNFTが担保から解放された際のイベントを記録するためのものです。

#### イベント `Defaulted`

- `Defaulted`イベントは、借入がデフォルトし、NFTが貸し手に移転した場合に発行されなければなりません。
- 用途: 借入のデフォルトと、NFTが貸し手に移転した事を記録するためのイベントです。

#### 関数 `collateralize`

- `collateralize`関数は`external`として実装されるべきです。
- 用途: NFT所有者がNFTを担保に入れて借入を行えるようにするための関数です。

#### 関数 `repayLoan`

- `repayLoan`関数は`external`として実装されるべきです。
- 用途: NFT所有者が借入を返済し、NFTを取り戻せるようにするための関数です。
  
#### 関数 `getLoanTerms`

- `getLoanTerms`関数は`external` `view`として実装されてもよいです。
- 用途: 特定のNFTの借入条件を照会するための関数です。

#### 関数 `currentOwner`

- `currentOwner`関数は`external` `view`として実装されてもよいです。
- 用途: 特定のNFTの現在の所有者を照会するための関数です。

#### 関数 `viewRepayAmount`

- `viewRepayAmount`関数は`external` `view`として実装されてもよいです。
- 用途: 特定のNFTの現在の返済額を表示するための関数です。
  
## 根拠

### 設計の動機

この規格の設計は、特にコラテラルとして資産がロックされている際の流動性と管理に関する課題に取り組むことを目的としています。DeFiにおける従来のメカニズムでは、資産保有者が貸借、ステーキング、収益ファーミングなどに参加するために資産をロックアップする必要があり、これによって流動性が失われてしまいます。この規格は、資産保有者が資産をロックしつつ一定の流動性を維持できるよう、より柔軟なアプローチを導入することで、DeFi製品の有用性と魅力を高めることを目的としています。

### 設計の決定

- デュアルロールシステム(資産所有者とDeFiプラットフォーム/コントラクト): NFT所有者(資産保有者)とNFTを担保として利用するDeFiプラットフォームやコントラクトの間で明確な役割分担が確立されています。この区別により、権利と責任の管理が簡素化され、潜在的な対立が軽減されます。

- 資産ロックの利点を損なわずに流動性を高める: この規格の重要な特徴は、流動性プールや ステーキングプログラムに ロックされた資産を表すNFTを担保として使用できるようにすることです。この方法により、資産保有者は資産を引き出すことなく一時的な流動性を得ることができ、利息の発生や議決権などの関連する利点を維持することができます。

- 自動化された借入と担保管理: 担保化されたNFTの条件と条件を管理するための自動化された機能の統合は、トランザクションコストと複雑さを最小限に抑えるための意図的な選択です。

- DeFiのコンポーザビリティ: 資産ロックとコラテラル化サービスの統合に重点を置いたDeFiのコンポーザビリティへの戦略的な重点は、この規格にとって重要です。この アプローチは、DeFiプラットフォームやサービス全体での規格の採用を促進し、DeFiエコシステム内の seamless な連携を目指しています。

### 代替案と関連する取り組み

- [ERC-4907](./eip-4907.md)との比較: [ERC-4907](./eip-4907.md)もNFTの所有者とユーザーの二重の役割モデルを導入していますが、当規格は特にDeFiトランザクションにおける担保化の利用に焦点を当てており、[ERC-4907](./eip-4907.md)のレンタル指向のアプローチとは異なっています。

- 従来の担保化手法に対する改善: 完全な資産ロックアップを必要とする従来のDeFi担保化と比較して、この規格は、継続的な流動性アクセスを可能にするより動的で柔軟なモデルを提案しています。

## 下位互換性

[ERC-721](./eip-721.md)と完全に互換性があり、[ERC-4907](./eip-4907.md)とのインテグレーションによりNFTのレンタルにも対応しています。

## テストケース

```solidity
// SPDX-License-Identifier: CC0-1.0 
pragma solidity ^0.8.0;

import "./PerpetualContractNFT.sol";

contract PerpetualContractNFTDemo is PerpetualContractNFT {

    constructor(string memory name, string memory symbol)
        PerpetualContractNFT(name, symbol)
    {         
    }

    function mint(uint256 tokenId, address to) public {
        _mint(to, tokenId);
    }
}
```

```solidity
import { expect } from "chai";
import { ethers } from "hardhat";

describe("Perpet
## テストケース

```solidity
// SPDX-License-Identifier: CC0-1.0 
pragma solidity ^0.8.0;

import "./PerpetualContractNFT.sol";

contract PerpetualContractNFTDemo is PerpetualContractNFT {

    constructor(string memory name, string memory symbol)
        PerpetualContractNFT(name, symbol)
    {         
    }

    function mint(uint256 tokenId, address to) public {
        _mint(to, tokenId);
    }
}
```

```solidity
import { expect } from "chai";
import { ethers } from "hardhat";

describe("PerpetualContractNFTDemo", function () {
    it("should allow an owner to collateralize an NFT, rent it to a contract, and then have the owner repay the loan", async function () {
        const [owner] = await ethers.getSigners();

        const PerpetualContractNFTDemo = await ethers.getContractFactory("PerpetualContractNFTDemo");
        const demo = await PerpetualContractNFTDemo.deploy("DemoNFT", "DNFT");
        await demo.waitForDeployment();
        expect(demo.target).to.be.properAddress;

        // オーナーにNFTをミントする
        await demo.mint(1, owner.address);

        // オーナーがNFTを担保に入れて借入を行う
        const loanAmount = ethers.parseUnits("1", "ether"); // 1 Ether in Wei. 精度エラーを避けるためにWeiを使用。
        const interest = 5; // 5%の金利
        const expiration = Math.floor(new Date().getTime() / 1000) + 3600; // 60分(3600秒)後に期限切れ。Solidityの`hours`は秒に変換されるため。
        
        await demo.connect(owner).collateralize(1, loanAmount, interest, expiration); // tokenId, loanAmount, interestRate, loanDuration

        // NFTの現在の所有者(コントラクトアドレス)を確認
        expect(await demo.userOf(1)).to.equal(demo.target);

        // 借り手が借入を返済してNFTを取り戻す
        const repayAmountWei = await demo.connect(owner).viewRepayAmount(1);
        await demo.connect(owner).repayLoan(1, repayAmountWei);
        
        // 借入返済後にNFTが元の所有者に返されたことを確認
        expect(await demo.userOf(1)).to.equal("0x0000000000000000000000000000000000000000");
    });
    });
```

ターミナルで実行:

```console
npx hardhat test
```

## 参考実装

```solidity
// SPDX-License-Identifier: CC0-1.0 
pragma solidity ^0.8.0;

//import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IPerpetualContractNFT.sol";
import "./ERC4907/ERC4907.sol";

contract PerpetualContractNFT is ERC4907, IPerpetualContractNFT {
    struct LoanInfo {
        address borrower;   // NFTを担保に借入を行った借り手のアドレス
        uint256 loanAmount; // 借入金額
        uint256 interestRate; // 借入の金利
        uint64 loanDuration; // 借入期間
        uint256 loanStartTime; // 借入開始時刻
    }

    mapping(uint256 => LoanInfo) internal _loans;

    //与えられた名称とシンボルでPerpetual Contract NFTコントラクトを初期化するコンストラクタ
    constructor(string memory name_, string memory symbol_)
        ERC4907(name_, symbol_)
    {}

    function collateralize(uint256 tokenId, uint256 loanAmount, uint256 interestRate, uint64 loanDuration) public override {
        require(ownerOf(tokenId) == msg.sender || isApprovedForAll(ownerOf(tokenId), msg.sender) || getApproved(tokenId) == msg.sender, "Not owner nor approved");

        LoanInfo storage info = _loans[tokenId];
        info.borrower = msg.sender;
        // 借入金額は、NFTで表される資産の価値を考慮した適切なローン・トゥ・バリュー(LTV)比率を反映する必要がある。
        info.loanAmount = loanAmount;
        info.interestRate = interestRate;
        info.loanDuration = loanDuration;
        info.loanStartTime = block.timestamp;

        setUser(tokenId, address(this), loanDuration);
        emit Collateralized(tokenId, msg.sender, loanAmount, interestRate, loanDuration);

        // 貸付資産の管理に関するさらなるロジックを実装できる
    }

    function repayLoan(uint256 tokenId, uint256 repayAmount) public override {
        require(_loans[tokenId].borrower == msg.sender, "Not the borrower.");

        // 返済に必要な総額を計算
        uint256 totalDue = viewRepayAmount(tokenId);

        // 返済額が少なくとも総額の一部をカバーできることを確認
        require(repayAmount <= totalDue, "Repay amount exceeds total due.");

        // 返済後の残りの借入金額を計算
        _loans[tokenId].loanAmount = totalDue - repayAmount;

        // 借入金額が完済された場合、NFTのユーザーをデフォルトの状態にリセットする
        if(_loans[tokenId].loanAmount == 0) {
            setUser(tokenId, address(0), 0);
        }

        emit LoanRepaid(tokenId, msg.sender);
    }


    function getLoanTerms(uint256 tokenId) public view override returns (uint256, uint256, uint256, uint256) {
        LoanInfo storage info = _loans[tokenId];
        return (info.loanAmount, info.interestRate, info.loanDuration, info.loanStartTime);
    }

    function currentOwner(uint256 tokenId) public view override returns (address) {
        return ownerOf(tokenId);
    }

    function viewRepayAmount(uint256 tokenId) public view returns (uint256) {
        if (_loans[tokenId].loanAmount == 0) {
            // 借入金額がゼロの場合、返済する必要はない
            return 0;
        }

        // 金利は時間単位で計算され、実際の借入期間に応じて按分される。
        // 借り手が借入期間の半分で返済する場合、5%の年間金利なら2.5%の金利を支払う。
        uint256 elapsed = block.timestamp > (_loans[tokenId].loanStartTime + _loans[tokenId].loanDuration) 
                        ? _loans[tokenId].loanDuration  / 1 hours
                        : (block.timestamp - _loans[tokenId].loanStartTime) / 1 hours;

        // 切り上げ
        // 例: 15/4 = 3.75
        // round((15 + 4 - 1)/4) = 4, round((15/4) = 3)
        uint256 interest = ((_loans[tokenId].loanAmount * _loans[tokenId].interestRate / 100) * elapsed + (_loans[tokenId].loanDuration / 1 hours) - 1) / 
                    (_loans[tokenId].loanDuration / 1 hours);

        // 総返済額を計算
        uint256 totalDue = _loans[tokenId].loanAmount + interest;

        return totalDue;
    }

    // ローンのデフォルト、譲渡、NFTライフサイクルの他の側面を処理するための追加の関数とロジック
}
```

## セキュリティ上の考慮事項

<!-- 議論が必要です。 -->

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)により放棄されています。