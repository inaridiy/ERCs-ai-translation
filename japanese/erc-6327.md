---
original: 74670240c5e04fe91369a348f88b93ae7c775b35957cef48d933a9fd743f96ac
---

---
eip: 6327
title: エラスティック署名
description: パスワードを使ってデータに署名する秘密鍵
author: George (@JXRow)
discussions-to: https://ethereum-magicians.org/t/eip-6327-elastic-signature-es/12554
status: Draft
type: Standards Track
category: ERC
created: 2023-01-13
---


## 概要

エラスティック署名(ES)は、ユーザーフレンドリーなシークレットでデータに署名することを目的としています。シークレットはオンチェーンで完全に検証され、どこにも保存されません。ユーザーは必要に応じてシークレットを変更できます。シークレットの長さは固定されていません。シークレットはパスワードのようなものであり、非技術的なユーザーにとってはプライベートキーよりも理解しやすい概念です。このEIPでは、ESを検証および承認するためのスマートコントラクトインターフェイスを定義しています。


## 動機

「変更可能な秘密鍵」を使うことで何ができるでしょうか? 長年にわたり、特に技術経験の少ないユーザーのオンボーディングの障壁を下げる方法を探してきました。プライベートキーのカストディソリューションは、ユーザーフレンドリーなオンボーディング体験を提供しますが、ベンダー依存型であり分散化されていません。ESは、ゼロ知識技術を使ってこの課題に取り組みます。ユーザーはシークレットを知っていることの証明を生成し、スマートコントラクトがその証明を検証します。

### ユースケース

ESは代替の署名アルゴリズムです。プライベートキーの署名に対する「どちらか一方」の解決策ではありません。プライベートキーの署名メカニズムに追加の署名メカニズムとして設計されています。

- DeFiアプリは、トランザクションの資金移動プロセスにESを活用できます。ユーザーはトランザクションを完了するためにパスワードを提供する必要があります。これにより、プライベートキーが侵害された場合でも追加の保護が得られます。
- ESは、[ERC-4337](./eip-4337.md)のようなスマートコントラクトウォレットのプラグインとしても使用できます。プライベートキーの代わりに分散パスワードが選択されます。これにより、新しいEthereumDappユーザーのスムーズなオンボーディング体験につながる可能性があります。


## 仕様

以下の用語を使用します:

- `pwdhash`は秘密のパスワードのハッシュを表します。
- `datahash`は意図したトランザクションデータのハッシュを表します。
- `fullhash`は`datahash`とすべての既知の変数のハッシュを表します。
- `expiration`は意図したトランザクションの有効期限を表す時間スタンプです。
- `allhash`は`fullhash`と`pwdhash`のハッシュを表します。

関与する3つの当事者は、Verifier、Requester、Proverです。

- Verifier
  - `datahash`から`fullhash`を計算する必要があります。`datahash`はRequestorから提供されます。
  - 特定のアドレス(EOAまたはスマートコントラクトウォレット)の`pwdhash`を導出する必要があります。
  - Requestorから提供された`proof`、`fullhash`、`allhash`を使って、導出した`pwdhash`を検証する必要があります。
- Requester
  - `datahash`を生成し、`expiration`を決定する必要があります。
  - Proverから提供された`proof`、`allhash`、`datahash`、`expiration`を使って、Verifierに検証を要求する必要があります。
- Prover
  - Requestorと合意した`datahash`と`expiration`から、`proof`と`allhash`を生成する必要があります。
  - `nonce`やその他の既知の変数も使用します。

その他の要件は以下の通りです:

- 既知の変数は、すべての当事者が利用可能である必要があります。
  - `nonce`を含める必要があります。
  - `chainid`を含める必要があります。
  - Verifier固有の変数を含めることができます。
- 公開ステートメントには以下を含める必要があります:
  - `pwdhash`を反映するもの
  - `fullhash`を反映するもの
  - `allhash`を反映するもの
- `fullhash`の計算方法は、VerifierとProverの間で合意されている必要があります。
- `datahash`の計算方法

### `IElasticSignature` インターフェイス

これはVerifierのインターフェイスです。

```solidity
pragma solidity ^0.8.0;

interface IElasticSignature {
    /**
     * ユーザーがパスワードをセット/リセットした後に発行されるイベント
     * @param user - パスワードハッシュが設定されたユーザーのアドレス。スマートコントラクトウォレットアドレスまたはEOAウォレットアドレスになります。
     * @param pwdhash - パスワードハッシュ
     */
    event SetPassword(address indexed user, uint indexed pwdhash);

    /**
     * ユーザーの検証が正常に行われた後に発行されるイベント
     * @param user - 提出された`proof`が検証されたユーザーのアドレス。スマートコントラクトウォレットアドレスまたはEOAウォレットアドレスになります。
     * @param nonce - 最後に使用されたnonceを置き換える新しいnonce。
     */
    event Verified(address indexed user, uint indexed nonce);

    /**
     * ユーザーの`pwdhash`を取得する
     * @param user - ユーザーのアドレス
     * @return - 指定のアドレスの`pwdhash`
     */
    function pwdhashOf(address user) external view returns (uint);

    /**
     * ユーザーの`pwdhash`を更新する
     * @param proof1 - 古いパスワードで生成された証明
     * @param expiration1 - 古いパスワードの署名の有効期限(秒)
     * @param allhash1 - 古いパスワードで生成された`allhash`
     * @param proof2 - 新しいパスワードで生成された証明
     * @param pwdhash2 - 新しいパスワードのハッシュ
     * @param expiration2 - 新しいパスワードの署名の有効期限(秒)
     * @param allhash2 - 新しいパスワードで生成された`allhash`
     */
    function resetPassword(
        uint[8] memory proof1,
        uint expiration1,
        uint allhash1,
        uint[8] memory proof2,
        uint pwdhash2,
        uint expiration2,
        uint allhash2
    ) external;

    /**
     * 特定のユーザーの証明を検証する
     * 他のコントラクトから呼び出される必要があります。他のコントラクトは`datahash`を提供します。`proof`はユーザーが生成します。
     * @param user - 検証を行うユーザーのアドレス
     * @param proof - パスワードで生成された証明
     * @param datahash - ユーザーが署名するデータのハッシュ
     * @param expiration - 証明が有効期限切れになるまでの秒数
     * @param allhash - `proof`と一緒に生成された公開ステートメント
     */
    function verify(
        address user,
        uint[8] memory proof,
        uint datahash,
        uint expiration,
        uint allhash
    ) external;
}
```

`verify`関数は別のコントラクトから呼び出される必要があります。他のコントラクトは`datahash`を生成して呼び出す必要があります。この関数は、`allhash`がパスワードで正しく計算されていることを検証する必要があります。


## 根拠

コントラクトはすべてのユーザーの`pwdhash`を保存します。

![verifier-contract](../assets/eip-6327/zkpass-1.png)

以下のチャートはZKサーキットロジックを示しています。

![circuit-logic](../assets/eip-6327/zkpass-2.png)

署名を検証するには、`proof`、`allhash`、`pwdhash`、`fullhash`が必要です。

![workflow](../assets/eip-6327/zkpass-3.png)

Proverは`proof`と公開出力を生成します。それらをサードパーティのRequesterコントラクトに送信します。Requesterは`datahash`を生成します。`datahash`、`proof`、`allhash`、`expiration`、ProverのアドレスをVerifierコントラクトに送信します。コントラクトは、`datahash`がProverから来たものであることを検証します。つまり、引き出し操作がProverのパスワードで署名されていることを意味します。


## 下位互換性

このEIPは、署名検証に関する従来の取り組みとの下位互換性があります。なぜなら、このメソッドはパスワードベースの署名に特化しており、EOA署名には依存していないためです。


## 参考実装

署名コントラクトの実装例:

```solidity
pragma solidity ^0.8.0;

import "../interfaces/IElasticSignature.sol";
import "./verifier.sol";

contract ZKPass is IElasticSignature {
    Verifier verifier = new Verifier();

    mapping(address => uint) public pwdhashOf;

    mapping(address => uint) public nonceOf;

    constructor() {
    }

    function resetPassword(
        uint[8] memory proof1,
        uint expiration1,
        uint allhash1,
        uint[8] memory proof2,
        uint pwdhash2,
        uint expiration2,
        uint allhash2
    ) public override {
        uint nonce = nonceOf[msg.sender];

        if (nonce == 0) {
            //初期パスワード設定

            pwdhashOf[msg.sender] = pwdhash2;
            nonceOf[msg.sender] = 1;
            verify(msg.sender, proof2, 0, expiration2, allhash2);
        } else {
            //パスワードのリセット

            // 古いpwdhashを確認
            verify(msg.sender, proof1, 0, expiration1, allhash1);

            // 新しいpwdhashを確認
            pwdhashOf[msg.sender] = pwdhash2;
            verify(msg.sender, proof2, 0, expiration2, allhash2);
        }

        emit SetPassword(msg.sender, pwdhash2);
    }

    function verify(
        address user,
        uint[8] memory proof,
        uint datahash,
        uint expiration,
        uint allhash
    ) public override {
        require(
            block.timestamp < expiration,
            "ZKPass::verify: expired"
        );

        uint pwdhash = pwdhashOf[user];
        require(
            pwdhash != 0,
            "ZKPass::verify: user not exist"
        );

        uint nonce = nonceOf[user];
        uint fullhash = uint(keccak256(abi.encodePacked(expiration, block.chainid, nonce, datahash))) / 8; // 256b->254b
        require(
            verifyProof(proof, pwdhash, fullhash, allhash),
            "ZKPass::verify: verify proof fail"
        );

        nonceOf[user] = nonce + 1;

        emit Verified(user, nonce);
    }

    /////////// util ////////////

    function verifyProof(
        uint[8] memory proof,
        uint pwdhash,
        uint fullhash, //254b
        uint allhash
    ) internal view returns (bool) {
        return
            verifier.verifyProof(
                [proof[0], proof[1]],
                [[proof[2], proof[3]], [proof[4], proof[5]]],
                [proof[6], proof[7]],
                [pwdhash, fullhash, allhash]
            );
    }
}
```

verifier.solはsnarkjsによって自動生成されたものです。ソースコードcircuit.circomは以下の通りです。

```javascript
pragma circom 2.0.0;

include "../../node_modules/circomlib/circuits/poseidon.circom";

template Main() {
    signal input in[3];
    signal output out[3];

    component poseidon1 = Poseidon(2);
    component poseidon2 = Poseidon(2);

    poseidon1.inputs[0] <== in[0];  //pwd
    poseidon1.inputs[1] <== in[1];  //address
    out[0] <== poseidon1.out; //pwdhash

    poseidon2.inputs[0] <== poseidon1.out;
    poseidon2.inputs[1] <== in[2]; //fullhash
    out[1] <== in[2]; //fullhash
    out[2] <== poseidon2.out; //allhash
}

component main = Main();
```


## セキュリティ上の考慮事項

pwdhashが公開されているため、パスワードを解読される可能性があります。RTX3090のPoseidonハッシュレートは100Mhash/sと推定されており、解読時間の推定は以下の通りです:

8文字(数字) : 1秒

8文字(数字+英語) : 25日

8文字(数字+英語+記号) : 594日

12文字(数字) : 10000秒

12文字(数字+英語) : 1023042年


12文字(数字+英語+記号) : 116586246年

プライベートキーの解読難易度は2^256ですが、40文字(数字+英語+記号)の解読難易度は92^40です。92^40 > 2^256なので、パスワードが40文字の場合、プライベートキーよりも解読が困難になります。

## 著作権

著作権およびその関連権利は[CC0](../LICENSE.md)で放棄されています。