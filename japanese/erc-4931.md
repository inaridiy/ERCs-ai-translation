---
original: 6c9f26855caa2dc7e9a9a9b3bc9ada1015428b82f2d2d39bb673a27cc6e23962
---

---
eip: 4931
title: 汎用トークンアップグレード標準
description: ERC20トークンコントラクトのアップグレードのための標準インターフェースを作成する。
author: John Peterson (@John-peterson-coinbase), Roberto Bayardo (@roberto-bayardo), David Núñez (@cygnusv)
discussions-to: https://ethereum-magicians.org/t/eip-4931-generic-token-upgrade-standard/8687
status: 停滞
type: Standards Track
category: ERC
created: 2021-11-02
requires: 20
---


## 概要

以下の標準は、[ERC-20](./eip-20.md)トークンのアップグレードのための標準APIの実装を可能にします。この標準は、ある契約(「ソーストークン」と呼ばれる)からの トークンを別の契約(「デスティネーショントークン」と呼ばれる)に変換するためのインターフェースを定義し、トークンアップグレードに関する基本情報(ソースおよびデスティネーショントークン契約のアドレス、変換比率など)を提供するためのいくつかのヘルパーメソッドを指定しています。

## 動機

トークン契約のアップグレードには通常、資産保有者が開発者が提供する専用のインターフェースを使って古いトークンを新しいトークンに交換する必要があります。この標準インターフェースにより、資産保有者や集中型・分散型取引所がトークンアップグレードをより効率的に行えるようになります。なぜなら、トークン契約アップグレードスクリプトが再利用可能になるためです。標準化により、アップグレード契約の機能性を検証するためのセキュリティオーバーヘッドが削減されます。また、トークン発行者にもトークンアップグレードを効果的に実装する方法が明確に示されます。

## 仕様
この文書の中の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

注意: (Optional Ext.)と記されているメソッドは、ダウングレード機能の拡張の一部であり、ダウングレード機能が不要な場合は実装されないままでいてもかまいません。
### トークンアップグレードインターフェースコントラクト
``` solidity
interface IEIP4931 {
```
#### メソッド

##### upgradeSource

アップグレードされる元のトークン(ソーストークン)のアドレスを返します。

``` solidity
/// @dev アップグレードされる元のトークン契約("ソース契約")を取得するためのゲッター
/// @return ソーストークン契約のアドレス
function upgradeSource() external view returns(address)
```

##### upgradeDestination

アップグレード先のトークン契約のアドレスを返します。

``` solidity
/// @dev アップグレード先のトークン契約("デスティネーション契約")を取得するためのゲッター
/// @return デスティネーショントークン契約のアドレス
function upgradeDestination() external view returns(address)
```

##### isUpgradeActive

アップグレード機能の現在の状態を返します。アップグレード契約が機能し、アップグレードを提供している場合は `true` を、そうでない場合は `false` を返します。

``` solidity
/// @dev このメソッドはアップグレード契約がアップグレードを提供している場合はtrueを、そうでない場合はfalseを返す
/// @return アップグレードの状態をブール値で表す
function isUpgradeActive() external view returns(bool)
```
##### isDowngradeActive

ダウングレード機能の現在の状態を返します。ダウングレード契約が機能し、ダウングレードを提供している場合は `true` を、そうでない場合は `false` を返します。ダウングレードの Optional Ext. が実装されていない場合、このメソッドは常に `false` を返し、ダウングレードが利用できないことを示します。

``` solidity
/// @dev このメソッドはダウングレード契約がダウングレードを提供している場合はtrueを、そうでない場合はfalseを返す
/// @return ダウングレードの状態をブール値で表す
function isDowngradeActive() external view returns(bool)
```
##### ratio

アップグレードで使用されるデスティネーショントークンとソーストークンの比率を2つのuint256値として返します。例えば `(3, 1)` は、1つのソーストークンに対して3つのデスティネーショントークンが提供されることを意味します。

``` solidity
/// @dev アップグレード時のデスティネーショントークンとソーストークンの比率を取得するためのゲッター
/// @return 2つのuint256値、最初の値が分子、2番目の値が分母を表す
function ratio() external view returns(uint256, uint256)
```

##### totalUpgraded

ソーストークンからデスティネーショントークンにアップグレードされた合計トークン数を返します。ダウングレードの Optional Ext. が実装されている場合、`downgrade`の呼び出しによって `totalUpgraded`の返値が減少する可能性があります。ダウングレードが実装されていない場合、この値は単調増加します。

``` solidity
/// @dev ソーストークンからデスティネーショントークンにアップグレードされた合計トークン数を取得するためのゲッター。
/// ダウングレードの Optional Ext. が実装されている場合、この値は単調増加しない可能性があります。
/// @return ソーストークンからデスティネーショントークンにアップグレードされた合計トークン数
function totalUpgraded() external view returns(uint256)
```
##### computeUpgrade

指定された `sourceAmount` のソーストークンに対応するデスティネーショントークンの `destinationAmount` と、アップグレードできない残りのソーストークンの `sourceRemainder` の量を計算します。例えば、(3, 2)の比率の場合、2つのソーストークンに対して3つのデスティネーショントークンが提供されます。ソーストークン5つの場合、`computeUpgrade(5)` は `(6, 1)` を返します。これは、6つのデスティネーショントークンが提供される(この場合4つのソーストークンから)一方で、1つのソーストークンが残るということを意味します。
``` solidity
/// @dev アップグレードを呼び出した際に受け取るデスティネーショントークンの量と、
/// 残りのソーストークンの量を計算するメソッド
/// @param sourceAmount アップグレードされるソーストークンの量
/// @return destinationAmount アップグレードされた際に受け取るデスティネーショントークンの量
/// @return sourceRemainder アップグレードされずに残るソーストークンの量
function computeUpgrade(uint256 sourceAmount) external view
        returns (uint256 destinationAmount, uint256 sourceRemainder)
```

##### computeDowngrade (Optional Ext.)

指定された `destinationAmount` のデスティネーショントークンに対応するソーストークンの `sourceAmount` と、ダウングレードできない残りのデスティネーショントークンの `destinationRemainder` の量を計算します。例えば、(3, 2)の比率の場合、3つのデスティネーショントークンに対して2つのソーストークンが提供されます。デスティネーショントークン13個の場合、`computeDowngrade(13)` は `(4, 1)` を返します。これは、4つのソーストークンが提供される(この場合12個のデスティネーショントークンから)一方で、1つのデスティネーショントークンが残るということを意味します。
``` solidity
/// @dev ダウングレードを呼び出した際に受け取るソーストークンの量と、
/// 残りのデスティネーショントークンの量を計算するメソッド
/// @param destinationAmount ダウングレードされるデスティネーショントークンの量
/// @return sourceAmount ダウングレードされた際に受け取るソーストークンの量
/// @return destinationRemainder ダウングレードされずに残るデスティネーショントークンの量
function computeDowngrade(uint256 destinationAmount) external view
        returns (uint256 sourceAmount, uint256 destinationRemainder)
```


##### upgrade

`amount` のソーストークンをデスティネーショントークンに指定された比率でアップグレードします。デスティネーショントークンは `_to` アドレスに送信されます。この関数は、ソーストークンをアップグレード契約でロックするか、バーンしなければなりません。ダウングレードの Optional Ext. が実装されている場合、ソーストークンはバーンではなくロックされます。呼び出し元のアドレスにソーストークンが十分にない場合や、`isUpgradeActive` が `false` を返す場合、この関数は例外を投げなければなりません。また、`Upgrade` イベントを発行しなければなりません。ソーストークン契約に対して事前に `approve` を呼び出す必要があります。
``` solidity
/// @dev ソーストークンからデスティネーショントークンへのアップグレードを行うメソッド。
/// アップグレードの状態が有効でない、ソーストークン契約に対して事前にapproveが呼ばれていない、
/// またはmsg.sender のソーストークン残高が sourceAmount より小さい場合は失敗する。
/// 比率によって切り捨てられるトークンがある場合は、最も近い整数のソーストークン量をアップグレードし、
/// 余剰分をmsg.senderに返す。
/// Upgradeイベントを発行する
/// @param _to デスティネーショントークンが送信される先のアドレス
/// @param sourceAmount アップグレードされるソーストークンの量
function upgrade(address _to, uint256 sourceAmount) external
```


##### downgrade (Optional Ext.)
`amount` のデスティネーショントークンをソーストークンにダウングレードします。ソーストークンは `_to` アドレスに送信されます。この関数はデスティネーショントークンをソーストークンに戻さなければなりません。呼び出し元のアドレスにデスティネーショントークンが十分にない場合や、`isDowngradeActive` が `false` を返す場合、この関数は例外を投げなければなりません。また、`Downgrade` イベントを発行しなければなりません。デスティネーショントークン契約に対して事前に `approve` を呼び出す必要があります。
``` solidity
/// @dev デスティネーショントークンからソーストークンへのダウングレードを行うメソッド。
/// ダウングレードの状態が有効でない、デスティネーショントークン契約に対して事前にapproveが呼ばれていない、
/// またはmsg.sender のデスティネーショントークン残高が destinationAmount より小さい場合は失敗する。
/// 比率によって切り捨てられるトークンがある場合は、最も近い整数のデスティネーショントークン量をダウングレードし、
/// 余剰分をmsg.senderに返す。
///  Downgradeイベントを発行する
/// @param _to ソーストークンが送信される先のアドレス
/// @param destinationAmount ダウングレードされるデスティネーショントークンの量
function downgrade(address _to, uint256 destinationAmount) external
```

#### イベント

##### Upgrade

トークンがアップグレードされた際に必ず発生します。

``` solidity
/// @param _from アップグレードを呼び出したアドレス
/// @param _to デスティネーショントークンが送信された先のアドレス
/// @param sourceAmount アップグレードされたソーストークンの量
/// @param destinationAmount _to アドレスに送信されたデスティネーショントークンの量
event Upgrade(address indexed _from, address indexed _to, uint256 sourceAmount, uint256 destinationAmount)
```

##### Downgrade (Optional Ext.)

トークンがダウングレードされた際に必ず発生します。

``` solidity
/// @param _from ダウングレードを呼び出したアドレス
/// @param _to ソーストークンが送信された先のアドレス
/// @param sourceAmount _to アドレスに送信されたソーストークンの量
/// @param destinationAmount ダウングレードされたデスティネーショントークンの量
event Downgrade(address indexed _from, address indexed _to, uint256 sourceAmount, uint256 destinationAmount)
}
```

## 根拠
これまでいくつかの重要なERC20アップグレード(例: Golem: GNT -> GLM)では、アップグレード機能がトークン契約に直接組み込まれていました。これはアップグレードの機能をトークンの機能に密結合させるため、最適な方法ではないと考えられます。このEIPでは、アップグレードの機能をトークン契約から切り離した第三者の契約を使ってアップグレードを行うことを推奨しています。アップグレードの機能を標準化することで、資産保有者や取引所がアップグレードを行うためのスクリプトを簡単に再利用できるようになり、将来のアップグレードの手間が軽減されます。このインターフェースは意図的に広範囲にわたるように設計されており、アップ
グレードの詳細はほとんど実装者に任されているため、トークン契約の実装がアップグレードプロセスに干渉することはありません。最後に、ソーストークンの処理方法を厳格に定めることで、トークンアップグレードに対する安全性と妥当性をより高めることを目指しています。これは`upgrade`メソッドの仕様によって達成されます。焼却可能なトークンは焼却されるべきであり、そうでない場合は`0x00`アドレスに送信されるべきだと考えられています。ダウングレードの Optional Ext. が実装されている場合、連続的な`upgrade`と`downgrade`の呼び出しによってどちらかのトークン(ソースまたはデスティネーション)の供給量が人為的に増加するのを避けるため、デフォルトではソーストークンをアップグレード契約でロックすることになっています。

## 下位互換性
下位互換性の問題はありません。以前に実装されたトークンアップグレードの中には、この標準に準拠していないものがあるかもしれません。そのような場合、資産発行者がそのアップグレードがEIP-4931に準拠していないことを伝えることが関連してくるでしょう。

## 参考実装
``` solidity
//SPDX-License-Identifier: Apache-2.0
pragma solidity 0.8.9;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "./IEIP4931.sol";

contract SourceUpgrade is  IEIP4931 {
	using SafeERC20  for IERC20;

	uint256 constant RATIO_SCALE = 10**18;
    
	IERC20 private source;
	IERC20 private destination;
	bool private upgradeStatus;
	bool private downgradeStatus;
	uint256 private numeratorRatio;
	uint256 private denominatorRatio;
	uint256 private sourceUpgradedTotal;

	mapping(address => uint256) public upgradedBalance;

	constructor(address _source, address _destination, bool _upgradeStatus, bool _downgradeStatus, uint256 _numeratorRatio, uint256 _denominatorRatio) {
		require(_source != _destination, "SourceUpgrade: source and destination addresses are the same");
		require(_source != address(0), "SourceUpgrade: source address cannot be zero address");
		require(_destination != address(0), "SourceUpgrade: destination address cannot be zero address");
		require(_numeratorRatio > 0, "SourceUpgrade: numerator of ratio cannot be zero");
		require(_denominatorRatio > 0, "SourceUpgrade: denominator of ratio cannot be zero");

		source = IERC20(_source);
		destination = IERC20(_destination);
		upgradeStatus = _upgradeStatus;
		downgradeStatus = _downgradeStatus;
		numeratorRatio = _numeratorRatio;
		denominatorRatio = _denominatorRatio;
	}

	/// @dev アップグレードされる元のトークン契約("ソース契約")を取得するためのゲッター
	/// @return ソーストークン契約のアドレス
	function upgradeSource() external view returns(address) {
		return address(source);
	}

	/// @dev アップグレード先のトークン契約("デスティネーション契約")を取得するためのゲッター
	/// @return デスティネーショントークン契約のアドレス
	function upgradeDestination() external view returns(address) {
		return address(destination);
	}

	/// @dev このメソッドはアップグレード契約がアップグレードを提供している場合はtrueを、そうでない場合はfalseを返す
	/// @return アップグレードの状態をブール値で表す
	function isUpgradeActive() external view returns(bool) {
		return upgradeStatus;
	}

	/// @dev このメソッドはダウングレード契約がダウングレードを提供している場合はtrueを、そうでない場合はfalseを返す
	/// @return ダウングレードの状態をブール値で表す
	function isDowngradeActive() external view returns(bool) {
		return downgradeStatus;
	}

	/// @dev アップグレード時のデスティネーショントークンとソーストークンの比率を取得するためのゲッター
	/// @return 2つのuint256値、最初の値が分子、2番目の値が分母を表す
	function ratio() external view returns(uint256, uint256) {
		return (numeratorRatio, denominatorRatio);
	}

	/// @dev ソーストークンからデスティネーショントークンにアップグレードされた合計トークン数を取得するためのゲッター。
	/// ダウングレードの Optional Ext. が実装されている場合、この値は単調増加しない可能性があります。
	/// @return ソーストークンからデスティネーショントークンにアップグレードされた合計トークン数
	function totalUpgraded() external view returns(uint256) {
		return sourceUpgradedTotal;
	}

	/// @dev アップグレードを呼び出した際に受け取るデスティネーショントークンの量と、
	/// 残りのソーストークンの量を計算するメソッド
	/// @param sourceAmount アップグレードされるソーストークンの量
	/// @return destinationAmount アップグレードされた際に受け取るデスティネーショントークンの量
	/// @return sourceRemainder アップグレードされずに残るソーストークンの量
	function computeUpgrade(uint256 sourceAmount)
		public
		view
		returns (uint256 destinationAmount, uint256 sourceRemainder)
	{
		sourceRemainder = sourceAmount % (numeratorRatio / denominatorRatio);
		uint256 upgradeableAmount = sourceAmount - (sourceRemainder * RATIO_SCALE);
		destinationAmount = upgradeableAmount * (numeratorRatio / denominatorRatio);
	}

	/// @dev ダウングレードを呼び出した際に受け取るソーストークンの量と、
	/// 残りのデスティネーショントークンの量を計算するメソッド
	/// @param destinationAmount ダウングレードされるデスティネーショントークンの量
	/// @return sourceAmount ダウングレードされた際に受け取るソーストークンの量
	/// @return destinationRemainder ダウングレードされずに残るデスティネーショントークンの量
	function computeDowngrade(uint256 destinationAmount)
		public
		view
		returns (uint256 sourceAmount, uint256 destinationRemainder)
	{
		destinationRemainder = destinationAmount % (denominatorRatio / numeratorRatio);
		uint256 upgradeableAmount = destinationAmount - (destinationRemainder * RATIO_SCALE);
		sourceAmount = upgradeableAmount / (denominatorRatio / numeratorRatio);
	}

	/// @dev ソーストークンからデスティネーショントークンへのアップグレードを行うメソッド。
	/// アップグレードの状態が有効でない、ソーストークン契約に対して事前にapproveが呼ばれていない、
	/// またはmsg.sender のソーストークン残高が sourceAmount より小さい場合は失敗する。
	/// 比率によって切り捨てられるトークンがある場合は、最も近い整数のソーストークン量をアップグレードし、
	/// 余剰分をmsg.senderに返す。
	/// Upgradeイベントを発行する
	/// @param _to デスティネーショントークンが送信される先のアドレス
	/// @param sourceAmount アップグレードされるソーストークンの量
	function upgrade(address _to, uint256 sourceAmount) external {
		require(upgradeStatus == true, "SourceUpgrade: upgrade status is not active");
		(uint256 destinationAmount, uint256 sourceRemainder) = computeUpgrade(sourceAmount);
		sourceAmount -= sourceRemainder;
		require(sourceAmount > 0, "SourceUpgrade: disallow conversions of zero value");

		upgradedBalance[msg.sender] += sourceAmount;
		source.safeTransferFrom(
			msg.sender,
			address(this),
			sourceAmount
			);
		destination.safeTransfer(_to, destinationAmount);
		sourceUpgradedTotal += sourceAmount;
		emit Upgrade(msg.sender, _to, sourceAmount, destinationAmount);
	}

	/// @dev デスティネーショントークンからソーストークンへのダウングレードを行うメソッド。
	/// ダウングレードの状態が有効でない、デスティネーショントークン契約に対して事前にapproveが呼ばれていない、
	/// またはmsg.sender のデスティネーショントークン残高が destinationAmount より小さい場合は失敗する。
	/// 比率によって切り捨てられるトークンがある場合は、最も近い整数のデスティネーショントークン量をダウングレードし、
	/// 余剰分をmsg.senderに返す。
	///  Downgradeイベントを発行する
	/// @param _to ソーストークンが送信される先のアドレス
	/// @param destinationAmount ダウングレードされるデスティネーショントークンの量
	function downgrade(address _to, uint256 destinationAmount) external {
		require(upgradeStatus == true, "SourceUpgrade: upgrade status is not active");
		(uint256 sourceAmount, uint256 destinationRemainder) = computeDowngrade(destinationAmount);
		destinationAmount -= destinationRemainder;
		require(destinationAmount > 0, "SourceUpgrade: disallow conversions of zero value");
		require(upgradedBalance[msg.sender] >= sourceAmount,
			"SourceUpgrade: can not downgrade more than previously upgraded"
			);

		upgradedBalance[msg.sender] -= sourceAmount;
		destination.safeTransferFrom(
			msg.sender,
			address(this),
			destinationAmount
			);
		source.safeTransfer(_to, sourceAmount);
		sourceUpgradedTotal -= sourceAmount;
		emit Downgrade(msg.sender, _to, sourceAmount, destinationAmount);
	}
}
```


## セキュリティ上の考慮事項
主なセキュリティ上の考慮事項は、アップグレード時にソーストークンを適切に処理し、二重にアップグレードされないようにすることです。慎重に処理しないと、ソーストークンの有効性が疑問視される可能性があります。そのため、EIP-4931では焼却可能なトークンは必ず焼却するよう厳格に定めています。焼却不可能なトークンの場合は、`0x00`アドレスに送信することが認められています。ダウングレードの Optional Ext. が実装されている場合、この制約は緩和され、ソーストークンをアップグレード契約で保持することができます。

## 著作権
著作権およびそれに関連する権利は[CC0](https://creativecommons.org/publicdomain/zero/1.0/)によって放棄されています。