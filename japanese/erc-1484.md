---
original: 4d6b7cca8edf5bd1c3c4d9e2da56c115a354552c253ec7831f232c3171dbe468
---

---
eip: 1484
title: デジタル ID アグリゲーター
author: Anurag Angara <anurag.angara@gmail.com>, Andy Chorlian <andychorlian@gmail.com>, Shane Hampton <shanehampton1@gmail.com>, Noah Zinsmeister <noahwz@gmail.com>
discussions-to: https://github.com/ethereum/EIPs/issues/1495
status: 停滞
type: Standards Track
category: ERC
created: 2018-10-12
requires: 191
---

## 簡単な要約
既存の、提案された、および仮想的な将来のデジタル ID 標準と広く相互運用可能なデジタル ID 情報を集約するプロトコル。

## 概要
このEIPは、Ethereumブロックチェーン上のアイデンティティ管理およびアグリゲーションフレームワークを提案しています。これにより、エンティティは単一の「Identity Registry」スマートコントラクトを介して「Identity」を主張し、それをEthereum アドレスと様々な意味のある方法で関連付け、スマートコントラクトと対話することができます。これにより、任意の複雑なアイデンティティ関連の機能が可能になります。特に(他の機能の中でも)ERC-1484の「Identities」は、自主的であり、[ERC-725](./eip-725.md)および[ERC-1056](./eip-1056.md)のアイデンティティをネイティブにサポートし、[DID準拠](https://github.com/NoahZinsmeister/ERC-1484/blob/master/best-practices/DID-Method.md)しており、[メタトランザクション](https://github.com/NoahZinsmeister/ERC-1484/tree/master/contracts/examples/Providers/MetaTransactions)によって完全に駆動されることができます。

## 動機
Ethereumコミュニティによって提案されたERC/EIP [725](./eip-725.md)、[735](https://github.com/ethereum/EIPs/issues/735)、[780](https://github.com/ethereum/EIPs/issues/780)、[1056](./eip-1056.md)などの新興のアイデンティティ標準およびそれに関連するフレームワークは、デジタル ID を様々な方法で定義および実装しています。既存のアプローチが成熟し、新しい標準が出現し、アイデンティティに関する孤立した非標準的なアプローチが開発されるにつれ、ブロックチェーンユーザーやデベロッパーがアイデンティティを調整することが非常に煩雑になり、不必要な作業の重複が発生するようになります。

オンチェーンのアイデンティティソリューションの増加は、それぞれがアイデンティティの概念を具体化し、Ethereumの特定の側面(クレームプロトコル、アイデンティティごとのスマートコントラクト、署名検証スキーム など)にリンクさせているためです。この提案では、そのアプローチを避け、代わりにEthereum ネットワークと個々のアイデンティティアプリケーションの間にプロトコルレイヤーを導入しています。これにより、アイデンティティ管理と相互運用性の課題を解決し、アイデンティティ駆動型のアプリケーションがオピニオンのないアイデンティティ管理プロトコルを活用できるようにします。

## 定義
- `Identity Registry`: すべての `Identities` のハブとなる単一のスマートコントラクト。`Registry` の主な責任は、Ethereum 識別番号(EIN)で個別に表される `Identities` のグローバル名前空間の規則を定義および施行することです。

- `Identity`: 回復アドレス、関連付けられたアドレスのセット、プロバイダーのセット、リゾルバーのセットなど、アイデンティティに関連する重要な情報を含むデータ構造。`Identities` は1から始まる増分`uint`識別子であるEINで表され、一意ですが他の情報は含まれていません。各`Identity`はSolidity構造体です:

```solidity
struct Identity {
    address recoveryAddress;
    AddressSet.Set associatedAddresses;
    AddressSet.Set providers;
    AddressSet.Set resolvers;
}
```

- `Associated Address`: `Identity`に公開的に関連付けられたEthereum アドレス。アドレスが `Associated Address` になるには、`Identity`が既存の `Associated Address` から適切に署名されたメッセージを送信するか、そのアドレスから取引を行う必要があります。これにより、関連付けの意図を示します。`Associated Address` は、`Identity`から取引/署名を行うことで削除できます。ある アドレスは、任意の時点で1つの `Identity` の `Associated Address` のみになることができます。

- `Provider`: `Identities` に代わって行動するよう許可されたEthereum アドレス(通常はスマートコントラクトですが、必須ではありません)。これには、`Associated Address`、`Provider`、`Resolver`セットの管理が含まれますが、これに限定されません。`Providers`は、ユーザーが `Identities` を管理するのを容易にするために存在します。

- `Resolver`: `Identities`に関する任意の情報を含むスマートコントラクト。リゾルバーは、ERC-725などのアイデンティティ標準を実装したり、`Identities`に関する識別情報を活用または宣言するスマートコントラクトで構成されたりする可能性があります。これらは単純な証明構造や、より洗練された金融dApp、ソーシャルメディアdAppなどになる可能性があります。`Identity`に追加された各`Resolver`は、`Identity`をより情報豊富にします。

- `Recovery Address`: `Identity`の紛失を回復するために使用できるEthereum アドレス(アカウントまたはスマートコントラクト)。

- `Destruction`: `Identity`の制御が取り返しのつかない状態で失われた場合の緊急措置として、`Destruction`は`Identity`を永久に無効化するものです。これにより、すべての`Associated Addresses`、`Providers`、および必要に応じて`Resolvers`が削除されますが、`Identity`の存在は保持されます。`Identity`の制御は無効化されます。

## 仕様
この提案におけるデジタル ID は、任意のアイデンティティアプリケーションよりも多くの情報を含む包括的なアカウントと見なすことができます。この包括的なアイデンティティは、`Resolvers`と呼ばれる無制限の数のサブアイデンティティに解決可能です。これにより、`Identity`という原子的なエンティティが、`Resolvers`と呼ばれる抽象的なデータ構造に解決可能になります。`Resolvers`は、その`Associated Addresses`または`EIN`によって`Identities`を認識します。

このプロトコルは、`Identity`を主張し、`Associated Addresses`、`Providers`、`Resolvers`を管理することを中心としています。`Identities`は、1つ以上の`Providers`にこの責任の大部分または全部を委任するか、`Associated Address`から直接実行することができます。`Associated Addresses`/`Providers`は、`Resolvers`と`Providers`を自由に追加および削除できます。`Associated Addresses`は、適切な許可がある場合にのみ追加または削除できます。

### Identity Registry
`Identity Registry`には、新しい`Identities`を作成し、既存の`Identities`が`Associated Addresses`、`Providers`、`Resolvers`を管理するための機能が含まれています。この登録簿は、`Identity`を構築するすべての側面に対してトランザクションを根本的に必要とすることに注意することが重要です。ただし、dAppやアイデンティティアプリケーションのアクセシビリティの重要性を認識し、ユーザーがガス代を支払う必要なく`Identities`を構築できるよう、`Providers`を強化しています。このパターンの例は、しばしばメタトランザクションと呼ばれ、[リファレンス実装](https://github.com/NoahZinsmeister/ERC-1484/tree/master/contracts/examples/Providers/MetaTransactions)で確認できます。

複数のアドレスを1つのアイデンティティに関連付けることができる(逆は不可能)ため、`Identities`はEINで表されます。この`uint`識別子は、QRフォーマットでエンコードしたり、`Provider`または`Resolver`レベルの登録簿で`string`などのユーザーフレンドリーな形式にマッピングしたりできます。

### アドレス管理
アドレス管理機能は、ユーザー所有の複数の`Associated Addresses`を`Identity`に信頼できる方法で接続することです。これは、任意の`Associated Address`に特別な地位を与えるものではなく、むしろERC-725標準で指定された`management`、`action`、`claim`、`encryption`キーや、ERC-1056で指定された`identifiers`および`delegates`など、ユーザーのアイデンティティの上位レベルのアプリケーションで指定されるようにしています。これにより、ユーザーは複数のウォレットからコモンアイデンティティデータにアクセスできるようになりながら、次のことも可能になります:

- アイデンティティ以外の契約と対話する機能を維持する
- ユーザーのアイデンティティのアプリケーション層で確立されたアドレス固有の権限を活用する

アドレス管理機能の信頼性は、堅牢な許可スキームによって実現されます。`Identity`に`Associated Address`を追加するには、1) レジストリ内の既存のアドレスと2) 主張されるアドレスの両方からの暗黙の許可または明示的な署名が必要です。重要なのは、許可が確立されていれば、トランザクションの送信者は特定のアドレスである必要がないということです。これにより、ユーザーだけでなく、企業、政府など第三者もアイデンティティ管理の負担を負うことができます。侵害された`Associated Address`が他の`Associated Addresses`を一方的に削除できないようにするため、`Associated Address`を削除するには、そのアドレスからトランザクションを行うか署名を生成する必要があります。

ERC-1484で要求されるすべての署名は、[ERC-191](./eip-191.md) v0仕様に従って設計されています。リプレイ攻撃を回避するため、すべての署名には`block.timestamp`の範囲内のタイムスタンプを含める必要があります。詳細については、リファレンス実装の[ベストプラクティス文書](https://github.com/NoahZinsmeister/ERC-1484/blob/master/best-practices/VerifyingSignatures.md)を参照してください。

### プロバイダー管理
このプロトコルでは、ユーザーが直接アイデンティティ管理機能を呼び出すことができますが、`Providers`(通常はスマートコントラクト)にユーザーに代わってアイデンティティ管理機能を実行させることで、より堅牢で将来的にも対応できるようにすることも目的としています。`Identity`によって設定された`Provider`は、ユーザーの`EIN`を関数呼び出しに渡すことで、アドレス管理およびリゾルバー管理機能を実行できます。

### リゾルバー管理
`Resolver`は、`Identity`に解決される情報をエンコードするスマートコントラクトです。リゾルバーに特定の情報をエンコードできる方法や、それが可能にする機能については、中立的な立場を維持しています。`Resolver`の存在は、このERCがアイデンティティ*プロトコル*であり、アイデンティティ*アプリケーション*ではないことの主な理由です。`Resolver`は、抽象的なデータをスマートコントラクトに解決し、`Identity`という原子的なエンティティに結び付けます。

### 回復
ユーザーが`Associated Address`の制御を失った場合、`Recovery Address`がフォールバックメカニズムを提供します。`Identity`の作成時に、作成者によって`Recovery Address`がパラメーターとして渡されます。回復機能は3つのシナリオでトリガーされます:

**1. 回復アドレスの変更**: 回復キーが失われた場合、`Associated Address`/`Provider`は[triggerRecoveryAddressChange](#triggerrecoveryaddresschange)/[triggerRecoveryAddressChangeFor](#triggerrecoveryaddresschangefor)を呼び出すことができます。`Associated Address`または`Provider`の制御を奪った悪意のある行為者が回復アドレスを自分の制御下のアドレスに変更するのを防ぐため、この操作は14日間の異議申し立て期間をトリガーします。この期間中、古い`Recovery Address`は[triggerRecovery](#triggerrecovery)を呼び出すことで変更を拒否できます。`Recovery Address`が14日以内に変更を拒否しない場合、`Recovery Address`が変更されます。

**2. 回復**: ユーザーが`Associated Address`または`Recovery Address`が失われたか盗まれたことを認識した場合、回復が発生します。この場合、`Recovery
Address`が[triggerRecovery](#triggerrecovery)を呼び出す必要があります。これにより、対応する`Identity`からすべての`Associated Addresses`と`Providers`が削除され、関数呼び出しで渡されたアドレスに置き換えられます。`Identity`および関連する`Resolvers`は完全性を維持します。ユーザーは、適切な侵害されていないアドレスを自分の`Identity`に再度追加する責任を負います。

*重要なのは、`Recovery Address`がユーザー管理のウォレットや、マルチシグウォレットやスマートコントラクトなどの別のアドレスになる可能性があることです。これにより、任意の複雑な回復ロジックが可能になります! これには、[DID](https://decentralized.id/)などの標準に完全に準拠した回復も含まれます。*

**3. 破壊**
回復スキームは`Recovery Address`に相当の権限を与えます。したがって、`Recovery Address`が侵害された場合に悪用を防ぐための最終手段として、`Destruction`が用意されています。悪意のある行為者が`Recovery Address`を侵害し、回復をトリガーした場合、`Recovery`プロセスで削除されたアドレスは14日以内に[triggerDestruction](#triggerdestruction)を呼び出すことができ、`Identity`を永久に無効化します。その後、ユーザーは新しい`Identity`を作成する必要があり、`Resolver`または`Provider`レイヤーに構築されたアイデンティティアプリケーションの回復スキームに取り組む必要があります。

#### 代替の回復方法の検討
上記の回復プロセスを考案する際、多くの代替案を検討しました。最終的に、最も中立的で、モジュラーで、`Associated Address`、`Provider`、`Resolver`コンポーネントの理念と一致するスキームを選択しました。それでも、他の回復オプションの一部を強調することが重要だと感じています。これは、私たちがどのように結論に至ったかを説明するためです。

**高レベルの懸念**
根本的に、回復スキームは、侵害されたアドレスが`Identity`の制御を奪うことに耐性がある必要があります。2番目の懸念は、侵害されたアドレスが、オフチェーンのユーティリティのために`Identity`を悪意的に破壊することを防ぐことです。これは最適な状況ではありませんが、制御を獲得された場合よりも厳しい状況です。

**代替1: 核オプション**
このアプローチでは、他の`Associated Address`が侵害された場合、任意の`Associated Address`が`Identity`を破壊できるようになります。これは過激に見えるかもしれませんが、このERCはアイデンティティ*プロトコル*であって、アイデンティティ*アプリケーション*ではないため、強く検討しました。これは、ユーザーの侵害された`Identity`が破壊されても、`Resolver`および/または`Provider`レベルの実際のアイデンティティに利用可能な復元メカニズムがまだ存在するはずだからです。最終的に、この方法を却下した理由は次の2つです:

- 1つの`Associated Address`しかないユーザーの場合、堅牢ではない
- 寛容でない性質のため、アイデンティティアプリケーションへの回復リクエストの頻度が増加する

**代替2: プロバイダーによる一方的なアドレス削除**
これにより、`Associated Addresses`/`Providers`は、対象のアドレスからの署名なしで`Associated Addresses`を削除できるようになります。この実装では、`Providers`が不正なアドレスを削除するための任意の複雑なスキーム(マルチシグ要件、中央集権型オフチェーン検証、ユーザー管理のマスターアドレス、管轄契約への委任など)を含めることができます。侵害された`Associated Address`が単に悪意のある`Provider`を設定して、侵害されていないアドレスを削除できないようにするために、`Provider`が設定されてから`Associated Address`を削除できるようになるまでの待機期間が必要でした。この方法を却下した理因は、`Providers`に過度の負担を課すと感じたためです。ユーザーに洗練された機能範囲を提供する`Provider`がデプロイ後に回復ロジックに脅威が見つかった場合、`Provider`固有のインフラを再構築する必要があります。また、ユーザーが`Provider`に`Associated Addresses`を一方的に削除する権限を与えるかどうかを決めるフラグを含めることも検討しました。最終的に、`Recovery Address`を介してのみ`Associated Addresses`の削除を許可することで、同等に洗練された回復ロジックを可能にしつつ、`Providers`の機能から回復ロジックを分離し、ユーザーが潜在的に欠陥のある実装に制御を委ねる余地を少なくできると結論付けました。

## 根拠
プロトコルレイヤーでは、アイデンティティが特定のクレームや証明の構造に依存するべきではなく、任意の洗練されたクレームや証明の構造を構築できる信頼できるフレームワークの一部であるべきだと考えています。

既存のアイデンティティソリューションの主な批判は、それらが過度に制限的であるということです。私たちは要件を制限し、アイデンティティをモジュラーで将来対応可能なものに保ち、特定のアイデンティティコンポーネントが持つ機能について中立的な立場を維持することを目指しています。この提案により、ユーザーはブロックチェーン上で堅牢な`Identity`を使用するか、単なるアドレスを使用するかを選択できるようになります。

## 実装
**ERC-1484のリファレンス実装は[NoahZinsmeister/ERC-1484](https://github.com/NoahZinsmeister/ERC-1484)にあります。**

#### identityExists

渡された`EIN`で表される`Identity`が存在するかどうかを示す`bool`を返します。

```solidity
function identityExists(uint ein) public view returns (bool);
```

#### hasIdentity

渡された`_address`がアイデンティティに関連付けられているかどうかを示す`bool`を返します。

```solidity
function hasIdentity(address _address) public view returns (bool);
```

#### getEIN

渡された`_address`に関連付けられた`EIN`を返します。アドレスがEINに関連付けられていない場合はエラーをスローします。

```solidity
function getEIN(address _address) public view returns (uint ein);
```

#### isAssociatedAddressFor

渡された`EIN`に`_address`が関連付けられているかどうかを示す`bool`を返します。

```solidity
function isAssociatedAddressFor(uint ein, address _address) public view returns (bool);
```

#### isProviderFor

渡された`EIN`によって`provider`が設定されているかどうかを示す`bool`を返します。

```solidity
function isProviderFor(uint ein, address provider) public view returns (bool);
```

#### isResolverFor

渡された`EIN`によって`resolver`が設定されているかどうかを示す`bool`を返します。

```solidity
function isResolverFor(uint ein, address resolver) public view returns (bool);
```

#### getIdentity

渡された`EIN`の`recoveryAddress`、`associatedAddresses`、`providers`、`resolvers`を返します。

```solidity
function getIdentity(uint ein) public view
    returns (
        address recoveryAddress,
        address[] memory associatedAddresses, address[] memory providers, address[] memory resolvers
    );
```

#### createIdentity

`Identity`を作成し、`msg.sender`を唯一の`Associated Address`に設定します。新しい`Identity`のEINを返します。

```solidity
function createIdentity(address recoveryAddress, address[] memory providers, address[] memory resolvers)
    public returns (uint ein);
```

イベントをトリガー: [IdentityCreated](#identitycreated)

#### createIdentityDelegated

`createIdentity`と同じ論理を実行しますが、任意のアドレスから呼び出すことができます。この関数は、`associatedAddress`の同意を確保するために署名を要求します。

```solidity
function createIdentityDelegated(
    address recoveryAddress, address associatedAddress, address[] memory providers, address[] memory resolvers,
    uint8 v, bytes32 r, bytes32 s, uint timestamp
)
    public returns (uint ein);
```

イベントをトリガー: [IdentityCreated](#identitycreated)

#### addAssociatedAddress

`approvingAddress`の`EIN`に`addressToAdd`を追加します。`msg.sender`は`approvingAddress`または`addressToAdd`のいずれかである必要があり、署名は他方から必要です。

```solidity
function addAssociatedAddress(
    address approvingAddress, address addressToAdd, uint8 v, bytes32 r, bytes32 s, uint timestamp
)
    public
```

イベントをトリガー: [AssociatedAddressAdded](#associatedaddressadded)

#### addAssociatedAddressDelegated

`approvingAddress`の`EIN`に`addressToAdd`を追加します。`approvingAddress`と`addressToAdd`の両方からの署名が必要です。

```solidity
function addAssociatedAddressDelegated(
    address approvingAddress, address addressToAdd,
    uint8[2] memory v, bytes32[2] memory r, bytes32[2] memory s, uint[2] memory timestamp
)
    public
```

イベントをトリガー: [AssociatedAddressAdded](#associatedaddressadded)

#### removeAssociatedAddress

`msg.sender`を、その`EIN`の`Associated Address`から削除します。

```solidity
function removeAssociatedAddress() public;
```

イベントをトリガー: [AssociatedAddressRemoved](#associatedaddressremoved)


#### removeAssociatedAddressDelegated

`addressToRemove`を、その関連する`EIN`から削除します。`addressToRemove`からの署名が必要です。

```solidity
function removeAssociatedAddressDelegated(address addressToRemove, uint8 v, bytes32 r, bytes32 s, uint timestamp)
    public;
```

イベントをトリガー: [AssociatedAddressRemoved](#associatedaddressremoved)

#### addProviders

`msg.sender`の`Identity`に`Providers`の配列を追加します。

```solidity
function addProviders(address[] memory providers) public;
```

イベントをトリガー: [ProviderAdded](#provideradded)

#### addProvidersFor

`addProviders`と同じ論理を実行しますが、`Provider`が呼び出す必要があります。

```solidity
function addProvidersFor(uint ein, address[] memory providers) public;
```

イベントをトリガー: [ProviderAdded](#provideradded)

#### removeProviders

`msg.sender`の`Identity`から`Providers`の配列を削除します。

```solidity
function removeProviders(address[] memory providers) public;
```

イベントをトリガー: [ProviderRemoved](#providerremoved)


#### removeProvidersFor

`removeProviders`と同じ論理を実行しますが、`Provider`が呼び出す必要があります。

```solidity
function removeProvidersFor(uint ein, address[] memory providers) public;
```

イベントをトリガー: [ProviderRemoved](#providerremoved)


#### addResolvers

`msg.sender`の`EIN`に`Resolvers`の配列を追加します。

```solidity
function addResolvers(address[] memory resolvers) public;
```

イベントをトリガー: [ResolverAdded](#resolveradded)

#### addResolversFor

`addResolvers`と同じ論理を実行しますが、`Provider`が呼び出す必要があります。

```solidity
function addResolversFor(uint ein, address[] memory resolvers) public;
```

イベントをトリガー: [ResolverAdded](#resolveradded)

#### removeResolvers

`msg.sender`の`EIN`から`Resolvers`の配列を削除します。

```solidity
function removeResolvers(address[] memory resolvers) public;
```

イベントをトリガー: [ResolverRemoved](#resolverremoved)

#### removeResolversFor

`removeResolvers`と同じ論理を実行しますが、`Provider`が呼び出す必要があります。

```solidity
function removeResolversFor(uint ein, address[] memory resolvers) public;
```

イベントをトリガー: [ResolverRemoved](#resolverremoved)

#### triggerRecoveryAddressChange

`msg.sender`の`EIN`の現在の`recoveryAddress`を変更するプロセスを開始します。

```solidity
function triggerRecoveryAddressChange(address newRecoveryAddress) public;
```

イベントをトリガー: [RecoveryAddressChangeTriggered](#recoveryaddresschangetriggered)

#### triggerRecoveryAddressChangeFor

指定された`EIN`の現在の`recoveryAddress`を変更するプロセスを開始します。

```solidity
function triggerRecoveryAddressChangeFor(uint ein, address newRecoveryAddress) public;
```

イベントをトリガー: [RecoveryAddressChangeTriggered](#recoveryaddresschangetriggered)

#### triggerRecovery

現在の`recoveryAddress`、または2週間以内に変更された場合は古い`recoveryAddress`から`EIN`の回復をトリガーします。

```solidity
function triggerRecovery(uint ein, address newAssociatedAddress, uint8 v, bytes32 r, bytes32 s, uint timestamp) public;
```

イベントをトリガー: [RecoveryTriggered](#recoverytriggered)

#### triggerDestruction

`EIN`の破壊
をトリガーします。これにより、`Identity`が永久に使用不可能になります。

```solidity
function triggerDestruction(uint ein, address[] memory firstChunk, address[] memory lastChunk, bool clearResolvers)
  public;
```

イベントをトリガー: [IdentityDestroyed](#identitydestroyed)

### イベント

#### IdentityCreated

`Identity`が作成されたときにトリガーされる必要があります。

```solidity
event IdentityCreated(
    address indexed initiator, uint indexed ein,
    address recoveryAddress, address associatedAddress, address[] providers, address[] resolvers, bool delegated
);
```

#### AssociatedAddressAdded

アドレスが`Identity`に追加されたときにトリガーされる必要があります。

```solidity
event AssociatedAddressAdded(
    address indexed initiator, uint indexed ein, address approvingAddress, address addedAddress, bool delegated
);
```

#### AssociatedAddressRemoved

アドレスが`Identity`から削除されたときにトリガーされる必要があります。

```solidity
event AssociatedAddressRemoved(address indexed initiator, uint indexed ein, address removedAddress, bool delegated);
```

#### ProviderAdded

プロバイダーが`Identity`に追加されたときにトリガーされる必要があります。

```solidity
event ProviderAdded(address indexed initiator, uint indexed ein, address provider, bool delegated);
```

#### ProviderRemoved

プロバイダーが削除されたときにトリガーされる必要があります。

```solidity
event ProviderRemoved(address indexed initiator, uint indexed ein, address provider, bool delegated);
```

#### ResolverAdded

リゾルバーが追加されたときにトリガーされる必要があります。

```solidity
event ResolverAdded(address indexed initiator, uint indexed ein, address resolvers, bool delegated);
```

#### ResolverRemoved

リゾルバーが削除されたときにトリガーされる必要があります。

```solidity
event ResolverRemoved(address indexed initiator, uint indexed ein, address resolvers, bool delegated);
```

#### RecoveryAddressChangeTriggered

回復アドレスの変更がトリガーされたときにトリガーされる必要があります。

```solidity
event RecoveryAddressChangeTriggered(
    address indexed initiator, uint indexed ein,
    address oldRecoveryAddress, address newRecoveryAddress, bool delegated
);
```

#### RecoveryTriggered

回復がトリガーされたときにトリガーされる必要があります。

```solidity
event RecoveryTriggered(
    address indexed initiator, uint indexed ein, address[] oldAssociatedAddresses, address newAssociatedAddress
);
```

#### IdentityDestroyed

`Identity`が破壊されたときにトリガーされる必要があります。

```solidity
event IdentityDestroyed(address indexed initiator, uint indexed ein, address recoveryAddress, bool resolversReset);
```

### Solidity インターフェース
```solidity
interface IdentityRegistryInterface {
    function isSigned(address _address, bytes32 messageHash, uint8 v, bytes32 r, bytes32 s)
        external pure returns (bool);

    // Identity View Functions /////////////////////////////////////////////////////////////////////////////////////////
    function identityExists(uint ein) external view returns (bool);
    function hasIdentity(address _address) external view returns (bool);
    function getEIN(address _address) external view returns (uint ein);
    function isAssociatedAddressFor(uint ein, address _address) external view returns (bool);
    function isProviderFor(uint ein, address provider) external view returns (bool);
    function isResolverFor(uint ein, address resolver) external view returns (bool);
    function getIdentity(uint ein) external view returns (
        address recoveryAddress,
        address[] memory associatedAddresses, address[] memory providers, address[] memory resolvers
    );

    // Identity Management Functions ///////////////////////////////////////////////////////////////////////////////////
    function createIdentity(address recoveryAddress, address[] calldata providers, address[] calldata resolvers)
        external returns (uint ein);
    function createIdentityDelegated(
        address recoveryAddress, address associatedAddress, address[] calldata providers, address[] calldata resolvers,
        uint8 v, bytes32 r, bytes32 s, uint timestamp
    ) external returns (uint ein);
    function addAssociatedAddress(
        address approvingAddress, address addressToAdd, uint8 v, bytes32 r, bytes32 s, uint timestamp
    ) external;
    function addAssociatedAddressDelegated(
        address approvingAddress, address addressToAdd,
        uint8[2] calldata v, bytes32[2] calldata r, bytes32[2] calldata s, uint[2] calldata timestamp
    ) external;
    function removeAssociatedAddress() external;
    function removeAssociatedAddressDelegated(address addressToRemove, uint8 v, bytes32 r, bytes32 s, uint timestamp)
        external;
    function addProviders(address[] calldata providers) external;
    function addProvidersFor(uint ein, address[] calldata providers) external;
    function removeProviders(address[] calldata providers) external;
    function removeProvidersFor(uint ein, address[] calldata providers) external;
    function addResolvers(address[] calldata resolvers) external;
    function addResolversFor(uint ein, address[] calldata resolvers) external;
    function removeResolvers(address[] calldata resolvers) external;
    function removeResolversFor(uint ein, address[] calldata resolvers) external;

    // Recovery Management Functions ///////////////////////////////////////////////////////////////////////////////////
    function triggerRecoveryAddressChange(address newRecoveryAddress) external;
    function triggerRecoveryAddressChangeFor(uint ein, address newRecoveryAddress) external;
    function triggerRecovery(uint ein, address newAssociatedAddress, uint8 v, bytes32 r, bytes32 s, uint timestamp)
        external;
    function triggerDestruction(
        uint ein, address[] calldata firstChunk, address[] calldata lastChunk, bool resetResolvers
    ) external;

    // Events //////////////////////////////////////////////////////////////////////////////////////////////////////////
    event IdentityCreated(
        address indexed initiator, uint indexed ein,
        address recoveryAddress, address associatedAddress, address[] providers, address[] resolvers, bool delegated
    );
    event AssociatedAddressAdded(
        address indexed initiator, uint indexed ein, address approvingAddress, address addedAddress
    );
    event AssociatedAddressRemoved(address indexed initiator, uint indexed ein, address removedAddress);
    event ProviderAdded(address indexed initiator, uint indexed ein, address provider, bool delegated);
    event ProviderRemoved(address indexed initiator, uint indexed ein, address provider, bool delegated);
    event ResolverAdded(address indexed initiator, uint indexed ein, address resolvers);
    event ResolverRemoved(address indexed initiator, uint indexed ein, address resolvers);
    event RecoveryAddressChangeTriggered(
        address indexed initiator, uint indexed ein, address oldRecoveryAddress, address newRecoveryAddress
    );
    event RecoveryTriggered(
        address indexed initiator, uint indexed ein, address[] oldAssociatedAddresses, address newAssociatedAddress
    );
    event IdentityDestroyed(address indexed initiator, uint indexed ein, address recoveryAddress, bool resolversReset);
}
```

## 下位互換性
この標準に基づいて確立された`Identities`は既存のEthereum アドレスで構成されているため、下位互換性の問題はありません。デプロイ済みの非アップグレード可能なスマートコントラクトが`Identities`のリゾルバーになりたい場合は、アドレスを`EIN`で表される`Identities`に解決するラッパーコントラクトを記述する必要があります。

## 追加の参考資料
- [ERC-1484 リファレンス実装](https://github.com/NoahZinsmeister/ERC-1484)
- [ERC-191 署名](./eip-191.md)
- [ERC-725 Identities](./eip-725.md)
- [ERC-1056 Identities](./eip-1056.md)

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。