---
original: d127cf6d7051764d2b7eec16662e249333c21a27972a91f5c0c8fa22deb1000d
---

---
eip: 7615
title: コントラクト間の原子的プッシュベースデータフィード
description: パブリッシャーコントラクトがサブスクライバーコントラクトにデータをプッシュする原子的メカニズム
author: Elaine Zhang (@lanyinzly) <lz8aj@virginia.edu>、Jerry <jerrymindflow@gmail.com>、Amandafanny <amandafanny200@gmail.com>、Shouhao Wong (@wangshouh) <wongshouhao@outlook.com>、Doris Che (@Cheyukj) <dorischeyy@gmail.com>、Henry Yuan (@onehumanbeing) <hy2878@nyu.edu>
discussions-to: https://ethereum-magicians.org/t/erc-7615-smart-contract-data-push-mechanism/18466
status: Draft
type: Standards Track
category: ERC
created: 2024-02-03
---
## 概要
このERCは、データを送信するためのプッシュベースのメカニズムを提案しています。これにより、パブリッシャーコントラクトは呼び出し中に自動的にサブスクライバーコントラクトにデータをプッシュすることができます。具体的な実装には、パブリッシャーコントラクトがデータをプッシュするためのインターフェースと、サブスクライバーコントラクトがデータを受け取るためのインターフェースの2つが必要です。パブリッシャーコントラクトが呼び出されると、呼び出された関数がサブスクライバーアドレスに対応するかどうかを確認し、該当する場合はサブスクライバーコントラクトにデータをプッシュします。

## 動機
現在、多くのキーパーは、オフチェーンのデータや別個のデータ収集プロセスに依存して、チェーン上のイベントを監視しています。この提案は、パブリッシャーコントラクトがサブスクライバーコントラクトに更新情報を原子的にプッシュできるようなシステムを確立することを目的としています。パブリッシャーとサブスクライバーの直接的なオンチェーンの相互作用により、このシステムはより信頼性が高く効率的になります。

この提案は、DeFiの無限かつ無許可の拡張を可能にしたり、DAOのガバナンスを強化したりするなど、さまざまなアプリケーションに大きな利点をもたらします。

### レンディングプロトコル
パブリッシャーコントラクトの例としては、オラクルがあり、価格の更新をサブスクライバーのプロトコルに自動的にプッシュすることができます。レンディングプロトコルであるサブスクライバーは、受け取った価格に基づいて自動的にレンディングポジションを清算することができます。

### 自動支払い
サービスプロバイダーは、スマートコントラクトをパブリッシャーコントラクトとして使用することができ、ユーザーがこのコントラクトを呼び出すと、ユーザーのウォレットのようなサブスクライバーコントラクトにデータをプッシュすることができます。ユーザーのスマートコントラクトウォレットは、その後、自動的に対応する支払い操作を実行することができます。従来の`approve`アプローチと比較して、この解決策では、限定的な支払いなどの複雑な論理を実装することができます。

### 資産を移転せずにPoSを行う
一部のステーキングシナリオ、特にNFTステーキングでは、PoSコントラクトをサブスクライバーとし、NFTコントラクトをパブリッシャーとして設定することができます。これにより、ユーザーは資産を移転することなくステーキング報酬を得ることができます。

NFTの`transfer`などの操作が行われると、NFTコントラクトはこの情報をPoSコントラクトにプッシュし、PoSコントラクトは、ステーキングの解除や他の機能を実行することができます。

### DAOの投票
DAO ガバナンスコントラクトがパブリッシャーとして、投票が完了した後に自動的にプッシュメカニズムをトリガーし、関連するサブスクライバーコントラクトを呼び出して、投票結果を直接実装することができます。例えば、特定のアカウントやプールに資金を注入するなどです。

## 仕様

キーワードの "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY"、"OPTIONAL" は、RFC 2119に記載されているように解釈されるものとします。

### 概要
プッシュメカニズムは以下の4つのステップで構成されます:

1. パブリッシャーコントラクトが呼び出される。
2. パブリッシャーコントラクトは、呼び出された関数の`selector`からサブスクライバーリストを照会する。サブスクライバーコントラクトは、選択したデータを`inbox`に格納することができる。
3. パブリッシャーコントラクトは、`selector`とデータをサブスクライバーコントラクトの`exec`関数を呼び出してプッシュする。
4. サブスクライバーコントラクトは、プッシュされた`selector`とデータに基づいて実行されるか、必要に応じてパブリッシャーコントラクトの`inbox`関数から情報を要求することができる。

2番目のステップでは、呼び出された関数とそれに対応するサブスクライバーの関係を、パブリッシャーコントラクトで設定することができます。2つの設定方式が提案されています:

1. 無条件プッシュ: 設定された`selector`への呼び出しはすべてプッシュをトリガーする
2. 条件付きプッシュ: 設定された`selector`への条件付きの呼び出しのみがプッシュをトリガーする

単一の`selector`に対して、複数の異なるタイプのサブスクライバーコントラクトを設定することができます。パブリッシャーコントラクトは、各サブスクライバーコントラクトの`exec`関数を呼び出してデータをプッシュします。

サブスクライバーコントラクトの`isLocked`関数が`true`を返す場合、パブリッシャーコントラクトはサブスクライバーコントラクトのサブスクリプションを解除しなければなりません。

パブリッシャーコントラクトが`inbox`メカニズムを使用するかどうかは任意です。

4番目のステップでは、サブスクライバーコントラクトは、`exec`関数の実装において、可能な`selector`リクエストとデータをすべて処理する必要があります。場合によっては、`exec`が`inbox`関数を呼び出して、プッシュされたデータを完全に取得することができます。

![ワークフロー](../assets/eip-7615/ERC7615.svg)

### コントラクトインターフェース
上述のように、無条件プッシュと条件付きプッシュの2つの実装方式があります。無条件プッシュを実装するには、パブリッシャーコントラクトは以下のインターフェースを実装する必要があります:

```
interface IPushForce {
    event ForceApprove(bytes4 indexed selector, address indexed target);
    event ForceCancel(bytes4 indexed selector, address indexed target);
    event RenounceForceApprove();
    event RenounceForceCancel();

    error MustRenounce();
    error ForceApproveRenounced();
    error ForceCancelRenounced();

    function isForceApproved(bytes4 selector, address target) external returns (bool);
    function forceApprove(bytes4 selector, address target) external;
    function forceCancel(bytes4 selector, address target) external;
    function isRenounceForceApprove() external returns (bool);
    function isRenounceForceCancel() external returns (bool);
    function renounceForceApprove(bytes memory) external;
    function renounceForceCancel(bytes memory) external;
}
```

`isForceApproved`は、`selector`がすでに無条件に`target`アドレスのサブスクライバーコントラクトにバインドされているかどうかを照会するものです。
`forceApprove`は、`selector`を`target`サブスクライバーコントラクトにバインドするものです。`forceCancel`は、`selector`と`target`の間のバインド関係を解除するものですが、`target`の`isLocked`関数が`true`を返す必要があります。

`renounceForceApprove`は、`forceApprove`の権限を放棄するために使用されます。`renounceForceApprove`関数を呼び出した後は、`forceApprove`を呼び出すことができなくなります。同様に、`renounceForceCancel`は、`forceCancel`の権限を放棄するために使用されます。`renounceForceCancel`関数を呼び出した後は、`forceCancel`を呼び出すことができなくなります。

条件付きプッシュを実装するには、パブリッシャーコントラクトは以下のインターフェースを実装する必要があります:

```
interface IPushFree {
    event Approve(bytes4 indexed selector, address indexed target, bytes data);
    event Cancel(bytes4 indexed selector, address indexed target, bytes data);

    function inbox(bytes4 selector) external returns (bytes memory);
    function isApproved(bytes4 selector, address target, bytes calldata data) external returns (bool);
    function approve(bytes4 selector, address target, bytes calldata data) external;
    function cancel(bytes4 selector, address target, bytes calldata data) external;
}
```

`isApproved`、`approve`、`cancel`の機能は、`IPushForce`の対応する関数と同様ですが、プッシュが必要かどうかを確認するための`data`パラメーターが追加されています。
ここでの`inbox`は、サブスクライバーコントラクトから呼び出された場合にデータを格納するために使用されます。

パブリッシャーコントラクトは、`_push(bytes4 selector, bytes calldata data)`関数を実装する必要があります。これはフックとして機能します。パブリッシャーコントラクト内の、プッシュメカニズムを実装する必要のある関数はすべて、この内部関数を呼び出す必要があります。この関数は、`selector`と`data`に基づいて、無条件および条件付きのサブスクリプションコントラクトを照会し、対応する`exec`関数を呼び出す必要があります。

サブスクライバーは、以下のインターフェースを実装する必要があります:

```solidity
interface IExec {
    function isLocked(bytes4 selector, bytes calldata data) external returns (bool);
    function exec(bytes4 selector, bytes calldata data) external;
}
```

`exec`は、パブリッシャーコントラクトからのリクエストを受け取り、さらに実行するためのものです。
`isLocked`は、`selector`と`data`に基づいて、サブスクライバーコントラクトがパブリッシャーコントラクトからのサブスクリプション解除を許可するかどうかを確認するためのものです。これは、サブスクリプション解除のリクエストが受け取られた際にトリガーされます。

## 根拠

### 無条件と条件付きの設定
送信元コントラクトが呼び出されたときに、プッシュがトリガーされ、呼び出し元が結果的なガス代を支払う必要がある場合があります。
一部のケースでは、無条件プッシュが必要です。例えば、レンディングプロトコルに価格の変更をプッシュする場合などです。一方で、条件付きプッシュを使えば、不要なガス消費を削減できます。

### サブスクリプション解除前に`isLocked`をチェックする
`forceCancel`または`cancel`を呼び出す前に、パブリッシャーコントラクトはサブスクライバーコントラクトの`isLocked`関数を呼び出して、一方的な解除を避ける必要があります。サブスクライバーコントラクトはパブリッシャーコントラクトに大きな論理的依存関係を持っている可能性があり、解除によって深刻な問題が発生する可能性があります。したがって、サブスクライバーコントラクトは、十分な検討の上で`isLocked`関数を実装する必要があります。

### `inbox`メカニズム
特定のシナリオでは、パブリッシャーコントラクトは`selector`とともに必須のデータのみをサブスクライバーコントラクトにプッシュし、完全なデータは`inbox`に格納することができます。サブスクライバーコントラクトは、パブリッシャーコントラクトからのプッシュを受け取った後、必要に応じて`inbox`を呼び出すことができます。
`inbox`メカニズムは、プッシュ情報を簡素化しつつ、完全なデータの可用性を確保することで、ガス消費を削減することができます。

### 関数セレクターをパラメーターとして使用する
関数セレクターを使ってサブスクライバーコントラクト
の住所を取得することで、より詳細な設定が可能になります。
サブスクライバーコントラクトにとって、プッシュ情報に基づいて要求元の具体的な関数を持つことで、プッシュ情報の正確な処理が可能になります。

### 権限放棄の安全性強化
`forceApprove`と`forceCancel`の両方の権限は、それぞれの放棄関数を使って放棄することができます。`renounceForceApprove`と`renounceForceCancel`の両方が呼び出されると、登録されたプッシュターゲットを変更することができなくなり、セキュリティが大幅に向上します。

## 参考実装

```
pragma solidity ^0.8.24;

import {EnumerableSet} from "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";
import {IPushFree, IPushForce} from "./interfaces/IPush.sol";
import {IExec} from "./interfaces/IExec.sol";

contract Foo is IPushFree, IPushForce {
    using EnumerableSet for EnumerableSet.AddressSet;

    bool public override isRenounceForceApprove;
    bool public override isRenounceForceCancel;

    mapping(bytes4 selector => mapping(uint256 tokenId => EnumerableSet.AddressSet targets)) private _registry;
    mapping(bytes4 selector => EnumerableSet.AddressSet targets) private _registryOfAll;
    // mapping(bytes4 => bytes) public inbox;

    modifier notLock(bytes4 selector, address target, bytes memory data) {
        require(!IExec(target).isLocked(selector, data), "Foo: lock");
        _;
    }

    function inbox(bytes4 selector) public view returns (bytes memory data) {
        uint256 loadData;
        assembly {
            loadData := tload(selector)
        }

        data = abi.encode(loadData);
    }

    function isApproved(bytes4 selector, address target, bytes calldata data) external view override returns (bool) {
        uint256 tokenId = abi.decode(data, (uint256));
        return _registry[selector][tokenId].contains(target);
    }

    function isForceApproved(bytes4 selector, address target) external view override returns (bool) {
        return _registryOfAll[selector].contains(target);
    }

    function approve(bytes4 selector, address target, bytes calldata data) external override {
        uint256 tokenId = abi.decode(data, (uint256));
        _registry[selector][tokenId].add(target);
    }

    function cancel(bytes4 selector, address target, bytes calldata data)
        external
        override
        notLock(selector, target, data)
    {
        uint256 tokenId = abi.decode(data, (uint256));
        _registry[selector][tokenId].remove(target);
    }

    function forceApprove(bytes4 selector, address target) external override {
        if (isRenounceForceApprove) revert ForceApproveRenounced();
        _registryOfAll[selector].add(target);
    }

    function forceCancel(bytes4 selector, address target) external override notLock(selector, target, "") {
        if (isRenounceForceCancel) revert ForceCancelRenounced();
        _registryOfAll[selector].remove(target);
    }

    function renounceForceApprove(bytes memory data) external override {
        (bool burn) = abi.decode(data, (bool));
        if (burn != true) {
            revert MustRenounce();
        }

        isRenounceForceApprove = true;
        emit RenounceForceApprove();
    }

    function renounceForceCancel(bytes memory data) external override {
        (bool burn) = abi.decode(data, (bool));
        if (burn != true) {
            revert MustRenounce();
        }

        isRenounceForceCancel = true;
        emit RenounceForceCancel();
    }

    function send(uint256 message) external {
        _push(this.send.selector, message);
    }

    function _push(bytes4 selector, uint256 message) internal {
        assembly {
            tstore(selector, message)
        }

        address[] memory targets = _registry[selector][message].values();
        for (uint256 i = 0; i < targets.length; i++) {
            IExec(targets[i]).exec(selector, abi.encode(message));
        }

        targets = _registryOfAll[selector].values();
        for (uint256 i = 0; i < targets.length; i++) {
            IExec(targets[i]).exec(selector, abi.encode(message));
        }
    }
}

contract Bar is IExec {
    event Log(bytes4 indexed selector, bytes data, bytes inboxData);

    function isLocked(bytes4, bytes calldata) external pure override returns (bool) {
        return true;
    }

    function exec(bytes4 selector, bytes calldata data) external {
        bytes memory inboxData = IPushFree(msg.sender).inbox(selector);

        emit Log(selector, data, inboxData);
    }
}
```

## セキュリティ上の考慮事項

### `exec`攻撃
`exec`関数は`public`であるため、任意のプッシュ情報を挿入する悪意のある呼び出しに対して脆弱です。`exec`の実装では、呼び出しの任意性を慎重に検討し、`exec`関数によって渡されたデータを直接使用せずに検証する必要があります。

### 再入力攻撃
パブリッシャーコントラクトがサブスクライバーコントラクトの`exec`関数を呼び出すことで、再入力攻撃につながる可能性があります。悪意のあるサブスクリプションコントラクトは、`exec`内でパブリッシャーコントラクトに対する再入力攻撃を構築する可能性があります。

### 任意のターゲット承認
`forceApprove`と`approve`の実装では、適切なアクセス制御を持つ必要があります。そうしないと、呼び出し側に不要なガス損失を課す可能性があります。

`exec`関数のガス使用量をチェックする。

### `isLocked`の実装
サブスクライバーコントラクトは、サブスクリプション解除によって引き起こされる可能性のある損失を避けるために、`isLocked`関数を適切に実装する必要があります。これは、このプロポーザルを実装するレンディングプロトコルにとって特に重要です。不適切な解除は、異常なクリアリングを引き起こし、大きな損失につながる可能性があります。

同様に、サブスクリプションを行う際、パブリッシャーコントラクトは、`isLocked`が適切に実装されているかどうかを検討する必要があり、取り返しのつかないサブスクリプションを防ぐ必要があります。

## 著作権
著作権およびその関連権利は[CC0](../LICENSE.md)により放棄されています。