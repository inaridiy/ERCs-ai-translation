---
original: c3dd19120306c4bbcb71b9611ace92eed13768492df0fdabb264643b592ef124
---

---
eip: 7720
title: 延期トークン転送
description: ユーザーが指定された将来の時間にERC-20トークンの転送をスケジュールできるようにし、延期された支払いを可能にします。
author: Chen Liaoyuan (@chenly) <cly@kip.pro>
discussions-to: https://ethereum-magicians.org/t/erc-7720-deferred-token-transfer/20245
status: Draft
type: Standards Track
category: ERC
created: 2024-06-09
requires: 20
---

## 概要

この規格は、ユーザーが受益者のために[ERC-20](./eip-20.md)トークンを預金できるようにすることを定めています。受益者は、指定された将来のタイムスタンプ以降にのみトークンを引き出すことができます。各預金取引には一意のIDが割り当てられ、トークンアドレス、送信者、受取人、金額、解除時間、引出状況などの詳細が含まれています。

## 動機

ベスティングスケジュール、エスクロー・サービス、または時間制限付きの報酬など、さまざまなシナリオでは、延期された支払いが必要になります。このコントラクトは、指定されたタイムスタンプに達した後にのみトークンを転送できるようにする、安全で信頼性の高いメカニズムを提供します。構造化された遅延支払いを容易にすることで、トークン転送にセキュリティと予測可能性の追加レイヤーを追加します。これは、支払いが時間の経過に依存するシナリオで特に有用です。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

このスタンダードの実装者は、以下のすべての機能を持っている必要があります:

```solidity
pragma solidity ^0.8.0;

interface ITokenTransfer {
    // トランザクションの開始時に発行されるイベント。
    event Transfer(
        uint256 txnId,
        address indexed token,
        address indexed from,
        address indexed to,
        uint256 amount,
        uint40 unlockTime,
        bytes32 referenceNo
    );

    // トークンが引き出されたときに発行されるイベント。
    event Withdraw(
        uint256 txnId,
        address indexed token,
        address indexed from,
        address indexed to,
        uint256 amount
    );

    // トークン転送を開始する関数。
    // パラメーター:
    // - _token: ERC20トークンコントラクトのアドレス。
    // - _from: 送信者のアドレス。
    // - _to: 受取人のアドレス。
    // - _amount: 転送するトークンの量。
    // - _unlockTime: トークンが引き出せるようになる時間。
    // - _reference: トランザクションの参照ID。
    // 返り値: トランザクションID。
    function transferFrom(
        address _token,
        address _from,
        address _to,
        uint256 _amount,
        uint40 _unlockTime,
        bytes32 _reference
    ) external returns (uint256 txnId);

    // トランザクションからトークンを引き出す関数。
    // パラメーター:
    // - _txnId: 引き出し元のトランザクションID。
    function withdraw(uint256 _txnId) external;

    // トランザクションの詳細を取得する関数。
    // パラメーター:
    // - _txnId: トランザクションのID。
    // 返り値: トランザクションの詳細。
    function getTransaction(uint256 _txnId)
        external
        view
        returns (
            address token,
            address from,
            address to,
            uint256 amount,
            uint40 unlockTime,
            bytes32 referenceNo,
            bool withdrawn
        );
}
```

## 根拠

延期トークン転送コントラクトの設計は、時間ロックされたトークン転送を安全かつ簡単に処理する方法を提供することを目的としています。その開発中に以下の考慮事項が行われました:

**`uint40`による解除時間の精度**: `_unlockTime`に`uint40`全体を選択したのは、実用的なすべてのタイムロックシナリオをカバーするのに十分な範囲を提供するためです。これにより、ベスティングスケジュールや長期エスクローなど、長期にわたる正確なタイミングを必要とする延期された支払いを処理できます。

**`transferFrom`から`txnId`を返す**: `transferFrom`関数は、各トランザクションに一意の`txnId`を返します。この設計上の選択は、各トランザクションを簡単に独立して追跡できるようにするためです。一意のIDを持つことで、ユーザーは特定のトランザクションを管理および参照でき、明確性を確保し、混乱を防ぐことができます。このアプローチにより、各トランザクションの状態を独立して管理できるため、引出しプロセスが簡素化されます。

**既存のERC-20トークンとの互換性**: このスタンダードは、ERC-20の拡張ではなく、別のインターフェイスとして設計されているため、柔軟性と広範な互換性が確保されています。ERC-20標準を直接変更せずに、この提案は既存のERC-20トークンと連携できます。この柔軟性により、流通中の多くのトークンに適用できるため、ユーティリティとアドプションの可能性が高まります。

## 参考実装

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

contract TokenTransfer {
    using SafeERC20 for IERC20;

    struct Transaction {
        address token;      // ERC20トークンコントラクトのアドレス。
        address from;       // 送信者のアドレス。
        address to;         // 受取人のアドレス。
        uint256 amount;     // 転送するトークンの量。
        uint40 unlockTime; // トークンが引き出せるようになる時間。
        bytes32 referenceNo;  // トランザクションの参照ID。
        bool withdrawn;     // トークンが引き出されたかどうかのフラグ。
    }

    // トランザクションIDからトランザクション構造体へのマッピング。
    mapping(uint256 => Transaction) public transactions;

    // 次のトランザクションIDを追跡するための変数。
    uint256 public lastTxnId = 0;

    // トランザクションの開始時に発行されるイベント。
    event Transfer(
        uint256 txnId,
        address indexed token,
        address indexed from,
        address indexed to,
        uint256 amount,
        uint40 unlockTime,
        bytes32 referenceNo
    );

    // トークンが引き出されたときに発行されるイベント。
    event Withdraw(
        uint256 txnId,
        address indexed token,
        address indexed from,
        address indexed to,
        uint256 amount
    );

    constructor() {}

    // トークン転送を開始する関数。
    // パラメーター:
    // - _token: ERC20トークンコントラクトのアドレス。
    // - _from: 送信者のアドレス。
    // - _to: 受取人のアドレス。
    // - _amount: 転送するトークンの量。
    // - _unlockTime: トークンが引き出せるようになる時間。
    // - _reference: トランザクションの参照ID。
    // 返り値: トランザクションID。
    function transferFrom(
        address _token,
        address _from,
        address _to,
        uint256 _amount,
        uint40 _unlockTime,
        bytes32 _reference
    ) external returns (uint256 txnId) {
        require(_amount > 0, "Invalid transfer amount");

        // トークンを送信者からこのコントラクトに転送する。
        IERC20(_token).safeTransferFrom(_from, address(this), _amount);

        lastTxnId++;

        // トランザクションの詳細を保存する。
        transactions[lastTxnId] = Transaction({
            token: _token,
            from: _from,
            to: _to,
            amount: _amount,
            unlockTime: _unlockTime,
            referenceNo: _reference,
            withdrawn: false
        });

        // トランザクション作成のためのイベントを発行する。
        emit Transfer(lastTxnId, _token, _from, _to, _amount, _unlockTime, _reference);
        return lastTxnId;
    }

    // トランザクションからトークンを引き出す関数。
    // パラメーター:
    // - _txnId: 引き出し元のトランザクションID。
    function withdraw(uint256 _txnId) external {
        Transaction storage transaction = transactions[_txnId];
        require(transaction.amount > 0, "Invalid transaction ID");
        require(block.timestamp >= transaction.unlockTime, "Current time is before unlock time");
        // require(transaction.to == msg.sender, "Only the recipient can withdraw the tokens");
        require(!transaction.withdrawn, "Tokens already withdrawn");

        IERC20(transaction.token).safeTransfer(transaction.to, transaction.amount);

        transaction.withdrawn = true;

        // トークン引出しのためのイベントを発行する。
        emit Withdraw(_txnId, transaction.token, transaction.from, transaction.to, transaction.amount);
    }

    // トランザクションの詳細を取得する関数。
    // パラメーター:
    // - _txnId: トランザクションのID。
    // 返り値: トランザクションの詳細。
    function getTransaction(uint256 _txnId)
        external
        view
        returns (
            address token,
            address from,
            address to,
            uint256 amount,
            uint40 unlockTime,
            bytes32 referenceNo,
            bool withdrawn
        )
    {
        Transaction storage transaction = transactions[_txnId];
        require(transaction.amount > 0, "Invalid transaction ID");

        return (
            transaction.token,
            transaction.from,
            transaction.to,
            transaction.amount,
            transaction.unlockTime,
            transaction.referenceNo,
            transaction.withdrawn
        );
    }
}
```

## セキュリティ上の考慮事項

**オーナーレスなコントラクト設計**: 預金後のトークン損失リスクを防ぐため、コントラクトにオーナーを持たせるべきではありません。これにより、コントラクトのトークン残高を指定された受益者以外の任意のアドレスに転送することができなくなります。

**厳格な受益者管理**: 引出し時には、預金時に指定された受益者にのみトークンが転送されるよう、厳格に確認する必要があります。これにより、不正アクセスを防ぎ、意図された受取人のみがトークンを引き出せるようになります。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)により放棄されています。