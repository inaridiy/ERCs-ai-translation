---
original: bbee5be4e88cf6575a234f0aa988b49354f725603f08853cc370d41ba732ddb2
---

---
eip: 4546
title: ラップされた預金
description: 資産預金を管理するためのシングルトンコントラクト。
author: Justice Hudson (@jchancehud)
discussions-to: https://ethereum-magicians.org/t/wrapped-deposit-contract-eip/7740
status: 停滞
type: Standards Track
category: ERC
created: 2021-12-11
---

## 概要
ラップされた預金コントラクトは、ユーザーに代わって資産(Ether、[ERC-20](./eip-20.md)、[ERC-721](./eip-721.md))の預金を処理します。ユーザーは一度支出限度額の承認を行えば、その後さまざまなアプリケーションに資産を預金できます。

## 動機
現在のdappにおける資産預金のユーザーフローは、不便で安全性が低いです。ERC-20資産を預金するには、以下のいずれかを行う必要があります:

  - 預金する前に正確な金額の承認トランザクションを送信し、その後の各預金でこの操作を繰り返す。
  - 預金する前に無制限の支出額の承認トランザクションを送信する。

前者のオプションは不便で高コストです。後者のオプションは安全性が低い。さらに、新規ユーザーや非技術的なユーザーに承認について説明するのは混乱を招きます。これは、ERC20預金をサポートする_すべての_dappで行う必要があります。

## 仕様
ラップされた預金コントラクトは、識別可能なアドレス(例: `0x1111119a9e30bceadf9f939390293ffacef93fe9`)にデプロイされるべきです。コントラクトは、状態変数を変更する機能を持たない、アップグレード不可能なものでなければなりません。

ラップされた預金コントラクトには、以下の公開関数が必要です:

```js
depositERC20(address to, address token, uint amount) external;
depositERC721(address to, address token, uint tokenId) external;
safeDepositERC721(address to, address token, uint tokenId, bytes memory data) external;
safeDepositERC1155(address to, address token, uint tokenId, uint value, bytes calldata data) external;
batchDepositERC1155(address to, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external;
depositEther(address to) external payable;
```

これらの関数はすべて、`to`がゼロコードサイズのアドレスの場合に失敗しなければなりません。各関数は、`to`アドレスが預金を受け入れる意思と能力があることを確認するメソッドを呼び出す必要があります。この関数呼び出しが真の値を返さない場合、実行は失敗しなければなりません。資産の転送が成功しない場合も、実行は失敗しなければなりません。

預金を受け入れるコントラクトには、以下のインターフェースが存在するべきです:

```ts
interface ERC20Receiver {
  function acceptERC20Deposit(address depositor, address token, uint amount) external returns (bool);
}

interface ERC721Receiver {
  function acceptERC721Deposit(address depositor, address token, uint tokenId) external returns (bool);
}

interface ERC1155Receiver {
  function acceptERC1155Deposit(address depositor, address token, uint tokenId, uint value, bytes calldata data) external returns (bool);
  function acceptERC1155BatchDeposit(address depositor, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external returns (bool);
}

interface EtherReceiver {
  function acceptEtherDeposit(address depositor, uint amount) external returns (bool);
}
```

受信コントラクトは、必要に応じてこれらの関数を実装できます。特定の関数が実装されていない場合、その資産タイプの預金は送信されてはなりません。

## 根拠
単一のコントラクトがすべてのトークン転送を処理することで、ユーザーはトークンごとに1回の承認を行うだけで、さまざまなコントラクトに預金できるようになります。ユーザーは受信コントラクトのトークン支出承認を信頼する必要がなく、受信コントラクトはトークン転送を自分で実装する必要がなくなります。

ユーザーエクスペリエンスも向上します。"他のアプリで使用するためにトークンを有効化する"というシンプルなグローバルdappを実装できるようになります。

## 下位互換性

このEIPは下位互換性がありません。このデポジットシステムを使用するコントラクトは、特定の関数を実装する必要があります。アップグレード可能な既存のコントラクトは、1つ以上の受け入れ預金関数を実装することで、このEIPをさかのぼってサポートできます。

アップグレードされたコントラクトは、既存の承認を保持するために、旧システム(コントラクト自体への承認)と提案されたデポジットシステムの両方を許可できます。新しいユーザーは、提案されたデポジットシステムを使うよう促されるべきです。

## 参考実装
```ts
pragma solidity ^0.7.0;

interface ERC20Receiver {
  function acceptERC20Deposit(address depositor, address token, uint amount) external returns (bool);
}

interface ERC721Receiver {
  function acceptERC721Deposit(address depositor, address token, uint tokenId) external returns (bool);
}

interface ERC1155Receiver {
  function acceptERC1155Deposit(address depositor, address token, uint tokenId, uint value, bytes calldata data) external returns (bool);
  function acceptERC1155BatchDeposit(address depositor, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) external returns (bool);
}

interface EtherReceiver {
  function acceptEtherDeposit(address depositor, uint amount) external returns (bool);
}

interface IERC20 {
  function transferFrom(address sender, address recipient, uint amount) external returns (bool);
}

interface IERC721 {
  function transferFrom(address _from, address _to, uint256 _tokenId) external payable;
  function safeTransferFrom(address _from, address _to, uint256 _tokenId, bytes memory data) external payable;
}

interface IERC1155 {
  function safeTransferFrom(address _from, address _to, uint _id, uint _value, bytes calldata _data) external;
  function safeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external;
}

contract WrappedDeposit {
  function depositERC20(address to, address token, uint amount) public {
    _assertContract(to);
    require(ERC20Receiver(to).acceptERC20Deposit(msg.sender, token, amount));
    bytes memory data = abi.encodeWithSelector(
      IERC20(token).transferFrom.selector,
      msg.sender,
      to,
      amount
    );
    (bool success, bytes memory returndata) = token.call(data);
    require(success);
    // 転送関数を正しく実装していないトークンとの下位互換性
    if (returndata.length > 0) {
      require(abi.decode(returndata, (bool)), "ERC20 operation did not succeed");
    }
  }

  function depositERC721(address to, address token, uint tokenId) public {
    _assertContract(to);
    require(ERC721Receiver(to).acceptERC721Deposit(msg.sender, token, tokenId));
    IERC721(token).transferFrom(msg.sender, to, tokenId);
  }

  function safeDepositERC721(address to, address token, uint tokenId, bytes memory data) public {
    _assertContract(to);
    require(ERC721Receiver(to).acceptERC721Deposit(msg.sender, token, tokenId));
    IERC721(token).safeTransferFrom(msg.sender, to, tokenId, data);
  }

  function safeDepositERC1155(address to, address token, uint tokenId, uint value, bytes calldata data) public {
    _assertContract(to);
    require(ERC1155Receiver(to).acceptERC1155Deposit(msg.sender, to, tokenId, value, data));
    IERC1155(token).safeTransferFrom(msg.sender, to, tokenId, value, data);
  }

  function batchDepositERC1155(address to, address token, uint[] calldata tokenIds, uint[] calldata values, bytes calldata data) public {
    _assertContract(to);
    require(ERC1155Receiver(to).acceptERC1155BatchDeposit(msg.sender, to, tokenIds, values, data));
    IERC1155(token).safeBatchTransferFrom(msg.sender, to, tokenIds, values, data);
  }

  function depositEther(address to) public payable {
    _assertContract(to);
    require(EtherReceiver(to).acceptEtherDeposit(msg.sender, msg.value));
    (bool success, ) = to.call{value: msg.value}('');
    require(success, "nonpayable");
  }

  function _assertContract(address c) private view {
    uint size;
    assembly {
      size := extcodesize(c)
    }
    require(size > 0, "noncontract");
  }
}
```
## セキュリティ上の考慮事項
ラップされた預金の実装は、バグのリスクを最小限に抑えるために可能な限り小さくする必要があります。エンジニアが数分で読み取り、理解できるほど小さいコントラクトでなければなりません。

受信コントラクトは、`msg.sender`がラップされた預金コントラクトと等しいことを必ず確認する必要があります。そうしないと、誰でも預金をシミュレートできてしまいます。

## 著作権
著作権およびその関連権利は[CC0](../LICENSE.md)により放棄されています。