---
original: 0fedd654b23a899920713f007ad3fd064f561182f3a4ce5d453771dc252a4588
---

---
eip: 7628
title: ERC-721 所有権シェア拡張
description: ERC-721トークンに所有権シェアを導入し、照会可能、譲渡可能、承認可能な分割所有権を可能にします。
author: Chen Liaoyuan (@chenly) <cly@kip.pro>
discussions-to: https://ethereum-magicians.org/t/erc-7628-erc-721-ownership-shares-extension/18744
status: Draft
type: Standards Track
category: ERC
created: 2024-02-20
requires: 721
---

## 概要

このプロポーザルでは、NFTの各トークンに所有権と利益配当の割合を表す属性を導入します。この属性は、NFTの特定の特権に関連する所有権と利益配当の持分を示すものであり、これらの持分の照会、譲渡、承認を可能にすることで、各トークンが幅広い用途に適用できるようになります。

## 動機

配当の分配や、NFTトークンの所有権に基づく権利の割り当てを行う際、各トークンに所有権の割合を示す属性を装備する必要があります。[ERC-1155](./eip-1155.md)では、ウォレットアドレスが保有するトークンの残高によって所有権の持分を表すことができますが、各トークンの一意性が失われます。一方、[ERC-721](./eip-721.md)は各トークンの一意性を維持しますが、所有権の割合を示す属性がなく、メタデータでは所有者がこれらの持分を自由に譲渡することができません。この拡張機能は、[ERC-1155](./eip-1155.md)と[ERC-721](./eip-721.md)の機能を融合させ、各持分の所有者が実質的にトークンの所有者と同様の特性を持つことで、持分の表現とトークンの一意性の間のギャップを埋めることを目指しています。

## 仕様

この文書の中の "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY"、"OPTIONAL" というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

このエクステンションの実装者は、以下のすべての関数を持たなければなりません:

```solidity
pragma solidity ^0.8.0;

interface IERC7628 /* is IERC721 */ {
    /// @notice 所有権シェアの小数点以下の桁数を返します。
    /// @return 所有権シェアの小数点以下の桁数。
    function shareDecimals() external view returns (uint8);

    /// @notice 全トークンの所有権シェアの合計を返します。
    /// @return 所有権シェアの合計。
    function totalShares() external view returns (uint256);

    /// @notice 指定したトークンの所有権シェアを返します。
    /// @param tokenId トークンの識別子。
    /// @return トークンの所有権シェア。
    function shareOf(uint256 tokenId) external view returns (uint256);

    /// @notice 所有者が指定したスペンダーに対して承認した、指定したトークンのシェア量を返します。
    /// @param tokenId トークンの識別子。
    /// @param spender スペンダーのアドレス。
    /// @return スペンダーに承認されたシェア量。
    function shareAllowance(uint256 tokenId, address spender) external view returns (uint256);

    /// @notice 呼び出し元の代わりに、指定したスペンダーが指定したシェア量を使用することを承認します。
    /// @param tokenId トークンの識別子。
    /// @param spender スペンダーのアドレス。
    /// @param shares 承認するシェア量。
    function approveShare(uint256 tokenId, address spender, uint256 shares) external;

    /// @notice あるトークンからほかのトークンにシェアを移転します。
    /// @param fromTokenId 送信元トークンの識別子。
    /// @param toTokenId 受信先トークンの識別子。
    /// @param shares 移転するシェア量。
    function transferShares(uint256 fromTokenId, uint256 toTokenId, uint256 shares) external;

    /// @notice あるトークンから別のアドレスにシェアを移転します(新しいトークンの作成または受信先アドレスのシェア増加につながる)。
    /// @param fromTokenId 送信元トークンの識別子。
    /// @param to 受信先アドレス。
    /// @param shares 移転するシェア量。
    function transferSharesToAddress(uint256 fromTokenId, address to, uint256 shares) external; 

    /// @notice トークンにシェアを追加します(オーナーのみ呼び出し可能)。
    /// @param tokenId トークンの識別子。
    /// @param shares 追加するシェア量。
    function addSharesToToken(uint256 tokenId, uint256 shares) external;

    /// @notice あるトークンからほかのトークンにシェアが移転されたときに発行されるイベント。
    /// @param fromTokenId 送信元トークンの識別子。
    /// @param toTokenId 受信先トークンの識別子。
    /// @param amount 移転されたシェア量。
    event SharesTransfered(uint256 indexed fromTokenId, uint256 indexed toTokenId, uint256 amount);

    /// @notice オーナーがスペンダーにシェアの使用を承認したときに発行されるイベント。
    /// @param tokenId トークンの識別子。
    /// @param spender スペンダーのアドレス。
    /// @param amount 承認されたシェア量。
    event SharesApproved(uint256 indexed tokenId, address indexed spender, uint256 amount);
}
```

## 根拠

#### トークンへのシェアの発行

トークンにさらにシェアを発行することで、デジタル資産の所有権の構造を柔軟に管理できるようになり、利害関係者のニーズの変化に対応できます。これにより、利益配当や投資調整などの場合に、所有権構造を直接ブロックチェーン上で透明性と安全性を持って変更することができます。

#### アドレスへのシェアの移転

シェアをアドレスに移転できるようにすることで、NFTの流動性と可用性が向上し、分割所有権の販売や担保設定などの用途をサポートできるようになります。これにより、NFTがより幅広い層に適応可能で包括的なものになります。

## 下位互換性

このスタンダードは完全に[ERC-721](./eip-721.md)互換です。

## 参考実装

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

contract ERC7628 is IERC7628, ERC721, Ownable, ReentrancyGuard {
    mapping(uint256 => uint256) private _shareBalances;
    mapping(uint256 => mapping(address => uint256)) private _shareAllowances;
    uint256 private _totalShares;
    uint256 private _nextTokenId;

    constructor(address initialOwner)
        ERC721("MyToken", "MTK")
        Ownable(initialOwner)
    {}

    function addSharesToToken(uint256 tokenId, uint256 shares) public override onlyOwner {
        require(tokenId > 0, "ERC7628: tokenId cannot be zero");
        _shareBalances[tokenId] += shares;
        _totalShares += shares;
        emit SharesTransfered(0, tokenId, shares);
    }

    function shareDecimals() external pure override returns (uint8) {
        return 18;
    }

    function totalShares() external view override returns (uint256) {
        return _totalShares;
    }

    function shareOf(uint256 tokenId) external view override returns (uint256) {
        return _shareBalances[tokenId];
    }

    function shareAllowance(uint256 tokenId, address spender) external view override returns (uint256) {
        return _shareAllowances[tokenId][spender];
    }

    function approveShare(uint256 tokenId, address spender, uint256 shares) external override {
        require(spender != ownerOf(tokenId), "ERC7628: approval to current owner");
        require(msg.sender == ownerOf(tokenId), "ERC7628: approve caller is not owner");

        _shareAllowances[tokenId][spender] = shares;
        emit SharesApproved(tokenId, spender, shares);
    }

    function transferShares(uint256 fromTokenId, uint256 toTokenId, uint256 shares) external override nonReentrant {
        require(_shareBalances[fromTokenId] >= shares, "ERC7628: insufficient shares for transfer");
        require(_isApprovedOrOwner(msg.sender, fromTokenId), "ERC7628: transfer caller is not owner nor approved");

        _shareBalances[fromTokenId] -= shares;
        _shareBalances[toTokenId] += shares;
        emit SharesTransfered(fromTokenId, toTokenId, shares);
    }

    function transferSharesToAddress(uint256 fromTokenId, address to, uint256 shares) external override nonReentrant {
        require(_shareBalances[fromTokenId] >= shares, "ERC7628: insufficient shares for transfer");
        require(_isApprovedOrOwner(msg.sender, fromTokenId), "ERC7628: transfer caller is not owner nor approved");

        _nextTokenId++;
        _safeMint(to, _nextTokenId);
        _shareBalances[_nextTokenId] = shares;
        emit SharesTransfered(fromTokenId, _nextTokenId, shares);
    }

    // スペンダーがオーナーまたは承認されているかどうかを確認するヘルパー関数
    function _isApprovedOrOwner(address spender, uint256 tokenId) internal view returns (bool) {
        return (spender == ownerOf(tokenId) || getApproved(tokenId) == spender || isApprovedForAll(ownerOf(tokenId), spender));
    }
}
```

## セキュリティ上の考慮事項

#### 移転時の承認のクリア
トークンの所有権を移転する際は、既存の承認をすべてクリアすることが重要です。これにより、トークンの所有者が変わった後も、以前に承認された当事者がアクセスを保持することを防ぐことができます。

#### 再入力攻撃の防止
実装では、再入力攻撃に対して保護する必要があります。これには、特にシェアの移転時に、残高や所有権を変更する関数が、そのような脆弱性に対して安全であることを確認することが含まれます。

#### IDとアドレスの検証
すべての操作でトークンIDとウォレットアドレスの正当性を検証することが不可欠です。この手順により、エラーを回避し、トークンとそれに関連するシェアが正しく処理されることを保証します。

#### 所有権変更時のシェアの管理
トークンの所有権が移転される際は、シェア量の適切な管理が重要です。所有権と共にシェアも正確に移転されるよう、整合性を維持することが不可欠です。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。