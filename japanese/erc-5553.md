---
original: 8f663cc9ed8906459ad88d2bbd24eef515c32b27729f66e4e91b28f7f9a4dfb0
---

---
eip: 5553
title: 知的財産とその使用料構造の表現
description: 知的財産とその使用料構造をブロックチェーン上で表現する方法
author: Roy Osherove (@royosherove)
discussions-to: https://ethereum-magicians.org/t/eip-5553-representing-intellectual-property-on-chain-with-royalty-rights/10551
status: 停滞
type: Standards Track
category: ERC
created: 2022-08-17
requires: 20, 721
---

## 概要
この提案では、知的財産をブロックチェーン上で表現する一般的な方法と、洗練された使用料表現メカニズムおよび関連メタデータリンクを紹介します。この標準は特定の知的財産の種類に関連付けられておらず、音楽 IP、動画、書籍、画像など、さまざまな種類の IP を表現できます。
この標準は非常に一般的に設計されており、業界が新しいエコシステムを発展させることができ、それらすべてが同じ基本的な標準に依存できるようになります。

この標準により、市場参加者は以下のことができるようになります:
1) 知的財産の正規のオンチェーン表現を観察する
2) 関連するメタデータを発見する
3) 関連する使用料構造を発見する
4) これにより、知的財産資産の登録、ライセンス、支払いメカニズムを構築することができるようになります。

## 動機

知的財産をライセンスしたり、それを表現する標準化された仕組みはありません。ただし、通常の NFT は収集品の使用例しか表現できず、ライセンス IP のより複雑な使用例を簡単に表現することはできません。
以下のことができれば、そのようなライセンスメカニズムを実現できます:

1) IP が存在することを宣言する。購入可能性とは別に
2) そのような IP に対して支払われるべき複数の利害関係者を宣言する

1については、現在のところ標準はありません。

2については、NFT の購入や 0xsplits のようなメカニズムに基づいて、従来の分割標準が存在します。これらは主要な問題を解決しますが、複数の共同作業参加者の名前を付ける機能は含まれていません。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119 に記載されているように解釈されるものとします。

**IP をブロックチェーン上で表現したいコントラクトは、[EIP-721](./eip-721.md) と本提案を実装しなければなりません**

この標準は [EIP-721](./eip-721.md) を拡張し、以下の `IIPRepresentation` (IPR と略) インターフェースを持ちます。
この標準の実装者は、以下の機能をすべて持たなければなりません:

### royaltyPortionTokens() 関数
この関数は、[EIP-20](./eip-20.md) トークンの配列を返さなければなりません。これらのトークンは、IP の作成に関わる様々な利害関係者に対する使用料の部分を表します。

例えば、音楽 IP の場合、作曲/作詞/出版使用料の部分と録音/マスター使用料の部分を表す2つのトークンがあるかもしれません。これらの使用料部分トークンは共同作業参加者に分配され、後に保有者によって使用料の配分に使用できます。つまり、使用料部分トークンの10%を保有している場合、その保有者はその種類の使用料に関する財務配分の10%を受け取ることになります。

### metadataURI() 関数
この関数は、IP に必要なメタデータを含むメタデータファイルの URI を返さなければなりません。各 IP タイプは、別途定義されたメタデータ標準を実装できます。ファイルは IPFS、Arweave、またはその他のコンテンツアドレス可能なデcentral化されたシステムでホストされ、URI を変更せずにファイルの内容を変更することはできません。

### changeMetadataURI() 関数
この関数は、メタデータ URI を新しいメタデータファイルのバージョンを指すように変更できます。この関数を呼び出すと、成功した場合に `MetadataChanged` イベントがトリガーされます。

### ledger() 関数
この関数は、IP を初期化し、関連する使用料トークンを関連付けたレジストリまたはレジストラコントラクトアドレスまたは EOA アカウントを返さなければなりません。IP 表現は、異なる目的で異なるアクターによって複数の場所に登録される可能性があります。この関数により、市場参加者は IP の親となるレジストリメカニズムを発見し、IP の管理に特別なアクセス権を持つ可能性があります。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.9;
import '@openzeppelin/contracts/interfaces/IERC165.sol';


///
/// @dev 知的財産表現のインターフェース
///
interface IIPRepresentation is IERC165 {
    
    /// @notice メタデータファイルの新しい URI で呼び出される
    /// @param _newUri - メタデータファイルを指すURI(ファイル標準は実装者次第)
    /// @param _newFileHash - 将来の参照と検証のための新しいメタデータファイルのハッシュ
    function changeMetadataURI(string memory _newUri, string memory _newFileHash) external ;

    /// @return IP の使用料部分を表すERC20トークンのアドレスの配列
    /// @dev つまり、ERC5501 (IRoyaltyInterestToken インターフェース) を実装する
    function royaltyPortionTokens() external view returns (address[] memory) ;

    /// @return IP 登録を初期化したコントラクトまたは EOA のアドレス
    /// @dev つまり、レジストリまたはレジストラ。将来実装される
    function ledger() external view returns (address) ;

    /// @return IP の現在のメタデータファイルのURI
    function metadataURI() external view returns (string memory) ;

    /// @dev メタデータ URI が変更されたときにトリガーされるイベント
    /// @param byAddress この操作をトリガーしたアドレス
    /// @param oldURI 変更前のメタデータファイルのURI
    /// @param oldFileHash 変更前のメタデータファイルのハッシュ
    /// @param newURI 新しいメタデータファイルのURI
    /// @param newFileHash 新しいメタデータファイルのハッシュ
    event MetadaDataChanged(address byAddress, string oldURI, string oldFileHash, string newURI, string newFileHash);
}
```


## 根拠

### EIP-20 トークンの配列を返すことで、より堅牢な使用料部分構造を実現できる

現在の使用料の実装は、NFT の販売のみを扱っています。また、単一の使用料タイプしか許可していません。つまり、音楽 NFT の場合、購入やライセンスの種類に関係なく、すべての関係者に同じ使用料が支払われます。

この提案では、複数の**種類**の使用料シナリオが許可されます。典型的な例は音楽業界で、作曲/作詞の使用料と録音/マスターの使用料があります。ライセンスの種類によって、異なる割合で異なる当事者に支払われます。

楽曲カバーの場合、以下のようなライセンス料の計算式を作成できます:
a) 元の IP の作詞者/作曲者に、歌詞や楽曲の使用料が支払われる
b) 元の楽曲のレコーディングアーティストには支払われない(彼らのレコーディングは使用されていない)
c) 新しい IP のレコーディングアーティストに支払われる
d) カバー作品の作成者には作詞料は支払われない

さらに、この EIP には、すべての種類の使用料タイプに接続し、それらをより簡単に見つけられる単一の構造があります。
最後に、EIP-20 トークンを移動するのは、0xsplits コントラクトを管理するよりも簡単です。

### IP コントラクトと収集品およびライセンス NFT を分離することで、ライセンスの種類のスケーリングが可能になる
IP の正規バージョンと、それに対するさまざまなライセンス使用(NFT 購入、ストリーミング、アートの使用など)を分離することで、この EIP は、さまざまなライセンスの種類と支払い配分の生態系が発展する道筋を導入します。
つまり、この方式を使う人は、最初に音楽 NFT や美術 NFT を作成するのではなく、IP 表現を作成し、それに対してさまざまな種類のライセンスや収集品を作成するのです。

### IP のメタデータへの単一のポインター
IPR は IPFS または Arweave 内のメタデータを指し示し、簡単で標準的な方法でそれを変更し、変更を追跡できます。現在、唯一のメタデータ標準は NFT メタデータ拡張ですが、文書がどの標準に準拠しているかを知ることはできません。さまざまな IP タイプに対して、さまざまなメタデータ標準を策定し、簡単に関連するメタデータを発見できるようにすることができます。

## 参考実装

#### IIPRepresentation に基づいた音楽 IP 表現 (MIPR と略) の実装
```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.9;
import '@openzeppelin/contracts/token/ERC721/ERC721.sol';
import "./interfaces/IIPRepresentation.sol";
import "./interfaces/Structs.sol";


contract MusicalIP is ERC721, IIPRepresentation {
    address public songLedger;
    address public compToken;
    address public recToken;
    string public metadataURI;
    string public fileHash;
    uint256 public tokenId;
    bool public activated =false;

    function supportsInterface(bytes4 interfaceId) public view virtual override( ERC721, IERC165) returns (bool) {
        return
            interfaceId == type(IIPRepresentation).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    function getInterfaceId() public pure returns (bytes4){
        return type(IIPRepresentation).interfaceId;
    }

    constructor (
        uint256 _tokenId,
        address _songLedger,
        SongMintingParams memory _params,
        address _compAddress,
        address _recAddress
        )
    ERC721(_params.shortName, _params.symbol){

        songLedger = _songLedger;
        compToken = _compAddress;
        recToken = _recAddress;
        metadataURI = _params.metadataUri;
        fileHash = _params.fileHash;
        tokenId = _tokenId;
        
        _safeMint(_songLedger, _tokenId);
        emit Minted(_params.shortName,_songLedger,_compAddress,_recAddress,_msgSender(),tokenId,_params.metadataUri);
    }

    function changeMetadataURI(string memory _newURI,string memory _newFileHash) public 
     {
        string memory oldURI = metadataURI;
        string memory oldHash = fileHash;
        metadataURI = _newURI; 
        fileHash = _newFileHash;
        
        emit MetadataChanged(oldURI, oldHash,_newURI,_newFileHash);
    }
    
    function royaltyPortionTokens() external view returns (address[] memory) {
        address[] memory items = new address[](2); 
        items[0] = compToken;
        items[1] = recToken;
        return items;
    }
    function ledger() external view returns (address) {
         return songLedger;
    }

    event MetadataChanged(
        string  oldUri, string oldFileHash,
        string  newUri, string newFileHash
        );
    event Minted(
        string  abbvName,
        address ledger,
        address compToken,
        address recToken,
        address creator,
        uint256 tokenId,
        string metadataUri
        );
}



```

#### 簡単な楽曲レジストリコントラクトを使用して新しい音楽 IP をデプロイする

```solidity  
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.9;
import "@openzeppelin/contracts/utils/Counters.sol";
import "./MusicalIP.sol";
import "./CompositionRoyaltyToken.sol";
import "./RecordingRoyaltyToken.sol";


contract SimpleSongLedger is IERC721Receiver {
    using Counters for Counters.Counter;
    Counters.Counter private mipIds;
      function onERC721Received(address, address, uint256, bytes calldata) external pure returns (bytes4) {
        return IERC721Receiver.onERC721Received.selector;
    }

    function mintSong(SongMintingParams memory _params) public {
        CompositionRoyaltyToken comp = new CompositionRoyaltyToken(address(this),"SONGCOMP","COMP");
        RecordingRoyaltyToken rec = new RecordingRoyaltyToken(address(this),"SONGR
EC","REC");
        mipIds.increment();

        MusicalIP mip = new MusicalIP(
                                        mipIds.current(),
                                        address(this),
                                        _params,
                                        address(comp),
                                        address(rec)
                                    );
    }
}


## セキュリティ上の考慮事項

使用料部分トークンの保有者を説得して、それらのトークンを送らせ、さまざまな IPR の使用料部分を獲得しようとする攻撃者の可能性があります。ただし、これらは使用料に固有のものではなく、EIP-20 トークンの一般的な問題です。

IP 登録の所有権の場合、レジストリコントラクトが IP 登録を所有し、それが移転不可能(レジストリが作成したアカウントに固有)であることが推奨されます。

## 著作権

著作権およびその関連権利は [CC0](../LICENSE.md) により放棄されています。