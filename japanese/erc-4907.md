---
original: f356d0ff86966966c26999df1fa0ae54ab5322ba9753daad26eff0b99e90cb65
---

---
eip: 4907
title: レンタル NFT、EIP-721 の拡張
description: EIP-721 トークンに時間制限付きの役割と制限付きのアクセス許可を追加します。
author: Anders (@0xanders)、Lance (@LanceSnow)、Shrug <shrug@emojidao.org>
discussions-to: https://ethereum-magicians.org/t/idea-erc-721-user-and-expires-extension/8572
status: 最終
type: 標準トラック
category: ERC
created: 2022-03-11
requires: 165, 721
---

## 概要

このスタンダードは [EIP-721](./eip-721.md) の拡張です。アドレスに付与できる追加の役割 (`user`) と、その役割が自動的に取り消される時間 (`expires`) を提案しています。`user` 役割は NFT を "使用する"権限を表しますが、それを譲渡したり、ユーザーを設定する権限はありません。

## 動機

一部の NFT には特定の用途があります。例えば、バーチャルランドでシーンを構築したり、ゲームアセットを表す NFT をゲーム内で "使用" したりすることができます。所有者とユーザーが常に同じではない場合があります。NFT の所有者がそれをレンタルする "ユーザー" がいる可能性があります。"ユーザー" が NFT で行えるアクションは "所有者" とは異なるはずです (例えば、"ユーザー" は通常 NFT の所有権を売却することはできません)。このような場合、アドレスが "所有者" なのか "ユーザー" なのかを識別し、それに応じてアクションを実行できるようにする別々の役割を持つことが合理的です。

一部のプロジェクトでは既に "オペレーター" や "コントローラー" などの異なる名称でこの設計スキームを使用していますが、これがますます一般的になるにつれ、すべてのアプリケーションの連携を促進するための統一された標準が必要になります。

さらに、このモデルの適用 (レンタルなど) では、ユーザーアドレスが NFT の使用権限を一時的にのみ持つことが多くあります。通常、これは所有者が 2 つのオンチェーントランザクションを送信する必要があることを意味します。1 つは使用期間の開始時に新しいユーザー役割を割り当て、もう 1 つは期間終了時にユーザー役割を取り戻すためのものです。これは労力とガスの両方で非効率的であり、2 つ目のトランザクションが不正確であるため、複雑な問題が発生する可能性があります。`expires` 関数を導入することで、2 つ目のトランザクションを必要とせずに使用期間の自動終了を実現できます。

## 仕様

このドキュメントの "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY" および "OPTIONAL" というキーワードは、RFC 2119 で説明されているように解釈されるものとします。

### コントラクトインターフェース
Solidity インターフェースと NatSpec & OpenZeppelin v4 インターフェース ([`IERC4907.sol`](../assets/eip-4907/contracts/IERC4907.sol) でも利用可能):

```solidity
interface IERC4907 {

    // NFT のユーザーが変更されたか、有効期限が変更されたときにログに記録される
    /// @notice NFT のユーザーまたはユーザーの有効期限が変更されたときに発行されるイベント
    /// ユーザーが 0 アドレスの場合、ユーザーがいないことを示す
    event UpdateUser(uint256 indexed tokenId, address indexed user, uint64 expires);

    /// @notice NFT のユーザーと有効期限を設定する
    /// @dev ユーザーが 0 アドレスの場合、ユーザーがいないことを示す
    /// `tokenId` が有効な NFT でない場合は例外をスローする
    /// @param user  NFT の新しいユーザー
    /// @param expires  UNIX タイムスタンプ、ユーザーがこの NFT を使用できる期限
    function setUser(uint256 tokenId, address user, uint64 expires) external;

    /// @notice NFT のユーザーアドレスを取得する
    /// @dev ユーザーが 0 アドレスの場合、ユーザーがいないか有効期限が切れていることを示す
    /// @param tokenId ユーザーアドレスを取得する NFT
    /// @return この NFT のユーザーアドレス
    function userOf(uint256 tokenId) external view returns(address);

    /// @notice NFT のユーザーの有効期限を取得する
    /// @dev 値が 0 の場合、ユーザーがいないことを示す
    /// @param tokenId ユーザーの有効期限を取得する NFT
    /// @return この NFT のユーザーの有効期限
    function userExpires(uint256 tokenId) external view returns(uint256);
}
```

`userOf(uint256 tokenId)` 関数は `pure` または `view` として実装できます。

`userExpires(uint256 tokenId)` 関数は `pure` または `view` として実装できます。

`setUser(uint256 tokenId, address user, uint64 expires)` 関数は `public` または `external` として実装できます。

`UpdateUser` イベントは、ユーザーアドレスまたはユーザーの有効期限が変更されたときに必ず発行されなければなりません。

`supportsInterface` メソッドは、`0xad092b5c` が渡された場合に `true` を返さなければなりません。

## 根拠

このモデルは簡単な実装を目的としています。以下は、このスタンダードによって解決される問題の一部です:

### 明確な権利の割り当て

"所有者" と "ユーザー" の 2 つの役割により、レンダーとボロワーが NFT で行えるアクションとできないアクションを大幅に管理しやすくなります (つまり、それぞれの権利)。さらに、所有者はユーザーを制御できるため、他のプロジェクトも所有者またはユーザーに独自の権利を割り当てやすくなります。

### シンプルなオンチェーンの時間管理

レンタル期間が終了すると、ユーザー役割をリセットし、"ユーザー" が NFT の使用権を失う必要があります。これは通常、2 つ目のオンチェーントランザクションで行われますが、ガスが非効率的で不正確なため、複雑な問題が発生する可能性があります。`expires` 関数を使用すると、期間終了後にユーザーが自動的に無効になるため、別のトランザクションは不要です。

### 簡単な第三者統合

パーミッションレスな相互運用性の精神に基づき、このスタンダードにより、NFT の発行者やアプリケーションの許可なしで、第三者プロトコルが NFT の使用権を管理しやすくなります。プロジェクトが `user` 役割と `expires` を採用すれば、他のプロジェクトがこれらの機能に直接対話し、独自のトランザクションタイプを実装できます。例えば、このスタンダードを使用する PFP NFT は、ユーザーが 30 日間 NFT をレンタルできるレンタルプラットフォームと、ユーザーが分割払いで最終的に NFT の所有権を購入できる抵当プラットフォームの両方に統合できます。これはすべて、オリジナルの PFP プロジェクトの許可なしで行えます。

## 下位互換性

仕様セクションで述べたように、この標準は EIP-721 と完全に互換性があるように拡張関数セットを追加できます。

さらに、この標準で導入された新しい関数は、既存の EIP-721 関数と多くの類似点があります。これにより、開発者が標準を迅速に採用できます。

## テストケース

### テストコントラクト
`ERC4907Demo` 実装: [`ERC4907Demo.sol`](../assets/eip-4907/contracts/ERC4907Demo.sol)

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

import "./ERC4907.sol";

contract ERC4907Demo is ERC4907 {

    constructor(string memory name, string memory symbol)
     ERC4907(name,symbol)
     {         
     }

    function mint(uint256 tokenId, address to) public {
        _mint(to, tokenId);
    }

}
```

### テストコード
[test.js](../assets/eip-4907/test/test.js)

```JavaScript
const { assert } = require("chai");

const ERC4907Demo = artifacts.require("ERC4907Demo");

contract("test", async accounts => {

    it("should set user to Bob", async () => {
        // Get initial balances of first and second account.
        const Alice = accounts[0];
        const Bob = accounts[1];

        const instance = await ERC4907Demo.deployed("T", "T");
        const demo = instance;

        await demo.mint(1, Alice);
        let expires = Math.floor(new Date().getTime()/1000) + 1000;
        await demo.setUser(1, Bob, BigInt(expires));

        let user_1 = await demo.userOf(1);

        assert.equal(
            user_1,
            Bob,
            "User of NFT 1 should be Bob"
        );

        let owner_1 = await demo.ownerOf(1);
        assert.equal(
            owner_1,
            Alice ,
            "Owner of NFT 1 should be Alice"
        );
    });
});


```

ターミナルで実行:
```
truffle test ./test/test.js
```

## 参考実装
実装: [`ERC4907.sol`](../assets/eip-4907/contracts/ERC4907.sol)
```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC4907.sol";

contract ERC4907 is ERC721, IERC4907 {
    struct UserInfo 
    {
        address user;   // ユーザー役割のアドレス
        uint64 expires; // UNIX タイムスタンプ、ユーザーの有効期限
    }

    mapping (uint256  => UserInfo) internal _users;

    constructor(string memory name_, string memory symbol_)
     ERC721(name_, symbol_)
     {
     }
    
    /// @notice NFT のユーザーと有効期限を設定する
    /// @dev ユーザーが 0 アドレスの場合、ユーザーがいないことを示す
    /// `tokenId` が有効な NFT でない場合は例外をスローする
    /// @param user  NFT の新しいユーザー
    /// @param expires  UNIX タイムスタンプ、ユーザーがこの NFT を使用できる期限
    function setUser(uint256 tokenId, address user, uint64 expires) public virtual{
        require(_isApprovedOrOwner(msg.sender, tokenId), "ERC4907: transfer caller is not owner nor approved");
        UserInfo storage info =  _users[tokenId];
        info.user = user;
        info.expires = expires;
        emit UpdateUser(tokenId, user, expires);
    }

    /// @notice NFT のユーザーアドレスを取得する
    /// @dev ユーザーが 0 アドレスの場合、ユーザーがいないか有効期限が切れていることを示す
    /// @param tokenId ユーザーアドレスを取得する NFT
    /// @return この NFT のユーザーアドレス
    function userOf(uint256 tokenId) public view virtual returns(address){
        if( uint256(_users[tokenId].expires) >=  block.timestamp){
            return  _users[tokenId].user;
        }
        else{
            return address(0);
        }
    }

    /// @notice NFT のユーザーの有効期限を取得する
    /// @dev 値が 0 の場合、ユーザーがいないことを示す
    /// @param tokenId ユーザーの有効期限を取得する NFT
    /// @return この NFT のユーザーの有効期限
    function userExpires(uint256 tokenId) public view virtual returns(uint256){
        return _users[tokenId].expires;
    }

    /// @dev {IERC165-supportsInterface} を参照.
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC4907).interfaceId || super.supportsInterface(interfaceId);
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId
    ) internal virtual override{
        super._beforeTokenTransfer(from, to, tokenId);

        if (from != to && _users[tokenId].user != address(0)) {
            delete _users[tokenId];
            emit UpdateUser(tokenId, address(0), 0);
        }
    }
} 
```

## セキュリティ上の考慮事項

このEIPスタンダードは所有者の権利を完全に保護できます。所有者は NFT のユーザーと有効期限を随時変更できます。

## 著作権

著作権およびそれに関連する権利は [CC0](../LICENSE.md) により放棄されています。