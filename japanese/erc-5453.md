---
original: a3023dab645afb37ab324bcd98dc8150ad5c3b8c4a4a085660b72066147d90ce
---

---
eip: 5453
title: 承認 - あらゆる関数の許可
description: ERC-5750に依存する同一トランザクション内での関数呼び出しの承認に関する一般的なプロトコル。
author: Zainan Victor Zhou (@xinbenlv)
discussions-to: https://ethereum-magicians.org/t/erc-5453-endorsement-standard/10355
status: 最終コール
last-call-deadline: 2023-09-27
type: Standards Track
category: ERC
created: 2022-08-12
requires: 165, 712, 1271, 5750
---

## 概要

このEIPは、[ERC-5750](./eip-5750.md)に依存する同一トランザクション内での関数呼び出しの承認に関する一般的なプロトコルを確立します。
[ERC-2612](./eip-2612.md)の[ERC-20](./eip-20.md)や`ERC-4494`の[ERC-721](./eip-721.md)のように、通常は単一の振る舞い(`ERC-20`の`transfer`や`ERC-721`の`safeTransferFrom`)と2つのトランザクション(最初に`permit(...)`トランザクション、次に`transfer`のようなトランザクション)での単一の承認者しか許可しないのとは異なり、このEIPでは任意の振る舞いと任意の数の承認者からの承認を同一トランザクション内で集約することができ、マルチシグやしきい値署名の動作をサポートします。

## 動機

1. 関数呼び出しと並行して承認(permit)をサポートする。
2. 別のユーザーからの2番目の承認をサポートする。
3. 別のユーザーによる支払いをサポートする。
4. マルチシグをサポートする。
5. 承認による協調行動をサポートする。
6. 累積投票をサポートする。
7. オフラインでの署名をサポートする。

## 仕様

この文書の中の "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY"、"OPTIONAL" というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

### インターフェース

ここで参照されているインターフェースと構造体は以下の通りです。

```solidity
pragma solidity ^0.8.9;

struct ValidityBound {
    bytes32 functionParamStructHash;
    uint256 validSince;
    uint256 validBy;
    uint256 nonce;
}

struct SingleEndorsementData {
    address endorserAddress; // 32
    bytes sig; // dynamic = 65
}

struct GeneralExtensionDataStruct {
    bytes32 erc5453MagicWord;
    uint256 erc5453Type;
    uint256 nonce;
    uint256 validSince;
    uint256 validBy;
    bytes endorsementPayload;
}

interface IERC5453EndorsementCore {
    function eip5453Nonce(address endorser) external view returns (uint256);
    function isEligibleEndorser(address endorser) external view returns (bool);
}

interface IERC5453EndorsementDigest {
    function computeValidityDigest(
        bytes32 _functionParamStructHash,
        uint256 _validSince,
        uint256 _validBy,
        uint256 _nonce
    ) external view returns (bytes32);

    function computeFunctionParamHash(
        string memory _functionName,
        bytes memory _functionParamPacked
    ) external view returns (bytes32);
}

interface IERC5453EndorsementDataTypeA {
    function computeExtensionDataTypeA(
        uint256 nonce,
        uint256 validSince,
        uint256 validBy,
        address endorserAddress,
        bytes calldata sig
    ) external view returns (bytes memory);
}


interface IERC5453EndorsementDataTypeB {
    function computeExtensionDataTypeB(
        uint256 nonce,
        uint256 validSince,
        uint256 validBy,
        address[] calldata endorserAddress,
        bytes[] calldata sigs
    ) external view returns (bytes memory);
}
```

[`IERC5453.sol`](../assets/eip-5453/IERC5453.sol)を参照してください。

### 動作仕様

[ERC-5750 General Extensibility for Method Behaviors](./eip-5750.md)で指定されているように、最後のメソッドパラメータとして`bytes extraData`を持つ準拠メソッドは、[ERC-5453](./eip-5453.md)に準拠し、特定のユーザーからの許可を示すことができます。

1. このEIPに準拠するメソッドは、必ず[ERC-5750](./eip-5750.md)に準拠したメソッドでなければなりません。
2. 呼び出し側は、_インターフェース_セクションで指定された`GeneralExtensionDataStruct`のソリディティメモリエンコーディングレイアウトバイトに準拠する最後のパラメータ`bytes extraData`を渡さなければなりません。以下の説明は、`bytes extraData`を`GeneralExtensionDataStruct`にデコードした場合に基づいています。
3. `GeneralExtensionDataStruct`でデコードされた`extraData`では、呼び出し側は`GeneralExtensionDataStruct.erc5453MagicWord`の値を`keccak256("ERC5453-ENDORSEMENT")`に設定しなければなりません。
4. 呼び出し側は、`GeneralExtensionDataStruct.erc5453Type`の値を、サポートされている値の1つに設定しなければなりません。

```solidity
uint256 constant ERC5453_TYPE_A = 1;
uint256 constant ERC5453_TYPE_B = 2;
```

5. `GeneralExtensionDataStruct.erc5453Type`の値が`ERC5453_TYPE_A`に設定されている場合、`GeneralExtensionDataStruct.endorsementPayload`は`SingleEndorsementData`のABIエンコーディングバイトでなければなりません。
6. `GeneralExtensionDataStruct.erc5453Type`の値が`ERC5453_TYPE_B`に設定されている場合、`GeneralExtensionDataStruct.endorsementPayload`は`SingleEndorsementData[]`(動的配列)のABIエンコーディングバイトでなければなりません。

7. 各`SingleEndorsementData`には、`address endorserAddress;`と65バイトの`bytes sig`署名が含まれていなければなりません。

8. 各`bytes sig`は、`endorserAddress`の対応するアドレスの秘密鍵を使用して、[EIP-712](./eip-712.md)のhashTypeDataV4の`ValidityBound`データ構造のhashStructを署名したECDSA(secp256k1)署名でなければなりません。

```solidity
bytes32 validityDigest =
    eip712HashTypedDataV4(
        keccak256(
            abi.encode(
                keccak256(
                    "ValidityBound(bytes32 functionParamStructHash,uint256 validSince,uint256 validBy,uint256 nonce)"
                ),
                functionParamStructHash,
                _validSince,
                _validBy,
                _nonce
            )
        )
    );
```

9. `functionParamStructHash`は以下のように計算されなければなりません。

```solidity
        bytes32 functionParamStructHash = keccak256(
            abi.encodePacked(
                keccak256(bytes(_functionStructure)),
                _functionParamPacked
            )
        );
        return functionParamStructHash;
```

ここで、

- `_functionStructure`は`function methodName(type1 param1, type2 param2, ...)`のように計算されなければなりません。
- `_functionParamPacked`は`enc(param1) || enco(param2) ...`のように計算されなければなりません。

10. `endorserAddress == ecrecover(validityDigest, signature)`または`EIP1271(endorserAddress).isValidSignature(validityDigest, signature) == ERC1271.MAGICVALUE`が検証された場合、単一の承認は有効とみなされなければなりません。
11. 準拠メソッドは、同じ`ERC5453_TYPE_B`の`endorsementPayload`で有効とみなされるための承認の数のしきい値を課すことができます。

12. `validSince`と`validBy`はどちらも包含的です。実装者は、ブロック番号またはタイムスタンプを使用することができます。実装者は、`validSince`と`validBy`がブロック番号かタイムスタンプかを示す方法を見つけるべきです。

## 根拠

1. `ERC5453_TYPE_A`(単一承認)と`ERC5453_TYPE_B`(複数承認、同一のnonce全体のコントラクト)の両方を選択したのは、より広範な使用例のバランスを取るためです。例えば、ERC-2612と`ERC-4494`と同じ使用例は、`ERC5453_TYPE_A`でサポートできます。しきい値承認は`ERC5453_TYPE_B`で行うことができます。より複雑な承認タイプは、新しい`ERC5453_TYPE_?`を定義することで拡張することもできます。

2. `validSince`と`validBy`の両方を含めたのは、有効期限の柔軟性を最大限に高めるためです。これは、採用された場合の`ERC-5081`によってEVM ネイティブでもサポートされますが、`ERC-5081`はすぐには採用されないでしょう。そのため、プロトコルにこれら2つの数値を追加して、スマートコントラクトレベルのサポートを可能にしました。

## 下位互換性

このデザインは、将来の拡張の柔軟性を最大限に高めるために、`bytes calldata extraData`を前提としています。この前提は、[ERC-721](eip-721.md)、[ERC-1155](eip-1155.md)、および多くの他のERC-trackのEIPと互換性があります。[ERC-20](./eip-20.md)のようにそうでないものも、ラッパーコントラクトやプロキシアップグレードを使って、このサポートを追加することができます。

## 参考実装

承認者の署名の検証アルゴリズムの指定に加えて、以下の参考実装も提示します。

```solidity
pragma solidity ^0.8.9;

import "@openzeppelin/contracts/utils/cryptography/SignatureChecker.sol";
import "@openzeppelin/contracts/utils/cryptography/EIP712.sol";

import "./IERC5453.sol";

abstract contract AERC5453Endorsible is EIP712,
    IERC5453EndorsementCore, IERC5453EndorsementDigest, IERC5453EndorsementDataTypeA, IERC5453EndorsementDataTypeB {
    // ...

    function _validate(
        bytes32 msgDigest,
        SingleEndorsementData memory endersement
    ) internal virtual {
        require(
            endersement.sig.length == 65,
            "AERC5453Endorsible: wrong signature length"
        );
        require(
            SignatureChecker.isValidSignatureNow(
                endersement.endorserAddress,
                msgDigest,
                endersement.sig
            ),
            "AERC5453Endorsible: invalid signature"
        );
    }
    // ...

    modifier onlyEndorsed(
        bytes32 _functionParamStructHash,
        bytes calldata _extensionData
    ) {
        require(_isEndorsed(_functionParamStructHash, _extensionData));
        _;
    }

    function computeExtensionDataTypeB(
        uint256 nonce,
        uint256 validSince,
        uint256 validBy,
        address[] calldata endorserAddress,
        bytes[] calldata sigs
    ) external pure override returns (bytes memory) {
        require(endorserAddress.length == sigs.length);
        SingleEndorsementData[]
            memory endorsements = new SingleEndorsementData[](
                endorserAddress.length
            );
        for (uint256 i = 0; i < endorserAddress.length; ++i) {
            endorsements[i] = SingleEndorsementData(
                endorserAddress[i],
                sigs[i]
            );
        }
        return
            abi.encode(
                GeneralExtensionDataStruct(
                    MAGIC_WORLD,
                    ERC5453_TYPE_B,
                    nonce,
                    validSince,
                    validBy,
                    abi.encode(endorsements)
                )
            );
    }
}

```

[`AERC5453.sol`](../assets/eip-5453/AERC5453.sol)を参照してください。

### `EndorsableERC721`の参考実装

`ERC-4494`と同様の動作を実現する`EndorsableERC721`の参考実装です。

```solidity
pragma solidity ^0.8.9;

contract EndorsableERC721 is ERC721, AERC5453Endorsible {
    //...

    function mint(
        address _to,
        uint256 _tokenId,
        bytes calldata _extraData
    )
        external
        onlyEndorsed(
            _computeFunctionParamHash(
                "function mint(address _to,uint256 _tokenId)",
                abi.encode(_to, _tokenId)
            ),
            _extraData
        )
    {
        _mint(_to, _tokenId);
    }
}
```

[`EndorsableERC721.sol`](../assets/eip-5453/EndorsableERC721.sol)を参照してください。

### `ThresholdMultiSigForwarder`の参考実装

Gnosis-Safeウォレットのようなマルチシグしきい値承認リモートコントラクト呼び出しの動作を実現する`ThresholdMultiSigForwarder`の参考実装です。

```solidity
pragma solidity ^0.8.9;

contract ThresholdMultiSigForwarder is AERC5453Endorsible {
    //...
    function forward(
        address _dest,
uint256 _value,
        uint256 _gasLimit,
        bytes calldata _calldata,
        bytes calldata _extraData
    )
        external
        onlyEndorsed(
            _computeFunctionParamHash(
                "function forward(address _dest,uint256 _value,uint256 _gasLimit,bytes calldata _calldata)",
                abi.encode(_dest, _value, _gasLimit, keccak256(_calldata))
            ),
            _extraData
        )
    {
        string memory errorMessage = "Fail to call remote contract";
        (bool success, bytes memory returndata) = _dest.call{value: _value}(
            _calldata
        );
        Address.verifyCallResult(success, returndata, errorMessage);
    }

}

```

[`ThresholdMultiSigForwarder.sol`](../assets/eip-5453/ThresholdMultiSigForwarder.sol)を参照してください。

## セキュリティ上の考慮事項

### リプレイ攻撃

リプレイ攻撃は、暗号認証に対する一種の攻撃です。狭義では、メッセージに署名された既存の署名を再利用することで署名検証を回避する攻撃のことを指します。このEIPに依存する実装では、ここで説明されているすべてのスマート承認が公開の暗号署名であることを認識し、同じスマートコントラクトの展開だけでなく、同様のスマートコントラクトの他の展開や、同じコントラクトの別の`chainId`での展開、あるいはその他の同様の攻撃面でのトランザクションの再生を予見しなければなりません。`nonce`、`validSince`、`validBy`フィールドは攻撃面を制限することを目的としていますが、[フィッシング](#フィッシング)の項で述べるように、そのようなすべての攻撃リスクを完全に排除するものではありません。

### フィッシング

特殊な形態のリプレイ攻撃であるフィッシングにも注意が必要です。悪意のある者が、ユーザーを合法的な目的で見せかけてスマート承認に署名させ、その署名データが実際のターゲットアプリケーションと一致するように設計することができます。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。