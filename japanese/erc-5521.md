---
original: f82a45ae0f0318bf5a9b06c3e4b463a6464912f39423a4d769d9c228c2628248
---

---
eip: 5521
title: 参照可能なNFT
description: NFT間の参照関係を構築するためのERC-721の拡張
author: Saber Yu (@OniReimu)、Qin Wang <qin.wang@data61.csiro.au>、Shange Fu <shange.fu@monash.edu>、Yilin Sai <yilin.sai@data61.csiro.au>、Shiping Chen <shiping.chen@data61.csiro.au>、Sherry Xu <xiwei.xu@data61.csiro.au>、Jiangshan Yu <jiangshan.yu@monash.edu>
discussions-to: https://ethereum-magicians.org/t/eip-x-erc-721-referable-nft/10310
status: 最終版
type: 標準化トラック
category: ERC
created: 2022-08-10
requires: 165, 721
---

## 概要

この標準は[ERC-721](./eip-721.md)の拡張です。参照元(referring)と参照先(referred)の2つの参照指標と、時間ベースの指標`createdTimestamp`を提案しています。各NFTの関係は有向非循環グラフ(DAG)を形成します。この標準により、ユーザーは関係性を照会、追跡、分析することができます。

![システムアーキテクチャ](../assets/eip-5521/system-arch.png)

## 動機

多くのシナリオでは、NFTの継承、参照、拡張が必要とされます。例えば、アーティストが以前のNFTを基に新しいNFT作品を開発したり、DJが2つの人気曲をリミックスしたりするなどです。現在のNFT標準には、NFTと元の作成者との関係性を確立する仕組みが欠けています。この欠如により、NFTは孤立したものとなり、各NFTの売買が一回限りの取引になってしまい、知的財産の長期的な価値向上を阻害しています。

このため、既存のNFTに対する参照可能なソリューションを提案し、相互参照の効率的な照会を可能にすることが必要です。NFT間の参照関係を導入することで、NFTの創造、利用、プロモーションに継続的に取り組むインセンティブとなる持続可能な経済モデルを構築できます。

本標準では、参照可能なNFT(rNFT)という新しい概念を導入し、静的なNFTを動的に拡張可能なネットワークに変換します。参照情報、すなわち`referring`と`referred`の関係を埋め込むことで、有向非循環グラフ(DAG)ベースのNFTネットワークを形成します。この構造により、透明性のある履歴記録が提供され、ユーザーが関係性を照会、追跡、分析できるようになります。NFTクリエイターが既存の作品を基に新しい作品を作成できるようになります。

具体的な例として、ユーザーは既存のNFT(A、B)を参照して新しいNFT(C、D、E)を作成できます。その際、`referred`機能により、元のNFT(A、B)に対して引用されたことが通知されます(A &#8592; D、C &#8592; E、B &#8592; E、A &#8592; E)。ここで、`createdTimestamp`(ブロックレベル)は、NFT(A、B、C、D、E)の作成時間を示す指標として機能します。

### 主なメリット

この標準には以下のようなメリットがあります:

*明確な所有権継承*: この標準は静的なNFTを仮想的に拡張可能なNFTネットワークに拡張します。アーティストは他者の作品から独立して作品を作成する必要がなくなります。所有権の継承により、同じ作品を繰り返し作成する必要がなくなります。

*インセンティブ互換性*: この標準は、異なるNFT間の参照関係を明確にし、元のNFTの所有者と新しいクリエイターの両方に対するインセンティブモデルの統合を支援します。

*容易な統合*: この標準は、既存のトークン標準や第三者プロトコルとの統合を容易にします。例えば、rNFTを[ERC-5006](./eip-5006.md)のレンタル可能なシナリオに適用して、階層的なレンタル市場を構築できます。同一期間に複数のユーザーが同じNFTをレンタルしたり、1人のユーザーが同一期間に複数のNFTをレンタルしたりすることができます。

*スケーラブルな相互運用性*: この標準は、コントラクト間の参照を可能にし、より広範な一般ユーザーに対する強力な相互運用性を提供します。

## 仕様

キーワード「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」は、RFC 2119に従って解釈されます。

- `UpdateNode`: `setNode`が呼び出されたときに発行されるイベント
- `safeMint`: 新しいrNFTをミントする
- `setNode`: rNFTの参照リストを設定し、参照リストにある各rNFTの参照先リストを更新する
    - `setNodeReferring`: rNFTの参照リストを設定する
    - `setNodeReferred`: 異なるコントラクトから取得したrNFTの参照先リストを設定する
        - `setNodeReferredExternal`: 外部コントラクトから取得したrNFTの参照先リストを設定する
- `referringOf`: rNFTの参照リストを取得する
- `referredOf`: rNFTの参照先リストを取得する
- `createdTimestampOf`: rNFTが作成された時のタイムスタンプを取得する

この標準を実装する際は、以下の関数をすべて実装する必要があります:

```solidity

pragma solidity ^0.8.4;

import "@openzeppelin/contracts/utils/introspection/IERC165.sol";

interface IERC_5521 is IERC165 {

    /// rNFTのノードが参照され、変更されたときに発行されるイベント
    /// @notice `node`(つまりrNFT)が変更されたときに発行されます。
    event UpdateNode(uint256 indexed tokenId, 
                     address indexed owner, 
                     address[] _address_referringList,
                     uint256[][] _tokenIds_referringList,
                     address[] _address_referredList,
                     uint256[][] _tokenIds_referredList
    );

    /// @notice rNFTの参照先リストを設定し、参照リストにある各rNFTの参照元リストを更新する。`addresses`と`tokenIds`の重複チェックを**推奨**する。
    /// @param `tokenId` 設定対象のrNFT。`addresses` 参照先のrNFTが存在するコントラクトアドレス。
    /// @requirement 
    /// - `addresses`のサイズは`tokenIds`のサイズと同じでなければならない
    /// - `tokenIds`のサイズが0以外の場合、内部のサイズも0以外でなければならない
    /// - `tokenId`は同一コントラクト内で一意でなければならない
    /// - `addresses[i]`が`address(this)`の場合、`tokenIds[i][j]`と同じ`tokenId`であってはならない
    function setNode(uint256 tokenId, address[] memory addresses, uint256[][] memory tokenIds) external;

    /// @notice rNFTの参照リストを取得する
    /// @param `tokenId` 対象のrNFT、`_address` 対象のrNFTが存在するコントラクトアドレス
    /// @return rNFTの参照マッピング
    function referringOf(address _address, uint256 tokenId) external view returns(address[] memory, uint256[][] memory);

    /// @notice rNFTの参照先リストを取得する
    /// @param `tokenId` 対象のrNFT、`_address` 対象のrNFTが存在するコントラクトアドレス
    /// @return rNFTの参照先マッピング
    function referredOf(address _address, uint256 tokenId) external view returns(address[] memory, uint256[][] memory);

    /// @notice rNFTが作成された時のタイムスタンプを取得する
    /// @param `tokenId` 対象のrNFT、`_address` 対象のrNFTが存在するコントラクトアドレス
    /// @return rNFTが作成された時のタイムスタンプ(uint256形式)
    function createdTimestampOf(address _address, uint256 tokenId) external view returns(uint256);
    
    /// @notice サポートされるインターフェースを確認する(ERC165に準拠)
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

interface TargetContract is IERC165 {
    /// @notice rNFTの外部コントラクトアドレスからの参照先リストを設定する
    /// @param `_tokenIds` `tokenId`のrNFTが参照する外部コントラクトアドレス`_address`のrNFT
    /// @requirement
    /// - `_address`は`address(this)`(つまり、このコントラクトが実行されているコントラクト)と同じであってはならない
    function setNodeReferredExternal(address _address, uint256 tokenId, uint256[] memory _tokenIds) external;

    function referringOf(address _address, uint256 tokenId) external view returns(address[] memory, uint256[][] memory);

    function referredOf(address _address, uint256 tokenId) external view returns(address[] memory, uint256[][] memory);

    function createdTimestampOf(address _address, uint256 tokenId) external view returns(uint256);
    
    function supportsInterface(bytes4 interfaceId) external view returns (bool);
}

```

## 根拠

### このイベントは十分に情報的ですか?
`UpdateNode`: このイベントは、rNFTのID、所有者、参照元または参照先のコントラクトアドレスとIDのリストといった重要な情報を発信します。このデータセットにより、rNFTエコシステムに内在する複雑な関係性を効率的に管理およびナビゲートできます。

実装者は、構造体(推奨される構造体は参考実装に示されています)、複数の個別のマッピング、またはその他の任意のストレージメカニズムを選択できます。選択したメカニズムは、`UpdateNode`イベントの出力を満たす限り、契約の動作に影響を与えません。

### なぜ`createdTimestampOf`なのですか?
`createdTimestamp`: この標準の主要な原則の1つは、rNFTがコミュニティで既に受け入れられているコンテンツを参照することです(参加者に知られているタイムシーケンス)。したがって、rNFTのグローバルタイムスタンプは不可欠で、トランザクション処理やブロック編成における競合状態(同時性の問題)を防ぐ役割を果たします。ブロックレベルのタイムスタンプ`createdTimestamp = block.timestamp`を定義しています。参照の粒度がブロックタイムスタンプに連動しているため、同一ブロック内の2つのrNFTの順序を見分けるのは実用的ではありません。

### コントラクト間の参照はどのように行われますか?
`setNodeReferredExternal`: この関数は、外部コントラクトでのインターフェース検証が成功した場合にのみ条件付きで動作します。このような選択的な呼び出しにより、指定されたインターフェースに準拠している限り、既存のコントラクトとの下位互換性と統合が確保されます。

## 下位互換性

この標準は[ERC-721](./eip-721.md)と完全に互換性があり、拡張機能セットを追加することで対応できます。

## テストケース

テストケースは[ERC_5521.test.js](../assets/eip-5521/ERC_5521.test.js)に含まれています。

## 参考実装

推奨される実装は以下のようになります:

- `Relationship`: `referring`、`referred`、`referringKeys`、`referredKeys`、`createdTimestamp`、および`privityOfAgreement`(rNFTが作成された時点での参照NFTの所有権を記録する)や`profitSharing`(`referring`の利益配分を記録する)などのカスタマイズ可能なオプションの属性を含む構造体
- `referring`: 出次数指標、このNFTが参照しているユーザーを示す
- `referred`: 入次数指標、このNFTを参照しているユーザーを示す
- `referringKeys`: 出次数指標のマッピング変換のヘルパー、イベント用
- `referredKeys`: 入次数指標のマッピング変換のヘルパー、イベント用
- `createdTimestamp`: 時間ベースの指標、ミント時のタイムスタンプを示し、呼び出し側によって編集できないようにする
- `referringOf`と`referredOf`: まず、現在の`referringOf`と`referredOf`は、`_relationship`への直接アクセスではなくクロスコントラクトの参照を可能にします。次に、プライバシーが問題にならない場合は、`_relationship`を公開することで、Solidityの自動生成されたゲッターに依存して契約を
簡素化できます。ただし、データの可視性を制御する必要がある場合は、状態変数を非公開にし、特定のゲッター関数を提供するのが最善のアプローチです。例えば、`_relationship`に特定のユーザーの相互作用やトランザクション、または拡張可能なプライベートパラメーターの詳細が含まれている場合、これらのデータをすべて公開すると、ユーザーの行動パターンや嗜好が明らかになり、プライバシーの侵害につながる可能性があります。

- `convertMap`: この関数は、構造体内の完全なマッピング内容を取得するために不可欠です。`_relationship`が公開されている場合でも、ゲッターでは特定のキーに対する個別の値しか取得できません。イベントの要件を満たすには、`convertMap`が必要です。

```solidity

pragma solidity ^0.8.4;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC_5521.sol";

contract ERC_5521 is ERC721, IERC_5521, TargetContract {

    struct Relationship {
        mapping (address => uint256[]) referring;
        mapping (address => uint256[]) referred;
        address[] referringKeys;
        address[] referredKeys;
        uint256 createdTimestamp; // rNFTが作成された時のUnixタイムスタンプ

        // 拡張可能なパラメーター
        // ...
    }

    mapping (uint256 => Relationship) internal _relationship;
    address contractOwner = address(0);

    constructor(string memory name_, string memory symbol_) ERC721(name_, symbol_) {
        contractOwner = msg.sender;
    }

    function safeMint(uint256 tokenId, address[] memory addresses, uint256[][] memory _tokenIds) public {
        // require(msg.sender == contractOwner, "ERC_rNFT: Only contract owner can mint");
        _safeMint(msg.sender, tokenId);
        setNode(tokenId, addresses, _tokenIds);
    }

    /// @notice rNFTの参照先リストを設定し、参照リストにある各rNFTの参照元リストを更新する
    /// @param tokenIds rNFTの配列、呼び出し側で重複チェックを行うことを推奨
    function setNode(uint256 tokenId, address[] memory addresses, uint256[][] memory tokenIds) public virtual override {
        require(
            addresses.length == tokenIds.length,
            "Addresses and TokenID arrays must have the same length"
        );
        for (uint i = 0; i < tokenIds.length; i++) {
            if (tokenIds[i].length == 0) { revert("ERC_5521: the referring list cannot be empty"); }
        }
        setNodeReferring(addresses, tokenId, tokenIds);
        setNodeReferred(addresses, tokenId, tokenIds);
    }

    /// @notice rNFTの参照リストを設定する
    /// @param _tokenIds 各アドレスに関連付けられたrNFTの配列、呼び出し側で重複チェックを行うことを推奨
    function setNodeReferring(address[] memory addresses, uint256 tokenId, uint256[][] memory _tokenIds) private {
        require(_isApprovedOrOwner(msg.sender, tokenId), "ERC_5521: transfer caller is not owner nor approved");

        Relationship storage relationship = _relationship[tokenId];

        for (uint i = 0; i < addresses.length; i++) {
            if (relationship.referring[addresses[i]].length == 0) { relationship.referringKeys.push(addresses[i]); } // 新しいエントリの場合はアドレスを追加
            relationship.referring[addresses[i]] = _tokenIds[i];
        }

        relationship.createdTimestamp = block.timestamp;
        emitEvents(tokenId, msg.sender);
    }

    /// @notice rNFTの参照先リストを設定する
    /// @param _tokenIds 各アドレスに関連付けられたrNFTの配列、呼び出し側で重複チェックを行うことを推奨
    function setNodeReferred(address[] memory addresses, uint256 tokenId, uint256[][] memory _tokenIds) private {
        for (uint i = 0; i < addresses.length; i++) {
            if (addresses[i] == address(this)) {
                for (uint j = 0; j < _tokenIds[i].length; j++) {
                    Relationship storage relationship = _relationship[_tokenIds[i][j]];
                    if (relationship.referred[addresses[i]].length == 0) { relationship.referredKeys.push(addresses[i]); } // 新しいエントリの場合はアドレスを追加
                    
                    require(tokenId != _tokenIds[i][j], "ERC_5521: self-reference not allowed");
                    if (relationship.createdTimestamp >= block.timestamp) { revert("ERC_5521: the referred rNFT needs to be a predecessor"); } // 参照が時系列順に従っていることを確認

                    relationship.referred[address(this)].push(tokenId);
                    emitEvents(_tokenIds[i][j], ownerOf(_tokenIds[i][j]));
                }
            } else {
                TargetContract targetContractInstance = TargetContract(addresses[i]);
                bool isSupports = targetContractInstance.supportsInterface(type(TargetContract).interfaceId);
                if (isSupports) {
                    // ターゲットコントラクトがインターフェースをサポートしている場合、安全にインターフェースの関数を呼び出せる
                    targetContractInstance.setNodeReferredExternal(address(this), tokenId, _tokenIds[i]);
                }
            }
        }
    }

    /// @notice rNFTの外部コントラクトアドレスからの参照先リストを設定する
    /// @param _tokenIds 各アドレスに関連付けられたrNFTの配列、呼び出し側で重複チェックを行うことを推奨
    function setNodeReferredExternal(address _address, uint256 tokenId, uint256[] memory _tokenIds) external {
        for (uint i = 0; i < _tokenIds.length; i++) {
            Relationship storage relationship = _relationship[_tokenIds[i]];
            if (relationship.referred[_address].length == 0) { relationship.referredKeys.push(_address); } // 新しいエントリの場合はアドレスを追加

            require(_address != address(this), "ERC_5521: this must be an external contract address");
            if (relationship.createdTimestamp >= block.timestamp) { revert("ERC_5521: the referred rNFT needs to be a predecessor"); } // 参照が時系列順に従っていることを確認

            relationship.referred[_address].push(tokenId);
            emitEvents(_tokenIds[i], ownerOf(_tokenIds[i]));
        }
    }

    /// @notice rNFTの参照リストを取得する
    /// @param tokenId 対象のrNFT、_address 対応するコントラクトアドレス
    /// @return rNFTの参照マッピング
    function referringOf(address _address, uint256 tokenId) external view virtual override(IERC_5521, TargetContract) returns (address[] memory, uint256[][] memory) {
        address[] memory _referringKeys;
        uint256[][] memory _referringValues;

        if (_address == address(this)) {
            require(_exists(tokenId), "ERC_5521: token ID not existed");
            (_referringKeys, _referringValues) = convertMap(tokenId, true);
        } else {
            TargetContract targetContractInstance = TargetContract(_address);
            require(targetContractInstance.supportsInterface(type(TargetContract).interfaceId), "ERC_5521: target contract not supported");
            (_referringKeys, _referringValues) = targetContractInstance.referringOf(_address, tokenId);     
        }      
        return (_referringKeys, _referringValues);
    }

    /// @notice rNFTの参照先リストを取得する
    /// @param tokenId 対象のrNFT、_address 対応するコントラクトアドレス
    /// @return rNFTの参照先マッピング
    function referredOf(address _address, uint256 tokenId) external view virtual override(IERC_5521, TargetContract) returns (address[] memory, uint256[][] memory) {
        address[] memory _referredKeys;
        uint256[][] memory _referredValues;

        if (_address == address(this)) {
            require(_exists(tokenId), "ERC_5521: token ID not existed");
            (_referredKeys, _referredValues) = convertMap(tokenId, false);
        } else {
            TargetContract targetContractInstance = TargetContract(_address);
            require(targetContractInstance.supportsInterface(type(TargetContract).interfaceId), "ERC_5521: target contract not supported");
            (_referredKeys, _referredValues) = targetContractInstance.referredOf(_address, tokenId);           
        }
        return (_referredKeys, _referredValues);
    }

    /// @notice rNFTが作成された時のタイムスタンプを取得する
    /// @param `tokenId` 対象のrNFT、`_address` 対象のrNFTが存在するコントラクトアドレス
    /// @return rNFTが作成された時のタイムスタンプ(uint256形式)
    function createdTimestampOf(address _address, uint256 tokenId) external view returns(uint256) {
        uint256 memory createdTimestamp;

        if (_address == address(this)) {
            require(_exists(tokenId), "ERC_5521: token ID not existed");
            Relationship storage relationship = _relationship[tokenId];
            createdTimestamp = relationship.createdTimestamp;
        } else {
            TargetContract targetContractInstance = TargetContract(_address);
            require(targetContractInstance.supportsInterface(type(TargetContract).interfaceId), "ERC_5521: target contract not supported");
            createdTimestamp = targetContractInstance.createdTimestampOf(_address, tokenId);            
        }
        return createdTimestamp;
    }

    /// @dev {IERC165-supportsInterface}を参照.
    function supportsInterface(bytes4 interfaceId) public view virtual override (ERC721, IERC_5521, TargetContract) returns (bool) {
        return interfaceId == type(IERC_5521).interfaceId
            || interfaceId == type(TargetContract).interfaceId
            || super.supportsInterface(interfaceId);    
    }

    // @notice UpdateNodeイベントを発行する
    function emitEvents(uint256 tokenId, address sender) private {
        (address[] memory _referringKeys, uint256[][] memory _referringValues) = convertMap(tokenId, true);
        (address[] memory _referredKeys, uint256[][] memory _referredValues) = convertMap(tokenId, false);
        
        emit UpdateNode(tokenId, sender, _referringKeys, _referringValues, _referredKeys, _referredValues);
    }

    // @notice 特定の`local`トークンマッピングをキー配列と値配列に変換する
    function convertMap(uint256 tokenId, bool isReferring) private view returns (address[] memory, uint256[][] memory) {
        Relationship storage relationship = _relationship[tokenId];

        address[] memory returnKeys;
        uint256[][] memory returnValues;

        if (isReferring) {
            returnKeys = relationship.referringKeys;
            returnValues = new uint256[][](returnKeys.length);
            for (uint i = 0; i < returnKeys.length; i++) {
                returnValues[i] = relationship.referring[returnKeys[i]];
            }            
        } else {
            returnKeys = relationship.referredKeys;
            returnValues = new uint256[][](returnKeys.length);
            for (uint i = 0; i < returnKeys.length; i++) {
                returnValues[i] = relationship.referred[returnKeys[i]];
            }
        }
        return (returnKeys, returnValues);
    }
}