---
original: 5a0df4ad6608fc1a2f493917ba25ffc5271f349412e1125291475cc06526f259
---

---
eip: 6806
title: ERC-721 保有時間トラッキング
description: ERC-721トークンに保有時間情報を追加する
author: Saitama (@saitama2009), Combo <combo@1combo.io>, Luigi <luigi@1combo.io>
discussions-to: https://ethereum-magicians.org/t/draft-eip-erc721-holding-time-tracking/13605
status: Draft
type: Standards Track
category: ERC
created: 2023-03-30
requires: 721
---

## 概要

この規格は[ERC-721](./eip-721.md)の拡張です。アカウントによる非代替性トークン(NFT)の保有時間を追跡し、説明するインターフェースを追加します。

## 動機

一部のユースケースでは、NFTがアカウントによってどの程度の期間保有されているかを知ることが重要です。この情報は、長期保有者への報酬、独占コンテンツへのアクセス、あるいは保有時間に基づいた特定のビジネスロジックの実装など、さまざまな用途に役立ちます。しかし、現行のERC-721標準には、NFTの保有時間を追跡するための組み込みメカニズムがありません。

この提案は、ERC-721標準を拡張して保有時間トラッキング機能を含めることで、これらの制限に対処することを目的としています。

## 仕様

この文書の中の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

**インターフェース**

以下のインターフェースは、既存のERC-721標準を拡張するものです:

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0

interface IERC6806 {
    function getHoldingInfo(
        uint256 tokenId
    ) external view returns (address holder, uint256 holdingTime);
}
```

**関数**

### getHoldingInfo

```
function getHoldingInfo(uint256 tokenId) external view returns (address holder, uint256 holdingTime);
```

この関数は、指定されたNFTの現在の所有者と、そのNFTが現在のアカウントによって保有されている期間(秒数)を返します。

* `tokenId`: NFTの一意の識別子。
* 戻り値: 現在の所有者のアドレスと保有時間(秒数)のタプル。

## 根拠

ERC-721標準の拡張に`getHoldingInfo`関数を追加することで、保有時間情報を必要とするNFTベースのアプリケーションの実装が可能になります。この拡張は既存のERC-721実装との互換性を維持しつつ、新しいユースケースに対する追加機能を提供します。

`getHoldingInfo`関数は、NFTの保有時間と所有者アドレスを簡単に取得する方法を提供します。保有期間の単位を秒数とすることで、スマートコントラクト内の他の時間関数との互換性が確保されます。

`getHoldingInfo`は`holder`と`holdingTime`の両方を返すため、一部のトークン所有者(実装によって決定される)を保有時間の計算から除外することができます。例えば、ローン担保としてNFTの所有権を取得するコントラクトがある場合、そのようなローンコントラクトは無視され、実際の所有者の保有時間が適切に増加します。

## 下位互換性

この提案は、既存のERC-721標準との完全な下位互換性を持っています。コア機能に影響を与えることなく、新しい関数を追加するものです。

## 参考実装

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/access/Ownable.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC6806.sol";

contract ERC6806 is ERC721, Ownable, IERC6806 {
    mapping(uint256 => address) private _holder;
    mapping(uint256 => uint256) private _holdStart;
    mapping(address => bool) private _holdingTimeWhitelist;

    constructor(
        string memory name_,
        string memory symbol_
    ) ERC721(name_, symbol_) {}

    function _afterTokenTransfer(
        address from,
        address to,
        uint256 firstotTokenId,
        uint256
    ) internal override {
        if (_holdingTimeWhitelist[from] || _holdingTimeWhitelist[to]) {
            return;
        }

        if (_holder[firstotTokenId] != to) {
            _holder[firstotTokenId] = to;
            _holdStart[firstotTokenId] = block.timestamp;
        }
    }

    function getHoldingInfo(
        uint256 tokenId
    ) public view returns (address holder, uint256 holdingTime) {
        return (_holder[tokenId], block.timestamp - _holdStart[tokenId]);
    }

    function setHoldingTimeWhitelistedAddress(
        address account,
        bool ignoreReset
    ) public onlyOwner {
        _holdingTimeWhitelist[account] = ignoreReset;
        emit HoldingTimeWhitelistSet(account, ignoreReset);
    }
}
```

## セキュリティ上の考慮事項

このEIPは保有時間の追跡のための追加の状態管理を導入するため、セキュリティ上の影響がある可能性があります。実装者は、特に転送時の保有時間操作に関する脆弱性に注意を払う必要があります。

このEIPを実装する際は、再入力攻撃やフロントランニング攻撃などの潜在的な攻撃ベクターに加え、スマートコントラクトのセキュリティに関する一般的なベストプラクティスにも留意する必要があります。安全性と正確性を確保するために、十分なテストとコードレビューを行う必要があります。

また、保有時間情報の正確性は、基礎となるブロックチェーンのタイムスタンプの正確性に依存することにも注意が必要です。ブロックタイムスタンプは一般的に信頼できますが、マイナーによって多少操作される可能性があります。したがって、絶対的な精度が必要な状況では、保有時間データを唯一の真実として依存すべきではありません。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。