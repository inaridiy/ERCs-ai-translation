---
original: 3fa51d5e12abcd46ad3d559b1332139b92a270f97049acdc8ba4002eacb2b473
---

---
eip: 7561
title: シンプルなNFT、簡略化されたERC-721
description: コントラクトウォレットに設計され、ERC-721のapprove、setApprovalForAll、getApproved、isApprovedForAllおよびsafeTransferFromの機能を削除
author: Xiang (@wenzhenxiang)、Ben77 (@ben2077)、Mingshi S. (@newnewsms)
discussions-to: https://ethereum-magicians.org/t/erc-7561-simple-nft/16695
status: Draft
type: Standards Track
category: ERC
created: 2023-10-29
requires: 721
---

## 概要

このERCは、ユーザーコントラクトウォレット(アカウントアブストラクションを含む)に基づいて設計された新しいNFTアセットであり、[ERC-721](./eip-721.md)と前方互換性があります。NFTアセットを単純に保つために、このERCはERC-721の`approve`、`setApprovalForAll`、`getApproved`、`isApprovedForAll`および`safeTransferFrom`の機能を削除しています。

## 動機

[ERC-721](./eip-721.md)は、取引や移転が可能なEthereum ベースの標準NFTを定義していますが、ERC-721の本質は外部所有アカウント(EOA)ウォレットの設計に基づいています。EOAウォレットには状態とコードストレージがなく、スマートコントラクトウォレットとは異なります。

ほとんどすべてのNFTに関連するERCは機能を追加していますが、私たちの意見は逆です。私たちは、NFTコントラクトをより単純にし、スマートコントラクトウォレットが面倒を見るべきだと考えています。

私たちの提案は、スマートコントラクトウォレットに基づいたより単純なNFTアセットを設計することです。

以下の目標を達成することを目指しています:

1. NFTコントラクトを単純に保ち、`transferFrom`関数のみを担当する。
2. `approve`、`getApproved`、`setApprovalForAll`、`isApprovedForAll`の各関数はNFTコントラクトによって管理されません。代わりに、これらの権限はユーザーレベルで管理され、ユーザーにより大きな自律性と管理権を提供します。この変更は、ユーザーの自律性を高めるだけでなく、ERC-721コントラクトがこれらの関数を実装することに関連するリスクも軽減します。
3. `safeTransferFrom`関数を削除する。他者のNFTアセットを呼び出す better な方法は、NFTアセットコントラクトを直接呼び出すのではなく、相手方の自身のコントラクトにアクセスすることです。
4. ERC-721との前方互換性により、すべてのNFTがこの提案と互換性を持つことができます。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

準拠するコントラクトは、以下のインターフェースを実装しなければなりません:

```solidity
pragma solidity ^0.8.20;

/**
 * @title ERC7561 Simple NFT interface 
 * @dev See https://ercs.ethereum.org/ERCS/erc-7561
 */
interface IERC7561 {
    /**
     * @notice NFTの移転を通知するために使用される。
     * @param from 送信元アドレス
     * @param to 受信アドレス
     * @param tokenId 取引トークンID 
     */
    event Transfer(
        address indexed from,
        address indexed to,
        uint256 indexed tokenId
    );

    /**
     * @notice 所有者に割り当てられたすべてのNFTを数える
     * @param owner 所有者アドレス
     * @return `owner`が所有するNFTの数(ゼロの可能性あり)
     */
    function balanceOf(address owner) 
        external
        view
        returns (uint256);

    /**
     * @notice NFTの所有者を見つける
     * @param tokenId NFTの識別子
     * @return NFTの所有者アドレス
     */
    function ownerOf(uint256 tokenId) 
        external  
        view
        returns (address);
	  

    /**
     * @notice NFTの所有権を移転する
     * @param from 送信元アドレス
     * @param to 送信先アドレス
     * @param tokenId 移転するNFT
     */
    function transferFrom(address from, address to, uint256 tokenId) external;

}
```

## 根拠

この提案は、`approve`、`setApprovalForAll`、`getApproved`、`isApprovedForAll`、`safeTransferFrom`の各関数を削除することでNFT標準を簡素化することです。この簡素化は、セキュリティの向上、複雑さの削減、効率性の向上を目的としており、スマートコントラクトウォレット環境により適したものにしつつ、必要不可欠な機能を維持することを目指しています。

## 下位互換性

冒頭で述べたように、このERCは[ERC-721](./eip-721.md)と前方互換性があり、ERC-721は下位互換性を持っています。

## リファレンス実装

**[ERC-721](./eip-721.md)との前方互換性**

```solidity
pragma solidity ^0.8.20;

import "./IERC7561.sol";
import "../../math/SafeMath.sol";

/**
 * @title Standard ERC7561 NFT
 * @dev Note: the ERC-165 identifier for this interface is 0xc1b31357
 * @dev Implementation of the basic standard NFT.
 */
contract ERC7561 is IERC7561 {

    // トークン名
    string private _name;

    // トークンシンボル
    string private _symbol;

    mapping(uint256 tokenId => address) private _owners;

    mapping(address owner => uint256) private _balances;

    uint256 private _totalSupply;

    function totalSupply() external view returns (uint256) {
        return _totalSupply;
    }

    function balanceOf(address owner) public view  returns (uint256) {
        require (owner != address(0));
        
        return _balances[owner];
    }

    function ownerOf(uint256 tokenId) public view  returns (address) {
        return _requireOwned(tokenId);
    }


    function transferFrom(address from, address to, uint256 tokenId) public  {

        require(from == msg.sender);

        require (to != address(0) );

        address previousOwner = _update(to, tokenId);

        require(previousOwner == from);
    }


    function _ownerOf(uint256 tokenId) internal view virtual returns (address) {
        return _owners[tokenId];
    }

    function _requireOwned(uint256 tokenId) internal view returns (address) {
        address owner = _ownerOf(tokenId);
        require(owner != address(0));
            
        return owner;
    }

    function _update(address to, uint256 tokenId) internal virtual returns (address) {
        address from = _ownerOf(tokenId);

        
        // 更新を実行する
        if (from != address(0)) {         

            unchecked {
                _balances[from] -= 1;
            }
        }

        if (to != address(0)) {
            unchecked {
                _balances[to] += 1;
            }
        }

        _owners[tokenId] = to;

        emit Transfer(from, to, tokenId);

        return from;
    }

}
```


## セキュリティ上の考慮事項

このERCは[ERC-721](./eip-721.md)との下位互換性がないため、既存のdappsとの非互換性が生じることに注意が必要です。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。