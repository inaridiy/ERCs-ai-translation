---
original: b8cba545d05d1233c2ffccc3a39c5eb5913ead0da584137c7f88576d47d0a1df
---

---
eip: 7656
title: 一般化されたトークンリンクサービス
description: 特定のNFTにリンクされた汎用サービスのレジストリを定義する
author: Francesco Sullo (@sullof)
discussions-to: https://ethereum-magicians.org/t/variation-to-erc6551-to-deploy-any-kind-of-contract-linked-to-an-nft/19223
status: Draft
type: Standards Track
category: ERC
created: 2024-03-15
requires: 165, 1167, 5313, 6551
---

## 概要

この提案では、[ERC-6551](./eip-6551.md)の変形を紹介し、非代替性トークン(NFT)にリンクされたあらゆるタイプのサービス、つまりNFTに所有されているNFTの所有者によって所有されるコントラクトに拡張します。これを実現するために、関数、エラー、イベントの汎用的な言語を使用し、元の提案で課された厳しい制限と競合しないようにしています。

## 動機

[ERC-6551](./eip-6551.md)は、トークンIDによって所有されるアカウントをバインドすることを目的としています。このような新しい提案を試みる問題は、[ERC-6551](./eip-6551.md)が明示的に`IERC6551Account`と`IERC6551Execute`を実装する必要があるコントラクトのみを`ERC6551Registry`経由でデプロイできることです。つまり、それはアカウントでなければなりません。この要件は、インターフェースの関数名とイベントの選択によって強調されています。さらに、[ERC-6551](./eip-6551.md)は、`ERC6551Registry`スマートコントラクトがチェーン上の特定のアドレスにシングルトンとしてデプロイされることを指定しています。このサービスの集中化により、そのレジストリを介してデプロイされたアカウント以外のコントラクトをスパムや無効と見なす可能性があります。

この新しいERCでは、NFTに関連付けられる意味のある任意のタイプのコントラクトをデプロイできるように、汎用的な関数/イベント名を使用するより一般的なレジストリを提案しています。NFTの所有者が完全に制御できるようにするためです。[ERC-6551](./eip-6551.md)と比較して、この提案の柔軟性の1つの目標であるため、`ERC7656Registry`コントラクトがシングルトンとしてデプロイされることは期待されていません。その代わり、任意のプロジェクトがニーズに合わせて調整できるようにします。その結果、`IERC7656Registry`インターフェースをサポートする必要があります。

アカウント以外のサービスを管理するレジストリの機能を拡張することで、次のような利点があります:

- **柔軟性**: 開発者はNFTとより広範囲のリンクされたコントラクトを対話させることができ、新しいユースケースや機能性(レンディングシステム、ベスティング資産の配布、分割所有権、ID、など)のロックを解除できます。
- **互換性**: [ERC-6551](./eip-6551.md)のアカウントライクなコントラクトを識別できるようにすることで、従来の標準との下位互換性を維持しています。
- **イノベーション**: この提案は、NFTに関連付けられるコントラクトの種類に制限を設けないことで、NFT空間でのさらなるイノベーションを奨励します。これにより、ユーティリティNFTの可能性が開かれます。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

`IERC7656Registry`インターフェースは次のように定義されます:

```solidity
// interfaceId 0xc6bdc908
interface IERC7656Registry {
  /**
   * @notice レジストリは、コントラクトの作成に成功した場合、Created イベントを必ず発行しなければなりません。
   * @param contractAddress 作成されたコントラクトのアドレス
   * @param implementation 実装コントラクトのアドレス
   * @param salt create2 操作に使用するソルト
   * @param chainId コントラクトが作成されるチェーンのチェーンID
   * @param tokenContract トークンコントラクトのアドレス
   * @param tokenId トークンのID
   */
  event Created(
    address contractAddress,
    address indexed implementation,
    bytes32 salt,
    uint256 chainId,
    address indexed tokenContract,
    uint256 indexed tokenId
  );

  /**
   * レジストリは、create2 操作が失敗した場合、CreationFailed エラーを必ず発生させなければなりません。
   */
  error CreationFailed();

  /**
   * @notice 非代替性トークンのトークンリンクアカウントを作成します。
   * アカウントがすでに作成されている場合は、create2を呼び出すことなくアカウントアドレスを返します。
   * @param implementation 実装コントラクトのアドレス
   * @param salt create2 操作に使用するソルト
   * @param chainId アカウントが作成されるチェーンのチェーンID
   * @param tokenContract トークンコントラクトのアドレス
   * @param tokenId トークンのID
   * Created イベントを発行します。
   * @return account トークンリンクアカウントのアドレス
   */
  function create(
    address implementation,
    bytes32 salt,
    uint256 chainId,
    address tokenContract,
    uint256 tokenId
  ) external returns (address account);

  /**
   * @notice 非代替性トークンのトークンリンクアカウントのアドレスを計算して返します。
   * @param implementation 実装コントラクトのアドレス
   * @param salt create2 操作に使用するソルト
   * @param chainId アカウントが作成されるチェーンのチェーンID
   * @param tokenContract トークンコントラクトのアドレス
   * @param tokenId トークンのID
   * @return account トークンリンクアカウントのアドレス
   */
  function compute(
    address implementation,
    bytes32 salt,
    uint256 chainId,
    address tokenContract,
    uint256 tokenId
  ) external view returns (address account);
}
```

`ERC7656Registry`の実装は、必ず`IERC7656Registry`のインターフェースID `0xc6bdc908`をサポートしなければなりません。

[ERC-6551](./eip-6551.md)と同様に、レジストリは各トークンリンクアカウントを[ERC-1167](./eip-1167.md)の最小プロキシとしてデプロイする必要があります。ただし、不変の定数データがバイトコードに追加されます。

各トークンバインドアカウントのデプロイされたバイトコードは、次の構造を持たなければなりません:
```
ERC-1167 ヘッダー               (10 bytes)
<implementation (address)>    (20 bytes)
ERC-1167 フッター               (15 bytes)
<salt (bytes32)>              (32 bytes)
<chainId (uint256)>           (32 bytes)
<tokenContract (address)>     (32 bytes)
<tokenId (uint256)>           (32 bytes)
```

`ERC7656Registry`を使用して作成されたコントラクトは、`IERC7656Service`インターフェースを実装する必要があります:

```solidity
interface IERC7656Service {
  /**
  * @notice コントラクトにリンクされているトークンを返します
  * @return chainId トークンのチェーンID
  * @return tokenContract トークンコントラクトのアドレス
  * @return tokenId トークンのID
  */
  function token() external view returns (uint256 chainId, address tokenContract, uint256 tokenId);
}
```

または`IERC6551Account`インターフェースのいずれか、あるいは両方を実装する必要があります。この柔軟性により、[ERC-6551](./eip-6551.md)アカウントがこの提案と即座に互換性を持つことができます。

## 根拠

[ERC-7656](./eip-7656.md)の技術的な基盤は、NFTに関連付けられるコントラクトの種類の拡張と一般化に集中しています。このアプローチは、NFTリンクコントラクトの設計と適用における特定の制限と機会を解決するために選択されました。この提案における主な技術的な決定には以下のようなものがあります:

- 汎用的な関数/イベント名: 関数、エラー、イベントの汎用的な命名規則を採用することは意図的なものです。この設計上の決定により、[ERC-7656](./eip-7656.md)レジストリはアカウント以外のさまざまなコントラクトタイプをサポートできるようになります。特定の役割や機能を規定しないことで、NFTに関連付けられるアプリケーションの種類に関するイノベーションと柔軟性を許容します。これにより、インターフェースが簡素化され、さまざまなユースケースに適応しやすくなります。

- レジストリのシングルトン要件なし: 前身とは異なり、[ERC-7656](./eip-7656.md)ではレジストリをシングルトンとしてデプロイすることを義務付けていません。この決定は、さまざまなプロジェクトが固有の要件と制約を持っていることを認識したことによるものです。複数のレジストリインスタンスを許可することで、プロジェクトはニーズに合わせてレジストリの展開を カスタマイズ最適化できるようになり、エコシステム全体の多様性と耐性が向上します。

- `IERC7656Registry`インターフェースの明示的なサポート: 任意のレジストリが`IERC7656Registry`インターフェースを明示的にサポートすることを要求することは、相互運用性と認識を確保するための技術的な決定です。この要件は、準拠したレジストリの識別と相互作用を容易にし、より標準化された一貫したエコシステムの促進につながります。

- コントラクトの関連付けの柔軟性: この提案は、アカウントだけでなく、NFTに意味のある方法で関連付けられるあらゆるタイプのコントラクトに対応するように設計されています。この決定は、NFTユースケースの進化する景観に関する技術的な評価に基づいており、金融商品からアイデンティティ検証システムまで、さまざまな機能をサポートする標準の必要性を認識したものです。

- 下位互換性: この提案には、特に[ERC-6551](./eip-6551.md)のアカウントライクなコントラクトとの下位互換性を確保する規定が含まれています。この技術的な選択により、従来の標準に基づいて構築されたプロジェクトが、既存のインフラストラクチャや投資を捨てることなく、新しい標準に移行したり活用したりできるようになります。

これらの技術的な決定は、NFTリンクコントラクトの範囲と適用性を拡大し、開発者にイノベーションのためのより多くのツールを提供し、分散型アプリケーションの成長するエコシステムをサポートすることを目的としています。現在の制限と将来の機会の両方に取り組むことで、[ERC-7656](./eip-7656.md)は、次世代のNFT技術のための柔軟で堅牢な基盤を築くことを目指しています。

## 参考実装

`ERC7656Registry`の参考実装は`ERC6551Registry`から変更されたものですが、過度に具体的な関数名の変更、異なるイベントとエラーの発行、および`IERC7656Registry`のインターフェースIDに対してのみtrueを返す`supportsInterface`関数が追加されています。

```solidity
contract ERC7656Registry is IERC7656Registry {
  function create(
    address implementation,
    bytes32 salt,
    uint256 /* chainId */,
    address tokenContract,
    uint256 tokenId
  ) external override returns (address) {
    // solhint-disable-next-line no-inline-assembly
    assembly {
    // メモリレイアウト:
    // ----
    // 0x00   0xff                           (1 byte)
    // 0x01   registry (address)             (20 bytes)
    // 0x15   salt (bytes32)                 (32 bytes)
    // 0x35   Bytecode Hash (bytes32)        (32 bytes)
    // ----
    // 0x55   ERC-1167 Constructor + Header  (20 bytes)
    // 0x69   implementation (address)       (20 bytes)
    // 0x5D   ERC-1167 Footer                (15 bytes)
    // 0x8C   salt (uint256)                 (32 bytes)
    // 0xAC   chainId (uint256)              (32 bytes)
    // 0xCC   tokenContract (address)        (32 bytes)
    // 0xEC   tokenId (uint
256)              (32 bytes)

    // バイトコードと定数データをメモリにコピー
      calldatacopy(0x8c, 0x24, 0x80) // salt, chainId, tokenContract, tokenId
      mstore(0x6c, 0x5af43d82803e903d91602b57fd5bf3) // ERC-1167 フッター
      mstore(0x5d, implementation) // implementation
      mstore(0x49, 0x3d60ad80600a3d3981f3363d3d373d3d3d363d73) // ERC-1167 コンストラクタ + ヘッダー

    // create2 計算データをメモリにコピー
      mstore8(0x00, 0xff) // 0xFF
      mstore(0x35, keccak256(0x55, 0xb7)) // keccak256(bytecode)
      mstore(0x01, shl(96, address())) // レジストリアドレス
      mstore(0x15, salt) // ソルト

    // アカウントアドレスを計算
      let computed := keccak256(0x00, 0x55)

    // アカウントがまだデプロイされていない場合
      if iszero(extcodesize(computed)) {
      // アカウントコントラクトをデプロイ
        let deployed := create2(0, 0x55, 0xb7, salt)

      // デプロイに失敗した場合は巻き戻す
        if iszero(deployed) {
          mstore(0x00, 0xd786d393) // `CreationFailed()`
          revert(0x1c, 0x04)
        }

      // ソルトとチェーンIDの前にメモリにデプロイされたアカウントアドレスを格納
        mstore(0x6c, deployed)

      // Created イベントを発行
        log4(
          0x6c,
          0x60,
          0xc6989e4f290074742210cbd6491de7ded9cfe2cd247932a53d31005007a6341a,
          implementation,
          tokenContract,
          tokenId
        )

      // デプロイされたアカウントアドレスを返す
        return(0x6c, 0x20)
      }

    // そうでない場合は、計算されたアカウントアドレスを返す
      mstore(0x00, shr(96, shl(96, computed)))
      return(0x00, 0x20)
    }
  }

  function compute(
    address implementation,
    bytes32 salt,
    uint256 /* chainId */,
    address /* tokenContract */,
    uint256 /* tokenId */
  ) external view override returns (address) {
    // solhint-disable-next-line no-inline-assembly
    assembly {
    // バイトコードと定数データをメモリにコピー
      calldatacopy(0x8c, 0x24, 0x80) // salt, chainId, tokenContract, tokenId
      mstore(0x6c, 0x5af43d82803e903d91602b57fd5bf3) // ERC-1167 フッター
      mstore(0x5d, implementation) // implementation
      mstore(0x49, 0x3d60ad80600a3d3981f3363d3d373d3d3d363d73) // ERC-1167 コンストラクタ + ヘッダー

    // create2 計算データをメモリにコピー
      mstore8(0x00, 0xff) // 0xFF
      mstore(0x35, keccak256(0x55, 0xb7)) // keccak256(bytecode)
      mstore(0x01, shl(96, address())) // レジストリアドレス
      mstore(0x15, salt) // ソルト

    // 計算されたアカウントアドレスをメモリに格納
      mstore(0x00, shr(96, shl(96, keccak256(0x00, 0x55))))

    // 計算されたアカウントアドレスを返す
      return(0x00, 0x20)
    }
  }

  /// @dev IERC7656Registryのインターフェースidであればtrueを返す
  /// このコントラクトはバイトコードを可能な限り小さくするためにIERC165を明示的に拡張していない
  function supportsInterface(bytes4 interfaceId) external pure returns (bool) {
    return interfaceId == 0xc6bdc908;
  }
}
```

`IERC7656Service`の実装例:

```solidity
contract LinkedService is IERC7656Service, EIP5313 {

  function token() public view virtual returns (uint256, address, uint256) {
    bytes memory footer = new bytes(0x60);
     assembly {
      extcodecopy(address(), add(footer, 0x20), 0x4d, 0x60)
    }
    return abi.decode(footer, (uint256, address, uint256));
  }

  function owner() public view virtual override returns (address) {
    (uint256 chainId, address tokenContract_, uint256 tokenId_) = token();
    if (chainId != block.chainid) return address(0);
    return IERC721(tokenContract_).ownerOf(tokenId_);
  }
}
```

## セキュリティ上の考慮事項

この提案は、[ERC-6551](./eip-6551.md)で既に対処されているものを超えるセキュリティ上の新しい考慮事項を導入していません。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)により放棄されています。