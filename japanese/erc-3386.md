---
original: 4cdc7edf6abdcd9c189e0687fe620c9b1f7531a0ad84ed64046cc5ecc7debdaf
---

---
eip: 3386
title: ERC-721 and ERC-1155 to ERC-20 Wrapper
author: Calvin Koder (@ashrowz)
discussions-to: https://github.com/ethereum/EIPs/issues/3384
status: Stagnant
type: Standards Track
category: ERC
created: 2021-03-12
requires: 165
---

## 簡単な要約
一意のERC-721/ERC-1155トークンのプールから派生するジェネリックなERC-20トークンを作成するための標準インターフェイス。

## 概要

このスタンダードは、識別可能なトークンを fungible トークンでラップするためのスマートコントラクトインターフェイスを定義しています。これにより、基本の[ERC-721](./eip-721.md) non-fungible トークンと[ERC-1155](./eip-1155.md) multi トークンをプールにロックすることで、派生的な[ERC-20](./eip-20.md)トークンをミントできます。派生トークンは基本トークンの一意のIDを参照せず、基本トークンとの交換レートが比例するはずです。基本トークンの代表として、これらのジェネリックな派生トークンはERC-20に従って取引や利用ができます。

ERC-721とERC-1155トークンは、一意の識別子を持ち、同様のルールに従って転送されるため、有効な基本トークンとみなされます。これにより、ERC-721 NFTとERC-1155 Multi-Tokensの両方を単一の共通インターフェイスでラップできます。

## 動機

ERC-20トークン標準は、Ethereumで最も広く使われ、流動性の高いトークン標準です。一方、ERC-721とERC-1155トークンは、個別のIDで全量を転送する必要があります。派生トークンを使うことで、基本資産への露出を得つつ、ERC-20トークンを利用するコントラクトの恩恵を受けられます。これにより、基本トークンを分割、取引、プール化し、AMM、担保、その他のERC-20型コントラクトで使用できるようになります。この提案の実装は既に存在しますが、共通の標準がありませんでした。

基本トークンと派生トークンの固定為替レートがある場合、派生トークンの価値はプールされたトークンの最低価格に比例します。派生トークンがAMMで使用されることで、派生トークン市場と基本NFT市場の間に裁定の機会が生まれます。プールできる基本トークンのサブセットを指定することで、プール内の最低値と最高値の差を最小限に抑えられます。これにより、より大きなセット内の高価値トークンをプール可能にできます。さらに、NFT20が実装しているような、オランダ式オークションなどの価格計算手法を使うことで、基本トークンのサブクラスの価格発見ができます。これにより、より高価値の基本トークンを提供する人が、最低価格のトークンよりも比例的に多くの派生トークンを受け取れるようになります。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、[RFC 2119](https://www.ietf.org/rfc/rfc2119.txt)に記載されているように解釈されるものとします。

**IWrapperに準拠するすべてのコントラクトは、`IWrapper`と`ERC165`インターフェイスを実装しなければなりません**:

```solidity
pragma solidity ^0.8.0;

/**
    @title IWrapper Identifiable Token Wrapper Standard
    @dev {Wrapper} refers to any contract implementing this interface.
    @dev {Base} refers to any ERC-721 or ERC-1155 contract. It MAY be the {Wrapper}.
    @dev {Pool} refers to the contract which holds the {Base} tokens. It MAY be the {Wrapper}.
    @dev {Derivative} refers to the ERC-20 contract which is minted/burned by the {Wrapper}. It MAY be the {Wrapper}.
    @dev All uses of "single", "batch" refer to the number of token ids. This includes individual ERC-721 tokens by id, and multiple ERC-1155 by id. An ERC-1155 `TransferSingle` event may emit with a `value` greater than `1`, but it is still considered a single token.
    @dev All parameters named `_amount`, `_amounts` refer to the `value` parameters in ERC-1155. When using this interface with ERC-721, `_amount` MUST be 1, and `_amounts` MUST be either an empty list or a list of 1 with the same length as `_ids`.
*/
interface IWrapper /* is ERC165 */ {
    /**
     * @dev MUST emit when a mint occurs where a single {Base} token is received by the {Pool}.
     * The `_from` argument MUST be the address of the account that sent the {Base} token.
     * The `_to` argument MUST be the address of the account that received the {Derivative} token(s).
     * The `_id` argument MUST be the id of the {Base} token transferred.
     * The `_amount` argument MUST be the number of {Base} tokens transferred.
     * The `_value` argument MUST be the number of {Derivative} tokens minted.
     */
    event MintSingle (address indexed _from, address indexed _to, uint256 _id, uint256 _amount, uint256 _value);

    /**
     * @dev MUST emit when a mint occurs where multiple {Base} tokens are received by the {Wrapper}.
     * The `_from` argument MUST be the address of the account that sent the {Base} tokens.
     * The `_to` argument MUST be the address of the account that received the {Derivative} token(s).
     * The `_ids` argument MUST be the list ids of the {Base} tokens transferred.
     * The `_amounts` argument MUST be the list of the numbers of {Base} tokens transferred.
     * The `_value` argument MUST be the number of {Derivative} tokens minted.
     */
    event MintBatch (address indexed _from, address indexed _to, uint256[] _ids, uint256[] _amounts, uint256 _value);

    /**
     * @dev MUST emit when a burn occurs where a single {Base} token is sent by the {Wrapper}.
     * The `_from` argument MUST be the address of the account that sent the {Derivative} token(s).
     * The `_to` argument MUST be the address of the account that received the {Base} token.
     * The `_id` argument MUST be the id of the {Base} token transferred.
     * The `_amount` argument MUST be the number of {Base} tokens transferred.
     * The `_value` argument MUST be the number of {Derivative} tokens burned.
     */
    event BurnSingle (address indexed _from, address indexed _to, uint256 _id, uint256 _amount, uint256 _value);

    /**
     * @dev MUST emit when a mint occurs where multiple {Base} tokens are sent by the {Wrapper}.
     * The `_from` argument MUST be the address of the account that sent the {Derivative} token(s).
     * The `_to` argument MUST be the address of the account that received the {Base} tokens.
     * The `_ids` argument MUST be the list of ids of the {Base} tokens transferred.
     * The `_amounts` argument MUST be the list of the numbers of {Base} tokens transferred.
     * The `_value` argument MUST be the number of {Derivative} tokens burned.
     */
    event BurnBatch (address indexed _from, address indexed _to, uint256[] _ids, uint256[] _amounts, uint256 _value);

    /**
     * @notice Transfers the {Base} token with `_id` from `msg.sender` to the {Pool} and mints {Derivative} token(s) to `_to`.
     * @param _to       Target address.
     * @param _id       Id of the {Base} token.
     * @param _amount   Amount of the {Base} token.
     *
     * Emits a {MintSingle} event.
     */
    function mint(
        address _to,
        uint256 _id,
        uint256 _amount
    ) external;

    /**
     * @notice Transfers `_amounts[i]` of the {Base} tokens with `_ids[i]` from `msg.sender` to the {Pool} and mints {Derivative} token(s) to `_to`.
     * @param _to       Target address.
     * @param _ids      Ids of the {Base} tokens.
     * @param _amounts  Amounts of the {Base} tokens.
     *
     * Emits a {MintBatch} event.
     */
    function batchMint(
        address _to,
        uint256[] calldata _ids,
        uint256[] calldata _amounts
    ) external;

    /**
     * @notice Burns {Derivative} token(s) from `_from` and transfers `_amounts` of some {Base} token from the {Pool} to `_to`. No guarantees are made as to what token is withdrawn.
     * @param _from     Source address.
     * @param _to       Target address.
     * @param _amount   Amount of the {Base} tokens.
     *
     * Emits either a {BurnSingle} or {BurnBatch} event.
     */
    function burn(
        address _from,
        address _to,
        uint256 _amount
    ) external;

    /**
     * @notice Burns {Derivative} token(s) from `_from` and transfers `_amounts` of some {Base} tokens from the {Pool} to `_to`. No guarantees are made as to what tokens are withdrawn.
     * @param _from     Source address.
     * @param _to       Target address.
     * @param _amounts  Amounts of the {Base} tokens.
     *
     * Emits either a {BurnSingle} or {BurnBatch} event.
     */
    function batchBurn(
        address _from,
        address _to,
        uint256[] calldata _amounts
    ) external;

    /**
     * @notice Burns {Derivative} token(s) from `_from` and transfers `_amounts[i]` of the {Base} tokens with `_ids[i]` from the {Pool} to `_to`.
     * @param _from     Source address.
     * @param _to       Target address.
     * @param _id       Id of the {Base} token.
     * @param _amount   Amount of the {Base} token.
     *
     * Emits either a {BurnSingle} or {BurnBatch} event.
     */
    function idBurn(
        address _from,
        address _to,
        uint256 _id,
        uint256 _amount
    ) external;

    /**
     * @notice Burns {Derivative} tokens from `_from` and transfers `_amounts[i]` of the {Base} tokens with `_ids[i]` from the {Pool} to `_to`.
     * @param _from     Source address.
     * @param _to       Target address.
     * @param _ids      Ids of the {Base} tokens.
     * @param _amounts   Amounts of the {Base} tokens.
     *
     * Emits either a {BurnSingle} or {BurnBatch} event.
     */
    function batchIdBurn(
        address _from,
        address _to,
        uint256[] calldata _ids,
        uint256[] calldata _amounts
    ) external;
}
```

## 根拠

### 命名

プールされるERC-721/ERC-1155トークンは{Base}トークンと呼ばれます。代替名としては以下のようなものがあります:
- Underlying
- NFT (ただし、ERC-1155トークンは「semi-fungible」と考えられる)

ミントされ/バーンされるERC-20トークンは{Derivative}トークンと呼ばれます。代替名としては以下のようなものがあります:
- Wrapped
- Generic

`mint`と`burn`という関数名は、ERC-20のミントとバーンの拡張から借用しています。代替名としては以下のようなものがあります:
- `mint`/`redeem` ([NFTX](https://nftx.org))
- `deposit`/`withdraw` ([WrappedKitties](https://wrappedkitties.com/))
- `wrap`/`unwrap` ([MoonCatsWrapped](https://etherscan.io/address/0x7c40c393dc0f283f318791d746d894ddd3693572))

`*idBurn`という関数名は、何がバーンされているかを明確にするために選択されました。つまり、{Derivative}トークンがバーンされ、その対価として{Base}トークンが引き出されるのです。

ラッパー/プール自体は、NFTXによると「インデックスファンド」、NFT20によると「DEX」と呼ばれています。ただし、{NFT20Pair}コントラクトでは、NFT-NFTの直接スワップが可能ですが、これはこの標準の範囲外です。

### ミント
ミントには、{Derivative}トークンと引き換えに{Base}トークンをプールに転送する必要があります。このように預け入れられた{Base}トークンは、バーン機能を通じて以外は再転送できないようにする必要があります。これにより、{Derivative}トークンの価値が{Base}トークンの価値を正しく反映するようになります。

{Base}トークンをプールに転送する代わりに、Maker vaultsのように、担保としてロックし、{Derivative}トークンのローンを得るという提案もあります。これも一般的なミントパターンに従っており、{Base}トークンの転送可能性を制限し、{Derivative}トークンと交換するものです。

### バーン
バーンには、{Derivative}トークンをバーンして{Base}トークンをプールから引き出す必要があります。バーン関数は、引き出される{Base}トークンの数と質によって区別されます。
- IDを指定しないバーン: `burn`, `batchBurn`
- IDを指定したバーン: `idBurn`, `batchIdBurn`

IDを指定して引き出せるようにすることで、プール内の高価値{Base}トークンを選択的に引き出せるようになります。NFTXは、このような指定引き出しに手数料を課すことを提案しています。これは、プールの高価値{Base}トークンを引き出す誘因を相殺するためです。


### 価格設定
価格は必ずしも固定である必要はありません。そのため、Mint/Burn イベントには必ず ERC-20 の `_value` (ミントされた/バーンされた量)を含める必要があります。

既存の価格設定の実装は以下の通りです (基本:派生の比率で表示):
- 等価: 1 {Base} トークンが 1 {Derivative} トークンに相当
    - NFTX
    - Wrapped Kitties
- 比例
    - NFT20は 100 {Base} トークンが 1 {Derivative} トークンに相当する固定レートを設定しています。
- 変動
    - NFT20はミント時にオランダ式オークションも可能にしています。
    - NFTXは、{Base}トークンのIDを指定して引き出す際に追加手数料を支払うことを提案しています。

価格設定の実装が多様であるため、Mint\*とBurn\*イベントには必ず{Derivative}トークンのミント/バーン量を含める必要があります。

### 継承
#### ERC-20
{Wrapper}は{ERC20}を継承してもよく、その場合 `super.mint`と`super.burn`を直接呼び出せます。
{Wrapper}が{ERC20}を継承しない場合、{Derivative}コントラクトの`mint`、`burn`、およびその他のサプライ変更は{Wrapper}だけが行えるよう制限する必要があります。

#### ERC721Receiver, ERC1155Receiver
{ERC721Receiver}および/または{ERC1155Receiver}を継承しない場合、プールは{Wrapper}の`mint`、`burn`を通じてのみ{Base}トークンを転送できるよう制限する必要があります。

ERC-721トークンでは、(address, id)ペアごとに1つしか存在しません。一方、ERC-1155トークンでは、(address, id)ペアごとに数量が1以上になる可能性があります。したがって、各標準における「Single」と「Batch」の意味が異なります。両方の標準で、「single」はID1つを、「batch」は複数IDを指します。ERC-1155では、単一IDのイベント/関数でも、`value`フィールドに応じて複数のトークンを扱うことができます。

共通のイベントと関数セットを構築する際は、これらの実装の違いに注意する必要があります。現在の実装では、ERC-721トークンを特殊ケースとして扱っており、{Base}トークンの数量に関して:
- `_amount`で指定するパラメータは必ず`1`
- `_amounts`で指定するパラメータは空のリストか、`_ids`と同じ長さの`1`のリスト

これにより、ERC-1155と同様の一貫した列挙が可能になります。別の実装案としては以下のようなものがあります:
- 専用の関数を持つ共通インターフェイス。例: `mintFromERC721`
- タイプ別の個別インターフェイス。例: `ERC721Wrapper`, `ERC1155Wrapper`

#### ERC721, ERC1155
{Wrapper}は{ERC721}および/または{ERC1155}を継承してもよく、その場合 `super.mint`を直接呼び出せます。これは必須ではありません。{Base}トークンをコントラクト内で作成し、{Derivative}トークンを直接配布するような「Initial NFT Offering」では、この方法が使えます。

{Wrapper}が{ERC721}または{ERC1155}を継承しない場合、{IERC721}および{IERC1155}を呼び出して{Base}トークンを転送する必要があります。

### 承認
すべての基礎となる転送メソッドは{Wrapper}に依存せず、ERC-20/721/1155の転送メソッドを呼び出します。この標準の実装では:
- {Derivative}の転送承認を実装してバーンを、{Base}の転送承認を実装してミントを行う
- または{IERC721} / {IERC1155}を通じて外部の承認を確認してから実行する

のいずれかを行う必要があります。

## 下位互換性
ほとんどの既存の実装はERC-20を継承し、`mint`と`burn`の関数を使っています。
イベント:
- Mint
    - WK: DepositKittyAndMintToken
    - NFTX: Mint

- Burn
    - WK: BurnTokenAndWithdrawKity
    - NFTX: Redeem

## 参考実装
[ERC-3386 Reference Implementation](https://github.com/ashrowz/erc-3386)

## セキュリティ上の考慮事項
ラッパーコントラクトは、バーン可能なERC-20トークンを継承することが推奨されます。そうでない場合、{Derivative}トークンのサプライはラッパーによって制御される必要があります。同様に、価格設定の実装では、{Derivative}トークンが{Base}トークンのサプライを反映するようにする必要があります。

`idBurn`、`idBurns`関数により、ユーザーは最も価値の高いNFTを選択的に引き出せます。トークンの価値に大きな差がある場合、コントラクトは専用のプール(NFTX)や価格設定(NFT20)を検討する必要があります。

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。