---
original: fa972365a084adcec6e3c67c844a2b6098457612efebe03cc7b422cf1503482e
---

---
eip: 7417
title: トークン変換器
description: 1つのERC規格のトークンを別のERC規格のトークンに変換するスマートコントラクトサービス
author: Dexaran (@Dexaran) <dexaran@ethereumclassic.org>
discussions-to: https://ethereum-magicians.org/t/token-standard-converter/15252
status: Draft
type: Standards Track
category: ERC
created: 2023-07-27
requires: 20, 165, 223
---

## 概要

現在、Ethereumチェーン上には複数のトークン規格が存在しています。このEIPは、[ERC-20](./eip-20.md)トークンを[ERC-223](./eip-223.md)トークンに随時アップグレードできるサービスを作成することで、クロス規格の相互運用性の概念を導入します。[ERC-223](./eip-223.md)トークンは、下位互換性の問題を回避し、異なる規格が共存し、相互運用可能かつ交換可能になるよう、[ERC-20](./eip-20.md)バージョンに変換することができます。

変換を行うには、ユーザーは1つの規格のトークンをコンバーター契約に送信し、自動的に別の規格のトークンを受け取ります。

## 動機

ERC-20契約がアップグレードされると、新しいアドレスを見つけるリスクが生じます。この提案は、トークン変換を行うサービスを作成し、潜在的に安全でない実装が広がるのを防ぐものです。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

トークン変換システムは2つの主要な部分で構成されます:

- コンバーター契約
- 各[ERC-20](./eip-20.md)トークンの[ERC-223](./eip-223.md)ラッパー契約

コンバーター契約は、[ERC-223](./eip-223.md)ラッパーがまだ存在しない[ERC-20](./eip-20.md)トークンに対して、新しい[ERC-223](./eip-223.md)ラッパー契約をデプロイできます。[ERC-20](./eip-20.md)トークンごとに[ERC-223](./eip-223.md)ラッパーが1つ存在する必要があります。

コンバーター契約は、[ERC-20](./eip-20.md)トークンの預金を受け付け、預金者に1:1の比率で[ERC-223](./eip-223.md)トークンを送信しなければなりません(MUST)。`ERC-20 token_A`の1234単位を預金すると、預金者は正確に1234単位の`ERC-223 token_A`を受け取ることになります。これは、[ERC-20](./eip-20.md)の預金時に新しい[ERC-223](./eip-223.md)トークンを発行することで行われます。元の[ERC-20](./eip-20.md)トークンは、コンバーター契約に凍結され、後で請求できるようになります。

コンバーター契約は、[ERC-223](./eip-223.md)トークンの預金を受け付け、預金者に1:1の比率で[ERC-20](./eip-20.md)トークンを送信しなければなりません(MUST)。これは、[ERC-223](./eip-223.md)の預金時に元の[ERC-20](./eip-20.md)トークンを解放することで行われます。預金された[ERC-223](./eip-223.md)トークンは焼却されます。

### トークン変換器

#### コンバーター契約のメソッド

##### `getWrapperFor`

```solidity
function getWrapperFor(address _erc20Token) public view returns (address)
```

指定された[ERC-20](./eip-20.md)オリジナルトークンの[ERC-223](./eip-223.md)ラッパーのアドレスを返します。[ERC-20](./eip-20.md)トークンアドレスに対応する[ERC-223](./eip-223.md)バージョンがない場合は`0x0`を返します。トークン変換器契約によって作成された[ERC-223](./eip-223.md)ラッパーは、任意の[ERC-20](./eip-20.md)トークンアドレスに対して1つだけ存在する必要があります。

##### `getOriginFor`

```solidity
function getOriginFor(address _erc223Token) public view returns (address)
```

指定された[ERC-223](./eip-223.md)ラッパーの元の[ERC-20](./eip-20.md)トークンのアドレスを返します。提供された`_erc223Token`が、トークン変換器契約によって作成された[ERC-223](./eip-223.md)ラッパーのアドレスでない場合は`0x0`を返します。

##### `createERC223Wrapper`

```solidity
function createERC223Wrapper(address _erc20Token) public returns (address)
```

まだ存在していない場合、指定された`_erc20Token`の新しい[ERC-223](./eip-223.md)ラッパーを作成します。ラッパーがすでに存在する場合はトランザクションを取り消します。成功した場合は、新しいラッパートークン契約のアドレスを返します。

##### `convertERC20toERC223`

```solidity
function convertERC20toERC223(address _erc20token, uint256 _amount) public returns (bool)
```

トランザクション送信者の残高から`_amount`の[ERC-20](./eip-20.md)トークンを`transferFrom`関数で引き出します。送信者にその`_amount`の[ERC-223](./eip-223.md)ラッパートークンを送信します。元のトークンをトークン変換器契約の残高に保存し、将来の請求に備えます。成功した場合は`true`を返します。トークン変換器は、`convertERC20toERC223`関数で預金された[ERC-20](./eip-20.md)トークンの量を記録しておく必要があります。なぜなら、`transfer`関数を使って直接送信することで、任意の契約に[ERC-20](./eip-20.md)トークンを預金できるためです。

`_ERC20token`に対応する[ERC-223](./eip-223.md)ラッパーがない場合は、`createERC223Wrapper(_erc20toke)`関数を呼び出して作成します。

提供された`_erc20token`アドレスが[ERC-223](./eip-223.md)ラッパーのアドレスの場合は、トランザクションを取り消します。

##### `tokenReceived`

```solidity
function tokenReceived(address _from, uint _value, bytes memory _data) public override returns (bytes4)
```

これは標準的な[ERC-223](./eip-223.md)トランザクションハンドラー関数で、`_from`が`address(this)`アドレスに`_value`の[ERC-223](./eip-223.md)トークンを送信したときに呼び出されます。この関数の範囲内では、`msg.sender`が[ERC-223](./eip-223.md)トークン契約のアドレスで、`_from`がトランザクションの開始者です。

`msg.sender`がトークン変換器によって作成された[ERC-223](./eip-223.md)ラッパーのアドレスの場合、`_value`の[ERC-20](./eip-20.md)オリジナルトークンを`_from`アドレスに送信しなければなりません(MUST)。

`msg.sender`がトークン変換器に知られている[ERC-223](./eip-223.md)ラッパーのアドレスではない場合は、トランザクションを取り消して[ERC-223](./eip-223.md)トークンを送信者に返します。

これは、[ERC-223](./eip-223.md)ラッパートークンを元の[ERC-20](./eip-20.md)トークンに変換するために使用しなければならない(MUST)関数です。この関数は、[ERC-223](./eip-223.md)トークンがトークン変換器のアドレスに送信されたときに自動的に実行されます。任意の[ERC-223](./eip-223.md)トークンがトークン変換器に送信された場合、それは拒否されます。

`0x8943ec02`を返します。

##### `rescueERC20`

```solidity
function rescueERC20(address _token) external
```

このメソッドは、ユーザーが間違ってトークンを送信した場合に、資産を永久に凍結されるのを防ぐために、直接`transfer`関数でコントラクトに預金された[ERC-20](./eip-20.md)トークンを抽出することができます。トークン変換器は、`convertERC20toERC223`関数で正当に預金された量を計算しているため、「誤って預金されたトークン」の量を、[ERC-20](./eip-20.md)トークン契約の`balanceOf(address(this))`関数の返り値から記録された量を引くことで常に計算できます。

### [ERC-20](./eip-20.md)トークンから[ERC-223](./eip-223.md)への変換

[ERC-20](./eip-20.md)トークンを[ERC-223](./eip-223.md)に変換するには、トークン保有者は以下の手順を実行する必要があります:

1. [ERC-20](./eip-20.md)トークンの`approve`関数を呼び出し、トークン変換器がトークン保有者のアドレスからトークンを`transferFrom`できるよう許可する。
2. `approve`のトランザクションが承認されるまで待つ。
3. トークン変換器契約の`convertERC20toERC223`関数を呼び出す。

### [ERC-223](./eip-223.md)トークンを[ERC-20](./eip-20.md)に戻す

[ERC-223](./eip-223.md)トークンを[ERC-20](./eip-20.md)に変換するには、トークン保有者は以下の手順を実行する必要があります:

1. [ERC-223](./eip-223.md)トークン契約の`transfer`関数を使って、[ERC-223](./eip-223.md)トークンをトークン変換器契約のアドレスに送信する。

## 根拠

<!--
  The rationale fleshes out the specification by describing what motivated the design and why particular design decisions were made. It should describe alternate designs that were considered and related work, e.g. how the feature is supported in other languages.

  The current placeholder is acceptable for a draft.

  TODO: Remove this comment before submitting
-->

TBD

## 下位互換性

この提案は、異なるトークン規格を相互運用可能かつ交換可能にすることで、下位互換性の問題を解消することを目的としています。

このサービスは初の試みであるため、前例がないため、下位互換性の問題はありません。

## 参考実装

```solidity
    address public ownerMultisig;

    mapping (address => ERC223WrapperToken) public erc223Wrappers; // A list of token wrappers. First one is ERC-20 origin, second one is ERC-223 version.
    mapping (address => ERC20WrapperToken)  public erc20Wrappers;

    mapping (address => address)            public erc223Origins;
    mapping (address => address)            public erc20Origins;
    mapping (address => uint256)            public erc20Supply; // Token => how much was deposited.

    function getERC20WrapperFor(address _token) public view returns (address, string memory)
    {
        if ( address(erc20Wrappers[_token]) != address(0) )
        {
            return (address(erc20Wrappers[_token]), "ERC-20");
        }

        return (address(0), "Error");
    }

    function getERC223WrapperFor(address _token) public view returns (address, string memory)
    {
        if ( address(erc223Wrappers[_token]) != address(0) )
        {
            return (address(erc223Wrappers[_token]), "ERC-223");
        }

        return (address(0), "Error");
    }

    function getERC20OriginFor(address _token) public view returns (address)
    {
        return (address(erc20Origins[_token]));
    }

    function getERC223OriginFor(address _token) public view returns (address)
    {
        return (address(erc223Origins[_token]));
    }

    function tokenReceived(address _from, uint _value, bytes memory _data) public override returns (bytes4)
    {
        require(erc223Origins[msg.sender] == address(0), "Error: creating wrapper for a wrapper token.");
        // There are two possible cases:
        // 1. A user deposited ERC-223 origin token to convert it to ERC-20 wrapper
        // 2. A user deposited ERC-223 wrapper token to unwrap it to ERC-20 origin.

        if(erc20Origins[msg.sender] != address(0))
        {
            // Origin for deposited token exists.
            // Unwrap ERC-223 wrapper.

            safeTransfer(erc20Origins[msg.sender], _from, _value);

            erc20Supply[erc20Origins[msg.sender]] -= _value;
            //erc223Wrappers[msg.sender].burn(_value);
            ERC223WrapperToken(msg.sender
).burn(_value);
            
            return this.tokenReceived.selector;
        }
        // Otherwise origin for the sender token doesn't exist
        // There are two possible cases:
        // 1. ERC-20 wrapper for the deposited token exists
        // 2. ERC-20 wrapper for the deposited token doesn't exist and must be created.
        else if(address(erc20Wrappers[msg.sender]) == address(0))
        {
            // Create ERC-20 wrapper if it doesn't exist.
            createERC20Wrapper(msg.sender);
        }
        
        // Mint ERC-20 wrapper tokens for the deposited ERC-223 token
        // if the ERC-20 wrapper didn't exist then it was just created in the above statement.
        erc20Wrappers[msg.sender].mint(_from, _value);
        return this.tokenReceived.selector;
    }

    function createERC223Wrapper(address _token) public returns (address)
    {
        require(address(erc223Wrappers[_token]) == address(0), "ERROR: Wrapper exists");
        require(getERC20OriginFor(_token) == address(0), "ERROR: 20 wrapper creation");
        require(getERC223OriginFor(_token) == address(0), "ERROR: 223 wrapper creation");

        ERC223WrapperToken _newERC223Wrapper     = new ERC223WrapperToken(_token);
        erc223Wrappers[_token]                   = _newERC223Wrapper;
        erc20Origins[address(_newERC223Wrapper)] = _token;

        return address(_newERC223Wrapper);
    }

    function createERC20Wrapper(address _token) public returns (address)
    {
        require(address(erc20Wrappers[_token]) == address(0), "ERROR: Wrapper already exists.");
        require(getERC20OriginFor(_token) == address(0), "ERROR: 20 wrapper creation");
        require(getERC223OriginFor(_token) == address(0), "ERROR: 223 wrapper creation");

        ERC20WrapperToken _newERC20Wrapper       = new ERC20WrapperToken(_token);
        erc20Wrappers[_token]                    = _newERC20Wrapper;
        erc223Origins[address(_newERC20Wrapper)] = _token;

        return address(_newERC20Wrapper);
    }

    function depositERC20(address _token, uint256 _amount) public returns (bool)
    {
        if(erc223Origins[_token] != address(0))
        {
            return unwrapERC20toERC223(_token, _amount);
        }
        else return wrapERC20toERC223(_token, _amount);
    }

    function wrapERC20toERC223(address _ERC20token, uint256 _amount) public returns (bool)
    {
        // If there is no active wrapper for a token that user wants to wrap
        // then create it.
        if(address(erc223Wrappers[_ERC20token]) == address(0))
        {
            createERC223Wrapper(_ERC20token);
        }
        uint256 _converterBalance = IERC20(_ERC20token).balanceOf(address(this)); // Safety variable.
        safeTransferFrom(_ERC20token, msg.sender, address(this), _amount);
        
        erc20Supply[_ERC20token] += _amount;

        require(
            IERC20(_ERC20token).balanceOf(address(this)) - _amount == _converterBalance,
            "ERROR: The transfer have not subtracted tokens from callers balance.");

        erc223Wrappers[_ERC20token].mint(msg.sender, _amount);
        return true;
    }

    function unwrapERC20toERC223(address _ERC20token, uint256 _amount) public returns (bool)
    {
        require(IERC20(_ERC20token).balanceOf(msg.sender) >= _amount, "Error: Insufficient balance.");
        require(erc223Origins[_ERC20token] != address(0), "Error: provided token is not a ERC-20 wrapper.");

        ERC20WrapperToken(_ERC20token).burn(msg.sender, _amount);
        IERC223(erc223Origins[_ERC20token]).transfer(msg.sender, _amount);

        return true;
    }

    function isWrapper(address _token) public view returns (bool)
    {
        return erc20Origins[_token] != address(0) || erc223Origins[_token] != address(0);
    } 

/*
    function convertERC223toERC20(address _from, uint256 _amount) public returns (bool)
    {
        // If there is no active wrapper for a token that user wants to wrap
        // then create it.
        if(address(erc20Wrappers[msg.sender]) == address(0))
        {
            createERC223Wrapper(msg.sender);
        }
        
        erc20Wrappers[msg.sender].mint(_from, _amount);
        return true;
    }
*/

    function rescueERC20(address _token) external {
        require(msg.sender == ownerMultisig, "ERROR: Only owner can do this.");
        uint256 _stuckTokens = IERC20(_token).balanceOf(address(this)) - erc20Supply[_token];
        safeTransfer(_token, msg.sender, IERC20(_token).balanceOf(address(this)));
    }

    function transferOwnership(address _newOwner) public
    {
        require(msg.sender == ownerMultisig, "ERROR: Only owner can call this function.");
        ownerMultisig = _newOwner;
    }
    
    // ************************************************************
    // Functions that address problems with tokens that pretend to be ERC-20
    // but in fact are not compatible with the ERC-20 standard transferring methods.
    // EIP20 https://eips.ethereum.org/EIPS/eip-20
    // ************************************************************
    function safeTransfer(address token, address to, uint value) internal {
        // bytes4(keccak256(bytes('transfer(address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0xa9059cbb, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FAILED');
    }

    function safeTransferFrom(address token, address from, address to, uint value) internal {
        // bytes4(keccak256(bytes('transferFrom(address,address,uint256)')));
        (bool success, bytes memory data) = token.call(abi.encodeWithSelector(0x23b872dd, from, to, value));
        require(success && (data.length == 0 || abi.decode(data, (bool))), 'TransferHelper: TRANSFER_FROM_FAILED');
    }