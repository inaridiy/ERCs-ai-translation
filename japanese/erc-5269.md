---
original: cdaaec9ca3f8dfe8926ce8699ebfb0aa88984fdedb81989e16f680e2b656cfec
---

---
eip: 5269
title: ERC検出とディスカバリー
description: ERCで指定された主要な動作や任意の動作がある呼び出し元でサポートされているかを識別するためのインターフェース。
author: Zainan Victor Zhou (@xinbenlv)
discussions-to: https://ethereum-magicians.org/t/erc5269-human-readable-interface-detection/9957
status: Review
type: Standards Track
category: ERC
created: 2022-07-15
requires: 5750
---

## 概要

ERCの番号による識別と検出のためのインターフェース。
通常「ERCの番号」と呼ばれる`majorERCIdentifier`フィールドを指定します。例えば、`ERC-721`は`majorERCIdentifier = 721`です。このERCの`majorERCIdentifier = 5269`です。

`ERCNumber`ではなく`majorERCIdentifier`と呼ぶことで、将来的な拡張性を持たせています。将来的にERCが番号付けされなくなる可能性や、他の種類の標準を取り入れたい場合を想定しています。

また、個々のERCの作者が定義できる`minorERCIdentifier`という新しい概念も提案しています。例えば、ERC-721の作者は`ERC721Metadata`インターフェースを`minorERCIdentifier= keccak256("ERC721Metadata")`と定義できます。

さらに、スマートコントラクトが任意でサポートするERCを宣言するためのイベントも提案しています。

## 動機

このERCは[ERC-165](./eip-165.md)に対抗する標準として作成されました。

[ERC-165](./eip-165.md)との主な違いは以下の通りです。

1. [ERC-165](./eip-165.md)はメソッドのシグネチャのハッシュを使用するため、最低1つのメソッドが*存在*する必要があります。一方、データ形式や署名スキームに関連するERCなど、メソッドを持たないERCもあります。また、トランスファーを拒否するだけのSoul-Bound-Tokenなども存在します。
1. [ERC-165](./eip-165.md)は呼び出し元に基づいたクエリ機能を提供していません。このERCに準拠したコントラクトは、特定の呼び出し元に基づいてERCのサポートを応答します。

ERC-165が既に存在する中で、このERCを作成した理由は以下の通りです:

1. ERCの番号を使うことで、人間が読みやすくなり、ENSなどの名前付きコントラクトとの連携が容易になります。

2. ERC-165の識別子ではなく、ERCの番号を使ってERCを識別または指定する傾向が高まっています。例えば:

- [ERC-5267](./eip-5267.md)は`extensions`をERCの番号のリストとして指定しています。
- [ERC-600](./eip-600.md)と[ERC-601](./eip-601.md)は`m / purpose' / subpurpose' / ERC' / wallet'`パスでERCの番号を指定しています。
- [ERC-5568](./eip-5568.md)は`instruction_id of an instruction defined by an ERC MUST be its ERC number unless there are exceptional circumstances (be reasonable)`と指定しています。
- [ERC-6120](./eip-6120.md)は`struct Token { uint eip; ..., }`でERCの番号を識別子として指定しています。
- `ERC-867`(Stagnant)は`erpId: A string identifier for this ERP (likely the associated ERC number, e.g. "ERC-1234")`を提案しています。

3. ERCの検出インターフェースがあれば、ERC-165の識別子をルックアップテーブルに持つ必要がなくなり、ERCの動作拡張を指定する際に大幅に簡素化できます。

4. スマートコントラクトが呼び出し元のアカウントによって異なる動作をする可能性があることも認識しています。最も顕著な使用例は、Transparent Upgradable Patternを使用する際に、プロキシコントラクトがAdminアカウントとNon-Adminアカウントに異なる扱いをすることです。

## 仕様

以下の説明では、ERCとERC を互換的に使用しています。これは、ほとんどの場合、説明がERCの標準トラックのカテゴリに適用されますが、ERC番号空間はERC番号空間のサブスペースであり、ERCとして認識されていないが動作が問い合わせに値するものも存在する可能性があるためです。

1. 準拠するすべてのスマートコントラクトは、以下のインターフェースを実装しなければなりません。

```solidity
// DRAFTv1
pragma solidity ^0.8.9;

interface IERC5269 {
  event OnSupportERC(
      address indexed caller, // `address(0x0)`が発行された場合、すべての呼び出し元を意味する。
      uint256 indexed majorERCIdentifier,
      bytes32 indexed minorERCIdentifier, // 0は対象のERC全体を意味する
      bytes32 ercStatus,
      bytes extraData
  );

  /// @dev ERC検出の中心的なメソッド
  /// @param caller, 問い合わせの対象となる呼び出し元のアドレス。
  /// @param majorERCIdentifier, 問い合わせの対象となるERCの番号を表す`uint256`値。将来のERCによる上書きがない限り、この値は(0, 2^32-1]の範囲内であるべきです。`supportERC`の関数呼び出しの際、この範囲外の値は未定義とみなされ、実装の選択肢や将来のERCの仕様に委ねられます。
  /// @param minorERCIdentifier, 個々のERCの作者が指定できる`bytes32`値。例えば、[ERC-721](/ERCS/eip-721)の作者は`keccak256("ERC721Metadata")`や`keccak256("ERC721Metadata.tokenURI")`を`minorERCIdentifier`として指定できます。作者はこのminorERCIdentifierを使って、ERC-712のV1-V4のように、異なる動作のバージョンを指定することもできます。
  /// @param extraData, [ERC-5750](/ERCS/eip-5750)の将来の拡張のための`bytes`。
  /// @return ercStatus, ERCのサポート状況を示す`bytes32`。
  ///                    - 最終版のERCの場合、`keccak256("FINAL")`を返さなければなりません。
  ///                    - 最終版ではないERCの場合、`keccak256("DRAFT")`を返すべきです。
  ///                      ERC手順中は、ERCの作者が`keccak256("DRAFTv1")`や`keccak256("DRAFT-option1")`のように、
  ///                      `FINAL`または`DRAFT`以外のercStatusを指定することができ、その値はERCの本文に文書化されなければなりません。
  function supportERC(
    address caller,
    uint256 majorERCIdentifier,
    bytes32 minorERCIdentifier,
    bytes calldata extraData)
  external view returns (bytes32 ercStatus);
}
```

以下の説明では、`ERC_5269_STATUS`を`keccak256("DRAFTv1")`に設定しています。

コメントで指定された動作に加えて:

1. `minorERCIdentifier=0`は、問い合わせの対象となるERCの主要な動作を指すものとして予約されています。
2. 準拠するERCの作者は、オプションのインターフェース、動作、値範囲の一覧を`minorERCIdentifier`として宣言することが推奨されます。
3. このERCが最終版になった場合、準拠するコントラクトは`supportERC((any caller), 5269, 0, [])`の呼び出しに対して`ERC_5269_STATUS`を返さなければなりません。

*注意*: 現時点のスナップショットでは、`supportERC((any caller), 5269, 0, [])`は必ず`ERC_5269_STATUS`を返さなければなりません。

4. 準拠するコントラクトは、構築時またはアップグレード時に`OnSupportERC(address(0), 5269, 0, ERC_5269_STATUS, [])`イベントを発行すべきです。
5. 準拠するコントラクトは、メインの動作や部分的な動作のサポートを宣言するために、関連する値を持つ`OnSupportERC`イベントを発行することができます。また、特定の呼び出し元または全ての呼び出し元に対してERCのサポートを変更した場合にも、このイベントを発行することができます。
6. `ERC-XXX`が最終版ではない場合、`supportERC((any caller), xxx, (any minor identifier), [])`を問い合わせると、`keccak256("FINAL")`を返してはいけません。この場合は`0`を返すことが推奨されますが、他の`ercStatus`の値も許可されます。呼び出し側は、`keccak256("FINAL")`以外の値を受け取った場合は、それを最終版ではないものとして扱わなければなりません。また、0を受け取った場合は、明確にサポートされていないものとして扱わなければなりません。
7. `supportERC`関数は`view`属性を持たなければなりません。つまり、EVM上のグローバルな状態を変更してはいけません。

## 根拠

1. `uint256 majorERCIdentifier`のデータ型には、以下のような代替案がありました:

- (1) ERCの番号のハッシュ化
- (2) 生の数値の使用
- (3) ERC-165の識別子の使用

(1)のメリットは、将来のERCの番号付けや命名規則の変更にも対応できることです。
しかし、デメリットは`hash(ERC-number)`を見ただけでは、そのERCの番号がわからないことです。

動機で述べた理由から、(2)の選択肢を採用しました。

2. 設計では`bytes32 minorERCIdentifier`を採用しました。代替案としては(1)数値を使う方法もありましたが、すべてのERC作者にサブ動作の番号付けを定義させるのは難しいと判断しました。そのため、`bytes32`を採用し、ERC作者にはハッシュ化された文字列名を使うことを求めました。これは、彼らの仕様の中でインターフェース名やメソッド名を考案する際に既に行っていることです。

3. 代替案として、返り値に追加データを含めたり、サポートされているすべてのERCの配列を返すこともできますが、この複雑性がどの程度の価値をもたらすかや、その余分なオーバーヘッドが正当化されるかは不明です。

4. [ERC-165](./eip-165.md)と比較して、`address caller`という追加の入力を設けました。これは、[ERC-1967](./eip-1967.md)によって可能になったプロキシパターンの利用が増えていることを考慮したためです。なぜ単に`msg.sender`を使わないのかと尋ねられるかもしれません。これは、トランザクションを行わずにプロキシコントラクトからERCの番号`number`のインターフェースが特定の送信者に対して利用可能かどうかを問い合わせたいためです。

1. `majorERCIdentifier`が`2^32`以上の場合に備えて、ERC/ERCではない他の標準のコレクションをサポートする可能性を残しています。

## テストケース

```typescript

describe("ERC5269", function () {
  async function deployFixture() {
    // ...
  }

  describe("Deployment", function () {
    // ...
    it("Should emit proper OnSupportERC events", async function () {
      let { txDeployErc721 } = await loadFixture(deployFixture);
      let events = txDeployErc721.events?.filter(event => event.event === 'OnSupportERC');
      expect(events).to.have.lengthOf(4);

      let ev5269 = events!.filter(
        (event) => event.args!.majorERCIdentifier.eq(5269));
      expect(ev5269).to.have.lengthOf(1);
      expect(ev5269[0].args!.caller).to.equal(BigNumber.from(0));
      expect(ev5269[0].args!.minorERCIdentifier).to.equal(BigNumber.from(0));
      expect(ev5269[0].args!.ercStatus).to.equal(ethers.utils.id("DRAFTv1"));

      let ev721 = events!.filter(
        (event) => event.args!.majorERCIdentifier.eq(721));
      expect(ev721).to.have.lengthOf(3);
      expect(ev721[0].args!.caller).to.equal(BigNumber.from(0));
      expect(ev721[0].args!.minorERCIdentifier).to.equal(BigNumber.from(0));
      expect(ev721[0].args!.ercStatus).to.equal(ethers.utils.id("FINAL"));

      expect(ev721[1].args!.caller).to.equal(BigNumber.from(0));
      expect(ev721[1].args!.minorERCIdentifier).to.equal(ethers.utils.id("ERC721Metadata"));
      expect(ev721[1].args!.ercStatus).to.equal(ethers.utils.id("FINAL"));

      // ...
    });

    it
it("Should return proper ercStatus value when called supportERC() for declared supported ERC/features", async function () {
      let { erc721ForTesting, owner } = await loadFixture(deployFixture);
      expect(await erc721ForTesting.supportERC(owner.address, 5269, ethers.utils.hexZeroPad("0x00", 32), [])).to.equal(ethers.utils.id("DRAFTv1"));
      expect(await erc721ForTesting.supportERC(owner.address, 721, ethers.utils.hexZeroPad("0x00", 32), [])).to.equal(ethers.utils.id("FINAL"));
      expect(await erc721ForTesting.supportERC(owner.address, 721, ethers.utils.id("ERC721Metadata"), [])).to.equal(ethers.utils.id("FINAL"));
      // ...

      expect(await erc721ForTesting.supportERC(owner.address, 721, ethers.utils.id("WRONG FEATURE"), [])).to.equal(BigNumber.from(0));
      expect(await erc721ForTesting.supportERC(owner.address, 9999, ethers.utils.hexZeroPad("0x00", 32), [])).to.equal(BigNumber.from(0));
    });

    it("Should return zero as ercStatus value when called supportERC() for non declared ERC/features", async function () {
      let { erc721ForTesting, owner } = await loadFixture(deployFixture);
      expect(await erc721ForTesting.supportERC(owner.address, 721, ethers.utils.id("WRONG FEATURE"), [])).to.equal(BigNumber.from(0));
      expect(await erc721ForTesting.supportERC(owner.address, 9999, ethers.utils.hexZeroPad("0x00", 32), [])).to.equal(BigNumber.from(0));
    });
  });
});
```

See [`TestERC5269.ts`](../assets/eip-5269/test/TestERC5269.ts).

## 参考実装

このERCの参考実装は以下の通りです:

```solidity
contract ERC5269 is IERC5269 {
    bytes32 constant public ERC_STATUS = keccak256("DRAFTv1");
    constructor () {
        emit OnSupportERC(address(0x0), 5269, bytes32(0), ERC_STATUS, "");
    }

    function _supportERC(
        address /*caller*/,
        uint256 majorERCIdentifier,
        bytes32 minorERCIdentifier,
        bytes calldata /*extraData*/)
    internal virtual view returns (bytes32 ercStatus) {
        if (majorERCIdentifier == 5269) {
            if (minorERCIdentifier == bytes32(0)) {
                return ERC_STATUS;
            }
        }
        return bytes32(0);
    }

    function supportERC(
        address caller,
        uint256 majorERCIdentifier,
        bytes32 minorERCIdentifier,
        bytes calldata extraData)
    external virtual view returns (bytes32 ercStatus) {
        return _supportERC(caller, majorERCIdentifier, minorERCIdentifier, extraData);
    }
}
```

See [`ERC5269.sol`](../assets/eip-5269/contracts/ERC5269.sol).

[ERC-721](./eip-721.md)のコントラクトがこのERCも実装して、検出と発見を容易にする例は以下の通りです:

```solidity
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "../ERC5269.sol";
contract ERC721ForTesting is ERC721, ERC5269 {

    bytes32 constant public ERC_FINAL = keccak256("FINAL");
    constructor() ERC721("ERC721ForTesting", "E721FT") ERC5269() {
        _mint(msg.sender, 0);
        emit OnSupportERC(address(0x0), 721, bytes32(0), ERC_FINAL, "");
        emit OnSupportERC(address(0x0), 721, keccak256("ERC721Metadata"), ERC_FINAL, "");
        emit OnSupportERC(address(0x0), 721, keccak256("ERC721Enumerable"), ERC_FINAL, "");
    }

  function supportERC(
    address caller,
    uint256 majorERCIdentifier,
    bytes32 minorERCIdentifier,
    bytes calldata extraData)
  external
  override
  view
  returns (bytes32 ercStatus) {
    if (majorERCIdentifier == 721) {
      if (minorERCIdentifier == 0) {
        return keccak256("FINAL");
      } else if (minorERCIdentifier == keccak256("ERC721Metadata")) {
        return keccak256("FINAL");
      } else if (minorERCIdentifier == keccak256("ERC721Enumerable")) {
        return keccak256("FINAL");
      }
    }
    return super._supportERC(caller, majorERCIdentifier, minorERCIdentifier, extraData);
  }
}

```

See [`ERC721ForTesting.sol`](../assets/eip-5269/contracts/testing/ERC721ForTesting.sol).

## セキュリティ上の考慮事項

[ERC-165](./eip-165.md)と同様に、インターフェースの呼び出し側は、そのようなERCインターフェースをサポートすると宣言しているスマートコントラクトが、必ずしもそれらを正しくサポートしているとは限らないことを前提としなければなりません。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。