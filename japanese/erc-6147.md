---
original: b48792186b0d3eba4a395a81cf61da27d5303c98c72ee172dc6bd29ad4e27361
---

---
eip: 6147
title: NFT/SBTの管理者、ERC-721の拡張
description: NFTの保有権と譲渡権を分離する新しい管理者ロールが定義され、期限付きで実現されます。
author: 5660-eth (@5660-eth)、Wizard Wang
discussions-to: https://ethereum-magicians.org/t/guard-of-nft-sbt-an-extension-of-eip-721/12052
status: 最終
type: 標準トラック
category: ERC
created: 2022-12-07
requires: 165, 721
---

## 概要

このスタンダードは[ERC-721](./eip-721.md)の拡張です。非代替性トークン(NFT)とSoulbound Tokens(SBT)の保有権と譲渡権を分離し、`guard`と`expires`という新しい役割を定義しています。`guard`設定の柔軟性により、NFT盗難防止、NFTレンディング、NFTリース、SBTなどの設計が可能になります。

## 動機

NFTは使用価値と金融価値の両方を持つ資産です。

現在、NFT盗難の事例が多数存在し、NFT冷蔵庫への移転などの現在のNFT盗難防止策では、NFTの使用が不便になります。

現在のNFTレンディングでは、NFT所有者がNFTをレンディング契約に移転する必要があり、借入を得ている間はNFTを使用する権利がなくなります。現実世界では、例えば個人が自宅に抵当権を設定する場合でも、その家を使用する権利は維持されます。

SBTについては、一般的な見解は譲渡不可能であり、SBTはイーサアドレスに固定されます。しかし、ユーザーアドレスの秘密鍵が漏洩または紛失した場合、SBTの回復が複雑な作業になり、対応する標準がありません。SBTは本質的にNFTの保有権と譲渡権を分離しています。ウォレットが盗難に遭ったり利用できなくなった場合でも、SBTを回復できるようにする必要があります。

さらに、SBTの管理も必要です。例えば、大学がその卒業生にディプロマベースのSBTを発行し、後に大学がある卒業生の学業不正行為や大学の評判を損なう行為を発見した場合、ディプロマベースのSBTを回収する能力を持つべきです。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」のキーワードは、RFC 2119に記載されているように解釈されるものとします。

ERC-721準拠のコントラクトはこのEIPを実装してもよい。

guardは有効期限(expires)の前にのみ有効である必要があります。

トークンにguardがない、またはguardが期限切れの場合、`guardInfo`は`(address(0), 0)`を返さなければなりません。

トークンにguardがない、またはguardが期限切れの場合、トークンの所有者、承認された操作者、承認されたアドレスはguardとexpiresを設定する権限を持たなければなりません。

トークンに有効なguardがある場合、所有者、承認された操作者、承認されたアドレスはguardとexpiresを変更できず、トークンを転送することもできません。

トークンに有効なguardがある場合、`guardInfo`はguardのアドレスと有効期限を返さなければなりません。

トークンに有効なguardがある場合、guardはguardとexpiresを削除、変更、トークンを転送することができます。

トークンに有効なguardがある場合、トークンが焼却されるとguardも削除されなければなりません。

SBTを発行または鋳造する場合、管理を容易にするために、guardを指定のアドレスに一様に設定することができます。

### コントラクトインターフェース

```solidity
 interface IERC6147 {

    /// NFTのguardが変更されたときまたはexpiresが変更されたときにログ出力される
    /// @notice `guard`が変更されたときまたは`expires`が変更されたときにログ出力される
    ///         `newGuard`がゼロアドレスの場合、現在guardアドレスがないことを示す
    event UpdateGuardLog(uint256 indexed tokenId, address indexed newGuard, address oldGuard, uint64 expires);
    
    /// @notice NFTの所有者、承認された操作者、承認されたアドレスはNFTのguardとexpiresを設定でき、
    ///         有効なguardはNFTのguardとexpiresを変更できる
    ///         NFTに有効なguardロールがある場合、NFTの所有者、承認された操作者、承認されたアドレスは
    ///         guardとexpiresを変更できない
    /// @dev `newGuard`はゼロアドレスにはできない
    ///      `expires`は有効である必要がある
    ///      `tokenId`が有効なNFTでない場合は例外をスローする
    /// @param tokenId NFTのguardアドレスを取得するためのNFT
    /// @param newGuard NFTの新しいguardアドレス
    /// @param expires NFTを管理できるUNIXタイムスタンプ
    function changeGuard(uint256 tokenId, address newGuard, uint64 expires) external;

    /// @notice NFTのguardとexpiresを削除する
    ///         guardのみが自身のguardロールとexpiresを削除できる
    /// @dev guardアドレスはゼロアドレスに設定される
    ///      expiresは0に設定される
    ///      `tokenId`が有効なNFTでない場合は例外をスローする
    /// @param tokenId NFTのguardとexpiresを削除するためのNFT
    function removeGuard(uint256 tokenId) external;
    
    /// @notice NFTを転送し、guardとexpiresを削除する
    /// @dev NFTは`to`に転送され、guardアドレスはゼロアドレスに設定される
    ///      `tokenId`が有効なNFTでない場合は例外をスローする
    /// @param from NFTの前の所有者のアドレス
    /// @param to NFTの受取人のアドレス
    /// @param tokenId 転送するNFT
    function transferAndRemove(address from, address to, uint256 tokenId) external;

    /// @notice NFTのguardアドレスとexpiresを取得する
    /// @dev ゼロアドレスはguardがいないことを示す
    /// @param tokenId NFTのguardアドレスとexpiresを取得するためのNFT
    /// @return NFTのguardアドレスとexpires
   function guardInfo(uint256 tokenId) external view returns (address, uint64);   
}
  ```

`changeGuard(uint256 tokenId, address newGuard, uint64 expires)`関数は`public`または`external`として実装できます。

`removeGuard(uint256 tokenId)`関数は`public`または`external`として実装できます。

`transferAndRemove(address from,address to,uint256 tokenId)`関数は`public`または`external`として実装できます。

`guardInfo(uint256 tokenId)`関数は`pure`または`view`として実装できます。

`UpdateGuardLog`イベントはguardが変更されたときに必ず発行されなければなりません。

`supportsInterface`メソッドは`0xb61d1057`が呼び出されたときに`true`を返さなければなりません。

## 根拠

### 汎用性

NFT/SBTには多くの適用シナリオがあり、それぞれに専用のEIPを提案する必要はなく、これにより全体的なEIPの数が増加し、開発者の負担が増えることを避けられます。このスタンダードは、現実世界の資産に付随する権利の分析に基づいており、NFT/SBTの権利を保有権と譲渡権に抽象化することで、より汎用的なものになっています。

例えば、このスタンダードには以下のようなユースケースがあります:

SBT。SBTの発行者は、SBTがミントされる前に、SBTに一様な`guard`ロールを割り当てることができ、それにより、対応するホルダーがSBTを転送できず、SBTの発行者がguardを通じて管理できるようになります。

NFT盗難防止。NFTホルダーがNFTの`guard`アドレスを自身のコールドウォレットアドレスに設定した場合、NFTホルダーはNFTを使用できますが、盗難のリスクが大幅に低減されます。

NFTレンディング。借り手がNFTの`guard`を貸し手のアドレスに設定すると、借り手はNFTを使用する権利を維持したまま、ローンを得ることができます。借り手がローンの返済に失敗した場合、貸し手はNFTを転送および売却できます。

さらに、`guard`に`expires`を設定することで、プロトコルのスケーラビリティがさらに向上します。以下の例のように:

より柔軟なNFT発行。NFTのミント時に、一定期間ロックされたNFTに対して割引を提供することができ、NFTの使用性に影響を与えません。

より安全なNFT管理。`guard`アドレスの秘密鍵が紛失しても、`owner`はexpires後にNFTを回復できます。

有効なSBT。一部のSBTには使用期間があります。`guard`と`expires`を通じてより効果的な管理が可能になります。

### 拡張性
  
このスタンダードは`guard`と`expires`のみを定義しています。NFTやSBTに必要な複雑な機能、例えば社会的回復やマルチシグネチャなどについては、`guard`を第三者プロトコルのアドレスとして設定することで、特定のアプリケーションシナリオに基づいてより柔軟で多様な機能を実現できます。

### 命名

代替案としては`guardian`と`guard`がありますが、どちらも基本的に対応する役割の権限に合っています:NFTの保護または用途に応じた必要な管理。`guard`は`guardian`よりも文字数が少なく、より簡潔です。

## 下位互換性

このスタンダードは拡張機能セットを追加することで、完全にERC-721互換にすることができます。

上記のスタンダードに基づいて発行されたNFTに`guard`が設定されていない場合、現在のERC-721標準に基づいて発行されたNFTと既存の機能に違いはありません。

## 参考実装
  
```solidity

// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.8;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC6147.sol";

abstract contract ERC6147 is ERC721, IERC6147 {

    /// @dev guardアドレスとexpiresを表す構造体
    /// @param guard guardロールのアドレス
    /// @param expirs トークンを管理できるUNIXタイムスタンプ
    struct GuardInfo{
        address guard;
        uint64 expires;
    }
    
    mapping(uint256 => GuardInfo) internal _guardInfo;

    /// @notice NFTの所有者、承認された操作者、承認されたアドレスはNFTのguardとexpiresを設定でき、
    ///         有効なguardはNFTのguardとexpiresを変更できる
    ///         NFTに有効なguardロールがある場合、NFTの所有者、承認された操作者、承認されたアドレスは
    ///         guardとexpiresを変更できない
    /// @dev `newGuard`はゼロアドレスにはできない
    ///      `expires`は有効である必要がある
    ///      `tokenId`が有効なNFTでない場合は例外をスローする
    /// @param tokenId NFTのguardアドレスを取得するためのNFT
    /// @param newGuard NFTの新しいguardアドレス
    /// @param expires NFTを管理できるUNIXタイムスタンプ
    function changeGuard(uint256 tokenId, address newGuard, uint64 expires) public virtual{
        require(expires > block.timestamp, "ERC6147: invalid expires");
        _updateGuard(tokenId, newGuard, expires, false);
    }

    /// @notice NFTのguardとexpiresを削除する
    ///         guardのみが自身のguardロールとexpiresを削除できる
    /// @dev guardアドレスはゼロアドレスに設定される
    ///      expiresは0に設定される
    ///      `tokenId`が有効なNFTでない場合は例外をスローする
    /// @param tokenId NFTのguardとexpiresを削除するためのNFT
    function removeGuard(uint256 tokenId) public virtual  {
        _updateGuard(tokenId, address(0), 0, true);
    }
    
    /// @notice NFTを転送し、guardとexpiresを削除する
    /// @dev NFTは`to`に転送され、guardアドレスはゼロアドレスに設定される
    ///      `tokenId`が有効なNFTでない場合は例外をスローする
    /// @param from NFTの前の所有者のアドレス
    /// @
to NFTの受取人のアドレス
    /// @param tokenId 転送するNFT
    function transferAndRemove(address from, address to, uint256 tokenId) public virtual {
        safeTransferFrom(from, to, tokenId);
        removeGuard(tokenId);
    }
    
    /// @notice NFTのguardアドレスとexpiresを取得する
    /// @dev ゼロアドレスはguardがいないことを示す
    /// @param tokenId NFTのguardアドレスとexpiresを取得するためのNFT
    /// @return NFTのguardアドレスとexpires
    function guardInfo(uint256 tokenId) public view virtual returns (address, uint64) {
        if(_guardInfo[tokenId].expires >= block.timestamp){
            return (_guardInfo[tokenId].guard, _guardInfo[tokenId].expires);
        }
        else{
            return (address(0), 0);
        }
    }

    /// @notice NFTのguardを更新する
    /// @dev 削除機能: guardをゼロアドレスに設定し、expiresを0に設定する;
    ///      更新機能: guardを新しいアドレスに設定し、expiresを設定する
    ///      `tokenId`が有効なNFTでない場合は例外をスローする
    /// @param tokenId NFTのguardアドレスを更新するためのNFT
    /// @param newGuard 新しいguardアドレス
    /// @param expires NFTを管理できるUNIXタイムスタンプ
    /// @param allowNull ゼロアドレスを許可する
    function _updateGuard(uint256 tokenId, address newGuard, uint64 expires, bool allowNull) internal {
        (address guard,) = guardInfo(tokenId);
        if (!allowNull) {
            require(newGuard != address(0), "ERC6147: new guard can not be null");
        }
        if (guard != address(0)) { 
            require(guard == _msgSender(), "ERC6147: only guard can change it self"); 
        } else { 
            require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC6147: caller is not owner nor approved");
        } 

        if (guard != address(0) || newGuard != address(0)) {
            _guardInfo[tokenId] = GuardInfo(newGuard,expires);
            emit UpdateGuardLog(tokenId, newGuard, guard, expires);
        }
    }
    
    /// @notice guardアドレスをチェックする
    /// @dev ゼロアドレスはguardがいないことを示す
    /// @param tokenId NFTのguardアドレスをチェックするためのNFT
    /// @return guardアドレス
    function _checkGuard(uint256 tokenId) internal view returns (address) {
        (address guard, ) = guardInfo(tokenId);
        address sender = _msgSender();
        if (guard != address(0)) {
            require(guard == sender, "ERC6147: sender is not guard of the token");
            return guard;
        }else{
            return address(0);
        }
    }
 
    /// @dev NFTを転送する前に、guardアドレスをチェックする必要がある
    function transferFrom(address from, address to, uint256 tokenId) public virtual override {
        address guard;
        address new_from = from;
        if (from != address(0)) {
            guard = _checkGuard(tokenId);
            new_from = ownerOf(tokenId);
        }
        if (guard == address(0)) {
            require(
                _isApprovedOrOwner(_msgSender(), tokenId),
                "ERC721: transfer caller is not owner nor approved"
            );
        }
        _transfer(new_from, to, tokenId);
    }

    /// @dev NFTを安全に転送する前に、guardアドレスをチェックする必要がある
    function safeTransferFrom(address from, address to, uint256 tokenId, bytes memory _data) public virtual override {
        address guard;
        address new_from = from;
        if (from != address(0)) {
            guard = _checkGuard(tokenId);
            new_from = ownerOf(tokenId);
        }
        if (guard == address(0)) {
            require(
                _isApprovedOrOwner(_msgSender(), tokenId),
                "ERC721: transfer caller is not owner nor approved"
            );
        }
        _safeTransfer(from, to, tokenId, _data);
    }

    /// @dev トークンが焼却されたら、`token_guard_map[tokenId]`を削除する
    /// これは送信者が操作する権限を確認しない内部関数です。
    function _burn(uint256 tokenId) internal virtual override {
        (address guard, )=guardInfo(tokenId);
        super._burn(tokenId);
        delete _guardInfo[tokenId];
        emit UpdateGuardLog(tokenId, address(0), guard, 0);
    }

    /// @dev {IERC165-supportsInterface}を参照してください。
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC6147).interfaceId || super.supportsInterface(interfaceId);
    }
}

```

## セキュリティ上の考慮事項

アプリケーションシナリオに応じて、適切な`expires`を`guard`に設定する必要があります。

NFTに有効なguardがある場合、`approve`または`setApprovalForAll`で承認されたアドレスでも、NFTを転送する権限はありません。

NFTに有効なguardがある場合、`owner`はNFTを売却できません。一部の取引プラットフォームは`setApprovalForAll`とオーナーの署名を通じてNFTをリストアップしますが、`guardInfo`をチェックしてこれらのNFTのリストアップを防ぐことをお勧めします。

## 著作権

著作権およびその関連権利は[CC0](../LICENSE.md)により放棄されています。