---
original: b7ddae871bf676771ce322562b53b01fa503a0e09238a3a24f7bdf059cbfb4e4
---

---
eip: 6105
title: 仲介業者のいないNFTトレーディングプロトコル
description: ERC-721にマーケットプレイス機能と多様なロイヤルティスキームを追加する
author: 5660-eth (@5660-eth)、Silvere Heraudeau (@lambdalf-dev)、Martin McConnell (@offgridgecko)、Abu <team10kuni@gmail.com>、Wizard Wang
discussions-to: https://ethereum-magicians.org/t/eip6105-no-intermediary-nft-trading-protocol/12171
status: Final
type: Standards Track
category: ERC
created: 2022-12-02
requires: 20, 165, 721, 2981
---

## 概要

このERCは[ERC-721](./eip-721.md)にマーケットプレイス機能を追加し、仲介業者のいないNFTトレーディングを可能にします。同時に、クリエイターはより多様なロイヤルティスキームを実装できます。

## 動機

現在のほとんどのNFTトレーディングは、仲介業者としてのNFTトレーディングプラットフォームに依存しており、以下のような問題があります:

1. `setApprovalForAll`関数による承認に関するセキュリティ上の懸念があります。NFTトレーディングプラットフォームに付与された権限は不必要なリスクにさらされます。トレーディングプラットフォームのコントラクトに問題が発生した場合、業界全体に大きな損失をもたらす可能性があります。さらに、ユーザーがトレーディングプラットフォームに自分のNFTの取り扱いを許可すると、フィッシング詐欺によってユーザーに低価格の注文を行わせ、自分を受取人として指定することができます。これは一般ユーザーが防ぐのが難しい問題です。
2. 取引コストが大きな問題です。一方で、トレーディングプラットフォームの数が増えるにつれ、NFTの流動性が分散されます。ユーザーが迅速に取引を行う必要がある場合、複数のプラットフォームで承認と注文を行う必要があり、リスク露出が増加し、各承認に追加のガス代が必要になります。例えば、BAYCを例にとると、総供給量が10,000で現在の保有者が6,000人以上いるため、1人当たりの平均保有数は2未満です。`setApprovalForAll`は単一のプラットフォームでの保留中の注文にはガス代を節約しますが、複数のプラットフォームを承認すると、ユーザーの全体的なガス代が増加します。一方で、トレーディングプラットフォームが課す手数料も取引コストとして考慮する必要があり、多くの場合、必要なガス代よりもはるかに高くなっています。
3. アグリゲーターはリクイディティを集約する解決策を提供しますが、意思決定プロセスは中央集権的です。さらに、トレーディングプラットフォームのオフチェーンの注文情報のため、アグリゲーターがデータを取得する効率は、トレーディングプラットフォームのAPIの頻度に影響され、時には、トレーディングプラットフォームがAPIの配布を停止し、頻度を制限することがあります。
4. プロジェクト関係者のロイヤルティ収入は、NFTトレーディングプラットフォームの中央集権的な意思決定に依存しています。一部のトレーディングプラットフォームは、プロジェクト関係者の同意なしにオプションのロイヤルティを実装しており、彼らの利益を侵害しています。
5. NFTトレーディングプラットフォームは検閲に強くありません。一部のプラットフォームは多数のNFTを上場停止しており、上場停止ルールの策定と実施は中央集権的で透明性が不足しています。過去には、一部のNFTトレーディングプラットフォームが倒産し、特定のNFTを誤って上場停止したことで、市場に混乱を引き起こしたこともあります。

## 仕様

このドキュメントの「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

準拠するコントラクトは、以下のインターフェースを実装しなければなりません:

```solidity
interface IERC6105 {

  /// @notice トークンが販売リストに追加または削除されたときに発行されるイベント
  /// @dev `salePrice`がゼロの場合、そのトークンは販売対象ではないことを示す
  ///      `expires`がゼロの場合、そのトークンは販売対象ではないことを示す
  /// @param tokenId - 販売対象のトークンID
  /// @param from - トークンを販売する側のアドレス
  /// @param salePrice - トークンの販売価格
  /// @param expires - トークンが購入可能な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  ///                         ゼロアドレスの場合、対応トークンはETHであることを示す
  ///                         購入者はサポートされているトークンで購入する必要がある
  /// @param benchmarkPrice - 追加の価格パラメータ、ロイヤルティ計算に使用される可能性がある
  event UpdateListing(
    uint256 indexed tokenId,
    address indexed from,
    uint256 salePrice,
    uint64 expires,
    address supportedToken,
    uint256 benchmarkPrice
    );

  /// @notice トークンが購入されたときに発行されるイベント
  /// @param tokenId - 購入されたトークンのID
  /// @param from - トークンを販売する側のアドレス
  /// @param to - トークンを購入する側のアドレス
  /// @param salePrice - トークンの販売価格
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  ///                         ゼロアドレスの場合、対応トークンはETHであることを示す
  ///                         購入者はサポートされているトークンで購入する必要がある
  /// @param royalties - この購入に支払われたロイヤルティ額
  event Purchased(
    uint256 indexed tokenId,
    address indexed from,
    address indexed to,
    uint256 salePrice,
    address supportedToken,
    uint256 royalties
    );

  /// @notice `tokenId`のリストを作成または更新する
  /// @dev `salePrice`をゼロに設定することはできません
  /// @param tokenId - 販売対象のトークンID
  /// @param salePrice - トークンの販売価格
  /// @param expires - トークンが購入可能な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  ///                         ゼロアドレスの場合、対応トークンはETHであることを示す
  ///                         購入者はサポートされているトークンで購入する必要がある
  /// 要件:
  /// - `tokenId`が存在する必要がある
  /// - 呼び出し元がトークンの所有者、承認された操作者、または承認されたアドレスである必要がある
  /// - `salePrice`をゼロに設定することはできません
  /// - `expires`が有効である必要がある
  /// - {UpdateListing}イベントを発行する必要がある
  function listItem(
    uint256 tokenId,
    uint256 salePrice,
    uint64 expires,
    address supportedToken
    ) external;

  /// @notice `tokenId`のリストを作成または更新する(benchmarkPriceを含む)
  /// @dev `salePrice`をゼロに設定することはできません
  /// @param tokenId - 販売対象のトークンID
  /// @param salePrice - トークンの販売価格
  /// @param expires - トークンが購入可能な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  ///                         ゼロアドレスの場合、対応トークンはETHであることを示す
  ///                         購入者はサポートされているトークンで購入する必要がある
  /// @param benchmarkPrice - 追加の価格パラメータ、ロイヤルティ計算に使用される可能性がある
  /// 要件:
  /// - `tokenId`が存在する必要がある
  /// - 呼び出し元がトークンの所有者、承認された操作者、または承認されたアドレスである必要がある
  /// - `salePrice`をゼロに設定することはできません
  /// - `expires`が有効である必要がある
  /// - {UpdateListing}イベントを発行する必要がある
  function listItem(
    uint256 tokenId,
    uint256 salePrice,
    uint64 expires,
    address supportedToken,
    uint256 benchmarkPrice
    ) external;
 
  /// @notice `tokenId`のリストを削除する
  /// @param tokenId - 販売リストから削除するトークンID
  /// 要件:
  /// - `tokenId`が存在し、販売リストに登録されている必要がある
  /// - 呼び出し元がトークンの所有者、承認された操作者、または承認されたアドレスである必要がある
  /// - {UpdateListing}イベントを発行する必要がある
  function delistItem(uint256 tokenId) external;
 
  /// @notice トークンを購入し、呼び出し元に転送する
  /// @dev `salePrice`と`supportedToken`は、フロントランニング攻撃を防ぐために、予想される購入価格とトークンと一致する必要がある
  /// @param tokenId - 購入対象のトークンID
  /// @param salePrice - トークンの販売価格
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  /// 要件:
  /// - `tokenId`が存在し、販売リストに登録されている必要がある
  /// - `salePrice`が予想される購入価格と一致する必要がある(フロントランニング攻撃を防ぐため)
  /// - `supportedToken`が予想される購入トークンと一致する必要がある(フロントランニング攻撃を防ぐため)
  /// - 呼び出し元が`tokenId`の掲載価格を支払える必要がある
  /// - {Purchased}イベントを発行する必要がある
  function buyItem(uint256 tokenId, uint256 salePrice, address supportedToken) external payable;

  /// @notice `tokenId`のリストを返す
  /// @dev `salePrice`がゼロの場合、そのトークンは販売対象ではないことを示す
  ///      `expires`がゼロの場合、そのトークンは販売対象ではないことを示す
  ///      `supportedToken`のアドレスがゼロの場合、対応トークンはETHであることを示す
  /// @param tokenId 照会対象のトークンID
  /// @return 指定されたリスト(販売価格、有効期限、対応トークン、ベンチマーク価格)
  function getListing(uint256 tokenId) external view returns (uint256, uint64, address, uint256);
}
```

### オプションのコレクションオファー拡張

```solidity
/// コレクションオファー拡張はERC-6105スマートコントラクトにとってオプションです。これにより、スマートコントラクトがコレクションオファー機能をサポートできるようになります。
interface IERC6105CollectionOffer {

  /// @notice コレクションにオファーが行われたときや、オファーがキャンセルされたときに発行されるイベント
  /// @dev `salePrice`がゼロの場合、そのコレクションオファーがキャンセルされたことを示す
  ///      `expires`がゼロの場合、そのコレクションオファーがキャンセルされたことを示す
  /// @param from - オファーを行った側のアドレス
  /// @param amount - `salePrice`で購入したいトークンの数
  /// @param salePrice - コレクションに対して提示されたトークン1つ当たりの価格
  /// @param expires - オファーが有効な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス
  ///                          購入者はサポートされているトークンで購入したい
  event UpdateCollectionOffer(address indexed from, uint256 amount, uint256 salePrice ,uint64 expires, address supportedToken);

  /// @notice コレクションに対するオファーを作成または更新する
  /// @dev `salePrice`をゼロに設定することはできません
  /// @param amount - `salePrice`で購入したいトークンの数
  /// @param salePrice - コレクションに対して提示されたトークン1つ当たりの価格
  /// @param expires - オファーが有効な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス
  ///                         購入者はサポートされているトークンで購入したい
  ///
要件:
/// - 呼び出し元が十分なサポートトークンを持っており、コントラクトに十分な量を承認している必要がある
/// - `salePrice`をゼロに設定することはできません
/// - `amount`をゼロに設定することはできません
/// - `expires`が有効である必要がある
/// - {UpdateCollectionOffer}イベントを発行する必要がある
function makeCollectionOffer(uint256 amount, uint256 salePrice, uint64 expires, address supportedToken) external;

  /// @notice コレクションオファーを受け入れ、トークンを購入者に転送する
  /// @dev `salePrice`と`supportedToken`は、フロントランニング攻撃を防ぐために、予想される購入価格とトークンと一致する必要がある
  ///      取引が完了したら、購入者が購入したいトークンの`amount`を1減らす必要がある
  /// @param tokenId - 提示されているトークンのID
  /// @param salePrice - トークンの提示価格
  /// @param supportedToken - 対応する ERC20 トークンのアドレス
  /// @param buyer - トークンを購入したいアドレス
  /// 要件:
  /// - `tokenId`が存在し、提示されている必要がある
  /// - 呼び出し元がトークンの所有者、承認された操作者、または承認されたアドレスである必要がある
  /// - {Purchased}イベントを発行する必要がある
  function acceptCollectionOffer(uint256 tokenId, uint256 salePrice, address supportedToken, address buyer) external;

  /// @notice コレクションオファーを受け入れ、トークンを購入者に転送する(benchmarkPriceを含む)
  /// @dev `salePrice`と`supportedToken`は、フロントランニング攻撃を防ぐために、予想される購入価格とトークンと一致する必要がある
  ///      取引が完了したら、購入者が購入したいトークンの`amount`を1減らす必要がある
  /// @param tokenId - 提示されているトークンのID
  /// @param salePrice - トークンの提示価格
  /// @param supportedToken - 対応する ERC20 トークンのアドレス
  /// @param buyer - トークンを購入したいアドレス
  /// @param benchmarkPrice - 追加の価格パラメータ、ロイヤルティ計算に使用される可能性がある
  /// 要件:
  /// - `tokenId`が存在し、提示されている必要がある
  /// - 呼び出し元がトークンの所有者、承認された操作者、または承認されたアドレスである必要がある
  /// - {Purchased}イベントを発行する必要がある
  function acceptCollectionOffer(uint256 tokenId, uint256 salePrice, address supportedToken, address buyer, uint256 benchmarkPrice) external;

  /// @notice コレクションに対するオファーを削除する
  /// 要件:
  /// - 呼び出し元がオファー提示者である必要がある
  /// - {UpdateCollectionOffer}イベントを発行する必要がある
  function cancelCollectionOffer() external;

  /// @notice `buyer`が提示したコレクションオファーを返す
  /// @dev `amount`がゼロの場合、オファーがないことを示す
  ///      `salePrice`がゼロの場合、オファーがないことを示す
  ///      `expires`がゼロの場合、オファーがないことを示す
  /// @param buyer トークンを購入したいアドレス
  /// @return 指定されたオファー(amount、sale price、expires、supported token)
  function getCollectionOffer(address buyer) external view returns (uint256, uint256, uint64, address);
}
```

### オプションのアイテムオファー拡張

```solidity
/// アイテムオファー拡張はERC-6105スマートコントラクトにとってオプションです。これにより、スマートコントラクトがアイテムオファー機能をサポートできるようになります。
interface IERC6105ItemOffer {

  /// @notice トークンにオファーが行われたときや、オファーがキャンセルされたときに発行されるイベント
  /// @dev `salePrice`がゼロの場合、そのトークンのオファーがキャンセルされたことを示す
  ///      `expires`がゼロの場合、そのトークンのオファーがキャンセルされたことを示す
  /// @param tokenId - オファーの対象となるトークンID
  /// @param from - トークンを購入したいアドレス
  /// @param salePrice - トークンの提示価格
  /// @param expires - オファーが有効な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス
  ///                          購入者はサポートされているトークンで購入したい
  event UpdateItemOffer(
    uint256 indexed tokenId,
    address indexed from,
    uint256 salePrice,
    uint64 expires,
    address supportedToken
    );

  /// @notice `tokenId`に対するオファーを作成または更新する
  /// @dev `salePrice`をゼロに設定することはできません
  /// @param tokenId - オファーの対象となるトークンID
  /// @param salePrice - トークンの提示価格
  /// @param expires - オファーが有効な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス
  ///                         購入者はサポートされているトークンで購入したい
  /// 要件:
  /// - `tokenId`が存在する必要がある
  /// - 呼び出し元が十分なサポートトークンを持っており、コントラクトに十分な量を承認している必要がある
  /// - `salePrice`をゼロに設定することはできません
  /// - `expires`が有効である必要がある
  /// - {UpdateItemOffer}イベントを発行する必要がある
  function makeItemOffer(uint256 tokenId, uint256 salePrice, uint64 expires, address supportedToken) external;

  /// @notice `tokenId`に対するオファーを削除する
  /// @param tokenId - オファーの対象となるトークンID
  /// 要件:
  /// - `tokenId`が存在し、オファーされている必要がある
  /// - 呼び出し元がオファー提示者である必要がある
  /// - {UpdateItemOffer}イベントを発行する必要がある
  function cancelItemOffer(uint256 tokenId) external;

  /// @notice オファーを受け入れ、トークンを購入者に転送する
  /// @dev `salePrice`と`supportedToken`は、フロントランニング攻撃を防ぐために、予想される購入価格とトークンと一致する必要がある
  ///      取引が完了したら、オファー情報を削除する必要がある
  /// @param tokenId - オファーの対象となるトークンID
  /// @param salePrice - トークンの提示価格
  /// @param supportedToken - 対応する ERC20 トークンのアドレス
  /// @param buyer - トークンを購入したいアドレス
  /// 要件:
  /// - `tokenId`が存在し、オファーされている必要がある
  /// - 呼び出し元がトークンの所有者、承認された操作者、または承認されたアドレスである必要がある
  /// - {Purchased}イベントを発行する必要がある
  function acceptItemOffer(uint256 tokenId, uint256 salePrice, address supportedToken, address buyer) external;

  /// @notice オファーを受け入れ、トークンを購入者に転送する(benchmarkPriceを含む)
  /// @dev `salePrice`と`supportedToken`は、フロントランニング攻撃を防ぐために、予想される購入価格とトークンと一致する必要がある
  ///      取引が完了したら、オファー情報を削除する必要がある
  /// @param tokenId - オファーの対象となるトークンID
  /// @param salePrice - トークンの提示価格
  /// @param supportedToken - 対応する ERC20 トークンのアドレス
  /// @param buyer - トークンを購入したいアドレス
  /// @param benchmarkPrice - 追加の価格パラメータ、ロイヤルティ計算に使用される可能性がある
  /// 要件:
  /// - `tokenId`が存在し、オファーされている必要がある
  /// - 呼び出し元がトークンの所有者、承認された操作者、または承認されたアドレスである必要がある
  /// - {Purchased}イベントを発行する必要がある
  function acceptItemOffer(uint256 tokenId, uint256 salePrice, address supportedToken, address buyer, uint256 benchmarkPrice) external;

  /// @notice `buyer`が`tokenId`に対して提示したオファーを返す
  /// @dev `salePrice`がゼロの場合、オファーがないことを示す
  ///      `expires`がゼロの場合、オファーがないことを示す
  /// @param tokenId 照会対象のトークンID
  /// @param buyer トークンを購入したいアドレス
  /// @return 指定されたオファー(sale price、expires、supported token)
  function getItemOffer(uint256 tokenId, address buyer) external view returns (uint256, uint64, address);
}
```

## 根拠

### 一部のローカル変数に関する考慮事項

`listItem`関数の`salePrice`をゼロに設定することはできません。まず、呼び出し側がわざわざ価格をゼロに設定することはまれであり、そうなった場合はほとんどが操作ミスによるものであり、資産の損失につながる可能性があります。次に、この関数を呼び出すにはガス代を支払う必要があるため、呼び出し側が価格をゼロに設定できると、実際の収入がマイナスになってしまい、経済学の「経済人」の概念に合致しません。さらに、トークンの価格がゼロであれば、そのアイテムは販売対象ではないことを示すため、リファレンス実装をより簡潔にすることができます。

`listItem`関数の`expires`を設定することで、呼び出し側がリストを better に管理できるようになります。リストが自動的に期限切れになれば、トークンの所有者が手動で`delistItem`を行う必要がなくなり、ガス代を節約できます。

`listItem`関数の`supportedToken`を設定することで、呼び出し側やコントラクトの所有者が、受け入れるトークンを選択できるようになります。単一のトークンに制限されるのではなく、選択肢が広がります。

`acceptCollectionOffer`および`acceptItemOffer`関数の変数設定の根拠は、上述のものと同様です。

### より多様なロイヤルティスキーム

`listItem`、`acceptCollectionOffer`、`acceptItemOffer`関数に`benchmarkPrice`パラメータを導入することで、[ERC-2981](./eip-2981.md)インターフェースの`royaltyInfo(uint256 _tokenId, uint256 _salePrice)`関数の`_salePrice`を`taxablePrice`に変更することができ、ERC-2981のロイヤルティスキームをより多様化できます。以下は、いくつかのロイヤルティスキームの例です:

`(address royaltyRecipient, uint256 royalties) = royaltyInfo(tokenId, taxablePrice)`

1. 付加価値ロイヤルティ(VAR、ロイヤルティは売り手の利益部分にのみ課金):`taxablePrice=max(salePrice- historicalPrice, 0)`
2. 売上ロイヤルティ(SR):`taxablePrice=salePrice`
3. 上限付きロイヤルティ(CR):`taxablePrice=min(salePrice, constant)`
4. 定量的ロイヤルティ(QR、各トークンの取引に固定のロイヤルティが支払われる):`taxablePrice= constant`

### オプションのブラックリスト

一部の意見では、ロイヤルティスキームに準拠しないインターメディアリー市場でのトークン取引を防ぐべきだとされていますが、この規格は仲介業者のいないNFTトレーディングのための機能のみを提供しており、これらの市場でのトークン取引を防ぐための標準化されたインターフェースは提供していません。プロジェクトチームやコミュニティの利益をより良く保護するために必要であれば、実装コントラクトにブラックリストを追加して、プロジェクトのロイヤルティスキームに準拠しないプラットフォームでのNFT取引を防ぐことを検討することができます。

## 下位互換性

この規格は[ERC-721](./eip-721.md)および[ERC-2981](./eip-2981.md)と互換性があります。

## リファレンス実装

```solidity
 // SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.8;
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/common/ERC2981.sol";
import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";
import "./IERC6105.sol";

/// @title No Intermediary NFT Trading Protocol with Value-added Royalty
/// @dev このリファレンス実装で使用されるロイヤルティスキームは付加価値ロイヤルティです
contract ERC6105 is ERC721, ERC2981, IERC6105, ReentrancyGuard{
/// @dev 上場トークンを表す構造体
  ///      `salePrice`がゼロの場合、そのトークンは販売対象ではないことを示す
  ///      `expires`がゼロの場合、そのトークンは販売対象ではないことを示す
  /// @param salePrice - トークンの販売価格
  /// @param expires - トークンが購入可能な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  ///                         ゼロアドレスの場合、対応トークンはETHであることを示す
  ///                         購入者はサポートされているトークンで購入する必要がある
  /// @param historicalPrice - 売り手が最後にこのトークンを購入した価格
  struct Listing {
    uint256 salePrice;
    uint64 expires;
    address supportedToken;
    uint256 historicalPrice;
  }

  // トークンIDからリスト索引へのマッピング
  mapping(uint256 => Listing) private _listings;

  constructor(string memory name_, string memory symbol_)
    ERC721(name_, symbol_)
    {
    }

  /// @notice `tokenId`のリストを作成または更新する
  /// @dev `salePrice`をゼロに設定することはできません
  /// @param tokenId - 販売対象のトークンID
  /// @param salePrice - トークンの販売価格
  /// @param expires - トークンが購入可能な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  ///                         ゼロアドレスの場合、対応トークンはETHであることを示す
  ///                         購入者はサポートされているトークンで購入する必要がある
  function listItem (
    uint256 tokenId,
    uint256 salePrice,
    uint64 expires,
    address supportedToken
    ) external virtual{
        listItem(tokenId, salePrice, expires, supportedToken, 0);
    }

  /// @notice `tokenId`のリストを作成または更新する(historicalPriceを含む)
  /// @dev `price`をゼロに設定することはできません
  /// @param tokenId - 販売対象のトークンID
  /// @param salePrice - トークンの販売価格
  /// @param expires - トークンが購入可能な期限(UNIX timestamp)
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  ///                         ゼロアドレスの場合、対応トークンはETHであることを示す
  ///                         購入者はサポートされているトークンで購入する必要がある
  /// @param historicalPrice - 売り手が最後にこのトークンを購入した価格
  function listItem (
    uint256 tokenId,
    uint256 salePrice,
    uint64 expires,
    address supportedToken,
    uint256 historicalPrice
    ) public virtual{

    address tokenOwner = ownerOf(tokenId);
    require(salePrice > 0, "ERC6105: token sale price MUST NOT be set to zero");
    require(expires > block.timestamp, "ERC6105: invalid expires");
    require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC6105: caller is not owner nor approved");

    _listings[tokenId] = Listing(salePrice, expires, supportedToken, historicalPrice);
    emit UpdateListing(tokenId, tokenOwner, salePrice, expires, supportedToken, historicalPrice);
  }

  /// @notice `tokenId`のリストを削除する
  /// @param tokenId - 販売リストから削除するトークンID
  function delistItem(uint256 tokenId) external virtual{
    require(_isApprovedOrOwner(_msgSender(), tokenId), "ERC6105: caller is not owner nor approved");
    require(_isForSale(tokenId), "ERC6105: invalid listing" );

    _removeListing(tokenId);
  }

  /// @notice トークンを購入し、呼び出し元に転送する
  /// @dev `salePrice`と`supportedToken`は、フロントランニング攻撃を防ぐために、予想される購入価格とトークンと一致する必要がある
  /// @param tokenId - 購入対象のトークンID
  /// @param salePrice - トークンの販売価格
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  function buyItem(uint256 tokenId, uint256 salePrice, address supportedToken) external nonReentrant payable virtual{
    address tokenOwner = ownerOf(tokenId);
    address buyer = msg.sender;
    uint256 historicalPrice = _listings[tokenId].historicalPrice;

    require(salePrice == _listings[tokenId].salePrice, "ERC6105: inconsistent prices");
    require(supportedToken ==  _listings[tokenId].supportedToken,"ERC6105: inconsistent tokens");
    require(_isForSale(tokenId), "ERC6105: invalid listing");

    /// @dev ロイヤルティの処理
    (address royaltyRecipient, uint256 royalties) = _calculateRoyalties(tokenId, salePrice, historicalPrice);

    uint256 payment = salePrice - royalties;
    if(supportedToken == address(0)){
        require(msg.value == salePrice, "ERC6105: incorrect value");
        _processSupportedTokenPayment(royalties, buyer, royaltyRecipient, address(0));
        _processSupportedTokenPayment(payment, buyer, tokenOwner, address(0));
    }
    else{
        uint256 num = IERC20(supportedToken).allowance(buyer, address(this));
        require (num >= salePrice, "ERC6105: insufficient allowance");
        _processSupportedTokenPayment(royalties, buyer, royaltyRecipient, supportedToken);
        _processSupportedTokenPayment(payment, buyer, tokenOwner, supportedToken);
    }

    _transfer(tokenOwner, buyer, tokenId);
    emit Purchased(tokenId, tokenOwner, buyer, salePrice, supportedToken, royalties);
  }

  /// @notice `tokenId`のリストを返す
  /// @dev `salePrice`がゼロの場合、そのトークンは販売対象ではないことを示す
  ///      `expires`がゼロの場合、そのトークンは販売対象ではないことを示す
  ///      `supportedToken`のアドレスがゼロの場合、対応トークンはETHであることを示す
  /// @param tokenId 照会対象のトークンID
  /// @return 指定されたリスト(販売価格、有効期限、対応トークン、ベンチマーク価格)
  function getListing(uint256 tokenId) external view virtual returns (uint256, uint64, address, uint256) {
    if(_listings[tokenId].salePrice > 0 && _listings[tokenId].expires >=  block.timestamp){
    uint256 salePrice = _listings[tokenId].salePrice;
    uint64 expires = _listings[tokenId].expires;
    address supportedToken =  _listings[tokenId].supportedToken;
    uint256 historicalPrice = _listings[tokenId].historicalPrice;
    return (salePrice, expires, supportedToken, historicalPrice);
    }
    else{
      return (0, 0, address(0), 0);
    }
  }

  /// @dev `tokenId`のリストを削除する
  /// @param tokenId - 販売リストから削除するトークンID
  function _removeListing(uint256 tokenId) internal virtual{
    address tokenOwner = ownerOf(tokenId);
    delete _listings[tokenId];
    emit UpdateListing(tokenId, tokenOwner, 0, 0, address(0), 0);
  }

  /// @dev トークンが販売対象かどうかを確認する
  function _isForSale(uint256 tokenId) internal virtual returns(bool){
    if(_listings[tokenId].salePrice > 0 && _listings[tokenId].expires >= block.timestamp){
        return true;
    }
    else{
        return false;
    }    
  }
  
  /// @dev 付加価値ロイヤルティを処理する
  function _calculateRoyalties(
    uint256 tokenId,
    uint256 price,
    uint256 historicalPrice
    ) internal virtual returns(address, uint256){
    uint256 taxablePrice;
    if(price > historicalPrice){
      taxablePrice = price - historicalPrice;
    }
    else{
      taxablePrice = 0 ;
    }

    (address royaltyRecipient, uint256 royalties) = royaltyInfo(tokenId, taxablePrice);
    return(royaltyRecipient, royalties);
  }

  /// @dev `supportedToken`の`amount`分の支払いを`recipient`に処理する
  /// @param amount - 送金する金額
  /// @param from - 支払い者
  /// @param recipient - 受取人
  /// @param supportedToken - 対応する ERC20 トークンのアドレス、またはゼロアドレス
  ///                         ゼロアドレスの場合、対応トークンはETHであることを示す
  function _processSupportedTokenPayment(
    uint256 amount,
    address from,
    address recipient,
    address supportedToken
    ) internal virtual{
    if(supportedToken == address(0))
    {
      (bool success,) = payable(recipient).call{value: amount}("");
      require(success, "Ether Transfer Fail"); 
    }
    else{
    (bool success) = IERC20(supportedToken).transferFrom(from, recipient, amount);
    require(success, "Supported Token Transfer Fail");
    }
  }
  
  /// @dev {IERC165-supportsInterface}を参照.
  function supportsInterface(bytes4 interfaceId) public view virtual override (ERC721, ERC2981) returns (bool) {
     return interfaceId == type(IERC6105).interfaceId || super.supportsInterface(interfaceId);
  }

  /// @dev NFTを転送する前に、リストを削除する必要がある
  function _beforeTokenTransfer(address from, address to, uint256 tokenId, uint256 batchSize) internal virtual override{
      super._beforeTokenTransfer(from, to, tokenId, batchSize);
      if(_isForSale(tokenId)){
          _removeListing(tokenId);
      }
  }
}