---
original: ebd0b0597d33eff437b5c1753e09ffe1c0350369673c173c79c04786d767d8b5
---

---
eip: 6864
title: 可変機能性トークン
description: 可変機能性トークン、ERC-20の単純な拡張
author: Jeff Huang (@jeffishjeff)
discussions-to: https://ethereum-magicians.org/t/eip-6864-upgradable-fungible-token-a-simple-extension-to-erc-20/13781
status: Draft
type: Standards Track
category: ERC
created: 2023-04-05
requires: 20
---

## 概要

このプロポーザルは、ユーザーの残高を維持しながら既存の[ERC-20](./eip-20.md)スマートコントラクトをアップグレード/ダウングレードするためのスマートコントラクトインターフェイスを概説しています。このインターフェイス自体はERC-20標準の拡張であり、アドレス以外の変更なしに他のスマートコントラクトが更新されたスマートコントラクトと対話し続けることができます。

## 動機

設計上、スマートコントラクトは不変であり、ERC-20などのトークン標準は最小限に抑えられています。これらの設計原則は分散型アプリケーションの基本的なものですが、以下のような場合にERC-20トークンをアップグレードする機能が望ましい実用的な状況があります:

- バグを修正し、制限を取り除くため
- 新機能や新しい標準を採用するため
- 変化する規制に準拠するため

`delegatecall`オペコードを使ったプロキシパターンは、不変性とアップグレード可能性を両立する合理的な一般的な解決策ですが、自身の欠点もあります:

- スマートコントラクトはプロキシパターンをサポートしている必要があり、つまりプロキシを使って展開されていないコントラクトには使えない
- アップグレードは無音で取り消し不可能で、ユーザーにオプトアウトする選択肢がない

対照的に、ERC-20トークンに範囲を限定することで、この提案は既存または将来のERC-20スマートコントラクトで動作する簡単に実装・維持できるERC-20の拡張を標準化し、逆転や入れ子も可能で、ユーザーが明示的にアップグレードにオプトインする二重確認の機会を提供します。

[ERC-4931](./eip-4931.md)は同じ問題に取り組もうとしていますが、アップグレード/ダウングレード操作のロジックをトークンロジックから切り離すために第三の「ブリッジ」コントラクトを導入しています。この設計はアップグレード/ダウングレードロジックをトークンロジックから切り離しますが、ERC-4931ではトークンがターゲットのスマートコントラクトで事前にマイニングされ、ブリッジコントラクトが所有する必要があるため、アップグレード時の転送や「見通し」機能をサポートできません。

## 仕様

この文書の中の "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"NOT RECOMMENDED"、"MAY"、"OPTIONAL" というキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

```solidity
pragma solidity ^0.8.0;

/**
    @title 可変機能性トークン
    @dev https://eips.ethereum.org/EIPS/eip-6864 を参照
 */
interface IERC6864 is IERC20 {
    /**
      @dev トークンがアップグレードされたときに必ず発行される
      @param from 基本のERC-20トークンの前の所有者
      @param to ERC-6864トークンの新しい所有者
      @param amount アップグレードされた量
    */
    event Upgrade(address indexed from, address indexed to, uint256 amount);

    /**
      @dev トークンがダウングレードされたときに必ず発行される
      @param from ERC-6864トークンの前の所有者
      @param to 基本のERC-20トークンの新しい所有者
      @param amount ダウングレードされた量
    */
    event Downgrade(address indexed from, address indexed to, uint256 amount);

    /**
      @notice `msg.sender`が直接所有する基本のERC-20トークンの`amount`を`to`に所有するERC-6864トークンにアップグレードする
      @dev `msg.sender`が直接`amount`以上の基本のERC-20トークンを所有している必要がある
      `to`がゼロアドレスの場合は失敗する
      `msg.sender`が直接`amount`以上の基本のERC-20トークンを所有していない場合は失敗する
      @param to ERC-6864トークンを受け取るアドレス
      @param amount アップグレードする基本のERC-20トークンの量
    */
    function upgrade(address to, uint256 amount) external;

    /**
      @notice `from`が所有するERC-6864トークンの`amount`を基本のERC-20トークンに`to`に所有させてダウングレードする
      @dev `msg.sender`が`from`の十分なERC-6864トークンを直接所有しているか承認されている必要がある
      `to`がゼロアドレスの場合は失敗する
      `from`が直接`amount`以上のERC-6864トークンを所有していない場合は失敗する
      `msg.sender`が`from`ではなく、`amount`以上のERC-6864トークンの使用が承認されていない場合は失敗する
      @param from ERC-6864トークンを解放するアドレス
      @param to 基本のERC-20トークンを受け取るアドレス
      @param amount ダウングレードするERC-6864トークンの量
    */
    function downgrade(address from, address to, uint256 amount) external;

    /**
      @notice 基本のERC-20スマートコントラクトのアドレスを取得する
      @return 基本のERC-20スマートコントラクトのアドレス
    */
    function baseToken() external view returns (address);
}
```

### 「見通し」拡張

**「見通し」拡張**は任意です。この[ERC-6864](./eip-6864.md)と基本のERC-20スマートコントラクト間の組み合わされた状態を簡単に表示できるようにします。

```solidity
pragma solidity ^0.8.0;

interface IERC6864SeeThrough is IERC6864 {
  /**
    @notice この ERC-6864 と基本の ERC-20 スマートコントラクト間の合計トークン供給量を取得する
    @return 合計トークン供給量
  */
  function combinedTotalSupply() external view returns (uint256);

  /**
    @notice `account`がこのERC-6864と基本のERC-20スマートコントラクト間で保有するトークン残高の合計を取得する
    @param account トークンを所有するアドレス
    @return 合計トークン残高
  */
  function combinedBalanceOf(address account) external view returns (uint256);

  /**
    @notice `owner`が`spender`に許可しているこのERC-6864と基本のERC-20スマートコントラクト間の合計使用許可量を取得する
    @param owner トークンを所有するアドレス
    @param spender トークンの使用が許可されているアドレス
    @return 合計使用許可量
  */
  function combinedAllowance(address owner, address spender) external view returns (uint256);
}
```

## 根拠

### ERC-20標準の拡張

このプロポーザルの目的は、ユーザーの残高を維持したままアップグレードすることなので、既存のデータ構造とメソッドを活用するのが最小限の工数と最大の相互運用性を実現する道筋です。

### ダウングレードのサポート

ダウングレードする機能があると、同じ基本のERC-20スマートコントラクト上で複数のIERC-6864実装を行き来できるようになります。また、ERC-6864スマートコントラクトにバグや制限が見つかった場合や、ユーザーが変更を望む場合の逃げ道にもなります。

### 任意の「見通し」拡張

これらの機能は多くの状況で有用ですが、他の公開関数を使って簡単に計算できるため、コアインターフェイスではなく任意の拡張に含めることにしました。

## 下位互換性

ERC-6864はERC-20標準と概して互換性があります。唯一の注意点は、一部のスマートコントラクトが`transfer`を実装して、標準の`balanceOf`ではなく組み合わされた残高全体で動作するようにすることです(これはユーザーの手間を減らすのに役立ちます、参考実装を参照)。この場合、そのようなコントラクトは`totalSupply`と`balanceOf`を、このERC-6864と基本のERC-20スマートコントラクト間の合計額を返すように実装することが推奨されます。

## 参考実装

```solidity
import {IERC20, ERC20} from "@openzeppelin-contracts/token/ERC20/ERC20.sol";

contract ERC6864 is IERC6864, ERC20 {
  IERC20 private immutable s_baseToken;

    constructor(string memory name, string memory symbol, address baseToken_) ERC20(name, symbol) {
        s_baseToken = IERC20(baseToken_);
    }

    function baseToken() public view virtual override returns (address) {
        return address(s_baseToken);
    }

    function upgrade(address to, uint256 amount) public virtual override {
        address from = _msgSender();

        s_baseToken.transferFrom(from, address(this), amount);
        _mint(to, amount);

        emit Upgrade(from, to, amount);
    }

    function downgrade(address from, address to, uint256 amount) public virtual override {
        address spender = _msgSender();

        if (from != spender) {
            _spendAllowance(from, spender, amount);
        }
        _burn(from, amount);
        s_baseToken.transfer(to, amount);

        emit Downgrade(from, to, amount);
    }

    function transfer(address to, uint256 amount) public virtual override returns (bool) {
        address from = _msgSender();
        uint256 balance = balanceOf(from);

        if (balance < amount) {
            upgrade(from, amount - balance);
        }

        _transfer(from, to, amount);
        return true;
    }

    function totalSupply() public view virtual override returns (uint256) {
        return return super.totalSupply() + s_baseToken.totalSupply() - s_baseToken.balanceOf(address(this));
    }

    function balanceOf(address account) public view virtual override returns (uint256) {
        return super.balanceOf(account) + s_baseToken.balanceOf(account);
    }
}
```

## セキュリティ上の考慮事項

- 基本のERC-20トークンをアップグレードするユーザーは、まずERC-6864スマートコントラクトにそれらを使う許可を与える必要があります。したがって、ユーザーの責任でERC-6864スマートコントラクトが健全で安全であり、承認する金額が適切であることを確認する必要があります。これは任意の`approve`操作と同じセキュリティ上の考慮事項を表します。
- ERC-6864スマートコントラクトは、アップグレード/ダウングレードに適切な任意の変換関数を実装できます: 1対1、線形、非線形。非線アーな変換関数の場合、`upgrade`と`downgrade`はフロントランニングやサンドイッチ攻撃の対象になる可能性があります(攻撃者の利益になるかどうかに関わらず)。これは、同様の非線形曲線を使う自動化された市場メーカー(AMM)と同じセキュリティ上の考慮事項を表します。
- ERC-6864スマートコントラクトは、ユーザーに無制限の使用許可を承認させ、`transfer`中に自動的にアップグレードを試みる可能性があります(参考実装を参照)。これにより、ユーザーがアップグレードする意図を三重に確認する機会(承認が二重確認)が失われます。
- 同じ基本のERC-20トークンに複数のIERC-6864実装を適用したり、ERC-6864スマートコントラクトをネストしたりできます。これによりトークンの複雑性が増し、既存のダッシュボードが正しくない、または一貫性のない結果を報告する可能性があります。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。