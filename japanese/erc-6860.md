---
original: d1e76f668b983cfd4a4c347797fcfb3a24a0c53b5a3c4f4cb3fb9938e13d7957
---

---
eip: 6860
title: Web3 URLからEVMコールメッセージへの変換
description: HTTP形式のWeb3 URLをEVMコールメッセージに変換する
author: Qi Zhou (@qizhou), Chao Pi (@pichaoqkc), Sam Wilson (@SamWilsn), Nicolas Deschildre (@nand2)
discussions-to: https://ethereum-magicians.org/t/eip-4804-web3-url-to-evm-call-message-translation/8300
status: Draft
type: Standards Track
category: ERC
created: 2023-09-29
requires: 137
---

## 概要

このスタンダードは、[RFC 3986](https://www.rfc-editor.org/rfc/rfc3986)形式のURIである `web3://uniswap.eth/` を次のようなEVMメッセージに変換します:

```
EVMメッセージ {
   To: 0xaabbccddee.... // uniswap.ethのENSに登録されたアドレス
   Calldata: 0x
   ...
}
```

⚠️ このプロポーザルは[ERC-4804](./eip-4804.md)を軽微な修正、明確化、変更を加えて更新したものです。

## 動機

現在、Web3からデータを読み取るには、Web2プロキシがWeb3ブロックチェーンに変換する必要があります。この変換は主にdAppウェブサイト/ノードサービスプロバイダー/Etherscanなどのプロキシによって行われており、ユーザーの管理下にはありません。このスタンダードは、Web2ユーザーがWeb3の内容、特にオンチェーンのWeb コンテンツ(SVG/HTML)に直接アクセスできるようにすることを目的としています。さらに、このスタンダードはURIと互換性のある他の標準(SVG/HTML)との相互運用性を可能にします。

## 仕様

この仕様では読み取り専用(Solidityの`view`関数)のセマンティクスのみを定義します。状態変更関数は将来の拡張として定義される可能性があります。

この仕様では[RFC 2234](https://www.rfc-editor.org/rfc/rfc2234)のAugmented Backus-Naur Form (ABNF)表記を使用しています。完全なURI構文はAppendix Aに記載されています。

Web3 URLは次の形式のASCII文字列です:

```
web3URL         = schema "://" [ userinfo "@" ] contractName [ ":" chainid ] pathQuery [ "#" fragment ]
schema          = "w3" / "web3"
userinfo        = address
```

**userinfo**は呼び出し元のユーザーを示し、EVMコールメッセージの"From"フィールドに相当します。指定されていない場合、プロトコルは0x0をsender addressとして使用します。

```
contractName    = address 
                / domainName
address         = "0x" 20( HEXDIG HEXDIG )
domainName      = *( unreserved / pct-encoded / sub-delims ) ; RFC 3986に準拠
```

**contractName**は呼び出されるコントラクトを示し、EVMコールメッセージの"To"フィールドに相当します。**contractName**がアドレスの場合、"To"フィールドにそのアドレスが使用されます。それ以外の場合、**contractName**はドメインネームサービスのドメイン名であり、"To"フィールドに使用するためにアドレスに解決される必要があります。

ドメイン名をドメインネームサービスからアドレスに解決する方法は、[ERC-6821](./eip-6821.md)のEthereum Name Serviceの仕様に記載されており、他のネームサービスについては後のERCで議論されます。

```
chainid         = %x31-39 *DIGIT
```

**chainid**は呼び出すチェーンを示します。指定されていない場合、プロトコルはネームサービスプロバイダーのプライマリチェーン(例えば1はeth)を使用します。ネームサービスプロバイダーが使用されていない場合、デフォルトのchainidは1になります。

```
pathQuery       = mPathQuery ; manual modeのpath+query
                / aPathQuery ; auto modeのpath+query
```

**pathQuery**は、パスとオプションのクエリから成り、"manual"モードと"auto"モードで構造が異なります。

```
fragment        = *VCHAR
```

**fragment**は、HTTPURLと同様に、リソースを参照する文字列で、スマートコントラクトに送信されません。

```
web3UrlRef      = web3URL 
                / relativeWeb3URL
relativeWeb3URL = relPathQuery
relPathQuery    = relMPathQuery ; manual modeの相対URLのpath+query
                / relAPathQuery ; auto modeの相対URLのpath+query
```

相対URLもサポートされていますが、解決モードによって対応が異なります。


### 解決モード

"To"アドレスとchainidが決定されると、プロトコルは"To"アドレスの`resolveMode`メソッドを呼び出して、解決モードを確認します。`resolveMode`のSolidity署名は次のとおりです:

```solidity
function resolveMode() external returns (bytes32);
```

プロトコルは現在2つの解決モードをサポートしています: auto mode と manual mode。

- `resolveMode`の返り値が`0x6d616e75616c0000000000000000000000000000000000000000000000000000`(bytes32で"manual")の場合、manual modeが使用されます。
- `resolveMode`の返り値が`0x6175746f00000000000000000000000000000000000000000000000000000000`(bytes32で"auto")、または
  - `0x0000000000000000000000000000000000000000000000000000000000000000`、または
  - `resolveMode`の呼び出しでエラーが発生した(メソッドが実装されていないかエラーが発生した)場合、auto modeが使用されます。
- それ以外の場合、プロトコルはリクエストを失敗させ、"unsupported resolve mode"エラーを返します。

#### Manual Mode

```
mPathQuery      = mPath [ "?" mQuery ]

mPath           = mPathAbempty ; "/"で始まるか空
mPathAbempty    = [ *( "/" segment ) "/" segment [ "." fileExtension ] ]
segment         = *pchar ; RFC 3986に準拠
fileExtension   = 1*( ALPHA / DIGIT )

mQuery = *( pchar / "/" / "?" ) ; RFC 3986に準拠
```

manual modeでは、**mPathQuery**をそのままcalldata(パーセントエンコーディングの解読は行わない)として使用します。**mPathQuery**が空の場合、送信されるcalldata は ``/`` (0x2f)になります。

返されたメッセージデータはABIエンコーディングされたバイトとして扱われ、デコードされたバイトがフロントエンドに返されます。

デフォルトのMIMEタイプは``text/html``ですが、**fileExtension**が存在する場合はそれに応じて上書きされます。この場合、MIMEタイプはファイル名拡張子から推測されます。

```
relMPathQuery   = relMPath [ "?" mQuery ]
relMPath        = mPathAbsolute ; "/"で始まるが"//"ではない
                / mPathNoscheme ; コロンのないセグメントで始まる
                / mPathEmpty    ; 0文字

mPathAbsolute   = "/" [ segmentNz *( "/" segment ) ] [ "." fileExtension ]
mPathNoscheme   = segmentNzNc *( "/" segment ) [ "." fileExtension ]
mPathEmpty      = 0<pchar>

segmentNz       = 1*pchar ; RFC 3986に準拠
segmentNzNc     = 1*( unreserved / pct-encoded / sub-delims / "@" )
                ; RFC 3986: コロン":"のない非ゼロ長のセグメント
```

manual modeの相対URLのサポートはHTTPURLと同様です: 現在のコントラクトに対する絶対パスと相対パスの両方の相対URLが許可されます。

#### Auto Mode

```
aPathQuery      = aPath [ "?" aQuery ]
aPath           = [ "/" [ method *( "/" argument ) ] ]
```

auto modeでは、**aPath**が空または"/"の場合、コントラクトは空のcalldata で呼び出されます。それ以外の場合、EVMメッセージのcalldata は標準のSolidityコントラクトABIを使用します。

```
method          = ( ALPHA / "$" / "_" ) *( ALPHA / DIGIT / "$" / "_" )
```

**method**は呼び出される関数メソッドを表す文字列です。

```
argument        = boolArg
                / uintArg
                / intArg
                / addressArg
                / bytesArg
                / stringArg
boolArg         = [ "bool!" ] ( "true" / "false" )
uintArg         = [ "uint" [ intSizes ] "!" ] 1*DIGIT
intArg          = "int" [ intSizes ] "!" 1*DIGIT
intSizes        = "8" / "16" / "24" / "32" / "40" / "48" / "56" / "64" / "72" / "80" / "88" / "96" / "104" / "112" / "120" / "128" / "136" / "144" / "152" / "160" / "168" / "176" / "184" / "192" / "200" / "208" / "216" / "224" / "232" / "240" / "248" / "256"
addressArg      = [ "address!" ] ( address / domainName )
bytesArg        = [ "bytes!" ] bytes
                / "bytes1!0x" 1( HEXDIG HEXDIG )
                / "bytes2!0x" 2( HEXDIG HEXDIG )
                ...
                / "bytes32!0x" 32( HEXDIG HEXDIG )
stringArg       = "string!" *pchar [ "." fileExtension ]
```

**argument**は、``[ type "!" ] value``という型非依存の構文を持つメソッドの引数です。**type**が指定されている場合、valueは対応する型に変換されます。プロトコルは現在、bool、int、uint、int&lt;X&gt;、uint&lt;X&gt;(Xは8から256まで8ずつ)、address、bytes&lt;X&gt;(Xは1から32まで)、bytes、stringをサポートしています。**type**が指定されていない場合、値は次の順序で自動的に検出されます:

  1. **type**="uint256"、**value**が数字の場合
  2. **type**="bytes32"、**value**が0x+32バイトのデータ16進数の場合
  3. **type**="address"、**value**が0x+20バイトのデータ16進数の場合
  4. **type**="bytes"、**value**が0xに続く20または32以外のバイト数の16進数の場合
  5. **type**="bool"、**value**が"true"または"false"の場合
  6. それ以外の場合、**type**="address"としてドメイン名として解釈します。ドメイン名の解決に失敗した場合、サポートされていないネームサービスプロバイダーエラーが返されます。

```
aQuery          = attribute *( "&" attribute )
attribute       = attrName "=" attrValue
attrName        = "returns"
                / "returnTypes"
attrValue       = [ "(" [ retTypes ] ")" ]
retTypes        = retType *( "," retType )
retType         = retRawType *( "[" [ %x31-39 *DIGIT ] "]" )
retRawType      = "(" retTypes ")"
                / retBaseType
retBaseType      = "bool" / "uint" [ intSizes ] / "int" [ intSize ] / "address" / "bytes" [ bytesSizes ] / "string"
bytesSizes      = %x31-39              ; 1-9
                / ( "1" / "2" ) DIGIT  ; 10-29
                / "31" / "32"          ; 31-32
```

**aQuery**の"returns"属性は、返されるデータの形式を指定します。Ethereumのコントラクトのメソッド署名の引数部分の構文に従います(``uint``と``int``のエイリアスが許可されます)。

- "returns"属性の値が未定義または空の場合、返されたメッセージデータはABIエンコーディングされたバイトとして扱われ、デコードされたバイトがフロントエンドに返されます。デフォルトのMIMEタイプは未定義ですが、最後の引数がstring型で**fileExtension**を持つ場合、MIMEタイプはファイル名拡張子から推測されます(注意: **fileExtension**はスマートコントラクトに渡される文字列引数から除外されません)
- "returns"属性の値が"()"の場合、返されたメッセージデータの生のバイトが"0x"プレフィックスの16進数文字列の配列としてJSONフォーマットで返されます: ``["0xXXXXX"]``
- それ以外の場合、返されたメッセージデータはABIデコードされ、指定された型でJSONフォーマットでエンコーディングされて返されます。データのエンコーディングはEthereum JSON-RPCフォーマットに従います:
  - 書式なしのデータ(bytes、address)は16進数でエンコーディングされ、"0x"プレフィックスが付きます
  - 数量(整数)は16進数でエンコーディングされ、"0x"プレフィックスが付きます(ゼロは"0x0"と表現されるという例外あり)
  - booleanとstringsはネイティブのJSON booleanとstringsです

複数
の"returns"属性が存在する場合、最後の"returns"属性の値が適用されます。"returnTypes"は"returns"のエイリアスですが、[ERC-4804](./eip-4804.md)との下位互換性のために主に使用されるため、使用は推奨されません。

```
relAPathQuery   = aPath [ "?" aQuery ]
```

auto modeの相対URLのサポートは限定的です: 現在のコントラクトに対する相対URLが許可され、自身(空)、``/``パス、または完全なメソッドとその引数を参照します。

### 例

#### 例1a

```
web3://w3url.eth/
```

**w3url.eth**のコントラクトがmanual modeの場合。

プロトコルはENSで**w3url.eth**のアドレスを見つけ、chainid 1(Mainnet)で解決します。次に、プロトコルは"Calldata" = `keccak("resolveMode()")[0:4]` = "0xDD473FAE"でアドレスを呼び出し、これが"manual"のABIタイプ"(bytes32)"を返します。コントラクトがmanual modeであることを確認した後、プロトコルは"To" = **contractAddress**、"Calldata" = "0x2F"でアドレスを呼び出します。返されたデータはABIタイプ"(bytes)"として扱われ、デコードされたバイトがフロントエンドに返されます。MIMEタイプは``text/html``と通知されます。

#### 例1b

```
web3://w3url.eth/
```

**w3url.eth**のコントラクトがauto modeの場合。

プロトコルはENSで**w3url.eth**のアドレスを見つけ、chainid 1(Mainnet)で解決します。次に、プロトコルは"Calldata" = `keccak("resolveMode()")[0:4]` = "0xDD473FAE"でアドレスを呼び出し、これが空(つまりコントラクトがauto modeであることを示す)を返します。コントラクトがauto modeであることを確認した後、プロトコルは"To" = **contractAddress**、"Calldata" = ""でアドレスを呼び出します。返されたデータはABIタイプ"(bytes)"として扱われ、デコードされたバイトがフロントエンドに返されます。MIMEタイプは未定義と通知されます。

#### 例2

```
web3://cyberbrokers-meta.eth/renderBroker/9999
```

**cyberbrokers-meta.eth**のコントラクトがauto modeの場合。

プロトコルはENSで**cyberbrokers-meta.eth**のアドレスを見つけ、chainid 1(Mainnet)で解決します。次に、プロトコルは"Calldata" = `keccak("resolveMode()")[0:4]` = "0xDD473FAE"でアドレスを呼び出し、これが空(つまりコントラクトがauto modeであることを示す)を返します。コントラクトがauto modeであることを確認した後、プロトコルは"To" = **contractAddress**、"Calldata" = "0x" + `keccak("renderBroker(uint256)")[0:4] + abi.encode(uint256(9999))`でアドレスを呼び出します。返されたデータはABIタイプ"(bytes)"として扱われ、デコードされたバイトがフロントエンドに返されます。MIMEタイプは未定義と通知されます。

#### 例3

```
web3://vitalikblog.eth:5/
```

**vitalikblog.eth:5**のコントラクトがmanual modeの場合。

プロトコルはENSで**vitalikblog.eth**のアドレスを見つけ、chainid 5(Goerli)で解決します。コントラクトがmanual modeであることを確認した後、プロトコルは"To" = **contractAddress**、"Calldata" = "0x2F"、chainid = 5でアドレスを呼び出します。返されたデータはABIタイプ"(bytes)"として扱われ、デコードされたバイトがフロントエンドに返されます。MIMEタイプは``text/html``と通知されます。

#### 例4

```
web3://0xe4ba0e245436b737468c206ab5c8f4950597ab7f:42170/
```

"0xe4ba0e245436b737468c206ab5c8f4950597ab7f:42170"のコントラクトがmanual modeの場合。

コントラクトがmanual modeであることを確認した後、プロトコルは"To" = "0xe4ba0e245436b737468c206ab5c8f4950597ab7f"、"Calldata" = "0x2F"、chainid = 42170(Arbitrum Nova)でアドレスを呼び出します。返されたデータはABIタイプ"(bytes)"として扱われ、デコードされたバイトがフロントエンドに返されます。MIMEタイプは``text/html``と通知されます。

#### 例5

```
web3://0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48/balanceOf/vitalik.eth?returns=(uint256)
```

"0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48"のコントラクトがauto modeの場合。

プロトコルはENSで**vitalik.eth**のアドレスを見つけ、chainid 1(Mainnet)で解決します。次に、コントラクトの"balanceOf(address)"メソッドを**vitalik.eth**のアドレスで呼び出します。コントラクトから返されたデータはABIタイプ"(uint256)"としてデコードされ、JSONフォーマットで`[ "0x9184e72a000" ]`のようにフロントエンドに返されます。MIMEタイプは``application/json``と通知されます。

#### 例6

```
web3://0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48/balanceOf/vitalik.eth?returns=()
```

"0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48"のコントラクトがauto modeの場合。

プロトコルはENSで**vitalik.eth**のアドレスを見つけ、chainid 1(Mainnet)で解決します。次に、コントラクトの"balanceOf(address)"メソッドをそのアドレスで呼び出します。コントラクトから返されたデータは生のバイトとして扱われ、JSONフォーマットで`["0x000000000000000000000000000000000000000000000000000009184e72a000"]`のようにエンコーディングされ、フロントエンドに返されます。MIMEタイプは``application/json``と通知されます。

### Appendix A: Web3 URLの完全なABNF

```
web3URL         = schema "://" [ userinfo "@" ] contractName [ ":" chainid ] pathQuery [ "#" fragment ]
schema          = "w3" / "web3"
userinfo        = address
contractName    = address 
                / domainName
chainid         = %x31-39 *DIGIT

pathQuery       = mPathQuery ; manual modeのpath+query
                / aPathQuery ; auto modeのpath+query
fragment        = *VCHAR

web3UrlRef      = web3URL 
                / relativeWeb3URL
relativeWeb3URL = relPathQuery
relPathQuery    = relMPathQuery ; manual modeの相対URLのpath+query
                / relAPathQuery ; auto modeの相対URLのpath+query

mPathQuery      = mPath [ "?" mQuery ]
mPath           = mPathAbempty ; "/"で始まるか空

relMPathQuery   = relMPath [ "?" mQuery ]
relMPath        = mPathAbsolute ; "/"で始まるが"//"ではない
                / mPathNoscheme ; コロンのないセグメントで始まる
                / mPathEmpty    ; 0文字

mPathAbempty    = [ *( "/" segment ) "/" segment [ "." fileExtension ] ]
mPathAbsolute   = "/" [ segmentNz *( "/" segment ) ] [ "." fileExtension ]
mPathNoscheme   = segmentNzNc *( "/" segment ) [ "." fileExtension ]
mPathEmpty      = 0<pchar>

segment         = *pchar ; RFC 3986に準拠
segmentNz       = 1*pchar ; RFC 3986に準拠
segmentNzNc     = 1*( unreserved / pct-encoded / sub-delims / "@" )
                ; RFC 3986: コロン":"のない非ゼロ長のセグメント

mQuery          = *( pchar / "/" / "?" ) ; RFC 3986に準拠

aPathQuery      = aPath [ "?" aQuery ]
aPath           = [ "/" [ method *( "/" argument ) ] ]
relAPathQuery   = aPath [ "?" aQuery ]
method          = ( ALPHA / "$" / "_" ) *( ALPHA / DIGIT / "$" / "_" )
argument        = boolArg
                / uintArg
                / intArg
                / addressArg
                / bytesArg
                / stringArg
boolArg         = [ "bool!" ] ( "true" / "false" )
uintArg         = [ "uint" [ intSizes ] "!" ] 1*DIGIT
intArg          = "int" [ intSizes ] "!" 1*DIGIT
intSizes        = "8" / "16" / "24" / "32" / "40" / "48" / "56" / "64" / "72" / "80" / "88" / "96" / "104" / "112" / "120" / "128" / "136" / "144" / "152" / "160" / "168" / "176" / "184" / "192" / "200" / "208" / "216" / "224" / "232" / "240" / "248" / "256"
addressArg      = [ "address!" ] ( address / domainName )
bytesArg        = [ "bytes!" ] bytes
                / "bytes1!0x" 1( HEXDIG HEXDIG )
                / "bytes2!0x" 2( HEXDIG HEXDIG )
                / "bytes3!0x" 3( HEXDIG HEXDIG )
                / "bytes4!0x" 4( HEXDIG HEXDIG )
                / "bytes5!0x" 5( HEXDIG HEXDIG )
                / "bytes6!0x" 6( HEXDIG HEXDIG )
                / "bytes7!0x" 7( HEXDIG HEXDIG )
                / "bytes8!0x" 8( HEXDIG HEXDIG )
                / "bytes9!0x" 9( HEXDIG HEXDIG )
                / "bytes10!0x" 10( HEXDIG HEXDIG )
                / "bytes11!0x" 11( HEXDIG HEXDIG )
                / "bytes12!0x" 12( HEXDIG HEXDIG )
                / "bytes13!0x" 13( HEXDIG HEXDIG )
                / "bytes14!0x" 14( HEXDIG HEXDIG )
                / "bytes15!0x" 15( HEXDIG HEXDIG )
                / "bytes16!0x" 16( HEXDIG HEXDIG )
                / "bytes17!0x" 17( HEXDIG HEXDIG )
                / "bytes18!0x" 18( HEXDIG HEXDIG )
                / "bytes19!0x" 19( HEXDIG HEXDIG )
                / "bytes20!0x" 20( HEXDIG HEXDIG )
                / "bytes21!0x" 21( HEXDIG HEXDIG )
                / "bytes22!0x" 22( HEXDIG HEXDIG )
                / "bytes23!0x" 23( HEXDIG HEXDIG )
                / "bytes24!0x" 24( HEXDIG HEXDIG )
                / "bytes25!0x" 25( HEXDIG HEXDIG )
                / "bytes26!0x" 26( HEXDIG HEXDIG )
                / "bytes27!0x" 27( HEXDIG HEXDIG )
                / "bytes28!0x" 28( HEXDIG HEXDIG )
                / "bytes29!0x" 29( HEXDIG HEXDIG )
                / "bytes30!0x" 30( HEXDIG HEXDIG )
                / "bytes31!0x" 31( HEXDIG HEXDIG )
                / "bytes32!0x" 32( HEXDIG HEXDIG )
stringArg       = "string!" *pchar [ "." fileExtension ]

aQuery          = attribute *( "&" attribute )
attribute       = attrName "=" attrValue
attrName        = "returns"
                / "returnTypes"
attrValue       = [ "(" [ retTypes ] ")" ]
retTypes        = retType *( "," retType )
retType         = retRawType *( "[" [ %x31-39 *DIGIT ] "]" )
retRawType      = "(" retTypes ")"
                / retBaseType
retBaseType      = "bool" / "uint" [ intSizes ] / "int" [ intSize ] / "address" / "bytes" [ bytesSizes ] / "string"
bytesSizes      = %x31-39              ; 1-9
                / ( "1" / "2" ) DIGIT  ; 10-29
                / "31" / "32"          ; 31-32

domainName      = *( unreserved / pct-encoded / sub-delims ) ; RFC 3986に準拠

fileExtension   = 1*( ALPHA / DIGIT )

address         = "0x" 20( HEXDIG HEXDIG )
bytes           = "0x" *( HEXDIG HEXDIG )

pchar           = unreserved / pct-encoded / sub-delims / ":" / "@" ; RFC 3986に準拠

pct-encoded     = "%" HEXDIG HEXDIG ; RFC 3986に準拠

unreserved      = ALPHA / DIGIT / "-" / "." / "_" / "~" ; RFC 3986に準拠
sub-delims    = "!" / "$" / "&" / "'" / "(" / ")"
                / "*" / "+" / "," / ";" / "=" ; RFC 3986に準拠

```

### Appendix B: [ERC-4804](./eip-4804.md)からの変更点

#### 修正点

- Manual mode: [ERC-4804](./eip-4804.md)
では、パス[ "?" クエリ ]の解釈がないと述べています。このERCでは、MIMEタイプの判定のためにパスの解釈があることを示しています。
- Auto mode: [ERC-4804](./eip-4804.md)では、**returns**属性がない場合、返されたデータはABIエンコーディングされたbytes32として扱われると述べています。このERCでは、実際にはABIエンコーディングされたbytesとして扱われることを示しています。

#### 明確化点

- 正式な仕様: このERCはURLフォーマットのABNF定義を追加しています。
- 解決モード: このERCは、解決モードの判定方法についてより詳細に説明しています。
- Manual mode: このERCは、URI-パーセントエンコーディングの扱い、返されたデータの扱い、MIMEタイプの判定方法について説明しています。
- Auto mode: このERCは、引数値のエンコーディング、**returns**値の形式と扱いについてより詳細に説明しています。
- 例: このERCは、例についてより詳細に説明しています。

#### 変更点

- プロトコル名: [ERC-4804](./eip-4804.md)では``ethereum-web3://``と``eth-web3://``が言及されていましたが、これらは削除されています。
- Auto mode: サポートされる型: [ERC-4804](./eip-4804.md)ではuint256、bytes32、address、bytes、stringのみがサポートされていましたが、このERCではより多くの型がサポートされるようになりました。
- Auto mode: **returns**属性が指定された場合の返された整数のエンコーディング: [ERC-4804](./eip-4804.md)の例5では整数を文字列としてエンコーディングすることを示唆していましたが、このERCではEthereum JSON RPCの仕様に従い、整数を"0x"プレフィックスの16進数文字列としてエンコーディングすることを示しています。

## 根拠

このプロポーザルの目的は、Ethereumの分散型プレゼンテーションレイヤーを追加することです。このレイヤーにより、人間が読みやすいURLを使ってオンチェーンのあらゆるWebコンテンツ(HTML/CSS/JPG/PNG/SVGなど)をレンダリングできるようになり、EVMを分散型バックエンドとして活用できるようになります。この標準の設計は以下の原則に基づいています:

- **人間が読みやすい**。Web3 URLはWeb2 URL(`http://`)と同様に人間が簡単に認識できるようにする必要があります。そのため、アドレスの代わりにネームサービスの名前をサポートしています。さらに、16進数のcalldata の代わりに人間が読みやすいメソッド+引数を使用し、calldata に変換しています。

- **HTTP-URLの標準との最大限の互換性**。Web3 URLはパス、クエリ、フラグメント、パーセントエンコーディングなどのHTTP-URLの標準と互換性を持つ必要があります。これにより、既存のWeb2ユーザーがこの標準を最小限の追加知識で利用できるようになります。

- **シンプル**。引数の明示的な型指定の代わりに、アドレス、bytes32、uint256などの"最大確率"の原則に基づいて自動的に型を検出しています。これにより、URLの長さを大幅に短縮でき、混乱を避けることができます。ただし、必要に応じて明示的な型指定もサポートしています。

- **柔軟性**。コントラクトは`resolveMode`メソッドを使ってエンコーディングルールをオーバーライドできるため、ユーザーが実際に探しているWebリソースを正確に理解できるようになっています。

## セキュリティ上の考慮事項

特に問題はありませんでした。

## 著作権

著作権およびその関連権利は[CC0](../LICENSE.md)で放棄されています。