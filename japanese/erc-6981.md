---
original: 516f7ce64b73eba1d082f40f4a6739f507f0cf97a1f6328e82cdb0c2854dea73
---

---
eip: 6981
title: 予約所有アカウント
description: 外部サービスのユーザーが所有するスマートコントラクトアカウントを生成するためのレジストリ
author: Paul Sullivan (@sullivph) <paul.sullivan@manifold.xyz>, Wilkins Chung (@wwchung) <wilkins@manifold.xyz>, Kartik Patel (@Slokh) <kartik@manifold.xyz>
discussions-to: https://ethereum-magicians.org/t/erc-6981-reserved-ownership-accounts/14118
status: ドラフト
type: Standards Track
category: ERC
created: 2023-04-25
requires: 1167, 1271, 6492
---

## 概要

以下の仕様は、サービスがユーザーにイーサリアムアドレスをリンクさせるためのシステムを定義しています。サービスはユーザーに署名付きメッセージとユニークなソルトを提供し、これを使ってレジストリコントラクトの `create2` オペコードを使ってスマートコントラクトウォレットをデプロイできます。

## 動機

ウェブサービスがユーザーに対してカストディアルウォレットを通じてオンチェーンアセットを保有することは一般的です。これらのウォレットは通常、EOA、デプロイ済みのスマートコントラクトウォレット、またはオムニバスコントラクトで、秘密鍵やアセットの所有権情報がデータベースに保存されています。この提案は、従来のアプローチに関連するセキュリティ上の懸念を回避し、サービスがユーザーのアセットを管理する必要性とその影響を排除するソリューションを概説しています。

外部サービスを利用するユーザーは、オンチェーンでの取引を行うことなく、イーサリアムアドレスを受け取ることができます。これらのユーザーは、後の時点でそのアドレスの管理権を取得することができます。したがって、オンチェーンのアセットをユーザーに事前に送信し、ユーザーがブロックチェーンと直接やり取りする必要なく、オンチェーンのアイデンティティを形成することができます。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」のキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

### 概要

予約所有アカウントを作成するシステムは以下で構成されています:

1. サービスユーザーの識別ソルトに基づいて決定論的なアドレスを提供し、署名検証機能を実装して、サービスのエンドユーザーがアカウントインスタンスを請求できるようにするアカウントレジストリ。
2. アカウントレジストリを通じてエンドユーザーが作成したアカウントインスタンス。これにより、決定論的なアドレスに受け取られたアセットにアクセスできます。

予約所有アカウントをユーザーに提供したいサービスは、ユーザーの識別情報とソルトの関係を維持する必要があります。サービスは、ソルトに対応する決定論的なアカウントアドレスを取得するためにアカウントレジストリインスタンスを参照する必要があります。サービスのユーザーは、自身の識別情報を検証することで、アカウントインスタンスを作成できます。サービスは、ユーザーにソルトの署名付きメッセージを提供する必要があります。ユーザーは、このメッセージと署名をサービスのアカウントレジストリインスタンスの `claimAccount` 関数に渡して、決定論的なアドレスにアカウントインスタンスをデプロイおよび請求する必要があります。

### アカウントレジストリ

アカウントレジストリは以下のインターフェースを実装する必要があります:

```solidity
interface IAccountRegistry {
    /**
     * @dev レジストリインスタンスは、アカウント作成が成功すると AccountCreated イベントを発行する
     */
    event AccountCreated(address account, address accountImplementation, uint256 salt);

    /**
     * @dev レジストリインスタンスは、アカウントが所有者によって請求されると AccountClaimed イベントを発行する
     */
    event AccountClaimed(address account, address owner);

    /**
     * @dev スマートコントラクトアカウントを作成する。
     *
     * アカウントがすでに作成されている場合は、create2を呼び出さずにアカウントアドレスを返す。
     *
     * @param salt       - ユーザーがアカウントインスタンスをデプロイしたいソルト
     *
     * AccountCreated イベントを発行する
     * @return 作成されたアカウントインスタンスのアドレス
     */
    function createAccount(uint256 salt) external returns (address);

    /**
     * @dev 所有者がこのレジストリで作成されたスマートコントラクトアカウントを請求できるようにする。
     *
     * アカウントがまだ作成されていない場合は、最初に `createAccount` を使ってアカウントを作成する
     *
     * @param owner      - 新しいアカウントインスタンスの初期所有者
     * @param salt       - ユーザーがアカウントインスタンスをデプロイしたいソルト
     * @param expiration - expiration > 0の場合、署名の有効期限を表す。それ以外の場合は署名は期限切れにならない
     * @param message    - owner、salt、expirationを検証するkeccak256ハッシュ
     * @param signature  - owner、salt、expirationを検証する署名
     *
     * AccountClaimed イベントを発行する
     * @return 請求されたアカウントインスタンスのアドレス
     */
    function claimAccount(
        address owner,
        uint256 salt,
        uint256 expiration,
        bytes32 message,
        bytes calldata signature
    ) external returns (address);

    /**
     * @dev 指定のソルトに対応するスマートコントラクトアカウントの計算されたアドレスを返す
     *
     * @return アカウントの計算されたアドレス
     */
    function account(uint256 salt) external view returns (address);

    /**
     * @dev 未請求のアカウントに対するフォールバック署名検証
     */
    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4);
}
```

#### createAccount

`createAccount` は、指定のソルトに対してアカウントインスタンスをデプロイするために使用されます。

- この関数は、[ERC-1167](./eip-1167.md) プロキシを使って新しいアカウントインスタンスをデプロイする必要があります。
- この関数は、アカウントインスタンスの初期所有者をアカウントレジストリインスタンスに設定する必要があります。
- アカウントの実装アドレスは不変でなければならず、これはアカウントインスタンスの決定論的なアドレスの計算に使用されます。
- アカウントインスタンスの正常なデプロイ時に、レジストリは `AccountCreated` イベントを発行する必要があります。

#### claimAccount

`claimAccount` は、指定のソルトに対してアカウントインスタンスの所有権を請求するために使用されます。

- この関数は、指定のソルトに対してアカウントインスタンスが存在しない場合、新しいアカウントインスタンスを作成する必要があります。
- この関数は、msg.senderがソルトと初期所有者に対してアカウントインスタンスの所有権を請求する権限があることを検証する必要があります。検証は、EOAの署名の場合はECDSAを、スマートコントラクトの署名の場合は[ERC-1271](./eip-1271.md)を使って行う必要があります。
- この関数は、block.timestamp < expirationまたはexpiration == 0であることを確認する必要があります。
- `claimAccount`への呼び出しで署名検証が成功した場合、レジストリはアカウントインスタンスに対する完全な管理権を放棄し、アカウントインスタンスの `setOwner` を呼び出して初期所有者に所有権を割り当てる必要があります。
- アカウントインスタンスの正常な請求時に、レジストリは `AccountClaimed` イベントを発行する必要があります。

#### isValidSignature

`isValidSignature` は、未請求のアカウントに対するフォールバック署名検証関数です。有効な署名は、元のメッセージハッシュとアカウントインスタンスのアドレスを合成したハッシュ(例: `bytes32 compositeHash = keccak256(abi.encodePacked(originalHash, accountAddress))`)に対して、レジストリの署名者が署名したものである必要があります。この関数は、`originalHash`がこの関数に渡されたハッシュ、`accountAddress`が`msg.sender`(未請求のアカウントインスタンス)である合成ハッシュを再構築し、レジストリの署名者に対して署名を検証する必要があります。

### アカウントインスタンス

アカウントインスタンスは以下のインターフェースを実装する必要があります:

```solidity
interface IAccount is IERC1271 {
    /**
     * @dev アカウントインスタンスの所有者を設定する。
     *
     * 現在の所有者、またはアカウントインスタンスがまだ請求されていない場合はレジストリのみが呼び出せる。
     *
     * @param owner      - アカウントインスタンスの新しい所有者
     */
    function setOwner(address owner) external;
}
```

- すべてのアカウントインスタンスは、アカウントレジストリインスタンスを使ってデプロイされる必要があります。
- アカウントインスタンスは、アカウントインスタンスがデプロイされているアドレスに事前に送信されたアセットへのアクセスを提供する必要があります。
- `setOwner` は所有者を更新し、アカウントインスタンスの現在の所有者のみが呼び出せる必要があります。
- アカウントインスタンスがデプロイされているが請求されていない場合、アカウントインスタンスの所有者はアカウントレジストリインスタンスに初期化される必要があります。
- アカウントインスタンスは、所有者がアカウントレジストリインスタンスであるかどうかを確認することで、請求されたかどうかを判断する必要があります。

#### アカウントインスタンスの署名

アカウントインスタンスは、`isValidSignature` 関数を実装することで[ERC-1271](./eip-1271.md)をサポートする必要があります。アカウントインスタンスの所有者がメッセージに署名する場合(例: dAppにログインする際)、署名は以下のいずれかの方法で生成される必要があります。

1. アカウントインスタンスがデプロイされ、請求されている場合、所有者が署名を生成し、`isValidSignature`は、メッセージハッシュと署名が現在の所有者に対して有効であることを検証する必要があります。
2. アカウントインスタンスがデプロイされているが未請求の場合、レジストリの署名者は[上記](#isvalidsignature)のように、元のメッセージとアカウントインスタンスのアドレスを合成したハッシュに署名し、`isValidSignature`はメッセージハッシュと署名をアカウントレジストリインスタンスの `isValidSignature` 関数に転送する必要があります。
3. アカウントインスタンスがデプロイされていない場合、レジストリの署名者は状況2と同様に合成ハッシュに署名し、[ERC-6492](./eip-6492.md#signer-side)に従ってその署名をラップする必要があります(例: `concat(abi.encode((registryAddress, createAccountCalldata, compositeHashSignature), (address, bytes, bytes)), magicBytes)`).

アカウントインスタンスの署名検証は、[ERC-6492](./eip-6492.md#verifier-side)に従って行う必要があります。

## 根拠

### サービス所有のレジストリインスタンス

予約所有アカウントのための汎用的なレジストリを実装およびデプロイすることが、ユーザーにとってより使いやすいように見えるかもしれませんが、外部サービスプロバイダーが自身のアカウントレジストリを所有および管理する選択肢を持つことが重要だと考えています。これにより、独自の許可制御とアカウントデプロイ認証フレームワークを実装する柔軟性が得られます。

私たちは、外部サービスのためにアカウントレジストリをデプロイできるレジストリファクトリを提供しています。これには以下が含まれます:

- 不変のアカウントインスタンス実装
- EOAの
署名者の場合はECDSA、スマートコントラクトの署名者の場合は[ERC-1271](./eip-1271.md)による `claimAccount` メソッドの検証
- アカウントレジストリのデプロイヤーが `claimAccount` 検証に使用する署名アドレスを変更できる機能

### アカウントレジストリとアカウント実装の結合

アカウントインスタンスが[ERC-1167](./eip-1167.md)プロキシとしてデプロイされるため、アカウント実装アドレスはある特定のアカウントレジストリインスタンスから作成されたアカウントのアドレスに影響します。レジストリインスタンスが単一の不変のアカウント実装アドレスにリンクされることを要求することで、ユーザーのソルトと特定のアカウントレジストリインスタンス上のリンクされたアドレスの一貫性が確保されます。

これにより、サービスがユーザーに信頼されるアカウント実装アドレスでレジストリをデプロイすることで、ユーザーの信頼を得ることもできます。

さらに、アカウント実装は更新可能に設計できるため、ユーザーはアカウントを作成したレジストリインスタンスで指定された実装に縛られる必要はありません。

### `createAccount` と `claimAccount` の分離

アカウントインスタンスの作成と請求の操作は意図的に分離されています。これにより、サービスはユーザーのアカウントインスタンスがデプロイされる前に、有効な[ERC-6492](./eip-6492.md)署名を提供できます。

## 参考実装

以下は、外部サービスプロバイダーが独自のアカウントレジストリインスタンスをデプロイするために使用できるアカウントレジストリファクトリの例です。

### アカウントレジストリファクトリ

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

/// @author: manifold.xyz

import {Create2} from "openzeppelin/utils/Create2.sol";

import {Address} from "../../lib/Address.sol";
import {ERC1167ProxyBytecode} from "../../lib/ERC1167ProxyBytecode.sol";
import {IAccountRegistryFactory} from "./IAccountRegistryFactory.sol";

contract AccountRegistryFactory is IAccountRegistryFactory {
    using Address for address;

    error InitializationFailed();

    address private immutable registryImplementation = 0x076B08EDE2B28fab0c1886F029cD6d02C8fF0E94;

    function createRegistry(
        uint96 index,
        address accountImplementation,
        bytes calldata accountInitData
    ) external returns (address) {
        bytes32 salt = _getSalt(msg.sender, index);
        bytes memory code = ERC1167ProxyBytecode.createCode(registryImplementation);
        address _registry = Create2.computeAddress(salt, keccak256(code));

        if (_registry.isDeployed()) return _registry;

        _registry = Create2.deploy(0, salt, code);

        (bool success, ) = _registry.call(
            abi.encodeWithSignature(
                "initialize(address,address,bytes)",
                msg.sender,
                accountImplementation,
                accountInitData
            )
        );
        if (!success) revert InitializationFailed();

        emit AccountRegistryCreated(_registry, accountImplementation, index);

        return _registry;
    }

    function registry(address deployer, uint96 index) external view override returns (address) {
        bytes32 salt = _getSalt(deployer, index);
        bytes memory code = ERC1167ProxyBytecode.createCode(registryImplementation);
        return Create2.computeAddress(salt, keccak256(code));
    }

    function _getSalt(address deployer, uint96 index) private pure returns (bytes32) {
        return bytes32(abi.encodePacked(deployer, index));
    }
}
```

### アカウントレジストリ

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

/// @author: manifold.xyz

import {Create2} from "openzeppelin/utils/Create2.sol";
import {ECDSA} from "openzeppelin/utils/cryptography/ECDSA.sol";
import {Ownable} from "openzeppelin/access/Ownable.sol";
import {Initializable} from "openzeppelin/proxy/utils/Initializable.sol";
import {IERC1271} from "openzeppelin/interfaces/IERC1271.sol";
import {SignatureChecker} from "openzeppelin/utils/cryptography/SignatureChecker.sol";

import {Address} from "../../lib/Address.sol";
import {IAccountRegistry} from "../../interfaces/IAccountRegistry.sol";
import {ERC1167ProxyBytecode} from "../../lib/ERC1167ProxyBytecode.sol";

contract AccountRegistryImplementation is Ownable, Initializable, IAccountRegistry {
    using Address for address;
    using ECDSA for bytes32;

    struct Signer {
        address account;
        bool isContract;
    }

    error InitializationFailed();
    error ClaimFailed();
    error Unauthorized();

    address public accountImplementation;
    bytes public accountInitData;
    Signer public signer;

    constructor() {
        _disableInitializers();
    }

    function initialize(
        address owner,
        address accountImplementation_,
        bytes calldata accountInitData_
    ) external initializer {
        _transferOwnership(owner);
        accountImplementation = accountImplementation_;
        accountInitData = accountInitData_;
    }

    /**
     * @dev {IAccountRegistry-createAccount}を参照
     */
    function createAccount(uint256 salt) external override returns (address) {
        bytes memory code = ERC1167ProxyBytecode.createCode(accountImplementation);
        address _account = Create2.computeAddress(bytes32(salt), keccak256(code));

        if (_account.isDeployed()) return _account;

        _account = Create2.deploy(0, bytes32(salt), code);

        (bool success, ) = _account.call(accountInitData);
        if (!success) revert InitializationFailed();

        emit AccountCreated(_account, accountImplementation, salt);

        return _account;
    }

    /**
     * @dev {IAccountRegistry-claimAccount}を参照
     */
    function claimAccount(
        address owner,
        uint256 salt,
        uint256 expiration,
        bytes32 message,
        bytes calldata signature
    ) external override returns (address) {
        _verify(owner, salt, expiration, message, signature);
        address _account = this.createAccount(salt);

        (bool success, ) = _account.call(
            abi.encodeWithSignature("transferOwnership(address)", owner)
        );
        if (!success) revert ClaimFailed();

        emit AccountClaimed(_account, owner);
        return _account;
    }

    /**
     * @dev {IAccountRegistry-account}を参照
     */
    function account(uint256 salt) external view override returns (address) {
        bytes memory code = ERC1167ProxyBytecode.createCode(accountImplementation);
        return Create2.computeAddress(bytes32(salt), keccak256(code));
    }

    /**
     * @dev {IAccountRegistry-isValidSignature}を参照
     */
    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
        bytes32 expectedHash = keccak256(abi.encodePacked(hash, msg.sender));
        bool isValid = SignatureChecker.isValidSignatureNow(
            signer.account,
            expectedHash,
            signature
        );
        if (isValid) {
            return IERC1271.isValidSignature.selector;
        }

        return "";
    }

    function updateSigner(address newSigner) external onlyOwner {
        uint32 signerSize;
        assembly {
            signerSize := extcodesize(newSigner)
        }
        signer.account = newSigner;
        signer.isContract = signerSize > 0;
    }

    function _verify(
        address owner,
        uint256 salt,
        uint256 expiration,
        bytes32 message,
        bytes calldata signature
    ) internal view {
        address signatureAccount;

        if (signer.isContract) {
            if (!SignatureChecker.isValidSignatureNow(signer.account, message, signature))
                revert Unauthorized();
        } else {
            signatureAccount = message.recover(signature);
        }

        bytes32 expectedMessage = keccak256(
            abi.encodePacked("\x19Ethereum Signed Message:\n84", owner, salt, expiration)
        );

        if (
            message != expectedMessage ||
            (!signer.isContract && signatureAccount != signer.account) ||
            (expiration != 0 && expiration < block.timestamp)
        ) revert Unauthorized();
    }
}
```

### アカウント実装の例

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

/// @author: manifold.xyz

import {IERC1271} from "openzeppelin/interfaces/IERC1271.sol";
import {SignatureChecker} from "openzeppelin/utils/cryptography/SignatureChecker.sol";
import {IERC165} from "openzeppelin/utils/introspection/IERC165.sol";
import {ERC165Checker} from "openzeppelin/utils/introspection/ERC165Checker.sol";
import {IERC721} from "openzeppelin/token/ERC721/IERC721.sol";
import {IERC721Receiver} from "openzeppelin/token/ERC721/IERC721Receiver.sol";
import {IERC1155Receiver} from "openzeppelin/token/ERC1155/IERC1155Receiver.sol";
import {Initializable} from "openzeppelin/proxy/utils/Initializable.sol";
import {Ownable} from "openzeppelin/access/Ownable.sol";
import {IERC1967Account} from "./IERC1967Account.sol";

import {IAccount} from "../../interfaces/IAccount.sol";

/**
 * @title ERC1967AccountImplementation
 * @notice 軽量で更新可能なスマートコントラクトウォレットの実装
 */
contract ERC1967AccountImplementation is
    IAccount,
    IERC165,
    IERC721Receiver,
    IERC1155Receiver,
    IERC1967Account,
    Initializable,
    Ownable
{
    address public registry;

    constructor() {
        _disableInitializers();
    }

    function initialize() external initializer {
        registry = msg.sender;
        _transferOwnership(registry);
    }

    function supportsInterface(bytes4 interfaceId) external pure returns (bool) {
        return (interfaceId == type(IAccount).interfaceId ||
            interfaceId == type(IERC1967Account).interfaceId ||
            interfaceId == type(IERC1155Receiver).interfaceId ||
            interfaceId == type(IERC721Receiver).interfaceId ||
            interfaceId == type(IERC165).interfaceId);
    }

    function onERC721Received(
        address,
        address,
        uint256,
        bytes memory
    ) public pure returns (bytes4) {
        return this.onERC721Received.selector;
    }

    function onERC1155Received(
        address,
        address,
        uint256,
        uint256,
        bytes memory
    ) public pure returns (bytes4) {
        return this.onERC1155Received.selector;
    }

    function onERC1155BatchReceived(
        address,
        address,
        uint256[] memory,
        uint256[] memory,
        bytes memory
    ) public pure returns (bytes4) {
        return this.onERC1155BatchReceived.selector;
    }

    /**
     * @dev {IERC1967Account-executeCall}を参照
     */
    function executeCall(
        address _target,
        uint256 _value,
        bytes calldata _data
    ) external payable override onlyOwner returns (bytes memory _result) {
        bool success;
        // solhint-disable-next-line avoid-low-level-calls
        (success, _result) = _target.call{value: _value}(_data);
        require(success, string(_result));
        emit TransactionExecuted(_target, _value, _data);
        return _result;
    }

    /**
     * @dev {IAccount-setOwner}を参照
     */
    function setOwner(address _owner) external override onlyOwner {
        _transferOwnership(_owner);
    }

    receive() external payable {}

    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
        if (owner() == registry) {
            return IERC1271(registry).isValidSignature(hash, signature);
        }

        bool isValid = SignatureChecker.isValidSignatureNow(owner(), hash, signature);
        if (isValid) {
            return IERC1271.isValidSignature.selector;
        }

        return "";
    }
}
```

## セキュリティ上の考慮事項

### フロントランニング

アカウントレジストリインスタンスへの `createAccount` 呼び出しを通じた予約所有アカウントのデプロイは、悪意のあるアクターによってフロントランニングされる可能性があります。ただし、悪意のあるアクターが呼び出しデータの `owner` パラメーターを変更しようとした場合、アカウントレジストリインスタンスは署名が無効であると判断し、トランザクションをリバートします。したがって、成功したフロントランニングトランザクションは、元のトランザクションと同一のアカウントインスタンスをデプロイすることになり、元の所有者がアドレスの管理権を得ることができます。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。