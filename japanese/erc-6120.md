---
original: 6259b6101be817e0f1ef87ac6b8af040f1693d8f8470e9f8305109b9db7d8349
---

---
eip: 6120
title: ユニバーサル トークン ルーター
description: 単一のルーター コントラクトにより、トークンを承認してから呼び出すパターンではなく、転送して呼び出すパターンでアプリケーション コントラクトにトークンを送信できるようになります。
author: Derivable (@derivable-labs)、Zergity (@Zergity)、Ngo Quang Anh (@anhnq82)、BerlinP (@BerlinP)、Khanh Pham (@blackskin18)、Hal Blackburn (@h4l)
discussions-to: https://ethereum-magicians.org/t/eip-6120-universal-token-router/12142
status: レビュー
type: Standards Track
category: ERC
created: 2022-12-12
requires: 20, 165, 721, 1014, 1155
---

## 概要

ETH は、トランザクションでデフォルトの動作として「転送して呼び出す」を設計しています。残念ながら、[ERC-20](./eip-20.md) はこのパターンを念頭に置いて設計されておらず、新しい規格は既に展開されているトークン コントラクトに適用できません。

アプリケーションとルーター コントラクトは「承認してから呼び出す」パターンを使用する必要があり、これにより、$n\times m\times l$ の `approve` (または `permit`) 署名が必要になります。ここで、$n$ はコントラクト数、$m$ はトークン数、$l$ は口座数です。これらの許可トランザクションは、ユーザー エクスペリエンスを損ない、ユーザー手数料とネットワーク ストレージを大量に消費し、さらにユーザーを深刻なセキュリティ リスクにさらします。なぜなら、監査されていない、検証されていない、アップグレード可能なプロキシ コントラクトを承認する必要があるためです。「承認してから呼び出す」パターンは非常に間違いが起きやすく、最近多くの許可関連のバグと攻撃が見つかっています。

ユニバーサル トークン ルーター (UTR) は、トークンの許可をアプリケーション ロジックから分離し、ETH と同じ方法でトークンをコントラクト呼び出しで使用できるようにします。つまり、他のアプリケーション コントラクトに承認する必要がありません。

ユニバーサル トークン ルーターに承認されたトークンは、所有者が直接署名したトランザクションでのみ使用でき、トークンの種類 (ETH、[ERC-20](./eip-20.md)、[ERC-721](./eip-721.md) または [ERC-1155](./eip-1155.md))、`amountIn`、`amountOutMin`、および `recipient` を含む明確なトークン転送動作が表示されます。

ユニバーサル トークン ルーター コントラクトは、[EIP-1014](./eip-1014.md) SingletonFactory コントラクトを使用して `0x8Bd6072372189A12A2889a56b6ec982fD02b0B87` に展開され、すべての EVM 互換ネットワーク全体で使用できます。これにより、新しいトークン コントラクトがそれを信頼できるスペンダーとして事前に構成できるため、対話的な使用時に承認トランザクションが不要になります。

## 動機

ユーザーがトークンをコントラクトに承認すると、次のことを信頼する必要があります:

* トークンは、ユーザーの許可 (`msg.sender` または `ecrecover`) でのみ使用される
* `delegatecall` を使用しない (つまり、アップグレード可能なプロキシ)

上記のセキュリティ条件を確保することで、ユニバーサル トークン ルーターをすべての対話型アプリケーションで共有でき、古いトークンの大部分の承認トランザクションと、新しいトークンのすべての承認トランザクションを節約できます。

このEIPの前は、ユーザーがトークンの承認済みを使用するトランザクションに署名する際、フロントエンド コードを完全に信頼する必要がありました。これにより、フィッシング サイトの大きなリスクにさらされていました。

ユニバーサル トークン ルーターの関数引数は、トランザクションに署名するときのユーザーの期待値を示すマニフェストとして機能できます。ウォレットのサポートにより、ユーザーはアプリケーション コントラクトとフロントエンド コードを盲目的に信頼するのではなく、予想されるトークンの動作を確認および確認できます。フィッシング サイトはユーザーにとってはるかに簡単に検出および回避できるようになります。

ほとんどのアプリケーション コントラクトは既にユニバーサル トークン ルーターと互換性があり、次の利点を得るためにそれを使用できます:

* ユーザーのトークン許可を他のすべてのアプリケーションと安全に共有する。
* 周辺コントラクトを必要に応じて頻繁に更新する。
* ルーター コントラクトの開発とセキュリティ監査コストを節約する。

ユニバーサル トークン ルーターは、「セキュリティ by プロセス」ではなく、「セキュリティ by 結果」モデルをデセントラライズド アプリケーションに促進します。出力の検証のためにトークン残高の変化を直接照会することで、誤ったまたは悪意のあるコントラクトと対話しても、ユーザー トランザクションを保護できます。非トークンの結果の場合、アプリケーション ヘルパー コントラクトは UTR の出力検証のための追加の結果チェック機能を提供できます。

## 仕様

RFC 2119 およびRFC 8174で説明されているように、この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、および「OPTIONAL」というキーワードは解釈されます。

UTR コントラクトのメイン インターフェイス:

```solidity
interface IUniversalTokenRouter {
    function exec(
        Output[] memory outputs,
        Action[] memory actions
    ) payable;
}
```

### 出力の検証

`Output` は検証のための予想されるトークン残高の変化を定義します。

```solidity
struct Output {
    address recipient;
    uint eip;           // トークン規格: 0 for ETH または EIP 番号
    address token;      // トークン コントラクト アドレス
    uint id;            // ERC-721 および ERC-1155 のトークン ID
    uint amountOutMin;
}
```

`recipient` アドレスのトークン残高は、`exec` 関数の開始時と終了時に各 `outputs` 項目について記録されます。任意の残高変化が `amountOutMin` 未満の場合、トランザクションは `INSUFFICIENT_OUTPUT_AMOUNT` でリバートされます。

ERC-721 用の特別な ID `ERC_721_BALANCE` が予約されており、出力アクションでこれを使用して `recipient` アドレスが所有するすべての ID の合計量を検証できます。

```solidity
ERC_721_BALANCE = keccak256('UniversalTokenRouter.ERC_721_BALANCE')
```

### アクション

`Action` は、トークン入力とコントラクト呼び出しを定義します。

```solidity
struct Action {
    Input[] inputs;
    address code;       // コントラクト コード アドレス
    bytes data;         // コントラクト入力データ
}
```

アクション コード コントラクトは、ID `0x61206120` で [ERC-165](./eip-165.md) インターフェイスを実装する必要があります。このインターフェイス チェックにより、UTR によるトークンの *許可使用* 関数 (例: `transferFrom`) への直接呼び出しが防止されます。したがって、新しいトークン コントラクトはこのインターフェイス ID を実装してはいけません。

```solidity
abstract contract NotToken is ERC165 {
    // IERC165-supportsInterface
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return
            interfaceId == 0x61206120 ||
            super.supportsInterface(interfaceId);
    }
}

contract Application is NotToken {
    // このコントラクトは UTR で使用できます
}
```

### 入力

`Input` は、アクション コントラクトの実行前に転送または準備するトークン入力を定義します。

```solidity
struct Input {
    uint mode;
    address recipient;
    uint eip;           // トークン規格: 0 for ETH または EIP 番号
    address token;      // トークン コントラクト アドレス
    uint id;            // ERC-721 および ERC-1155 のトークン ID
    uint amountIn;
}
```

`mode` は次のいずれかの値を取ります:

* `PAYMENT = 0`: `msg.sender` から `recipient` にトークンを転送するペンディングの支払いを呼び出し、同じトランザクション内のどこからでも `UTR.pay` を呼び出します。
* `TRANSFER = 1`: `msg.sender` から `recipient` にトークンを直接転送します。
* `CALL_VALUE = 2`: アクションに渡す `ETH` 金額を記録します。

`inputs` 引数の各入力は順次処理されます。簡単のため、重複した `PAYMENT` および `CALL_VALUE` 入力は有効ですが、最後の `amountIn` 値のみが使用されます。

#### 支払い入力

`PAYMENT` は、*転送コールバック* パターンを使用するアプリケーション コントラクトに推奨されるモードです。例: フラッシュローン コントラクト、Uniswap/v3-core、Derivable など。

`PAYMENT` モードの各 `Input` について、同じトランザクション内のどこからでも `UTR.pay` を呼び出すことで、最大 `amountIn` のトークンを `msg.sender` から `recipient` に転送できます。

```
UTR
 |
 | PAYMENT
 | (UTR.pay のためのペンディングの支払い)
 |
 |                                  アプリケーション コントラクト
action.code.call ---------------------> |
                                        |
UTR.pay <----------------------- (call) |
                                        |
 | <-------------------------- (return) |
 |
 | (すべてのペンディングの支払いをクリア)
 |
END
```

トークンの許可と `PAYMENT` は本質的に異なります:

* 許可: 特定の `spender` にトークンを誰にでも転送する許可を与える。
* `PAYMENT`: 誰でもそのトランザクション内で `recipient` にのみトークンを転送する許可を与える。

##### 支払いの使用

```solidity
interface IUniversalTokenRouter {
    function pay(bytes memory payment, uint amount);
}
```

`pay` を呼び出すには、`payment` パラメーターを次のようにエンコードする必要があります:

```solidity
payment = abi.encode(
    payer,      // address
    recipient,  // address
    eip,        // uint256
    token,      // address
    id          // uint256
);
```

`payment` バイトは、カスタムの支払いロジックを実行するためにアダプター UTR コントラクトによって渡されるコンテキストとペイロードを渡すためにも使用できます。

##### 支払いの破棄

時には、アプリケーション コントラクトが自身のトークンを `payment.payer` から焼却したい場合など、支払いを実行せずに破棄するのが便利な場合があります。次の関数を使用して、支払いをコールラーのアドレスに検証し、その一部を破棄できます。

```solidity
interface IUniversalTokenRouter {
    function discard(bytes memory payment, uint amount);
}
```

「セキュリティ上の考慮事項」の [支払いの破棄](#支払いの破棄-1) セクションを参照してください。

##### 支払いの有効期間

支払いは UTR ストレージに記録され、そのトランザクション内の `input.action` 外部呼び出しでのみ使用することを目的としています。`UTR.exec` の終了前に、すべての支払いストレージがクリアされます。

### ネイティブ トークンの転送

`UTR` は、ユーザー実行ロジックが ETH の転送を必要とする `receive()` 関数を持つ必要があります。ルーターに転送された `msg.value` は、複数の入力にわたって複数のアクションで使用できます。呼び出し側が `ETH` の移動の責任を負いますが、`exec` 関数は関数の終了前に残りの `ETH` を返金する必要があります。

「セキュリティ上の考慮事項」の [再入力](#再入力) セクションを参照してください。

### 使用例

#### Uniswap V2 ルーター

従来の関数:

```solidity
UniswapV2Router01.swapExactTokensForTokens(
    uint amountIn,
    uint amountOutMin,
    address[] calldata path,
address to,
uint deadline
)
```

`UniswapV2Helper01.swapExactTokensForTokens` は、トークン転送部分を除いた、これを修正したバージョンです。

ユーザーが署名してスワップを実行するこのトランザクション:

```javascript
UniversalTokenRouter.exec([{
    recipient: to,
    eip: 20,
    token: path[path.length-1],
    id: 0,
    amountOutMin,
}], [{
    inputs: [{
        mode: TRANSFER,
        recipient: UniswapV2Library.pairFor(factory, path[0], path[1]),
        eip: 20,
        token: path[0],
        id: 0,
        amountIn: amountIn,
    }],
    code: UniswapV2Helper01.address,
    data: encodeFunctionData("swapExactTokensForTokens", [
        amountIn,
        amountOutMin,
        path,
        to,
        deadline,
    ]),
}])
```

#### Uniswap V3 ルーター

従来のルーター コントラクト:

```solidity
contract SwapRouter {
    // プールがこの関数を呼び出して入力トークンを支払う
    function pay(
        address token,
        address payer,
        address recipient,
        uint256 value
    ) internal {
        ...
        // 支払いを引き出す
        TransferHelper.safeTransferFrom(token, payer, recipient, value);
    }
}
```

UTR で使用するヘルパー コントラクト:

```solidity
contract SwapHelper {
    // プールがこの関数を呼び出して入力トークンを支払う
    function pay(
        address token,
        address payer,
        address recipient,
        uint256 value
    ) internal {
        ...
        // 支払いを引き出す
        bytes memory payment = abi.encode(payer, recipient, 20, token, 0);
        UTR.pay(payment, value);
    }
}
```

ユーザーが `PAYMENT` モードを使用して `exactInput` 機能を実行するこのトランザクション:

```javascript
UniversalTokenRouter.exec([{
    eip: 20,
    token: tokenOut,
    id: 0,
    amountOutMin: 1,
    recipient: to,
}], [{
    inputs: [{
        mode: PAYMENT,
        eip: 20,
        token: tokenIn,
        id: 0,
        amountIn: amountIn,
        recipient: pool.address,
    }],
    code: SwapHelper.address,
    data: encodeFunctionData("exactInput", [...]),
}])
```

#### 許可アダプター

直接の許可を使用するアプリケーションとルーター コントラクト用の簡単な非再入力 ERC-20 アダプター。

```solidity
contract AllowanceAdapter is ReentrancyGuard {
    struct Input {
        address token;
        uint amountIn;
    }

    function approveAndCall(
        Input[] memory inputs,
        address spender,
        bytes memory data,
        address leftOverRecipient
    ) external payable nonReentrant {
        for (uint i = 0; i < inputs.length; ++i) {
            Input memory input = inputs[i];
            IERC20(input.token).approve(spender, input.amountIn);
        }

        (bool success, bytes memory result) = spender.call{value: msg.value}(data);
        if (!success) {
            assembly {
                revert(add(result, 32), mload(result))
            }
        }

        for (uint i = 0; i < inputs.length; ++i) {
            Input memory input = inputs[i];
            // 許可をすべてクリア
            IERC20(input.token).approve(spender, 0);
            uint leftOver = IERC20(input.token).balanceOf(address(this));
            if (leftOver > 0) {
                TransferHelper.safeTransfer(input.token, leftOverRecipient, leftOver);
            }
        }
    }
}
```

UTR を使用してUniswap V2 ルーターと対話するために構築されたこのトランザクション:

```javascript
const { data: routerData } = await uniswapRouter.populateTransaction.swapExactTokensForTokens(
    amountIn,
    amountOutMin,
    path,
    to,
    deadline,
)

const { data: adapterData } = await adapter.populateTransaction.approveAndCall(
    [{
        token: path[0],
        amountIn,
    }],
    uniswapRouter.address,
    routerData,
    leftOverRecipient,
)

await utr.exec([], [{
    inputs: [{
        mode: TRANSFER,
        recipient: adapter.address,
        eip: 20,
        token: path[0],
        id: 0,
        amountIn,
    }],
    code: adapter.address,
    data: adapterData,
}])
```

## 根拠

`Permit` 型の署名はサポートされていません。ユニバーサル トークン ルーターの目的は、新しいトークンのすべての対話型 `approve` 署名と、古いトークンの *ほとんど* の `approve` 署名を排除することです。

## 下位互換性

### トークン

古いトークン コントラクト (ERC-20、ERC-721 およびERC-1155) には、各アカウントに対してユニバーサル トークン ルーターの承認が1回必要です。

新しいトークン コントラクトは、ユニバーサル トークン ルーターを信頼できるスペンダーとして事前に構成できるため、対話的な使用時に承認トランザクションは不要です。

```solidity
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

/**
 * @dev {ERC20} トークン規格の実装で、信頼できる ERC6120 コントラクトを無制限のスペンダーとしてサポートします。
 */
contract ERC20WithUTR is ERC20 {
    address immutable UTR;

    /**
     * @dev {name}、{symbol} および ERC6120 の {utr} アドレスの値を設定します。
     *
     * これらの3つの値はすべて不変です。つまり、構築時に1回のみ設定できます。
     *
     * @param utr 信頼できる ERC6120 サポートを無効にするには 0 にできます。
     */
    constructor(string memory name, string memory symbol, address utr) ERC20(name, symbol) {
        UTR = utr;
    }

    /**
     * @dev {IERC20-allowance} を参照してください。
     */
    function allowance(address owner, address spender) public view virtual override returns (uint256) {
        if (spender == UTR && spender != address(0)) {
            return type(uint256).max;
        }
        return super.allowance(owner, spender);
    }

    /**
     * `spender` が UTR の場合は、許可をチェックまたは更新しません。
     */
    function _spendAllowance(address owner, address spender, uint256 amount) internal virtual override {
        if (spender == UTR && spender != address(0)) {
            return;
        }
        super._spendAllowance(owner, spender, amount);
    }
}
```

### アプリケーション

UTR と **互換性がない** アプリケーション コントラクトは、所有権転送機能なしに内部ストレージで `msg.sender` を受益者アドレスとして使用するものだけです。

`recipient` (または `to`) 引数を受益者アドレスとして受け入れるすべてのアプリケーション コントラクトは、そのままで UTR と互換性があります。

(ERC-20、ERC-721、およびERC-1155) トークンを転送するアプリケーション コントラクトは、`recipient` を関数に追加するためのアダプターが必要です。

```solidity
// WETH 用のサンプル アダプター コントラクト
contract WethAdapter {
    function deposit(address recipient) external payable {
        IWETH(WETH).deposit(){value: msg.value};
        TransferHelper.safeTransfer(WETH, recipient, msg.value);
    }
}
```

追加のヘルパーとアダプター コントラクトが必要になる可能性がありますが、それらはほとんどが周辺的で非侵襲的です。トークンや許可を保持していないため、頻繁に更新でき、コア アプリケーション コントラクトに与える影響はほとんどありません。

## 参考実装

Derivable Labs による参考実装で、Hacken によってオーディットされています。

```solidity
/// @title EIP-6120 の実装。
/// @author Derivable Labs
contract UniversalTokenRouter is ERC165, IUniversalTokenRouter {
    uint256 constant PAYMENT       = 0;
    uint256 constant TRANSFER      = 1;
    uint256 constant CALL_VALUE    = 2;

    uint256 constant EIP_ETH       = 0;

    uint256 constant ERC_721_BALANCE = uint256(keccak256('UniversalTokenRouter.ERC_721_BALANCE'));

    /// @dev 一時的なペンディングの支払い
    mapping(bytes32 => uint256) t_payments;

    /// @dev ユーザー実行のために ETH を受け入れる (例: WETH.withdraw)
    receive() external payable {}

    /// ルーターの主要なエントリ ポイント
    /// @param outputs 出力検証のためのトークンの動作
    /// @param actions ルーターのアクションと実行のための入力
    function exec(
        Output[] memory outputs,
        Action[] memory actions
    ) external payable virtual override {
    unchecked {
        // アクションが実行される前に予想される残高を追跡
        for (uint256 i = 0; i < outputs.length; ++i) {
            Output memory output = outputs[i];
            uint256 balance = _balanceOf(output);
            uint256 expected = output.amountOutMin + balance;
            require(expected >= balance, 'UTR: OUTPUT_BALANCE_OVERFLOW');
            output.amountOutMin = expected;
        }

        address sender = msg.sender;

        for (uint256 i = 0; i < actions.length; ++i) {
            Action memory action = actions[i];
            uint256 value;
            for (uint256 j = 0; j < action.inputs.length; ++j) {
                Input memory input = action.inputs[j];
                uint256 mode = input.mode;
                if (mode == CALL_VALUE) {
                    // eip と id は無視される
                    value = input.amountIn;
                } else {
                    if (mode == PAYMENT) {
                        bytes32 key = keccak256(abi.encode(sender, input.recipient, input.eip, input.token, input.id));
                        t_payments[key] = input.amountIn;
                    } else if (mode == TRANSFER) {
                        _transferToken(sender, input.recipient, input.eip, input.token, input.id, input.amountIn);
                    } else {
                        revert('UTR: INVALID_MODE');
                    }
                }
            }
            if (action.code != address(0) || action.data.length > 0 || value > 0) {
                require(
                    ERC165Checker.supportsInterface(action.code, 0x61206120),
                    "UTR: NOT_CALLABLE"
                );
                (bool success, bytes memory result) = action.code.call{value: value}(action.data);
                if (!success) {
                    assembly {
                        revert(add(result,32),mload(result))
                    }
                }
            }
            // すべての一時的なストレージをクリア
            for (uint256 j = 0; j < action.inputs.length; ++j) {
                Input memory input = action.inputs[j];
                if (input.mode == PAYMENT) {
                    // 一時的なストレージ
                    bytes32 key = keccak256(abi.encodePacked(
                        sender, input.recipient, input.eip, input.token, input.id
                    ));
                    delete t_payments[key];
                }
            }
        }

        // 残りの ETH を返金
        uint256 leftOver = address(this).balance;
        if (leftOver > 0) {
            TransferHelper.safeTransferETH(sender, leftOver);
        }

        // 残高の変化を検証
        for (uint256 i = 0; i < outputs.length; ++i) {
            Output memory output = outputs[i];
            uint256 balance = _balanceOf(output);
            // 注: output.amountOutMin は `expected` として再利用される
            require(balance >= output.amountOutMin, 'UTR: INSUFFICIENT_OUTPUT_AMOUNT');
        }
    } }
    
    /// ペンディングの支払いを使用する。input.action から呼び出すことを目的としています。
    /// @param payment エンコードされた支払いデータ
    /// @param amount 支払いで使用するトークン量
    function pay(bytes memory payment, uint256 amount) external virtual override {
        discard(payment, amount);
        (
            address sender,
            address recipient,
            uint256 eip,
            address token,
            uint256 id
        ) = abi.decode(payment, (address, address, uint256, address, uint256));
        _transferToken(sender, recipient, eip, token, id, amount);
    }

    /// ペンディングの支払いの一部を破棄する。トークンを転送せずに支払いを検証するために、input.action から呼び出すことができます。
    /// @param payment エンコードされた支払いデータ
    /// @param amount 支払いで使用するトークン量
    function discard(bytes memory payment, uint256 amount) public virtual override {
        bytes32 key = keccak256(payment);
        require(t_payments[key] >= amount, 'UTR: INSUFFICIENT_PAYMENT');
        unchecked {
            t_payments[key] -= amount;
        }
    }

    // IERC165-supportsInterface
    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return
            interfaceId == type(IUniversalTokenRouter).interfaceId ||
            super.supportsInterface(interfaceId);
    }

    function _transferToken(
        address sender,
        address recipient,
        uint256 eip,
        address token,
        uint256 id,
        uint256 amount
    ) internal virtual
{
        if (eip == 20) {
            TransferHelper.safeTransferFrom(token, sender, recipient, amount);
        } else if (eip == 1155) {
            IERC1155(token).safeTransferFrom(sender, recipient, id, amount, "");
        } else if (eip == 721) {
            IERC721(token).safeTransferFrom(sender, recipient, id);
        } else {
            revert("UTR: INVALID_EIP");
        }
    }

    function _balanceOf(
        Output memory output
    ) internal view virtual returns (uint256 balance) {
        uint256 eip = output.eip;
        if (eip == 20) {
            return IERC20(output.token).balanceOf(output.recipient);
        }
        if (eip == 1155) {
            return IERC1155(output.token).balanceOf(output.recipient, output.id);
        }
        if (eip == 721) {
            if (output.id == ERC_721_BALANCE) {
                return IERC721(output.token).balanceOf(output.recipient);
            }
            try IERC721(output.token).ownerOf(output.id) returns (address currentOwner) {
                return currentOwner == output.recipient ? 1 : 0;
            } catch {
                return 0;
            }
        }
        if (eip == EIP_ETH) {
            return output.recipient.balance;
        }
        revert("UTR: INVALID_EIP");
    }
}
```

## セキュリティ上の考慮事項

### ERC-165 トークン

トークン コントラクトは、ID `0x61206120` で ERC-165 インターフェイスをサポートしてはいけません。これは、UTR で呼び出されるためにトークン以外のコントラクトに予約されています。ID `0x61206120` のインターフェイスを持つトークンが UTR に承認されている場合、誰でも制限なくそれを使用できます。

### 再入力

UTR コントラクトに転送されたトークンは永久に失われます。これらを外部に転送する方法がないためです。中間アドレスでトークンを保持する必要があるアプリケーションは、安全な実行のためにリエントラント ガードを備えた独自のヘルパー コントラクトを使用する必要があります。

ETH は、アクション呼び出しで使用される前に UTR コントラクトに転送する必要があります (`CALL_VALUE` を使用)。この ETH 値は、アクション コードの内部または悪意のあるトークン機能でのリエントラント呼び出しを使用して UTR から吸い出すことができます。ユーザーがその取引で使用するよりも多くの ETH を転送しない限り、この攻撃は不可能です。

```solidity
// 100 を転送しますが、60 のみ使用します。
// したがって、この取引で最大 40 wei が悪用される可能性があります
UniversalTokenRouter.exec([
    ...
], [{
    inputs: [{
        mode: CALL_VALUE,
        eip: 20,
        token: 0,
        id: 0,
        amountIn: 60,   // 60 を使用
        recipient: AddressZero,
    }],
    ...
}], {
    value: 100,   // 100 を転送
})
```

### 支払いの破棄

`pay` 関数の結果は、呼び出し後のバランスを照会することで確認できるため、UTR コントラクトを無条件に呼び出すことができます。ただし、`discard` 関数の実行を検証する方法がないため、信頼できる UTR コントラクトでのみ使用する必要があります。

## 著作権

著作権およびその関連権利は [CC0](../LICENSE.md) で放棄されています。