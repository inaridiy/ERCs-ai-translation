---
original: cef274dd8d3c43198e55c617c468f9d166d88d089b854ff3522b744b37230ec6
---

---
eip: 6538
title: ステルスメタアドレスレジストリ
description: エンティティがシグネチャを使用して直接または第三者を通じてステルスメタアドレスを登録するための標準的なコントラクト。
author: Matt Solomon (@mds1)、Toni Wahrstätter (@nerolation)、Ben DiFrancesco (@apbendi)、Vitalik Buterin (@vbuterin)、Gary Ghayrat (@garyghayrat)
discussions-to: https://ethereum-magicians.org/t/stealth-meta-address-registry/12888
status: Final
type: Standards Track
category: ERC
created: 2023-01-24
requires: 712, 1271, 5564
---

## 概要

この仕様は、[ERC-5564](./eip-5564.md)を拡張することで、エンティティのステルスメタアドレスを保存および取得する標準化された方法を定義しています。エンティティは自身のステルスメタアドレスを直接登録できます。第三者も[EIP-712](./eip-712.md)または[EIP-1271](./eip-1271.md)のシグネチャを使用して代理で登録できます。一度登録されると、エンティティのステルスメタアドレスはどのスマートコントラクトやユーザーでも取得できます。[ERC-5564](./eip-5564.md)で指定された`generateStealthAddress`を使用して、エンティティのアドレスを明かすことなくステルスアドレスに資産を送信できます。

## 動機

ステルスアドレスの生成を標準化することで、Ethereumのプライバシー機能を大幅に向上させる可能性があります。受取人の匿名性を維持しながら、資産の転送を行うことができるためです。ユーザーがステルスメタアドレスを保存できる中央のスマートコントラクトを導入することで、EOAやコントラクトがさまざまなステルスアドレススキームを使用してステルス型のやり取りを行うことができます。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

このコントラクトは、エンティティのステルスメタアドレスを保存する`ERC6538Registry`を定義します。これらのエンティティは、アドレス、ENS名、またはその他の識別子によって識別される可能性があります。これは単一のコントラクトでなければならず、チェーンごとに1つのインスタンスが存在します。

コントラクトは以下のように指定されています。1バイトの整数を使用してステルスアドレススキームを識別します。この整数は、さまざまなステルスアドレススキームを区別するために使用されます。このERCでは、[ERC-5564](./eip-5564.md)で指定されているSECP256k1曲線暗号スキームとビュータグを持つスキームID `1`を概説しています。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.23;

/// @notice `ERC6538Registry` コントラクトは、アカウントとそのステルスメタアドレスをマッピングします。
/// [ERC-6538](https://eips.ethereum.org/EIPS/eip-6538)を参照してください。
contract ERC6538Registry {
  /// @notice `registerKeysOnBehalf`に無効なシグネチャが提供された場合に発生するエラー。
  error ERC6538Registry__InvalidSignature();

  /// @notice `user`が`registerKeysOnBehalf`で使用する次のnonce。
  /// @dev `registrant`は標準の160ビットアドレスまたはその他の識別子である可能性があります。
  /// @dev `schemeId`はステルスアドレススキームの整数識別子です。
  mapping(address registrant => mapping(uint256 schemeId => bytes)) public stealthMetaAddressOf;

  /// @notice 有効なシグネチャが1回しか使用されないようにするためのnonce。
  /// @dev `registrant`はユーザーアドレスです。
  /// @dev `nonce`は有効な`registerKeysOnBehalf`呼び出しごとにインクリメントされます。
  mapping(address registrant => uint256) public nonceOf;

  /// @notice `registerKeysOnBehalf`で使用されるEIP-712タイプハッシュ。
  bytes32 public constant ERC6538REGISTRY_ENTRY_TYPE_HASH =
    keccak256("Erc6538RegistryEntry(uint256 schemeId,bytes stealthMetaAddress,uint256 nonce)");

  /// @notice このコントラクトが最初に展開されたチェーンID。
  uint256 internal immutable INITIAL_CHAIN_ID;

  /// @notice このコントラクトで使用されるドメインセパレーター。
  bytes32 internal immutable INITIAL_DOMAIN_SEPARATOR;

  /// @notice 登録者がステルスメタアドレスを更新したときに発行されるイベント。
  /// @param registrant ステルスメタアドレスを登録したアカウント。
  /// @param schemeId ERC-5564で指定されているように、適用されたステルスアドレススキームの識別子(例: secp256k1の場合は1)。
  /// @param stealthMetaAddress ステルスメタアドレス。
  /// [ERC-5564](https://eips.ethereum.org/EIPS/eip-5564)は、[ERC-3770](https://eips.ethereum.org/EIPS/eip-3770)に基づいてステルスメタアドレスの形式を定義しており、
  /// それらを `st:<shortName>:0x<spendingPubKey>:<viewingPubKey>` として指定しています。
  /// チェーン(`shortName`)は`ERC6538Registry`がデプロイされているチェーンに基づいて暗黙的に決まるため、
  /// この`stealthMetaAddress`は圧縮された`spendingPubKey`と`viewingPubKey`を連結したものです。
  event StealthMetaAddressSet(
    address indexed registrant, uint256 indexed schemeId, bytes stealthMetaAddress
  );

  /// @notice 登録者がnonceをインクリメントしたときに発行されるイベント。
  /// @param registrant nonceをインクリメントしたアカウント。
  /// @param newNonce 新しいnonce値。
  event NonceIncremented(address indexed registrant, uint256 newNonce);

  constructor() {
    INITIAL_CHAIN_ID = block.chainid;
    INITIAL_DOMAIN_SEPARATOR = _computeDomainSeparator();
  }

  /// @notice 呼び出し元のステルスメタアドレスを指定のスキームIDに設定します。
  /// @param schemeId ERC-5564で指定されているように、適用されたステルスアドレススキームの識別子(例: secp256k1の場合は1)。
  /// @param stealthMetaAddress 登録するステルスメタアドレス。
  function registerKeys(uint256 schemeId, bytes calldata stealthMetaAddress) external {
    stealthMetaAddressOf[msg.sender][schemeId] = stealthMetaAddress;
    emit StealthMetaAddressSet(msg.sender, schemeId, stealthMetaAddress);
  }

  /// @notice `registrant`のステルスメタアドレスを指定のスキームIDに設定します。
  /// @param registrant 登録者のアドレス。
  /// @param schemeId ERC-5564で指定されているように、適用されたステルスアドレススキームの識別子(例: secp256k1の場合は1)。
  /// @param signature 登録を許可する`registrant`のシグネチャ。
  /// @param stealthMetaAddress 登録するステルスメタアドレス。
  /// @dev EOAシグネチャとEIP-1271シグネチャの両方をサポートしています。
  /// @dev シグネチャが無効な場合は失敗します。
  function registerKeysOnBehalf(
    address registrant,
    uint256 schemeId,
    bytes memory signature,
    bytes calldata stealthMetaAddress
  ) external {
    bytes32 dataHash;
    address recoveredAddress;

    unchecked {
      dataHash = keccak256(
        abi.encodePacked(
          "\x19\x01",
          DOMAIN_SEPARATOR(),
          keccak256(
            abi.encode(
              ERC6538REGISTRY_ENTRY_TYPE_HASH,
              schemeId,
              keccak256(stealthMetaAddress),
              nonceOf[registrant]++
            )
          )
        )
      );
    }

    if (signature.length == 65) {
      bytes32 r;
      bytes32 s;
      uint8 v;
      assembly ("memory-safe") {
        r := mload(add(signature, 0x20))
        s := mload(add(signature, 0x40))
        v := byte(0, mload(add(signature, 0x60)))
      }
      recoveredAddress = ecrecover(dataHash, v, r, s);
    }

    if (
      (
        (recoveredAddress == address(0) || recoveredAddress != registrant)
          && (
            IERC1271(registrant).isValidSignature(dataHash, signature)
              != IERC1271.isValidSignature.selector
          )
      )
    ) revert ERC6538Registry__InvalidSignature();

    stealthMetaAddressOf[registrant][schemeId] = stealthMetaAddress;
    emit StealthMetaAddressSet(registrant, schemeId, stealthMetaAddress);
  }

  /// @notice 送信者のnonceをインクリメントして既存のシグネチャを無効化します。
  function incrementNonce() external {
    unchecked {
      nonceOf[msg.sender]++;
    }
    emit NonceIncremented(msg.sender, nonceOf[msg.sender]);
  }

  /// @notice このコントラクトで使用されるドメインセパレーターを返します。
  /// @dev ドメインセパレーターはチェーンフォークが発生した場合に再計算されます。
  function DOMAIN_SEPARATOR() public view returns (bytes32) {
    return block.chainid == INITIAL_CHAIN_ID ? INITIAL_DOMAIN_SEPARATOR : _computeDomainSeparator();
  }

  /// @notice このコントラクトのドメインセパレーターを計算します。
  function _computeDomainSeparator() internal view returns (bytes32) {
    return keccak256(
      abi.encode(
        keccak256(
          "EIP712Domain(string name,string version,uint256 chainId,address verifyingContract)"
        ),
        keccak256("ERC6538Registry"),
        keccak256("1.0"),
        block.chainid,
        address(this)
      )
    );
  }
}

/// @notice https://eips.ethereum.org/EIPS/eip-1271[ERC-1271]で定義されているコントラクトのERC1271標準シグネチャ検証メソッドのインターフェース。
interface IERC1271 {
  /// @notice 提供されたシグネチャが_dataのハッシュに対して有効かどうかを返す必要があります。
  /// @param hash 署名対象のデータのハッシュ
  /// @param signature 関連付けられた_dataのシグネチャバイト配列
  function isValidSignature(bytes32 hash, bytes memory signature)
    external
    view
    returns (bytes4 magicValue);
}

```

このコントラクトのインターフェースは以下のように定義されています:

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity 0.8.23;

/// @dev アカウントのステルスメタアドレスをマッピングするために`ERC6538Registry`コントラクトを呼び出すためのインターフェース。
/// [ERC-6538](https://eips.ethereum.org/EIPS/eip-6538)を参照してください。
interface IERC6538Registry {
  /// @notice `registerKeysOnBehalf`に無効なシグネチャが提供された場合に発生するエラー。

  /// @dev 登録者がステルスメタアドレスを更新したときに発行されるイベント。
  /// @param registrant ステルスメタアドレスを登録したアカウント。
  /// @param schemeId ERC-5564で指定されているように、適用されたステルスアドレススキームの識別子(例: secp256k1の場合は1)。
  /// @param stealthMetaAddress ステルスメタアドレス。
  /// [ERC-5564](https://eips.ethereum.org/EIPS/eip-5564)は、[ERC-3770](https://eips.ethereum.org/EIPS/eip-3770)に基づいてステルスメタアドレスの形式を定義しており、
  /// それらを `st:<shortName>:0x<spendingPubKey>:<viewingPubKey>` として指定しています。
  /// チェーン(`shortName`)は`ERC6538Registry`がデプロイされているチェーンに基づいて暗黙的に決まるため、
  /// この`stealthMetaAddress`は`spendingPubKey`と`viewingPubKey`を連結したものです。
  event StealthMetaAddressSet(
    address indexed registrant, uint256 indexed schemeId, bytes stealthMetaAddress
  );

  /// @notice 登録者がnonceをインクリメントしたときに発行されるイベント。
  /// @param registrant nonceをインクリメントしたアカウント。
  /// @param newNonce 新しいnonce値。
  event NonceIncremented(address indexed registrant, uint256 newNonce);

  /// @notice 呼び出し元のステルスメタアドレスを指定のスキームIDに設定します。
  /// @param schemeId ERC-5564で指定されているように、適用され
たステルスアドレススキームの識別子(例: secp256k1の場合は1)。
/// @param stealthMetaAddress 登録するステルスメタアドレス。
function registerKeys(uint256 schemeId, bytes calldata stealthMetaAddress) external;

/// @notice `registrant`のステルスメタアドレスを指定のスキームIDに設定します。
/// @param registrant 登録者のアドレス。
/// @param schemeId ERC-5564で指定されているように、適用されたステルスアドレススキームの識別子(例: secp256k1の場合は1)。
/// @param signature 登録を許可する`registrant`のシグネチャ。
/// @param stealthMetaAddress 登録するステルスメタアドレス。
/// @dev EOAシグネチャとEIP-1271シグネチャの両方をサポートしています。
/// @dev シグネチャが無効な場合は失敗します。
function registerKeysOnBehalf(
  address registrant,
  uint256 schemeId,
  bytes memory signature,
  bytes calldata stealthMetaAddress
) external;

/// @notice 送信者のnonceをインクリメントして既存のシグネチャを無効化します。
function incrementNonce() external;

/// @notice このコントラクトで使用されるドメインセパレーターを返します。
function DOMAIN_SEPARATOR() external view returns (bytes32);

/// @notice 指定の`registrant`と`schemeId`のステルスメタアドレスを返します。
function stealthMetaAddressOf(address registrant, uint256 schemeId)
  external
  view
  returns (bytes memory);

/// @notice `registerKeysOnBehalf`で使用されるEIP-712タイプハッシュを返します。
function ERC6538REGISTRY_ENTRY_TYPE_HASH() external view returns (bytes32);

/// @notice 指定の`registrant`のnonceを返します。
function nonceOf(address registrant) external view returns (uint256);

```

### デプロイメントメソッド

`ERC6538Registry`コントラクトは、`0x4e59b44847b379578588920ca78fbf26c0b4956c`の決定論的デプロイヤーを使用して`CREATE2`によって`0x6538E6bf4B0eBd30A8Ea093027Ac2422ce5d6538`にデプロイされます。ソルトは`0x7cac4e512b1768c627c9e711c7a013f1ad0766ef5125c59fb7161dade58da078`です。

## 根拠

ステルスメタアドレスの登録に中央のスマートコントラクトを持つことには以下のような利点があります:

1. 登録されたステルスメタアドレスを簡単に取得および利用できるため、他のスマートコントラクトとの相互運用性が保証されます。これにより、ENSやGnosis Safeなどのアプリケーションがその情報を使用してステルスアドレスを自身のサービスに統合できます。

2. ユーザーがステルスメタアドレスを取得するためにオフチェーンのソースに依存する必要がなくなります。

3. このコントラクトでステルスメタアドレスを登録することで、ユーザーがステルスのやり取りに参加する準備ができていることを標準的な方法で示すことができます。

4. レジストリをシングルトンコントラクトとしてデプロイすることで、複数のプロジェクトが同じステルスメタアドレスセットにアクセスでき、標準化が向上します。

## 下位互換性

このEIPは完全に下位互換性があります。

## 参考実装

`ERC6538Registry`コントラクトの実装は[こちら](../assets/eip-6538/contracts/ERC6538Registry.sol)、`IERC6538Registry.sol`のインターフェースは[こちら](../assets/eip-6538/contracts/interfaces/IERC6538Registry.sol)にあります。

## セキュリティ上の考慮事項

秘密鍵が侵害された場合、登録者は今後の資金の損失を防ぐためにすぐにステルスキーレジストリから登録を解除する必要があります。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。