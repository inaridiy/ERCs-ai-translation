---
original: cee2aee6b61c2c501894042b9d266ea141f4cc1ac0ec8b1b482c3fa3c1826369
---

---
eip: 5247
title: スマートコントラクト実行可能提案インターフェース
description: 提案を作成し実行するためのインターフェース。
author: Zainan Victor Zhou (@xinbenlv)
discussions-to: https://ethereum-magicians.org/t/erc-5247-executable-proposal-standard/9938
status: Review
type: Standards Track
category: ERC
created: 2022-07-13
---

## 概要

このEIPは、"スマートコントラクト実行可能提案"のインターフェースを提示します。これは、提案が提出され、記録され、場合によっては実行されるものです。このような提案には、対象のコントラクトアドレス、送金する Ether の量、ガス制限、呼び出しデータなどの情報が含まれます。

## 動機

コードの実行と実際の実行を分離することが必要な場合があります。

このEIPの典型的な使用例は、分散型自治組織(DAO)です。提案者が提案を作成し、支持を訴えます。メンバーはその提案を承認するかどうかを選択し、投票します(「ERC-1202」を参照)。合意が形成されたら、最終的に提案が実行されます。

2つ目の典型的な使用例は、受託者、代理人、弁護士など、信頼できる人物がいる場合です。スマート提案が最初に作成され、議論、承認された後に実行されます。

3つ目の使用例は、ある人物が別の人物に"オファー"を行う場合です。スマート提案はオファーとして提示され、相手がそれを受け入れれば実行されます。

## 仕様

この文書における "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY"、"OPTIONAL" のキーワードは、RFC 2119に記載されているように解釈されるものとします。

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.17;

interface IERC5247 {
    event ProposalCreated(
        address indexed proposer,
        uint256 indexed proposalId,
        address[] targets,
        uint256[] values,
        uint256[] gasLimits,
        bytes[] calldatas,
        bytes extraParams
    );

    event ProposalExecuted(
        address indexed executor,
        uint256 indexed proposalId,
        bytes extraParams
    );

    function createProposal(
        uint256 proposalId,
        address[] calldata targets,
        uint256[] calldata values,
        uint256[] calldata gasLimits,
        bytes[] calldata calldatas,
        bytes calldata extraParams
    ) external returns (uint256 registeredProposalId);

    function executeProposal(uint256 proposalId, bytes calldata extraParams) external;
}
```

## 根拠

* 当初、このインターフェースは「ERC-1202」の一部でした。しかし、提案自体が投票以外の多くの用途を持つ可能性があります。提案に特定の形式を要求する必要がない場合もあります。そのため、投票インターフェースと提案インターフェースを*分離することにしました*。
* `target`、`value`、`calldata`の配列を使用することで、提案に任意の長さの複数の関数呼び出しを含めることができます。
* `createProposal`で`registeredProposalId`を返すことで、実装側が提案IDの形式を決めることができます。

## テストケース

簡単なテストケースは以下の通りです。

```ts
        it("Should work for a simple case", async function () {
            const { contract, erc721, owner } = await loadFixture(deployFixture);
            const callData1 = erc721.interface.encodeFunctionData("mint", [owner.address, 1]);
            const callData2 = erc721.interface.encodeFunctionData("mint", [owner.address, 2]);
            await contract.connect(owner)
                .createProposal(
                    0,
                    [erc721.address, erc721.address],
                    [0,0],
                    [0,0],
                    [callData1, callData2],
                    []);
            expect(await erc721.balanceOf(owner.address)).to.equal(0);
            await contract.connect(owner).executeProposal(0, []);
            expect(await erc721.balanceOf(owner.address)).to.equal(2);
        });
```

完全なテストセットは[testProposalRegistry.ts](../assets/eip-5247/testProposalRegistry.ts)を参照してください。

## 参考実装

簡単な参考実装は以下の通りです。

```solidity
    function createProposal(
        uint256 proposalId,
        address[] calldata targets,
        uint256[] calldata values,
        uint256[] calldata gasLimits,
        bytes[] calldata calldatas,
        bytes calldata extraParams
    ) external returns (uint256 registeredProposalId) {
        require(targets.length == values.length, "GeneralForwarder: targets and values length mismatch");
        require(targets.length == gasLimits.length, "GeneralForwarder: targets and gasLimits length mismatch");
        require(targets.length == calldatas.length, "GeneralForwarder: targets and calldatas length mismatch");
        registeredProposalId = proposalCount;
        proposalCount++;

        proposals[registeredProposalId] = Proposal({
            by: msg.sender,
            proposalId: proposalId,
            targets: targets,
            values: values,
            calldatas: calldatas,
            gasLimits: gasLimits
        });
        emit ProposalCreated(msg.sender, proposalId, targets, values, gasLimits, calldatas, extraParams);
        return registeredProposalId;
    }
    function executeProposal(uint256 proposalId, bytes calldata extraParams) external {
        Proposal storage proposal = proposals[proposalId];
        address[] memory targets = proposal.targets;
        string memory errorMessage = "Governor: call reverted without message";
        for (uint256 i = 0; i < targets.length; ++i) {
            (bool success, bytes memory returndata) = proposal.targets[i].call{value: proposal.values[i]}(proposal.calldatas[i]);
            Address.verifyCallResult(success, returndata, errorMessage);
        }
        emit ProposalExecuted(msg.sender, proposalId, extraParams);
    }
```

詳細は[ProposalRegistry.sol](../assets/eip-5247/ProposalRegistry.sol)を参照してください。

## セキュリティ上の考慮事項

議論が必要です。

## 著作権

著作権およびその関連権利は[CC0](../LICENSE.md)により放棄されています。