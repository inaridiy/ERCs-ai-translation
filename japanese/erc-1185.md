---
original: b97eb0851eb644d95316dd4039f8b252700868cf8c3ab96a8dd853825509f4fe
---

---
eip: 1185
title: ENS内のDNSレコードの保存
description: ENSコントラクト内でDNSレコードを保存および検索するシステム。
author: Jim McDonald (@mcdee)
discussions-to: https://ethereum-magicians.org/t/eip1185-dns-resolver-profile-for-ens/1589
status: Review
type: Standards Track
category: ERC
created: 2018-06-26
requires: 137
---

## 概要

このEIPは、DNSレコードの保存と検索機能を提供するENSのレゾルバープロファイルを定義します。これにより、ENSを信頼できるDNS情報の保管場所として使用できるようになります。

## 動機

ENSは、DNS情報を保存するのに非常に望ましい場所です。DNSの分散的な権威性を提供しつつ、情報の所有権と信頼できる提供を分離することができます。ENSでは、ドメインの所有者が自身のDNSレコードを完全に制御できます。また、ENSのスマートコントラクトを通じて、ドメインのサブドメインを別のエンティティに不可逆的に割り当てることができます。

## 仕様

ENSでDNSをサポートするレゾルバープロファイルは、[ERC-137](./eip-137.md)で定義されたレゾルバー仕様に従います。

従来のDNSはゾーンベースのシステムで、ゾーン内のすべてのレコードが同じファイルに保持されています。これにより、簡単さとゾーンの更新の原子性が得られますが、ENSに移植すると大量のガスコストが発生する可能性があります。そのため、このレゾルバーはレコードセットベースで動作します。レコードセットは、`(ドメイン、名前、リソースレコードタイプ)`のタプルで一意に定義されます。例えば、`(example.com、www.example.com、A)`というタプルは、ドメイン`example.com`の名前`www.example.com`のAレコードのレコードセットを定義します。レコードセットには0個以上の値を含めることができます。例えば、`www.example.com`にAレコード`1.2.3.4`と`5.6.7.8`がある場合、上記のタプルには2つの値が含まれます。

レコードセットレベルで動作することにより、このEIPでは一部のDNS機能(ゾーン転送やDNSSECなど)を完全にサポートできません。ゾーンレベルで動作するレゾルバープロファイルを構築することも可能ですが、更新を行うのが非常に高価になるため、このEIPでは検討されていません。

DNSレゾルバーインターフェイスには、DNS情報の設定と照会を行う2つの関数が含まれています。

### setDNSRecords(bytes32 node, bytes data)

`setDNSRecords()`は、指定したノードに対して1つ以上のDNSレコードを設定、更新、または削除します。関数シグネチャは`0x0af179d7`です。

関数の引数は以下の通りです:

- node: ENSの完全修飾ドメインのnamehash。namehashの定義は[ERC-137](./eip-137.md)を参照してください。
- data: DNSワイヤー形式の1つ以上のDNSレコード。値のないレコードは削除されます。同じRRセット内のすべてのレコードは連続している必要があり、そうでない場合は後のRRセットが前のものを上書きします。

### clearDNSZone(bytes32 node)

`clearDNSZone()`は、ドメインのすべてのDNSレコードを削除します。関数シグネチャは`0xad5780af`です。

`setDNSRecords()`を使ってレコードを個別に削除することも可能ですが、所有者がすべてのレコードを知っている必要があり(レゾルバーにはドメインのレコードを反復する方法がない)、複数のトランザクションが必要になる可能性があります。`clearDNSZone()`は、単一の操作でゾーン情報全体を削除します。

関数の引数は以下の通りです:

- node: ENSの完全修飾ドメインのnamehash。namehashの定義は[ERC-137](./eip-137.md)を参照してください。

### dnsRecords(bytes32 node, bytes32 name, uint16 resource) view returns (bytes)

`dnsRecords()`は、指定したノード、名前、リソースのDNSレコードを取得します。関数シグネチャは`0x2461e851`です。

関数の引数は以下の通りです:

- node: ENSの完全修飾ドメインのnamehash。namehashの定義は[ERC-137](./eip-137.md)を参照してください。
- name: レコードの名前のkeccak256ハッシュ(DNSワイヤー形式)。
- resource: リソースレコードID。リソースレコードIDはRFC1035およびその後のRFCで定義されています。

この関数は、DNSワイヤー形式のすべての一致するレコードを返します。レコードが存在しない場合は何も返しません。

### hasDNSRecords(bytes32 node, bytes32 name) view returns (bool)

`hasDNSRecords()`は、指定した名前のドメインにレコードが存在するかどうかを報告します。関数シグネチャは`0x4cbf6ba4`です。

このメソッドは、RFC4592で定義されたワイルドカードリソースを扱うDNSレゾルバーで必要とされます。

関数の引数は以下の通りです:

- node: ENSの完全修飾ドメインのnamehash。namehashの定義は[ERC-137](./eip-137.md)を参照してください。
- name: レコードの名前のkeccak256ハッシュ(DNSワイヤー形式)。

関数は、指定したノードと名前にレコードが存在する場合は`true`を、そうでない場合は`false`を返します。

## 根拠

DNSは連邦制の命名システムであり、上位のエンティティが下位のすべてのものの可用性を制御します(例えば`.org`が`ethereum.org`の可用性を制御します)。分散型のDNSバージョンでは、この制約がなく、ENS内の関連するレコードを持つ任意のドメインに対して直接検索できるようになります。

## 下位互換性

該当なし。

## 参考実装

DNSレゾルバーの参考実装は以下の通りです:

```solidity
pragma solidity ^0.7.4;
import "../ResolverBase.sol";
import "@ensdomains/dnssec-oracle/contracts/RRUtils.sol";

abstract contract DNSResolver is ResolverBase {
    using RRUtils for *;
    using BytesUtils for bytes;

    bytes4 constant private DNS_RECORD_INTERFACE_ID = 0xa8fa5682;
    bytes4 constant private DNS_ZONE_INTERFACE_ID = 0x5c47637c;

    // DNSRecordChanged is emitted whenever a given node/name/resource's RRSET is updated.
    event DNSRecordChanged(bytes32 indexed node, bytes name, uint16 resource, bytes record);
    // DNSRecordDeleted is emitted whenever a given node/name/resource's RRSET is deleted.
    event DNSRecordDeleted(bytes32 indexed node, bytes name, uint16 resource);
    // DNSZoneCleared is emitted whenever a given node's zone information is cleared.
    event DNSZoneCleared(bytes32 indexed node);

    // DNSZonehashChanged is emitted whenever a given node's zone hash is updated.
    event DNSZonehashChanged(bytes32 indexed node, bytes lastzonehash, bytes zonehash);

    // Zone hashes for the domains.
    // A zone hash is an ERC-1577 content hash in binary format that should point to a
    // resource containing a single zonefile.
    // node => contenthash
    mapping(bytes32=>bytes) private zonehashes;

    // Version the mapping for each zone.  This allows users who have lost
    // track of their entries to effectively delete an entire zone by bumping
    // the version number.
    // node => version
    mapping(bytes32=>uint256) private versions;

    // The records themselves.  Stored as binary RRSETs
    // node => version => name => resource => data
    mapping(bytes32=>mapping(uint256=>mapping(bytes32=>mapping(uint16=>bytes)))) private records;

    // Count of number of entries for a given name.  Required for DNS resolvers
    // when resolving wildcards.
    // node => version => name => number of records
    mapping(bytes32=>mapping(uint256=>mapping(bytes32=>uint16))) private nameEntriesCount;

    /**
     * Set one or more DNS records.  Records are supplied in wire-format.
     * Records with the same node/name/resource must be supplied one after the
     * other to ensure the data is updated correctly. For example, if the data
     * was supplied:
     *     a.example.com IN A 1.2.3.4
     *     a.example.com IN A 5.6.7.8
     *     www.example.com IN CNAME a.example.com.
     * then this would store the two A records for a.example.com correctly as a
     * single RRSET, however if the data was supplied:
     *     a.example.com IN A 1.2.3.4
     *     www.example.com IN CNAME a.example.com.
     *     a.example.com IN A 5.6.7.8
     * then this would store the first A record, the CNAME, then the second A
     * record which would overwrite the first.
     *
     * @param node the namehash of the node for which to set the records
     * @param data the DNS wire format records to set
     */
    function setDNSRecords(bytes32 node, bytes calldata data) external authorised(node) {
        uint16 resource = 0;
        uint256 offset = 0;
        bytes memory name;
        bytes memory value;
        bytes32 nameHash;
        // Iterate over the data to add the resource records
        for (RRUtils.RRIterator memory iter = data.iterateRRs(0); !iter.done(); iter.next()) {
            if (resource == 0) {
                resource = iter.dnstype;
                name = iter.name();
                nameHash = keccak256(abi.encodePacked(name));
                value = bytes(iter.rdata());
            } else {
                bytes memory newName = iter.name();
                if (resource != iter.dnstype || !name.equals(newName)) {
                    setDNSRRSet(node, name, resource, data, offset, iter.offset - offset, value.length == 0);
                    resource = iter.dnstype;
                    offset = iter.offset;
                    name = newName;
                    nameHash = keccak256(name);
                    value = bytes(iter.rdata());
                }
            }
        }
        if (name.length > 0) {
            setDNSRRSet(node, name, resource, data, offset, data.length - offset, value.length == 0);
        }
    }

    /**
     * Obtain a DNS record.
     * @param node the namehash of the node for which to fetch the record
     * @param name the keccak-256 hash of the fully-qualified name for which to fetch the record
     * @param resource the ID of the resource as per https://en.wikipedia.org/wiki/List_of_DNS_record_types
     * @return the DNS record in wire format if present, otherwise empty
     */
    function dnsRecord(bytes32 node, bytes32 name, uint16 resource) public view returns (bytes memory) {
        return records[node][versions[node]][name][resource];
    }

    /**
     * Check if a given node has records.
     * @param node the namehash of the node for which to check the records
     * @param name the namehash of the node for which to check the records
     */
    function hasDNSRecords(bytes32 node, bytes32 name) public view returns (bool) {
        return (nameEntriesCount[node][versions[node]][name] != 0);
    }

    /**
     * Clear all information for a DNS zone.
     * @param node the namehash of the node for which to clear the zone
     */
    function clearDNSZone(bytes32 node) public authorised(node) {
        versions[node]++;
        emit DNSZoneCleared(node);
    }

    /**
     * setZonehash sets the hash for the zone.
     * May only be called by the owner of that node in the ENS registry.
     * @param node The node to update.
     * @param hash The zonehash to set
     */
    function setZonehash(bytes32 node, bytes calldata hash) external authorised(node) {
        bytes memory oldhash = zonehashes[node];
        zonehashes[node] = hash;
        emit DNSZonehashChanged(node, oldhash, hash);
    }

    /**
     * zonehash obtains the hash for the zone.
     * @param node The ENS node to query.
     * @return The associated contenthash.
     */
    function zonehash(bytes32 node) external view returns (bytes memory) {
        return zonehashes[node];
    }

    function supportsInterface(bytes4 interfaceID) virtual override public pure returns(bool) {
        return interfaceID == DNS_RECORD_INTERFACE_ID ||
               interfaceID == DNS_ZONE_INTERFACE_ID ||
               super.supportsInterface(interfaceID);
    }

    function setDNSRRSet(
        bytes32 node,
        bytes memory name,
        uint16 resource,
        bytes memory data,
        uint256 offset,
        uint256 size,
        bool deleteRecord) private
    {
        uint256 version = versions[node];
        bytes32 nameHash = keccak256(name);
        bytes memory rrData = data.substring(offset, size);
        if (deleteRecord) {
            if (records[node][version][nameHash][resource].length != 0) {
                nameEntriesCount[node][version][nameHash]--;
            }
            delete(records[node][version][nameHash][resource]);
            emit DNSRecordDeleted(node, name, resource);
        } else {
            if (records[node][version][name
Hash][resource].length == 0) {
                nameEntriesCount[node][version][nameHash]++;
            }
            records[node][version][nameHash][resource] = rrData;
            emit DNSRecordChanged(node, name, resource, rrData);
        }
    }
}