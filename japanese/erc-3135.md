---
original: eb8deb768c86294352a484ab5b907e46d459844020e3b9c5a2350ca4de72f275
---

---
eip: 3135
title: 排他的な請求可能なトークン
author: Zhenyu Sun (@Ungigdu)
discussions-to: https://github.com/ethereum/EIPs/issues/3132
status: 停滞
type: Standards Track
category: ERC
created: 2020-08-10
requires: 20
---

## 簡単な要約

このスタンダードは、トークン発行者がペイヤーの署名を持っている場合にのみトークンを請求できるトークンを定義しています。

## 概要

このEIPは、ERC-20などのデフォルトのトークンスタンダードに追加機能を定義しています。これにより、オンライン/オフラインのサービスプロバイダーが、オフチェーンでトークンの消費に関するメッセージに署名および検証することで、任意の数のユーザーとマイクロペイメントチャネルを確立できます。この仕組みを使うことで、両当事者がブロックチェーンとの対話を最小限に抑えることができ、ガス代を節約し、パフォーマンスを向上させることができます。

## 動機

このEIPには2つの主な目的があります。1つはブロックチェーンとの対話を減らすことで、もう1つはイーサリアムを現実世界の支払い問題にリンクさせることです。

多くの小規模企業がブロックチェーンに基づいた支払いシステムを構築したいと考えていますが、それは難しいと感じています。基本的に2つの方法があります。

1. トークンで直接支払う。多くのウォレットがトークンの受け取りと送金ができますが、イーサリアムでのトランザクションにはガス代がかかり、確認に時間がかかります。
2. ユーザーがトークンをペイメントスマートコントラクトにロックし、サービスプロバイダーがユーザーが署名したペイメントメッセージを使ってトークンをリリースする、マイクロペイメントチャネルを確立する。この方法の利点は、ブロックチェーンとの対話が減り、署名/検証プロセスがオフチェーンで行われることです。しかし、ペイメントコントラクトと対話するには、サービスプロバイダーがDAppを構築する必要があり、多くの小規模企業にはそのリソースがありません。DAppを構築できたとしても、それらは標準化されておらず、ユーザーもDAppブラウザ付きのウォレットを持っていて、その使い方を学ばなければなりません。

このEIPは、マイクロペイメントシステムの相互作用を標準化し、将来的にウォレットがユニバーサルなUIを構築できるようにすることを目的としています。

## 仕様

```solidity

/// @return このトークンの画像URLまたは説明リソース
function iconUrl() external view returns (string memory);

/// @return このトークンの発行者。発行者のみが請求機能を実行できる
function issuer() external view returns (address);

/**
 *  @notice   ペイヤーの預金から消費分を減らす
 *  @dev      msg.sender == issuerであることを確認する
 *  @param    from          ペイヤーのアドレス
 *  @param    consumption   この期間に消費されたトークンの量
 *  @param    epoch         請求または引き出し後にエポックが1つ増加し、各エポックの始めに消費は0に戻る
 *  @param    signature     ペイヤーが署名したペイメントメッセージ
*/
function claim(address from, uint256 consumption, uint256 epoch, bytes calldata signature) external;

function transferIssuer(address newIssuer) external;

/// @notice   ペイヤーのトークン残高から預金残高に金額を移動し、支払いが十分であることを確保する
function deposit(uint256 amount) external;

/**
 *  @notice   残りの預金残高を "to" アカウントに返す、"refund" 機能として機能する
 *  @dev      前払いモジュールでは、引き出しは発行者アカウントから実行される
 *            ロック解除モジュールでは、引き出しはユーザーアカウントから実行される
 *  @param    to            残りの預金を受け取るアカウント
 *  @param    amount        返金されるトークンの量
*/
function withdraw(address to, uint256 amount) external;

function depositBalanceOf(address user) external view returns(uint256 depositBalance, uint256 epoch);

event Deposit(
    address indexed from,
    uint256 amount
);

event Withdraw(
    address indexed to,
    uint256 amount
);
    
event TransferIssuer(
    address indexed oldIssuer,
    address indexed newIssuer
);

event Claim(
    address indexed from,
    address indexed to,
    uint256 epoch,
    uint256 consumption
);

```

### 署名

ECDSAの署名を生成するための疑似コード:
```
sign(keccak256(abi_encode(
    "\x19Ethereum Signed Message:\n32", 
        keccak256(abi_encode(
            token_address,
            payer_address,
            token_issuer,
            token_consumption,        //ユーザークライアントで計算される
            epoch
        ))
    ))
,private_key)

```

### 検証プロセス

検証には署名と token_consumptionの両方のチェックが含まれます。

検証サーバーが実行する疑似コードは以下の通りです:

```

serving_loop:

    for {
        /**
         * unpaied_consumption はプロバイダーが計算する
         * signed_consumption は請求可能な量
         * tolerance により、ペイヤーがプロバイダーに一定の金額を「借りている」ことを許容する
        */
        //getSignedConsumption は既に請求可能な量を返す 
        if(unpaied_consumption <  signed_consumption + tolerance){
            informUser("ユーザーは充電が必要", unpaied_consumption)
            interruptService() 
        }else{
            isServing() || recoverService()
        }
    }

verification_loop:

    for {
        message = incomingMessage()
        if(recover_signer(message, signature) != payer_address){
            informUser("署名チェックに失敗", hash(message))
            continue
        }

        /**
        * オプション: エコーサーバーを使ってメッセージを検証サーバー間で同期する場合
        * このについては「セキュリティ上の考慮事項」セクションでより詳しく説明する
        */
        if(query(message) != message){
            informUser("メッセージが期限切れ", hash(message))
            continue   
        }

        if(epoch != message.epoch || message.consumption > getDepositBalance()){
            informUser("無効なメッセージ", epoch, unpaied_consumption)
            continue
        }
       
        signed_consumption = message.consumption
        save(message)
    }
    
claim_process:

    if(claim()){
        unpaied_consumption -= signed_consumption
        signed_consumption = 0
        epoch+=1
    }

```
### 引き出しについて

引き出し機能は、ビジネスモデルによって少し異なります。

1. 前払いモデル

前払いビジネスモデルでは、ユーザーが(暗号)通貨を事前に支払い、請求可能なトークンとしてリチャージカード(ボーナスや割引付き)を購入します。チェックアウト時、顧客はメッセージに更新された消費量(前回の消費量 + 今回の消費量)に署名し、店舗はこのメッセージをオフチェーンで検証します。ショッピングプロセスはブロックチェーンを介さずに繰り返されますが、顧客がカードを返金して残高を受け取りたい場合は例外です。前払いモデルでは、店舗が全ての通貨を保持しているため、引き出し機能はトークン発行者(店舗)が実行する必要があります。前払いモデルは、QRコードスキャン機能を備えたウォレットに簡単に組み込むことができます。

2. ロック解除モデル

トークンを支払い手段として受け入れる、エンドツーエンドの暗号化メールサービスを運営する場合は、ロック解除モデルを使うことができます。前払いとは異なり、N人の受信者にメールを送信する場合、X * N トークンを請求します。この「使用量に応じた支払い」シナリオでは、サービスの利用量の計算がクライアントとサーバー側の両方で行われます。クライアントは、サーバーが不正に動作している可能性があるため、サーバーが提示する請求額を信頼してはいけません。クライアントがサーバーを信頼しなくなった場合、メッセージに署名するのを止めますが、一部のトークンは預金残高に拘束されたままになります。この問題を解決するために、引き出し機能はエポックが1ヶ月以内に変わっていない場合、ペイヤーアカウントから実行されるべきです。

## 根拠

このEIPはERC-20トークンを対象としていますが、この拡張機能は他のトークンスタンダードとも互換性があるように設計されています。

トークンコントラクトではなく別の記録コントラクトにこれらの機能を実装する理由は以下の通りです:
- トークンの転送の方が便利で一般的
- トークンはより標準化されており、UIサポートも良い
- トークンはサービスと同等なので、トークン経済をより活性化できる
- approve プロセスを削除できる

## 下位互換性

このEIPは[ERC-20](./eip-20.md)の機能を拡張するものなので、完全な下位互換性があります。

## 実装

```solidity

mapping (address => StampBalance) private _depositBalance;
    
struct StampBalance{
    uint256 balance;
    uint256 epoch;
}
    
function deposit(uint256 value) override external{
    require(value <= _balances[msg.sender]);
    _balances[msg.sender] = _balances[msg.sender].sub(value);
    _depositBalance[msg.sender].balance = _depositBalance[msg.sender].balance.add(value);
    emit Deposit(msg.sender, value);
}

function withdraw(address to, uint256 value) override onlyIssuer external{
    require(value <= _depositBalance[to].balance);
    _depositBalance[to].balance = _depositBalance[to].balance.sub(value);
    _depositBalance[to].epoch += 1;
    _balances[to] = _balances[to].add(value);
    emit Withdraw(to, value);
}
    
function depositBalanceOf(address user) override public view returns(uint256 depositBalance, uint256 epoch){
    return (_depositBalance[user].balance, _depositBalance[user].epoch);
}

// 前払いモデル
function claim(address from, uint credit, uint epoch, bytes memory signature) override onlyIssuer external{
    require(credit > 0);
    require(_depositBalance[from].epoch + 1 == epoch);
    require(_depositBalance[from].balance >= credit);
    bytes32 message = keccak256(abi.encode(this, from, _issuer, credit, epoch));
    bytes32 msgHash = prefixed(message);
    require(recoverSigner(msgHash, signature) == from);
    _depositBalance[from].balance = _depositBalance[from].balance.sub(credit);
    _balances[_issuer] = _balances[_issuer].add(credit);
    _depositBalance[from].epoch += 1;
    emit Claim(from, msg.sender, credit, epoch);
}

function prefixed(bytes32 hash) internal pure returns (bytes32) {
    return keccak256(abi.encode("\x19Ethereum Signed Message:\n32", hash));
}

function recoverSigner(bytes32 message, bytes memory sig) internal pure  returns (address) {
    (uint8 v, bytes32 r, bytes32 s) = splitSignature(sig);
    return ecrecover(message, v, r, s);
}

function splitSignature(bytes memory sig) internal pure returns (uint8 v, bytes32 r, bytes32 s) {
    require(sig.length == 65);
    assembly {
        r := mload(add(sig, 32))
        s := mload(add(sig, 64))
        v := byte(0, mload(add(sig, 96)))
    }
    return (v, r, s);
}

```

## セキュリティ上の考慮事項

請求機能を発行者に制限することで、オンチェーンのレースコンディションはありません。ただし、発行者が複数の検証者を使用し、ペイヤーが同時に多くのペイメントメッセージに署名する場合、二重支払いの問題が発生する可能性があります。これらのメッセージの一部は、最大の消費量を持つメッセージのみが請求可能であるにもかかわらず、有効と判断される可能性があります。この問題は、メッセージを受け取る検証者に返す順番で最大の消費量と最大のエポック番号を持つメッセージを返すエコーサーバーを導入することで解決できます。検証者が自分が送信したメッセージと異なる応答を受け取った場合、最後に受け取ったメッセージと、このペイヤーに関する自身の状態情報を更新します。その後、検証者はペイヤーに新しいメッセージを要求します。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。