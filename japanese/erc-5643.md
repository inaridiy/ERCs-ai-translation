---
original: 833359d39a4126a0847a009d9fcde46d0e2679d6d4bf84cde6dbb3f486323591
---

---
eip: 5643
title: サブスクリプション NFT
description: EIP-721 トークンにサブスクリプションベースの機能を追加する
author: cygaar (@cygaar)
discussions-to: https://ethereum-magicians.org/t/eip-5643-subscription-nfts/10802
status: 停滞中
type: Standards Track
category: ERC
created: 2022-09-10
requires: 721
---

## 概要

このスタンダードは [EIP-721](./eip-721.md) の拡張です。定期的に期限が切れるサブスクリプション NFT を使用するためのインターフェースを提案しています。このインターフェースには、サブスクリプションを更新および解約するための機能が含まれています。

## 動機

NFT は、分散型アプリのアカウントやコミュニティ、イベントなどのメンバーシップパスとして一般的に使用されています。しかし、現在、有限の有効期限を持つこのようなNFTはまれです。ブロックチェーンの「永続性」により、有効期限のないメンバーシップが多く存在し、定期的な支払いが必要ありません。しかし、多くの現実世界のアプリケーションでは、アカウントやメンバーシップを有効に保つために、有料のサブスクリプションが必要です。

オンチェーンで更新可能なサブスクリプションモデルを利用している最も一般的なアプリケーションは Ethereum Name Service (ENS) です。ENSでは、下記で提案されているインターフェースと似たようなインターフェースを使用しています。各ドメインは一定期間更新でき、支払いが行われなくなると期限切れになります。共通のインターフェースがあれば、将来のプロジェクトがサブスクリプションベースのNFTを開発するのが容易になります。現在のWeb2の世界では、ユーザーが自分のサブスクリプションをすべて一か所で確認するのは難しいです。サブスクリプションの共通スタンダードがあれば、ユーザーが保有するサブスクリプションの数、有効期限、更新/解約を単一のアプリケーションで管理できるようになります。

さらに、NFT取引からの二次ロイヤリティが減少するにつれ、クリエイターは新しい収益モデルを必要とするでしょう。メンバーシップやアクセスパスとして機能するNFTの場合、サブスクリプションベースのモデルに移行することで、収入を確保し、発行者にも価値提供を続けさせることができます。

## 仕様

この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

```solidity
interface IERC5643 {
    /// @notice サブスクリプションの有効期限が変更されたときに発行されるイベント
    /// @dev サブスクリプションが解約された場合、有効期限の値も0になるはずです。
    event SubscriptionUpdate(uint256 indexed tokenId, uint64 expiration);

    /// @notice NFTのサブスクリプションを更新する
    /// `tokenId`が有効なNFTでない場合は例外をスローします
    /// @param tokenId サブスクリプションを更新するNFT
    /// @param duration サブスクリプションを延長する秒数
    function renewSubscription(uint256 tokenId, uint64 duration) external payable;

    /// @notice NFTのサブスクリプションを解約する
    /// @dev `tokenId`が有効なNFTでない場合は例外をスローします
    /// @param tokenId サブスクリプションを解約するNFT
    function cancelSubscription(uint256 tokenId) external payable;

    /// @notice サブスクリプションの有効期限を取得する
    /// @dev `tokenId`が有効なNFTでない場合は例外をスローします
    /// @param tokenId 有効期限を取得するNFT
    /// @return サブスクリプションの有効期限
    function expiresAt(uint256 tokenId) external view returns(uint64);

    /// @notice サブスクリプションが更新可能かどうかを判断する
    /// @dev `tokenId`が有効なNFTでない場合は例外をスローします
    /// @param tokenId 更新可能性を確認するNFT
    /// @return サブスクリプションの更新可能性
    function isRenewable(uint256 tokenId) external view returns(bool);
}
```

`expiresAt(uint256 tokenId)` 関数は `pure` または `view` として実装できます。

`isRenewable(uint256 tokenId)` 関数は `pure` または `view` として実装できます。

`renewSubscription(uint256 tokenId, uint64 duration)` 関数は `external` または `public` として実装できます。

`cancelSubscription(uint256 tokenId)` 関数は `external` または `public` として実装できます。

`SubscriptionUpdate` イベントは、サブスクリプションの有効期限が変更されるたびに必ず発行されなければなりません。

`supportsInterface` メソッドは、`0x8c65f84d` が渡された場合に `true` を返さなければなりません。

## 根拠

このスタンダードは、オンチェーンのサブスクリプションを可能な限り簡単に実装できるよう、サブスクリプションの実装に必要な最小限の機能とイベントを追加しています。このインターフェースでは、NFT 自体がサブスクリプションの所有権を表すものであり、他の代替可能または不可能なトークンの仲介は行われないことに注意が必要です。

### サブスクリプションの管理

サブスクリプションは、前払いをして何かを受け取ったり参加したりするための契約を表します。これらの契約を円滑に行うには、ユーザーがサブスクリプションを更新または解約できる必要があるため、`renewSubscription` および `cancelSubscription` 関数が用意されています。また、サブスクリプションの有効期限を知ることも重要です - ユーザーは更新のタイミングを知る必要があり、アプリケーションはサブスクリプション NFT の有効性を判断するためにこの情報が必要です。`expiresAt` 関数がこの機能を提供します。最後に、サブスクリプションが期限切れになった後に更新できない可能性があります。`isRenewable` 関数はこの情報をユーザーとアプリケーションに提供します。

### 簡単な統合

このスタンダードは完全に EIP-721 準拠なので、既存のプロトコルはサブスクリプション NFT の転送を標準で処理できます。わずかな機能を追加するだけで、プロトコルはサブスクリプションの有効期限の管理、期限切れの判断、更新可能性の確認を完全に行えるようになります。

## 下位互換性

このスタンダードは、拡張機能セットを追加することで、完全に EIP-721 互換にすることができます。

このスタンダードで導入された新しい関数は、既存の EIP-721 インターフェースに最小限の負荷しか追加しないため、開発者にとってのアドプションは簡単かつ迅速になるはずです。

## テストケース

以下のテストには Foundry が必要です。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import "../src/ERC5643.sol";

contract ERC5643Mock is ERC5643 {
    constructor(string memory name_, string memory symbol_) ERC5643(name_, symbol_) {}

    function mint(address to, uint256 tokenId) public {
        _mint(to, tokenId);
    }
}

contract ERC5643Test is Test {
    event SubscriptionUpdate(uint256 indexed tokenId, uint64 expiration);

    address user1;
    uint256 tokenId;
    ERC5643Mock erc5643;

    function setUp() public {
        tokenId = 1;
        user1 = address(0x1);

        erc5643 = new ERC5643Mock("erc5369", "ERC5643");
        erc5643.mint(user1, tokenId);
    }

    function testRenewalValid() public {
        vm.warp(1000);
        vm.prank(user1);
        vm.expectEmit(true, true, false, true);
        emit SubscriptionUpdate(tokenId, 3000);
        erc5643.renewSubscription(tokenId, 2000);
    }

    function testRenewalNotOwner() public {
        vm.expectRevert("Caller is not owner nor approved");
        erc5643.renewSubscription(tokenId, 2000);
    }

    function testCancelValid() public {
        vm.prank(user1);
        vm.expectEmit(true, true, false, true);
        emit SubscriptionUpdate(tokenId, 0);
        erc5643.cancelSubscription(tokenId);
    }

    function testCancelNotOwner() public {
        vm.expectRevert("Caller is not owner nor approved");
        erc5643.cancelSubscription(tokenId);
    }

    function testExpiresAt() public {
        vm.warp(1000);

        assertEq(erc5643.expiresAt(tokenId), 0);
        vm.startPrank(user1);
        erc5643.renewSubscription(tokenId, 2000);
        assertEq(erc5643.expiresAt(tokenId), 3000);

        erc5643.cancelSubscription(tokenId);
        assertEq(erc5643.expiresAt(tokenId), 0);
    }
}
```

## 参考実装

実装: `ERC5643.sol`

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.13;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "./IERC5643.sol";

contract ERC5643 is ERC721, IERC5643 {
    mapping(uint256 => uint64) private _expirations;

    constructor(string memory name_, string memory symbol_) ERC721(name_, symbol_) {}

    function renewSubscription(uint256 tokenId, uint64 duration) external payable {
        require(_isApprovedOrOwner(msg.sender, tokenId), "Caller is not owner nor approved");

        uint64 currentExpiration = _expirations[tokenId];
        uint64 newExpiration;
        if (currentExpiration == 0) {
            newExpiration = uint64(block.timestamp) + duration;
        } else {
            if (!_isRenewable(tokenId)) {
                revert SubscriptionNotRenewable();
            }
            newExpiration = currentExpiration + duration;
        }

        _expirations[tokenId] = newExpiration;

        emit SubscriptionUpdate(tokenId, newExpiration);
    }

    function cancelSubscription(uint256 tokenId) external payable {
        require(_isApprovedOrOwner(msg.sender, tokenId), "Caller is not owner nor approved");
        delete _expirations[tokenId];
        emit SubscriptionUpdate(tokenId, 0);
    }

    function expiresAt(uint256 tokenId) external view returns(uint64) {
        return _expirations[tokenId];
    }

    function isRenewable(uint256 tokenId) external pure returns(bool) {
        return true;
    }

    function supportsInterface(bytes4 interfaceId) public view virtual override returns (bool) {
        return interfaceId == type(IERC5643).interfaceId || super.supportsInterface(interfaceId);
    }
}
```

## セキュリティ上の考慮事項

このEIPスタンダードはNFTの所有権に影響を与えないため、安全であると考えられます。

## 著作権

著作権およびそれに関連する権利は [CC0](../LICENSE.md) により放棄されています。