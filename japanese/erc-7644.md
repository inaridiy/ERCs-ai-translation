---
original: d524845773a09613a7a0f473e9b0023dc39cb1026e268d87646364fa45ad8e04
---

---
eip: 7644
title: ERC-721 名称レジストリ拡張
description: ERC-721 トークン内の各トークンに期限付きの一意の名称を付与し、名称レジストリを使用して登録と管理を行います。
author: Chen Liaoyuan (@chenly)
discussions-to: https://ethereum-magicians.org/t/erc-7644-erc-721-name-registry-extension/19022
status: Draft
type: Standards Track
category: ERC
created: 2024-03-01
requires: 721
---

## 概要

この拡張機能は、[ERC-721](./eip-721.md) トークンに名称付与メカニズムを追加するインターフェースを定義します。各トークンに一意の名称と有効期限を設定することができ、現在の NFT コントラクト内で一意性が保証されます。このインターフェースには、名称の割り当て、更新、照会、およびそれらに関連付けられたトークンの管理を行う関数が含まれています。名称の設定は、具体的な使用例に応じて責任主体が異なります。

## 動機

分散型ドメイン登録方法がNFTとの統合とともに進化するにつれ、このパラダイムをユーザー名の領域にも拡張する機会が生まれています。トークンIDをユーザー名に関連付けることで、分散型エコシステム内のエンティティの直感的な識別が向上します。

この統合には以下のような目的があります:

- **直感性:** 数値のトークンIDは直感的な識別が困難です。ユーザー名を組み込むことで、トークンIDがより表現力のあるものになり、ユーザビリティが向上します。
  
- **ユーザー名経済の探索:** 登録メカニズムにより、ユーザー名経済の探索が可能になり、アイデンティティ認証やソーシャルインタラクションなどの恩恵が得られます。
  
- **NFTとの相乗効果:** ユーザー名とNFTの融合により、認証済みのソーシャルインタラクションやパーソナライズされたデジタルアセットなどの新しいアプリケーションが生み出されます。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」のキーワードは、RFC 2119およびRFC 8174に記載されているように解釈されるものとします。

このエクステンションの実装者は、以下のすべての関数を持たなければなりません:

```solidity
pragma solidity ^0.8.0;

/**
 * @title INameRegistry
 * @dev NameRegistry スマートコントラクトのインターフェース。
 * このインターフェースにより、NameRegistryとの対話が可能になり、
 * 特定のトークンに関連付けられた名称の登録、管理、検索ができます。
 */
interface IERC7644 /* is IERC721 */ {

    /**
     * @dev トークンの名称が変更された際に発行されるイベント。
     * @param tokenId 名称が変更されたトークンのID。
     * @param oldName トークンの以前の名称。
     * @param newName トークンに新しく割り当てられた名称。
     * @param expiryDate 新しい名称登録の有効期限。
     */
    event NameChanged(uint256 indexed tokenId, bytes32 oldName, bytes32 newName, uint256 expiryDate);

    /**
     * @dev 指定されたトークンの名称を返します。ただし、名称の有効期限が切れている場合は空のbytes32を返します。
     * @param tokenId 名称を照会するトークンのID。
     * @return トークンの名称。有効期限が切れている場合は空のbytes32。
     */
    function nameOf(uint256 tokenId) external view returns (bytes32);

    /**
     * @dev 指定された名称に関連付けられたトークンIDを返します。ただし、名称の登録が有効期限切れの場合は0を返します。
     * @param _name 関連付けられたトークンIDを照会する名称。
     * @return 名称に関連付けられたトークンID。名称が有効期限切れの場合は0。
     */
    function tokenIdOf(bytes32 _name) external view returns (uint256);

    /**
     * @dev トークンの所有者が、トークンの名称を設定または更新できます。名称の有効期限を指定できます。
     * @param tokenId 名称を設定または更新するトークンのID。
     * @param _name トークンに割り当てる新しい名称。
     * @param duration 名称の有効期限(秒単位)。この関数呼び出し時から有効期限が始まります。
     * 注意: 名称は一意でなければならず、現在有効な登録に使用されていてはいけません。
     */
    function setName(uint256 tokenId, bytes32 _name, uint256 duration) external;

    /**
     * @dev 指定された名称に関連付けられたトークンIDと有効期限を返します。ただし、名称の登録が有効期限切れの場合は0を返します。
     * @param _name 関連付けられたトークンIDと有効期限を照会する名称。
     * @return tokenId 名称に関連付けられたトークンID。
     * @return expiryDate 名称登録の有効期限。
     */
    function nameInfo(bytes32 _name) external view returns (uint256 tokenId, uint256 expiryDate);
	
}
```

## 根拠

#### 名称の有効期限

ユーザー名の有効期限を設定することで、いくつかの利点が得られます。この仕組みにより、使用されていないまたは期限切れのユーザー名を解放できるため、健全なエコシステムを維持できます。ユーザー名の入れ替わりが促され、長期的な独占を防ぐことができます。ユーザーはユーザー名ポートフォリオを管理する動機付けられ、価値のある名称を更新しつつ関連性の低い名称を手放すことになります。結果として、公平性と効率性が促進され、命名リソースが効果的に活用され、進化する需要に合わせて更新されます。

#### 名称の一意性

ユーザー名の一意性を強制することは、明確で直感的な識別システムを維持するために不可欠です。これにより、混乱が防止され、分散型エコシステム内での円滑なインタラクションが可能になります。一意のユーザー名は、発見性を高め、取引やソーシャルインタラクションにおける信頼を促進します。この要件は、明確な識別が信頼構築と効率的なインタラクションに不可欠である分散型環境における重要性を強調しています。

#### 名称登録システム

ユーザー名の登録システムを導入することで、悪用行為を防ぎ、命名リソースへの公平なアクセスを促進できます。予約および更新メカニズムにより、魅力的なユーザー名の独占を防ぐことができ、正当なユーザーが希望の名称を確保できるようになります。予約は、希望のユーザー名を公平に請求する機会を保証し、蓄積と投機的な活動を防ぎます。更新メカニズムは、ネーミングエコシステムへの積極的な関与と投資を奨励します。これらの機能により、バランスの取れた包括的な環境が創出され、活気あるユーザーコミュニティが育成されます。

## 下位互換性

この標準は完全に[ERC-721](./eip-721.md)互換です。

## 参考実装

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";

contract ERC7644 is ERC721 {
    event NameChanged(uint256 indexed tokenId, bytes32 oldName, bytes32 newName, uint256 expiryDate);

    struct NameRegistration {
        uint256 tokenId;
        uint256 expiryDate;
    }

    mapping(uint256 => bytes32) private _tokenNames;
    mapping(bytes32 => NameRegistration) private _nameRegistrations;
    mapping(uint256 => uint256) private _lastSetNameTime;

    uint256 public constant MAX_DURATION = 10 * 365 days;
    uint256 public constant MIN_SET_NAME_INTERVAL = 1 days;

    constructor() ERC721("Asd Token", "ASDT") {}

    function nameOf(uint256 tokenId) public view returns (bytes32) {
        if(_tokenNames[tokenId] != bytes32(0) && _nameRegistrations[_tokenNames[tokenId]].expiryDate > block.timestamp)
        {
            return _tokenNames[tokenId];
        }else{
            return bytes32(0);
        }
    }

    function tokenIdOf(bytes32 _name) public view returns (uint256) {
        require(_nameRegistrations[_name].expiryDate > block.timestamp, "NameRegistry: Name expired");
        if(_nameRegistrations[_name].tokenId > 0)
        {
            return _nameRegistrations[_name].tokenId;
        }else{
            return uint256(0);
        }
    }

    function setName(uint256 tokenId, bytes32 _name, uint256 duration) public {
        require(ownerOf(tokenId) == msg.sender, "NameRegistry: Caller is not the token owner");
        require(duration <= MAX_DURATION, "NameRegistry: Duration exceeds maximum limit");
        require(block.timestamp - _lastSetNameTime[tokenId] >= MIN_SET_NAME_INTERVAL, "NameRegistry: Minimum interval not met");
        require(tokenIdOf(_name) == uint256(0) || tokenIdOf(_name) == tokenId, "NameRegistry: Name already in use and not expired");

        bytes32 oldName = _tokenNames[tokenId];
        uint256 expiryDate = block.timestamp + duration;
        _setTokenName(tokenId, _name, expiryDate);

        emit NameChanged(tokenId, oldName, _name, expiryDate);

        _lastSetNameTime[tokenId] = block.timestamp;
    }

    function nameInfo(bytes32 _name) public view returns (uint256, uint256) {
        require(_nameRegistrations[_name].tokenId > 0 && _nameRegistrations[_name].expiryDate > block.timestamp, "NameRegistry: Name expired or does not exist");
        NameRegistration memory registration = _nameRegistrations[_name];
        return (registration.tokenId, registration.expiryDate);
    }

    function _setTokenName(uint256 tokenId, bytes32 _name, uint256 expiryDate) internal {
        _tokenNames[tokenId] = _name;
        _nameRegistrations[_name] = NameRegistration(tokenId, expiryDate);
    }
}
```

## セキュリティ上の考慮事項

#### 悪用行為と資源の独占の軽減

この設計には、悪用行為と資源の独占を防ぐためのメカニズムが含まれています。名称の設定間隔の最小値と有効期限の最大値が設定されており、スパムや悪意のある攻撃を抑制し、連続的な名称登録を制限し、命名リソースの公平かつ効率的な使用を促進します。これらの対策により、名称が無期限に独占されることを防ぎ、すべてのユーザーにとって持続可能で公平な環境が維持されます。

#### ユーザー名の制限

インデックス作成とガス効率を促進するため、ユーザー名の長さは3文字から32文字の範囲に制限されるべきです。この範囲は、過度に長い名称の登録を防ぎ、ガス代が高額になったり管理が困難になったりするのを防ぎます。文字を[a-zA-Z0-9]の範囲に制限することで、可読性が向上し、特殊文字の乱用によるネーミングシステムの悪用を防ぐことができます。これらの制約を実装することで、エコシステム内の高いユーザビリティが促進され、スパム登録の蔓延が抑制され、レジストリが真正のユーザーにとって利用可能かつ機能的に維持されます。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。