---
original: 590cf003c154d190831bc151585fe3017b6a2935177d8f97e3031e908fe2fde4
---

---
eip: 7303
title: トークン制御トークン流通
description: トークン所有権に基づくアクセス制御スキーム。
author: Ko Fujimura (@kofujimura)
discussions-to: https://ethereum-magicians.org/t/erc-7303-token-controlled-token-circulation/15020
status: Draft
type: Standards Track
category: ERC
created: 2023-07-09
requires: 721, 1155, 5679
---
## 概要

このERCは、トークン制御トークン流通(TCTC)と呼ばれるアクセス制御スキームを導入します。役割に関連する特権を[ERC-721](./eip-721.md)または[ERC-1155](./eip-1155.md)トークン(「コントロールトークン」と呼ばれる)で表現することで、役割の付与や取り消しをコントロールトークンのミントやバーンを通して行うことができます。

## 動機

特権アクションのアクセス制御を実装する方法は多数あります。一般的に使用されるパターンは[ERC-5982](./eip-5982.md)で指定されている「ロールベース」のアクセス制御です。しかし、この方法では役割の付与や取り消しをオフチェーンのマネジメントツールを通して行う必要があります。さらに、多くのウォレットにはユーザーに付与された特権を表示するユーザーインターフェイスがないため、ユーザーはウォレットを通してその特権状況を把握することができません。

### ユースケース

このERCは、[ERC-5982](./eip-5982.md)で説明されているロールベースのアクセス制御が使用されるシナリオで適用できます。具体的なユースケースは以下の通りです:

**ミント/バーン権限:**
チケット、クーポン、会員カード、サイトアクセス権などのアイテムをトークンとして流通させるアプリケーションでは、システム管理者にこれらのトークンをミントやバーンする権限を与える必要があります。これらの権限は、このスキームでは「コントロールトークン」として実現できます。

**転送権限:**
これらのアプリケーション内の一部のシナリオでは、特定の機関にのみトークンの転送を許可したい場合があります。このような場合、機関証明書を「コントロールトークン」として発行し、この「コントロールトークン」の所有権によってトークンの転送を管理することができます。

**アドレス検証:**
多くのアプリケーションでは、ミントや転送時の宛先アドレスの誤りを防ぐためにアドレス検証が必要です。ユーザーにアドレス検証の証明として「コントロールトークン」が発行され、ミントや転送時にこの「コントロールトークン」の所有が要求されることで、誤配送を防ぐことができます。この「アドレス検証用のコントロールトークン」は、場合によっては政府機関や特定の企業によって身元確認プロセス後に発行されることもあります。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「NOT RECOMMENDED」、「MAY」、「OPTIONAL」のキーワードは、RFC 2119およびRFC 8174に記載されている通り解釈されるものとします。

1. [ERC-7303](./eip-7303.md)規格を実装するスマートコントラクトは、役割に必要な特権をERC-721トークンまたはERC-1155トークンで表現しなければなりません。特権を表すトークンは、このERCでは「コントロールトークン」と呼ばれます。コントロールトークンは任意のタイプのトークンであり、その取引は別のコントロールトークンによって再帰的に制御される可能性があります。
2. 必要なコントロールトークンを役割に関連付けるには、コントロールトークンの以前にデプロイされたコントラクトのアドレスを使用しなければなりません。
3. アカウントが必要な役割を持っているかどうかを確認するには、ERC-721またはERC-1155で定義された`balanceOf`メソッドを使用して、コントロールトークンの残高が0より大きいことを確認すべきです。ERC-1155トークンの場合は、`typeId`も指定する必要があります。
4. 役割をアカウントに付与するには、その特権を表すコントロールトークンを[ERC-5679](./eip-5679.md)で定義された`safeMint`メソッドを使ってそのアカウントにミントすべきです。
5. 役割をアカウントから取り消すには、その特権を表すコントロールトークンをERC-5679の`burn`メソッドを使ってバーンすべきです。
6. 準拠するスマートコントラクト内の役割は`bytes32`形式で表現されます。そのような役割の値は、ロール名の文字列のkeccak256ハッシュとして計算することが推奨されます。例: `bytes32 role = keccak256("MINTER")`.

## 根拠

特権をERC-721またはERC-1155トークンとしてコントロールトークンとして使用することで、ユーザーがウォレット内でそれらの特権を視覚的に確認できるようになり、特権管理が簡単になります。

一般的に、特権をトークン化する際には、Soulbound Token(例: [ERC-5192](./eip-5192.md))のような仕様が使用されます。ERC-5192はERC-721を継承しているため、このERCではコントロールトークンとしてERC-721を要件としています。

転送可能なコントロールトークンを採用することで、役割の委任が必要なシナリオに対応できます。例えば、組織内の権限者の交代や休暇時に、その特権を別のメンバーに一時的に移譲できるようになります。コントロールトークンを転送可能にするかどうかの決定は、アプリケーションの具体的なニーズによって異なります。

## 下位互換性

このERCは、[ERC-721](./eip-721)、[ERC-1155](./eip-1155)、および[ERC-5679](./eip-5679)との互換性を持つように設計されています。

## 参考実装

ERC-7303は、アプリケーションでTCTC(トークン制御トークン流通)アクセス制御を実装するためのモディファイアーを提供しています。このモディファイアーは、アカウントが必要な役割を持っているかどうかをチェックします。ERC-7303はまた、特定の役割をある指定されたアカウントに付与する関数も含んでいます。

```solidity
// SPDX-License-Identifier: Apache-2.0

pragma solidity ^0.8.9;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";

abstract contract ERC7303 {
    struct ERC721Token {
        address contractId;
    }

    struct ERC1155Token {
        address contractId;
        uint256 typeId;
    }

    mapping (bytes32 => ERC721Token[]) private _ERC721_Contracts;
    mapping (bytes32 => ERC1155Token[]) private _ERC1155_Contracts;

    modifier onlyHasToken(bytes32 role, address account) {
        require(_checkHasToken(role, account), "ERC7303: not has a required token");
        _;
    }

    /**
     * @notice ERC-721コントラクトIDで指定されたコントロールトークンを所有するユーザーに役割を付与する。
     * 複数回呼び出すことができ、その場合ユーザーはそれらのトークンのいずれかを所有している必要がある。
     * @param role byte32 付与したい役割。
     * @param contractId address ユーザーが所有する必要があるトークンのコントラクトID。
     */
    function _grantRoleByERC721(bytes32 role, address contractId) internal {
        require(
            IERC165(contractId).supportsInterface(type(IERC721).interfaceId),
            "ERC7303: provided contract does not support ERC721 interface"
        );
        _ERC721_Contracts[role].push(ERC721Token(contractId));
    }

    /**
     * @notice ERC-1155コントラクトIDとtypeIdで指定されたコントロールトークンを所有するユーザーに役割を付与する。
     * 複数回呼び出すことができ、その場合ユーザーはそれらのトークンのいずれかを所有している必要がある。
     * @param role byte32 付与したい役割。
     * @param contractId address ユーザーが所有する必要があるトークンのコントラクトID。
     * @param typeId uint256 ユーザーが所有する必要があるトークンのタイプID。
     */
    function _grantRoleByERC1155(bytes32 role, address contractId, uint256 typeId) internal {
        require(
            IERC165(contractId).supportsInterface(type(IERC1155).interfaceId),
            "ERC7303: provided contract does not support ERC1155 interface"
        );
        _ERC1155_Contracts[role].push(ERC1155Token(contractId, typeId));
    }

    function _checkHasToken(bytes32 role, address account) internal view returns (bool) {
        ERC721Token[] memory ERC721Tokens = _ERC721_Contracts[role];
        for (uint i = 0; i < ERC721Tokens.length; i++) {
            if (IERC721(ERC721Tokens[i].contractId).balanceOf(account) > 0) return true;
        }

        ERC1155Token[] memory ERC1155Tokens = _ERC1155_Contracts[role];
        for (uint i = 0; i < ERC1155Tokens.length; i++) {
            if (IERC1155(ERC1155Tokens[i].contractId).balanceOf(account, ERC1155Tokens[i].typeId) > 0) return true;
        }

        return false;
    }
}
```

以下は、ERC-721トークンでERC-7303を使用して「ミンター」と「バーナー」の役割を定義する簡単な例です。これらの役割を持つアカウントは、ERC-721またはERC-1155のコントロールトークンを指定することで、新しいトークンの作成と既存トークンの破棄を許可されます:

```solidity
// SPDX-License-Identifier: Apache-2.0

pragma solidity ^0.8.9;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "./ERC7303.sol";

contract MyToken is ERC721, ERC7303 {
    bytes32 public constant MINTER_ROLE = keccak256("MINTER_ROLE");
    bytes32 public constant BURNER_ROLE = keccak256("BURNER_ROLE");

    constructor() ERC721("MyToken", "MTK") {
        // ERC721コントロールトークンのデプロイ済みコントラクトIDを指定する。
        _grantRoleByERC721(MINTER_ROLE, 0x...);
        _grantRoleByERC721(BURNER_ROLE, 0x...);

        // ERC1155コントロールトークンのデプロイ済みコントラクトIDとtypeIdを指定する。
        _grantRoleByERC1155(MINTER_ROLE, 0x..., ...);
        _grantRoleByERC1155(BURNER_ROLE, 0x..., ...);
    }

    function safeMint(address to, uint256 tokenId, string memory uri)
        public onlyHasToken(MINTER_ROLE, msg.sender)
    {
        _safeMint(to, tokenId);
    }

    function burn(uint256 tokenId) 
        public onlyHasToken(BURNER_ROLE, msg.sender) 
    {
        _burn(tokenId);
    }
}
```

## セキュリティ上の考慮事項

流通対象のトークンのセキュリティは、コントロールトークンのセキュリティに大きく依存します。管理特権、ミント/転送/バーン権限、およびコントロールトークンの契約更新の可能性について、慎重に検討する必要があります。

特に、コントロールトークンを転送可能にすることで、管理権限の一時的な委任などの柔軟な操作が可能になります。しかし、それにより不適切な第三者がトークン流通の権限を手に入れる可能性も高まります。したがって、一般的にはコントロールトークンを非転送可能にすべきです。コントロールトークンを転送可能にする場合でも、少なくともこれらのトークンを焼却する権限は信頼できる管理者が保持するべきです。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。