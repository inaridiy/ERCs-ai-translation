---
original: a3f212291fa1d8774b555a236538dafe48917668c160135c8be589c43ec6e66c
---

---
eip: 1822
title: 汎用アップグレード可能プロキシ標準 (UUPS)
author: Gabriel Barros <gabriel@terminal.co>, Patrick Gallagher <blockchainbuddha@gmail.com>
discussions-to: https://ethereum-magicians.org/t/eip-1822-universal-upgradeable-proxy-standard-uups
status: 停滞
type: Standards Track
category: ERC
created: 2019-03-04
---

## 簡単な要約

標準的なアップグレード可能なプロキシコントラクト。

## 概要

以下では、すべてのコントラクトと互換性があり、プロキシとビジネスロジックコントラクト間の非互換性を生み出さない、プロキシコントラクトの標準について説明します。これは、プロキシコントラクトにおいてロジックコントラクトのアドレスを格納する一意のストレージポジションを利用することで実現されます。互換性チェックにより、アップグレードが成功することが保証されます。アップグレードは無制限に、またはカスタムロジックによって決定された回数だけ実行できます。さらに、バイトコードの検証を阻害することなく、複数のコンストラクタから選択する方法も提供されています。

## 動機

- 開発者がプロキシおよびロジックコントラクトをデプロイおよび管理するための体験を改善するために、既存のプロキシ実装を改善する。

- プロキシコントラクトで使用されるバイトコードの検証方法を標準化し、改善する。

## 用語

- `delegatecall()` - コントラクト **A** の関数で、外部コントラクト **B** (委任元)が **A** のストレージを変更できるようにする (下図と [Solidity ドキュメント](https://solidity.readthedocs.io/en/v0.5.3/introduction-to-smart-contracts.html#delegatecall-callcode-and-libraries) を参照)
- **プロキシコントラクト** - データを保持するコントラクト **A** で、`delegatecall()` によって外部コントラクト **B** のロジックを使用する。
- **ロジックコントラクト** - プロキシコントラクト **A** で使用されるロジックを含むコントラクト **B**
- **Proxiable コントラクト** - ロジックコントラクト **B** に継承され、アップグレード機能を提供する

![](../assets/eip-1822/proxy-diagram.png)

## 仕様

ここで提案されるプロキシコントラクトは、そのままデプロイされ、既存のコントラクトライフサイクル管理方法の代替として使用されるべきです。プロキシコントラクトに加えて、アップグレードを妨げることなく既存のビジネスルールを実装できる Proxiable コントラクトインターフェイス/ベースを提案します。アップグレードを許可するロジックは必要に応じて実装できます。

### プロキシコントラクト

#### 関数

##### `fallback`

提案されるフォールバック関数は、[Zeppelin][1] や [Gnosis][2] などの他のプロキシコントラクト実装で見られる一般的なパターンに従います。

ただし、変数の使用を強制するのではなく、ロジックコントラクトのアドレスは定義されたストレージポジション `keccak256("PROXIABLE")` に格納されます。これにより、プロキシおよびロジックコントラクト内の変数の競合の可能性が排除され、任意のロジックコントラクトとの「汎用」の互換性が提供されます。

```javascript
function() external payable {
    assembly { // solium-disable-line
        let contractLogic := sload(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7)
        calldatacopy(0x0, 0x0, calldatasize)
        let success := delegatecall(sub(gas, 10000), contractLogic, 0x0, calldatasize, 0, 0)
        let retSz := returndatasize
        returndatacopy(0, 0, retSz)
        switch success
        case 0 {
            revert(0, retSz)
        }
        default {
            return(0, retSz)
        }
    }
}
```

#### `constructor`

提案されるコンストラクタは任意の数の任意の型の引数を受け入れるため、任意のロジックコントラクトのコンストラクタ関数と互換性があります。

プロキシコントラクトのコンストラクタの任意性により、ロジックコントラクトのソースコード内で利用可能な1つ以上のコンストラクタ関数 (例: `constructor1`、`constructor2` など) から選択する機能が提供されます。ロジックコントラクトに複数のコンストラクタが含まれている場合は、初期化後にコンストラクタを再度呼び出すことを禁止するチェックを含める必要があります。

複数のコンストラクタをサポートする機能を追加しても、初期化トランザクションのコールデータ (入力) をプロキシコントラクトのABIを使って最初に、次にロジックコントラクトのABIを使って解読することで、プロキシコントラクトのバイトコードの検証を阻害しないことに注意が必要です。

以下のコントラクトは、提案されるプロキシコントラクトの実装を示しています。

```javascript
contract Proxy {
    // ストレージ内のコードポジションは keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"
    constructor(bytes memory constructData, address contractLogic) public {
        // コードアドレスを保存する
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, contractLogic)
        }
        (bool success, bytes memory _ ) = contractLogic.delegatecall(constructData); // solium-disable-line
        require(success, "Construction failed");
    }

    function() external payable {
        assembly { // solium-disable-line
            let contractLogic := sload(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7)
            calldatacopy(0x0, 0x0, calldatasize)
            let success := delegatecall(sub(gas, 10000), contractLogic, 0x0, calldatasize, 0, 0)
            let retSz := returndatasize
            returndatacopy(0, 0, retSz)
            switch success
            case 0 {
                revert(0, retSz)
            }
            default {
                return(0, retSz)
            }
        }
    }
}
```

### Proxiable コントラクト

Proxiable コントラクトはロジックコントラクトに含まれ、アップグレードを実行するために必要な関数を提供します。互換性チェック `proxiable` は、アップグレード中の修復不可能な更新を防ぎます。

> :warning: 警告: `updateCodeAddress` と `proxiable` はロジックコントラクトに存在する必要があります。これらを含めないと、アップグレードが妨げられ、プロキシコントラクトが完全に使用不可能になる可能性があります。[危険な関数の制限](#危険な関数の制限)を参照してください。

#### 関数

##### `proxiable`

新しいロジックコントラクトが汎用アップグレード可能プロキシ標準を実装していることを確認するための互換性チェック。将来の実装をサポートするために、`bytes32` の比較を変更する (例: `keccak256("PROXIABLE-ERC1822-v1")`) ことができます。

##### `updateCodeAddress`

ロジックコントラクトのアドレスをプロキシコントラクトのストレージ `keccak256("PROXIABLE")` に格納します。

以下のコントラクトは、提案されるProxiable コントラクトの実装を示しています。

```javascript
contract Proxiable {
    // ストレージ内のコードポジションは keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"

    function updateCodeAddress(address newAddress) internal {
        require(
            bytes32(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7) == Proxiable(newAddress).proxiableUUID(),
            "Not compatible"
        );
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, newAddress)
        }
    }
    function proxiableUUID() public pure returns (bytes32) {
        return 0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7;
    }
}
```

## プロキシを使用する際の落とし穴

プロキシコントラクトを使用する際は、すべてのロジックコントラクトに対して以下の一般的なベストプラクティスを採用する必要があります。

### 変数とロジックの分離

新しいロジックコントラクトを設計する際は、アップグレード後にプロキシコントラクトの既存のストレージと互換性がなくなることを防ぐため、慎重に検討する必要があります。具体的には、新しいコントラクトで変数が初期化される順序を変更してはならず、既存の変数の後に新しい変数を追加する必要があります。

この慣行を促進するために、すべての変数を保持する単一の "ベース" コントラクトを使用し、それを後続のロジックコントラクトで継承することをお勧めします。この慣行は、変数の順序を誤って変更したり、ストレージを上書きする可能性を大幅に減らします。

### 危険な関数の制限

Proxiable コントラクトの互換性チェックは、汎用アップグレード可能プロキシ標準を実装していないロジックコントラクトにアップグレードするのを防ぐための安全機構です。ただし、パリティウォレットハックの際に起こったように、ロジックコントラクト自体に取り返しのつかない損害を与えることも可能です。

ロジックコントラクトへの損害を防ぐために、潜在的に危険な関数の権限を `onlyOwner` に制限し、ロジックコントラクトの所有権をデプロイ直後にヌルアドレス (例: address(1)) に譲渡することをお勧めします。潜在的に危険な関数には、ネイティブ関数の `SELFDESTRUCT` や、外部から発信されるコードを含む `CALLCODE` および `delegatecall()` などがあります。以下の [ERC-20 トークン](#erc-20-トークン)の例では、ロジックコントラクトの破壊を防ぐために `LibraryLock` コントラクトが使用されています。

## 例

### Owned

この例では、標準的な所有権の例を示し、`updateCodeAddress` を所有者のみに制限しています。

```javascript
contract Owned is Proxiable {
    // このコントラクトが一度デプロイされたら、誰も操作できないようにする
    address public owner = address(1);

    function constructor1() public{
        // *プロキシ*コントラクトごとに1回しか呼び出せないことを保証する
        require(owner == address(0));
        owner = msg.sender;
    }

    function updateCode(address newCode) onlyOwner public {
        updateCodeAddress(newCode);
    }

    modifier onlyOwner() {
        require(msg.sender == owner, "Only owner is allowed to perform this action");
        _;
    }
}
```

### ERC-20 トークン

#### プロキシコントラクト

```javascript
pragma solidity ^0.5.1;

contract Proxy {
    // ストレージ内のコードポジションは keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"
    constructor(bytes memory constructData, address contractLogic) public {
        // コードアドレスを保存する
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, contractLogic)
        }
        (bool success, bytes memory _ ) = contractLogic.delegatecall(constructData); // solium-disable-line
        require(success, "Construction failed");
    }

    function() external payable {
        assembly { // solium-disable-line
            let contractLogic := sload(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7)
            calldatacopy(0x0, 0x0, calldatasize)
            let success := delegatecall(sub(gas, 10000), contractLogic, 0x0, calldatasize,
0, 0)
            let retSz := returndatasize
            returndatacopy(0, 0, retSz)
            switch success
            case 0 {
                revert(0, retSz)
            }
            default {
                return(0, retSz)
            }
        }
    }
}
```

#### トークンロジックコントラクト

```javascript
contract Proxiable {
    // ストレージ内のコードポジションは keccak256("PROXIABLE") = "0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7"

    function updateCodeAddress(address newAddress) internal {
        require(
            bytes32(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7) == Proxiable(newAddress).proxiableUUID(),
            "Not compatible"
        );
        assembly { // solium-disable-line
            sstore(0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7, newAddress)
        }
    }
    function proxiableUUID() public pure returns (bytes32) {
        return 0xc5f16f0fcc639fa48a6947836d9850f504798523bf8c9a3a87d5876cf622bcf7;
    }
}


contract Owned {

    address owner;

    function setOwner(address _owner) internal {
        owner = _owner;
    }
    modifier onlyOwner() {
        require(msg.sender == owner, "Only owner is allowed to perform this action");
        _;
    }
}

contract LibraryLockDataLayout {
  bool public initialized = false;
}

contract LibraryLock is LibraryLockDataLayout {
    // ロジックコントラクトが一度デプロイされたら、誰も操作できないようにする。
    // パリティウォレットハック対策

    modifier delegatedOnly() {
        require(initialized == true, "The library is locked. No direct 'call' is allowed");
        _;
    }
    function initialize() internal {
        initialized = true;
    }
}

contract ERC20DataLayout is LibraryLockDataLayout {
  uint256 public totalSupply;
  mapping(address=>uint256) public tokens;
}

contract ERC20 {
    //  ...
    function transfer(address to, uint256 amount) public {
        require(tokens[msg.sender] >= amount, "Not enough funds for transfer");
        tokens[to] += amount;
        tokens[msg.sender] -= amount;
    }
}

contract MyToken is ERC20DataLayout, ERC20, Owned, Proxiable, LibraryLock {

    function constructor1(uint256 _initialSupply) public {
        totalSupply = _initialSupply;
        tokens[msg.sender] = _initialSupply;
        initialize();
        setOwner(msg.sender);
    }
    function updateCode(address newCode) public onlyOwner delegatedOnly  {
        updateCodeAddress(newCode);
    }
    function transfer(address to, uint256 amount) public delegatedOnly {
        ERC20.transfer(to, amount);
    }
}
```

## 参考文献

- ["Escape-hatch" proxy Medium Post](https://medium.com/terminaldotco/escape-hatch-proxy-efb681de108d)

## 著作権

著作権およびその関連権利は [CC0](../LICENSE.md) で放棄されています。

[1]: https://github.com/maraoz/solidity-proxy/blob/master/contracts/Dispatcher.sol
[2]: https://blog.gnosis.pm/solidity-delegateproxy-contracts-e09957d0f201