---
original: 9dbf6078431a782c068e2cd80842d4574ccfd86f9cf2456652d63dbcd47d55e5
---

---
eip: 3589
title: アセットをNFTにアセンブルする
author: Zhenyu Sun (@Ungigdu), Xinqi Yang (@xinqiyang)
discussions-to: https://github.com/ethereum/EIPs/issues/3590
status: 停滞
type: Standards Track
category: ERC
created: 2021-05-24
requires: 721
---

## 簡単な要約
このスタンダードは、アセンブリトークンと呼ばれるERC-721トークンを定義し、アセットの組み合わせを表すことができます。

## 概要
ERC-1155マルチトークンコントラクトは、トークンをバッチ転送する方法を定義していますが、それらのトークンはERC-1155コントラクト自体によってミントされる必要があります。このEIPは、ETH、ERC-20トークン、ERC-721トークン、ERC-1155トークンなどのアセットをERC-721トークンにアセンブルする機能を持つERC-721の拡張機能です。アセットがアセンブルされると、バッチ転送やスワップを非常に簡単に実装できます。

## 動機
NFTアートや収集家が急速に増加するにつれ、一部の収集家は従来の取引方法に満足していません。2人の収集家が自分のコレクションの一部を交換したい場合、現在は自分のNFTをマーケットに出品し、相手に購入を通知する必要がありますが、これは非効率的でガス集約的です。代わりに、一部の収集家は信頼できる第三者を見つけてNFTを交換しようとしています。第三者は収集家Aと収集家BからそれぞれのNFTを受け取り、Aの所有物をBに、Bの所有物をAに転送します。これは非常に危険です。

バッチスワップを安全に行う最良の方法は、アトミックスワップ、つまり1対1のスワップに変換することです。しかし、まずはETH、ERC-20トークン、ERC-721トークン、ERC-1155トークンをアセンブルする必要があり、これがこのEIPの主な目的です。

## 仕様
この文書の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

ERC-721準拠のコントラクトは、アセットをアセンブルするための標準的な方法を提供するためにこのEIPを実装してもよいです。

`mint`と`safeMint`は、アセットを1つのERC-721トークンにアセンブルします。`_transfer`が損失のない通常のERC-20トークンの場合は`mint`を実装する必要があります。PIG トークンのように`_transfer`関数に税金がかかるような損失のあるトークンの場合は、`safeMint`を実装する必要があります。

`hash`関数の`_salt`は、ユーザー入力として提供されるなど、他の方法で実装できます。ただし、トークンIDは`hash`関数によって生成される必要があります。

この標準の実装は、さまざまなアセットのセットをサポートできます。

このスタンダードの実装者は、必ず以下の機能をすべて持っている必要があります:

```
pragma solidity ^0.8.0;

interface AssemblyNFTInterface {

  event AssemblyAsset(address indexed firstHolder,
                    uint256 indexed tokenId,
                    uint256 salt,
                    address[] addresses,
                    uint256[] numbers);

  /**
  * @dev ハッシュ関数は、塩を使ってアセットの組み合わせをbytes32シグネチャに割り当て、それがトークンIDにもなります。
  * @param `_salt` ハッシュ衝突を防ぐためのもので、ユーザー入力やコントラクトからのインクリメントされるnonce値で選択できます。
  * @param `_addresses` アセットのアドレスを連結したもの。例: [ERC-20_address1, ERC-20_address2, ERC-721_address_1, ERC-1155_address_1, ERC-1155_address_2]
  * @param `_numbers` ETH、ERC-20トークンアドレスの長さ、ERC-721トークンアドレスの長さ、ERC-1155トークンアドレスの長さ、ERC-20トークンの量、ERC-721トークンのID、ERC-1155トークンのIDと量を表します。
  */
  function hash(uint256 _salt, address[] memory _addresses, uint256[] memory _numbers) external pure returns (uint256 tokenId);

  /// @dev 損失のないアセットをアセンブルする
  /// @param `_to` アセンブリトークンの受取人
  function mint(address _to, address[] memory _addresses, uint256[] memory _numbers) payable external returns(uint256 tokenId);

  /// @dev 実際に受け取った値を計算する追加のロジックを持つミント
  function safeMint(address _to, address[] memory _addresses, uint256[] memory _numbers) payable external returns(uint256 tokenId);

  /// @dev このトークンを焼却し、アセンブルされたアセットを解放する
  /// @param `_to` アセットを解放するアドレス
  function burn(address _to, uint256 _tokenId, uint256 _salt, address[] calldata _addresses, uint256[] calldata _numbers) external;

}

```

## 根拠
NFTをまとめて管理したい理由はたくさんあります。例えば、収集家がサッカー選手のNFTをサッカーチームにまとめたい、数百もの無分類のNFTを持っている収集家、完全なNFTコレクションを購入したい収集家など、それらのNFTをまとめる方法が必要です。

ERC-721標準をラッパーとして選択した理由は、ERC-721トークンがすでに広く使用され、NFTウォレットによく対応しているためです。アセンブリトークン自体も再びアセンブルできます。バッチ取引、バッチスワップ、コレクション交換などのシナリオでは、アセットのバッチよりもアセンブリトークンの方がスマートコントラクトで使いやすいです。

このスタンダードにはAssemblyAssetイベントがあり、アセンブリトークンが表すアセットの正確な種類と量が記録されます。ウォレットはトークンIDだけでそれらのNFTを簡単に表示できます。

## 下位互換性
このプロポーザルは既存の721拡張機能を組み合わせたものであり、ERC-721標準との下位互換性があります。

## 実装
```
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/token/ERC721/utils/ERC721Holder.sol";
import "@openzeppelin/contracts/token/ERC1155/ERC1155.sol";
import "@openzeppelin/contracts/token/ERC1155/utils/ERC1155Holder.sol";
import "./AssemblyNFTInterface.sol";

abstract contract AssemblyNFT is ERC721, ERC721Holder, ERC1155Holder, AssemblyNFTInterface{
  using SafeERC20 for IERC20;

  function supportsInterface(bytes4 interfaceId) public view virtual override(ERC721, ERC1155Receiver) returns (bool) {
        return ERC721.supportsInterface(interfaceId) || ERC1155Receiver.supportsInterface(interfaceId);
  }

  uint256 nonce;

  /**
  * _addressesのレイアウト:
  *     erc20アドレス | erc721アドレス | erc1155アドレス
  * _numbersのレイアウト:
  *     eth | erc20.length | erc721.length | erc1155.length | erc20の量 | erc721のID | erc1155のID | erc1155の量
   */

  function hash(uint256 _salt, address[] memory _addresses, uint256[] memory _numbers) public pure override returns (uint256 tokenId){
      bytes32 signature = keccak256(abi.encodePacked(_salt));
      for(uint256 i=0; i< _addresses.length; i++){
        signature = keccak256(abi.encodePacked(signature, _addresses[i]));
      }
      for(uint256 j=0; j<_numbers.length; j++){
        signature = keccak256(abi.encodePacked(signature, _numbers[j]));
      }
      assembly {
        tokenId := signature
      }
  }

  function mint(address _to, address[] memory _addresses, uint256[] memory _numbers) payable external override returns(uint256 tokenId){
      require(_to != address(0), "address(0)にはミントできません");
      require(msg.value == _numbers[0], "値が一致しません");
      require(_addresses.length == _numbers[1] + _numbers[2] + _numbers[3], "2つの配列の長さが一致しません");
      require(_addresses.length == _numbers.length -4 - _numbers[3], "numbersの長さが一致しません");
      uint256 pointerA; //最初のerc20アドレスを指す、もしあれば
      uint256 pointerB =4; //最初のerc20の量を指す、もしあれば
      for(uint256 i = 0; i< _numbers[1]; i++){
        require(_numbers[pointerB] > 0, "erc20の0量を転送");
        IERC20(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB++]);
      }
      for(uint256 j = 0; j< _numbers[2]; j++){
        IERC721(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB++]);
      }
      for(uint256 k =0; k< _numbers[3]; k++){
        IERC1155(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB], _numbers[_numbers[3] + pointerB++], "");
      }
      tokenId = hash(nonce, _addresses, _numbers);
      super._mint(_to, tokenId);
      emit AssemblyAsset(_to, tokenId, nonce, _addresses, _numbers);
      nonce ++;
  }

  function safeMint(address _to, address[] memory _addresses, uint256[] memory _numbers) payable external override returns(uint256 tokenId){
      require(_to != address(0), "address(0)にはミントできません");
      require(msg.value == _numbers[0], "値が一致しません");
      require(_addresses.length == _numbers[1] + _numbers[2] + _numbers[3], "2つの配列の長さが一致しません");
      require(_addresses.length == _numbers.length -4 - _numbers[3], "numbersの長さが一致しません");
      uint256 pointerA; //最初のerc20アドレスを指す、もしあれば
      uint256 pointerB =4; //最初のerc20の量を指す、もしあれば
      for(uint256 i = 0; i< _numbers[1]; i++){
        require(_numbers[pointerB] > 0, "erc20の0量を転送");
        IERC20 token = IERC20(_addresses[pointerA++]);
        uint256 orgBalance = token.balanceOf(address(this));
        token.safeTransferFrom(_msgSender(), address(this), _numbers[pointerB]);
        _numbers[pointerB++] = token.balanceOf(address(this)) - orgBalance;
      }
      for(uint256 j = 0; j< _numbers[2]; j++){
        IERC721(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB++]);
      }
      for(uint256 k =0; k< _numbers[3]; k++){
        IERC1155(_addresses[pointerA++]).safeTransferFrom(_msgSender(), address(this), _numbers[pointerB], _numbers[_numbers[3] + pointerB++], "");
      }
      tokenId = hash(nonce, _addresses, _numbers);
      super._mint(_to, tokenId);
      emit AssemblyAsset(_to, tokenId, nonce, _addresses, _numbers);
      nonce ++;
  }

  function burn(address _to, uint256 _tokenId, uint256 _salt, address[] calldata _addresses, uint256[] calldata _numbers) override external {
      require(_msgSender() == ownerOf(_tokenId), "所有していません");
      require(_tokenId == hash(_salt, _addresses, _numbers));
      super._burn(_tokenId);
      payable(_to).transfer(_numbers[0]);
      uint256 pointerA; //最初のerc20アドレスを指す、もしあれば
      uint256 pointerB =4; //最初のerc20の量を指す、もしあれば
      for(uint256 i = 0; i< _numbers[1]; i++){
        require(_numbers[pointerB] > 0, "erc20の0量を転送");
        IERC20(_addresses[pointerA++]).safeTransfer(_to, _numbers[pointerB++]);
      }
      for(uint256 j = 0; j< _numbers[2]; j++){
        IERC721(_addresses[pointerA++]).safeTransferFrom(address(this), _to, _numbers[pointerB++]);
      }
      for(uint256 k =0; k< _numbers[3]; k++){
        IERC1155(_addresses[pointerA++]).safeTransferFrom(address(this), _to, _numbers[pointerB], _numbers[_numbers[3] + pointerB++], "");
      }
  }

}
```

## セキュリティ上の考慮事項
`mint`または`safeMint`関数を使う前に、
ユーザーは、一部のトークンの実装が一時停止可能であることを認識しておく必要があります。アセンブルされた後にアセットの1つが一時停止された場合、`burn`関数が正常に実行されない可能性があります。このスタンダードを使用するプラットフォームは、このような状況を避けるためにサポートリストやブラックリストを作成する必要があります。

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。