---
original: f67523e25ff9395b4e9d896466ca2c44708003383e9e819fb71a3d9228724725
---

---
eip: 3561
title: 信頼最小化アップグレード可能プロキシ
description: 指定されたアップグレードがライブになる前に遅延を持つプロキシ
author: Sam Porter (@SamPorter1984)
discussions-to: https://ethereum-magicians.org/t/trust-minimized-proxy/5742
status: 停滞
type: Standards Track
category: ERC
created: 2021-05-09
---

## 概要

匿名の開発者にとって、アップグレード可能プロキシからの信頼を取り除くことが必要です。これを達成するためには、即座で潜在的に悪意のあるアップグレードを防ぐ必要があります。このEIPは、アップグレード可能なスマートコントラクトとの対話における信頼を低減すると考えられる、アップグレード可能プロキシのための追加のストレージスロットを導入します。管理者の実装ロジックは、ゼロ信頼期間が経過した後にのみ、アクティブな実装ロジックになることができます。

## 動機

匿名の開発者が利用するアップグレード可能プロキシは、コミュニティの信頼を得るのが一般的に難しい。

より公平で良い人類の未来には、提案するソリューションに重要な注目を集めつつ、匿名性を維持しながらアップグレード可能性の恩恵を活用できる開発者が必要不可欠です。

## 仕様

この仕様は、標準的な[EIP-1967](./eip-1967.md)透過プロキシデザインに追加されるものです。
この仕様は、追加するスロットに焦点を当てています。信頼最小化プロキシとの全ての管理者の相互作用は、管理者の行動を追跡可能にするためにイベントを発生させる必要があり、全ての管理者の行動は `onlyAdmin()` 修飾子で保護されなければなりません。

### 次のロジックコントラクトアドレス

ストレージスロット `0x19e3fabe07b65998b604369d85524946766191ac9434b39e27c424c976493685` (
`bytes32(uint256(keccak256('eip3561.proxy.next.logic')) - 1)` として取得)。
望ましい実装ロジックアドレスは、`IMPLEMENTATION_SLOT` の実際のロジック実装として機能する前に、最初に次のロジックとして定義されなければなりません。
次のロジックコントラクトアドレスとの管理者の相互作用は、以下のメソッドとイベントに対応します:

```solidity
// 次のロジックコントラクトアドレスを設定する。NextLogicDefinedイベントを発生させる
// 現在の実装が address(0) の場合、即座に IMPLEMENTATION_SLOT にアップグレードするため、データを引数として受け取る
function proposeTo(address implementation, bytes calldata data) external IfAdmin
// UPGRADE_BLOCK_SLOT が許可するようになったら、次の実装として保存されたアドレスを
// 現在の IMPLEMENTATION_SLOT に設定し、それを初期化する
function upgrade(bytes calldata data) external IfAdmin
// 次のロジックのアップグレードが呼び出されていない限り、キャンセルが可能
// NextLogicCanceledイベントを発生させる
function cancelUpgrade() external onlyAdmin;

event NextLogicDefined(address indexed nextLogic, uint earliestArrivalBlock); // 重要
event NextLogicCanceled(address indexed oldLogic);
```

### アップグレードブロック

ストレージスロット `0xe3228ec3416340815a9ca41bfee1103c47feb764b4f0f4412f5d92df539fe0ee` (
`bytes32(uint256(keccak256('eip3561.proxy.next.logic.block')) - 1)` として取得)。
このブロック以降、次のロジックコントラクトアドレスを EIP-1967 `IMPLEMENTATION_SLOT` に設定したり、つまり `upgrade()` を呼び出したりできます。ゼロ信頼期間に従って自動的に更新され、`NextLogicDefined` イベントの `earliestArrivalBlock` として表示されます。

### 提案ブロック

ストレージスロット `0x4b50776e56454fad8a52805daac1d9fd77ef59e4f1a053c342aaae5568af1388` (
`bytes32(uint256(keccak256('eip3561.proxy.propose.block')) - 1)` として取得)。
次のロジックを提案できるようになるブロックを定義します。便宜上必要とされ、例えば現在時刻から1年後に手動で設定できます。完全にコードをシールするために、最大値に設定することもできます。
このスロットとの管理者の相互作用は、以下のメソッドとイベントに対応します:

```solidity
function prolongLock(uint b) external onlyAdmin;
event ProposingUpgradesRestrictedUntil(uint block, uint nextProposedLogicEarliestArrival);
```

### ゼロ信頼期間

ストレージスロット `0x7913203adedf5aca5386654362047f05edbd30729ae4b0351441c46289146720` (
`bytes32(uint256(keccak256('eip3561.proxy.zero.trust.period')) - 1)` として取得)。
ブロック数単位のゼロ信頼期間で、前の値よりも大きくしか設定できません。デフォルト値(0)の間は、プロキシは標準的なEIP-1967透過プロキシと全く同じように動作します。ゼロ信頼期間が設定された後は、上記の仕様が強制されます。
このスロットとの管理者の相互作用は、以下のメソッドとイベントに対応する必要があります:

```solidity
function setZeroTrustPeriod(uint blocks) external onlyAdmin;
event ZeroTrustPeriodSet(uint blocks);
```

### 実装例

```solidity
pragma solidity >=0.8.0; //重要

// EIP-3561 信頼最小化プロキシの実装 https://github.com/ethereum/EIPs/blob/master/EIPS/eip-3561.md
// EIP-1967 アップグレード可能プロキシに基づいている: https://github.com/ethereum/EIPs/blob/master/EIPS/eip-1967.md

contract TrustMinimizedProxy {
    event Upgraded(address indexed toLogic);
    event AdminChanged(address indexed previousAdmin, address indexed newAdmin);
    event NextLogicDefined(address indexed nextLogic, uint earliestArrivalBlock);
    event ProposingUpgradesRestrictedUntil(uint block, uint nextProposedLogicEarliestArrival);
    event NextLogicCanceled();
    event ZeroTrustPeriodSet(uint blocks);

    bytes32 internal constant ADMIN_SLOT = 0xb53127684a568b3173ae13b9f8a6016e243e63b6e8ee1178d6a717850b5d6103;
    bytes32 internal constant LOGIC_SLOT = 0x360894a13ba1a3210667c828492db98dca3e2076cc3735a920a3ca505d382bbc;
    bytes32 internal constant NEXT_LOGIC_SLOT = 0x19e3fabe07b65998b604369d85524946766191ac9434b39e27c424c976493685;
    bytes32 internal constant NEXT_LOGIC_BLOCK_SLOT = 0xe3228ec3416340815a9ca41bfee1103c47feb764b4f0f4412f5d92df539fe0ee;
    bytes32 internal constant PROPOSE_BLOCK_SLOT = 0x4b50776e56454fad8a52805daac1d9fd77ef59e4f1a053c342aaae5568af1388;
    bytes32 internal constant ZERO_TRUST_PERIOD_SLOT = 0x7913203adedf5aca5386654362047f05edbd30729ae4b0351441c46289146720;

    constructor() payable {
        require(
            ADMIN_SLOT == bytes32(uint256(keccak256('eip1967.proxy.admin')) - 1) &&
                LOGIC_SLOT == bytes32(uint256(keccak256('eip1967.proxy.implementation')) - 1) &&
                NEXT_LOGIC_SLOT == bytes32(uint256(keccak256('eip3561.proxy.next.logic')) - 1) &&
                NEXT_LOGIC_BLOCK_SLOT == bytes32(uint256(keccak256('eip3561.proxy.next.logic.block')) - 1) &&
                PROPOSE_BLOCK_SLOT == bytes32(uint256(keccak256('eip3561.proxy.propose.block')) - 1) &&
                ZERO_TRUST_PERIOD_SLOT == bytes32(uint256(keccak256('eip3561.proxy.zero.trust.period')) - 1)
        );
        _setAdmin(msg.sender);
    }

    modifier IfAdmin() {
        if (msg.sender == _admin()) {
            _;
        } else {
            _fallback();
        }
    }

    function _logic() internal view returns (address logic) {
        assembly {
            logic := sload(LOGIC_SLOT)
        }
    }

    function _nextLogic() internal view returns (address nextLogic) {
        assembly {
            nextLogic := sload(NEXT_LOGIC_SLOT)
        }
    }

    function _proposeBlock() internal view returns (uint b) {
        assembly {
            b := sload(PROPOSE_BLOCK_SLOT)
        }
    }

    function _nextLogicBlock() internal view returns (uint b) {
        assembly {
            b := sload(NEXT_LOGIC_BLOCK_SLOT)
        }
    }

    function _zeroTrustPeriod() internal view returns (uint ztp) {
        assembly {
            ztp := sload(ZERO_TRUST_PERIOD_SLOT)
        }
    }

    function _admin() internal view returns (address adm) {
        assembly {
            adm := sload(ADMIN_SLOT)
        }
    }

    function _setAdmin(address newAdm) internal {
        assembly {
            sstore(ADMIN_SLOT, newAdm)
        }
    }

    function changeAdmin(address newAdm) external IfAdmin {
        emit AdminChanged(_admin(), newAdm);
        _setAdmin(newAdm);
    }

    function upgrade(bytes calldata data) external IfAdmin {
        require(block.number >= _nextLogicBlock(), 'too soon');
        address logic;
        assembly {
            logic := sload(NEXT_LOGIC_SLOT)
            sstore(LOGIC_SLOT, logic)
        }
        (bool success, ) = logic.delegatecall(data);
        require(success, 'failed to call');
        emit Upgraded(logic);
    }

    fallback() external payable {
        _fallback();
    }

    receive() external payable {
        _fallback();
    }

    function _fallback() internal {
        require(msg.sender != _admin());
        _delegate(_logic());
    }

    function cancelUpgrade() external IfAdmin {
        address logic;
        assembly {
            logic := sload(LOGIC_SLOT)
            sstore(NEXT_LOGIC_SLOT, logic)
        }
        emit NextLogicCanceled();
    }

    function prolongLock(uint b) external IfAdmin {
        require(b > _proposeBlock(), 'can be only set higher');
        assembly {
            sstore(PROPOSE_BLOCK_SLOT, b)
        }
        emit ProposingUpgradesRestrictedUntil(b, b + _zeroTrustPeriod());
    }

    function setZeroTrustPeriod(uint blocks) external IfAdmin {
        // これが設定される前は、通常のEIP 1967透過プロキシのように動作する
        uint ztp;
        assembly {
            ztp := sload(ZERO_TRUST_PERIOD_SLOT)
        }
        require(blocks > ztp, 'can be only set higher');
        assembly {
            sstore(ZERO_TRUST_PERIOD_SLOT, blocks)
        }
        _updateNextBlockSlot();
        emit ZeroTrustPeriodSet(blocks);
    }

    function _updateNextBlockSlot() internal {
        uint nlb = block.number + _zeroTrustPeriod();
        assembly {
            sstore(NEXT_LOGIC_BLOCK_SLOT, nlb)
        }
    }

    function _setNextLogic(address nl) internal {
        require(block.number >= _proposeBlock(), 'too soon');
        _updateNextBlockSlot();
        assembly {
            sstore(NEXT_LOGIC_SLOT, nl)
        }
        emit NextLogicDefined(nl, block.number + _zeroTrustPeriod());
    }

    function proposeTo(address newLogic, bytes calldata data) external payable IfAdmin {
        if (_zeroTrustPeriod() == 0 || _logic() == address(0)) {
            _updateNextBlockSlot();
            assembly {
                sstore(LOGIC_SLOT, newLogic)
            }
            (bool success, ) = newLogic.delegatecall(data);
            require(success, 'failed to call');
            emit Upgraded(newLogic);
        } else {
            _setNextLogic(newLogic);
        }
    }

    function _delegate(address logic_) internal {
        assembly {
            calldatacopy(0, 0, calldatasize())
            let result := delegatecall(gas(), logic_, 0, calldatasize(), 0, 0)
            returndatacopy(0, 0, returndatasize())
            switch result
            case 0 {
                revert(0, returndatasize())
            }
            default {
                return(0, returndatasize())
            }
        }
    }
}
```

## 根拠

「そもそもそのようなコントラクトをアップグレード可能にしないでおけば良い」という議論は、複雑なシステムや人的要因に大きく依存するシステムの場合には失敗します。最初から完璧にモデル化することは不可能かもしれません。EIP-1967プロキシを使ったデセントラライズドガバナンスによるアップグレード管理は
特定のプロトコルが成熟し、データが得られるまでは深刻なボトルネックになる可能性があります。

アップグレードの前に時間的な遅延がない単なるプロキシは明らかに悪用される可能性があります。時間的な遅延は避けられないと思われます。経験の浅い開発者がこれを使う自信を持てないという欠点はありますが、今日のスマートコントラクト開発にとって非常に重要なオプションです。

## セキュリティ上の考慮事項

ユーザーは、相互作用するトラスト最小化プロキシが上記のコード例を正確に表現しており、オーバーフローを許可しておらず、ゼロ信頼期間の長さが妥当(通常のアップグレードが事前に公開される場合は最低2週間、ほとんどの場合は1ヶ月以上)であることを確認する必要があります。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。