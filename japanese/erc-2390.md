---
original: 05c7f2f7b29de62d1e1347b2faff70256a8710c6618283192caa7e7eed226ab8
---

---
eip: 2390
title: Geo-ENS
author: James Choncholas (@james-choncholas)
discussions-to: https://github.com/ethereum/EIPs/issues/2959
status: 停滞
type: Standards Track
category: ERC
created: 2019-11-15
requires: 137, 165, 1062, 1185
---

## 簡単な要約
Geo-ENSは、ENSにジオグラフィックなスプリットホライズン機能を提供します。ENSのためのGeoDNSです!

## 概要
このEIPは、地理的にスプリットホライズンのDNSのためのENSリゾルバーインターフェースを指定しています。
地理的にスプリットホライズンのDNSは、エンドユーザーの位置に特化したリソースレコードを返します。
この手法は、ユーザーに最も近いコンテンツキャッシュにトラフィックを誘導するためにCDNで一般的に使用されています。
地理的にスプリットホライズンの解決は、主にEIP-1185で説明されているDNSリソースレコードを保存するENSリゾルバー向けですが、EIP-1062で説明されているIPFSコンテンツハッシュストレージなどの他のインターフェースでも使用できます。

## 動機
Amazon's Route53などの従来のGeoDNSシステムには、中央集権型Webでの多くの使用例があります。
これらの使用例には、近接ベースのロードバランシングや、クエリの地理的な場所に特化したコンテンツの提供などが含まれます。
残念ながら、ENS仕様には、地理特有の解決のためのメカニズムが提供されていません。
ENSは、EIP-1185で説明されているように、IPアドレスでクエリに応答できますが、地理特有のクエリに応答する方法はありません。
このEIPは、GeoDNSと同様の目的を果たすために、ENSシステムに地理的な近接性を提供する標準を提案しています。

Geo-ENSは、DNSベースのソリューションよりも多くのことができます。
地理的にスプリットホライズンのDNSに加えて、Geo-ENSは以下のようなことにも使用できます:
 - 現実世界の物理的なオブジェクトを表すデジタルリソース(スマートコントラクトなど)の位置特定。
 - 特定の場所に関連付けられた物理的なオブジェクトへのアクセスを管理するスマートコントラクト。
 - EIP-1062で説明されているENS + IPFSウェブホスティングでの、クエリ元の母語への翻訳コンテンツ。
 - 物理的な場所を持つオブジェクトのトークン化。

ENSの分散型の性質により、地理特有の解決は従来のGeoDNSとは異なります。
GeoDNSは次のように機能します。DNSクエリはクエリ元のIPアドレスによって識別されます。
このIPアドレスは、MaxMindの[GeoIP2](https://www.maxmind.com/en/geoip2-services-and-databases)などのデータベースで検索され、IPアドレスが位置に対応付けられます。
クエリ元の位置を特定するこの方法は、誤りやすく信頼性が低いです。
GeoIPデータベースが最新でない場合、クエリされた位置は実際の位置とは大きく異なる可能性があります。
Geo-ENSはデータベースに依存せず、ユーザーが自身の位置を含めてクエリを行うため、この問題はありません。

その結果、ユーザーは自分の位置ではなく、任意の位置でクエリを行うことができます。
従来のDNSでは、クエリの発信元に割り当てられたリソースのみが返されます。
Geo-ENSは、クエリの発信元と位置を関連付けないため、単一の位置から地球全体をクエリできます。

従来のDNSのもう1つの欠点は、特定の近接エリア内のサーバーのリストを返す方法がないことです。
これは、最低レイテンシのリソースを発見する必要がある使用例にとって重要です。
Geo-ENSでは、特定の位置内のリソース(IPアドレスなど)のリストを取得でき、クライアント自身がそのうちの最低レイテンシのリソースを判断できます。

最後に、一般公開されているGeoDNSサービスでは、GeoDNSクエリの地理的な領域の細かい制御ができません。
[Amazon's Route 53](https://aws.amazon.com/route53/)などのクラウドベースのDNSサービスでは、地理的な領域を米国の州の粒度でしか指定できません。
一方、Geo-ENSは8文字のジオハッシュ解像度を提供し、これは約±20メートルの精度に相当します。

## 仕様
このEIPは、ブロックチェーン上で地理空間情報を記録および取得できるように、ENSリゾルバーの新しいインターフェースを提案しています。
以下に説明する変更は、EIP137で説明されている「アドレスリゾルバー」に対するものですが、この考え方はEIP1185とEIP1062で説明されているDNSリゾルバー、Textリゾルバー、ABIリゾルバーなどのあらゆるレコードに適用できます。

### ジオハッシュとは何ですか?
[ジオハッシュ](https://en.m.wikipedia.org/wiki/Geohash#Algorithm_and_example)は、緯度と経度のビットを交互に並べたものです。
その長さによって精度が決まります。
ジオハッシュは通常、base 32の文字列で表現されます。

### function setGeoAddr(bytes32 node, bytes8 geohash, address addr) external authorised(node)
ノードとジオハッシュによりリソース(コントラクトアドレス、IP、ABI、TEXT等)を設定します。
ジオハッシュは、アドレスごとに一意で、正確に8文字の長さでなければなりません。
これにより、±20メートルの精度が得られます。
デフォルトの初期化されたリソース値 `address(0)` を書き込むことで、リゾルバーからリソースを削除できます。

### function geoAddr(bytes32 node, bytes8 geohash) external view returns (address[] memory ret)
指定したノードと位置のリゾルバーコントラクトを照会します。
提供されたノードとプレフィックスジオハッシュに一致するすべてのリソース(コントラクトアドレス、IPアドレス、ABI、TEXTレコードなど)が返されます。
これにより、正確な8文字のジオハッシュでその位置のコンテンツを取得したり、8文字未満の精度のジオハッシュで表される地理的な境界ボックスでクエリを行ったりできます。

[Z-order](https://en.wikipedia.org/wiki/Z-order_curve)、[Hilbert](https://en.wikipedia.org/wiki/Hilbert_curve)、または Google の[S2 Geometry](https://s2geometry.io/devguide/s2cell_hierarchy.html)ライブラリなど、さまざまなタイプのジオハッシュを使用できます。
また、常に長方形のクエリ領域にならずに、ジオハッシュを使ってジオグラフィックデータを検索する方法もあります。
[円形の領域を検索](https://github.com/ashwin711/proximityhash)するのはやや複雑で、複数のクエリが必要です。

## 根拠
提案の実装では、ストレージオーバーヘッドが低く、検索パフォーマンスが良い疎[クアッドツリー](https://dl.acm.org/doi/10.1007/BF00288933)トライを、リソースレコードのインデックスとして使用しています。
ツリーの葉ノードにリソースレコードが格納され、非葉ノードはジオハッシュの1文字を表します。
深さdのツリーのノードは、精度dのジオハッシュを表します。
ツリーの深さは8で、ジオハッシュの最大精度が8文字だからです。
ツリーのファンアウトは32で、ジオハッシュの基数が32だからです。
葉ノードに到達するパスの深さは常に8で、葉には、そのパスで表されるジオハッシュのコンテンツ(IPアドレスなど)が格納されています。
ツリーは疎で、地球の表面の71%が水域であるためです。
ツリーにより、地理的な境界ボックス内のリソースレコードのリストを返す一般的な走査アルゴリズム(DFS、BFS)が可能になります。

## 下位互換性
このEIPは下位互換性の問題を引き起こしません。

## テストケース
https://github.com/james-choncholas/resolvers/blob/master/test/TestPublicResolver.js を参照してください

## 実装
上記の仕様を実装したアドレスリゾルバーをSolidityで記述しました。
ここで示した考え方は、EIP137で指定されている他のリゾルバーインターフェースにも適用できます。
ジオハッシュは64ビットの符号なし整数として渡および保存されることに注意してください。
文字列ではなく整数を使用することで、特に `geomap` マッピングの場合、パフォーマンスが向上します。
比較のために、文字列実装の非効率な例として https://github.com/james-choncholas/geoens/tree/master/contracts/StringOwnedGeoENSResolver.sol を参照してください。

```solidity
pragma solidity ^0.5.0;

import "../ResolverBase.sol";

contract GeoENSResolver is ResolverBase {
    bytes4 constant ERC2390 = 0x8fbcc5ce;
    uint constant MAX_ADDR_RETURNS = 64;
    uint constant TREE_VISITATION_QUEUESZ = 64;
    uint8 constant ASCII_0 = 48;
    uint8 constant ASCII_9 = 57;
    uint8 constant ASCII_a = 97;
    uint8 constant ASCII_b = 98;
    uint8 constant ASCII_i = 105;
    uint8 constant ASCII_l = 108;
    uint8 constant ASCII_o = 111;
    uint8 constant ASCII_z = 122;

    struct Node {
        address data; // 0 if not leaf
        uint256 parent;
        uint256[] children; // always length 32
    }

    // ジオハッシュは8文字、base-32です。
    // ジオマップは、ファンアウト32(ジオハッシュがbase 32だから)、
    // 高さ8(ジオハッシュの長さが8文字だから)のツリーとして保存されます。
    mapping(bytes32=>Node[]) private geomap;

    event GeoENSRecordChanged(bytes32 indexed node, bytes8 geohash, address addr);

    // retの値の下位5ビットのみ使用
    function chartobase32(byte c) pure internal returns (uint8 b) {
        uint8 ascii = uint8(c);
        require( (ascii >= ASCII_0 && ascii <= ASCII_9) ||
                (ascii > ASCII_a && ascii <= ASCII_z));
        require(ascii != ASCII_a);
        require(ascii != ASCII_i);
        require(ascii != ASCII_l);
        require(ascii != ASCII_o);

        if (ascii <= (ASCII_0 + 9)) {
            b = ascii - ASCII_0;

        } else {
            // base32 b = 10
            // ascii 'b' = 0x60
            // base32はアルファベットの'a'を省略する
            b = ascii - ASCII_b + 10;

            // base32はさらに以下の文字も省略する
            if (ascii > ASCII_i)
                b --;
            if (ascii > ASCII_l)
                b --;
            if (ascii > ASCII_o)
                b --;
        }
        require(b < 32); // base 32は32以下でなければならない
        return b;
    }

    function geoAddr(bytes32 node, bytes8 geohash, uint8 precision) external view returns (address[] memory ret) {
        bytes32(node); // single node georesolverはnodeを無視する
        assert(precision <= geohash.length);

        ret = new address[](MAX_ADDR_RETURNS);
        if (geomap[node].length == 0) { return ret; }
        uint ret_i = 0;

        // ジオマップデータ構造に入っていく
        uint pointer = 0; // 実際のポインタではなく、geomap内のインデックス
        for(uint8 i=0; i < precision; i++) {

            uint8 c = chartobase32(geohash[i]);
            uint next = geomap[node][pointer].children[c];
            if (next == 0) {
                // このジオハッシュには何も見つからない
                // 早期リターン
                return ret;
            } else {
                pointer = next;
            }
        }

        // pointerは、クエリジオハッシュの解決を表すノードを指しています。
        // retが一杯になるか、すべてのアドレスが見つかるまでDFSを行う。
        // ブロックチェーンのため、再帰は使用しない。
        uint[] memory indexes_to_visit = new uint[](TREE_VISITATION_QUEUES
Z);
        indexes_to_visit[0] = pointer;
        uint front_i = 0;
        uint back_i = 1;

        while(front_i != back_i) {
            Node memory cur_node = geomap[node][indexes_to_visit[front_i]];
            front_i ++;

            // 葉ノードでない場合...
            if (cur_node.data == address(0)) {
                // 全ての子ノードを訪問する
                for(uint i=0; i<cur_node.children.length; i++) {
                    // 有効な子ノードのみ訪問する
                    if (cur_node.children[i] != 0) {
                        assert(back_i < TREE_VISITATION_QUEUESZ);
                        indexes_to_visit[back_i] = cur_node.children[i];
                        back_i ++;

                    }
                }
            } else {
                ret[ret_i] = cur_node.data;
                ret_i ++;
                if (ret_i > MAX_ADDR_RETURNS) break;
            }
        }

        return ret;
    }

    // 設定時、ジオハッシュは8桁の精度でなければならない
    function setGeoAddr(bytes32 node, bytes8 geohash, address addr) external authorised(node) {
        bytes32(node); // single node georesolverはnodeを無視する

        // まだ作成されていない場合はルートノードを作成する
        if (geomap[node].length == 0) {
            geomap[node].push( Node({
                data: address(0),
                parent: 0,
                children: new uint256[](32)
            }));
        }

        // ジオマップデータ構造に入っていく
        uint pointer = 0; // 実際のポインタではなく、geomap内のインデックス
        for(uint i=0; i < geohash.length; i++) {

            uint8 c = chartobase32(geohash[i]);

            if (geomap[node][pointer].children[c] == 0) {
                // このジオハッシュには何も見つからない
                // リーフへのパスを作成する必要がある
                geomap[node].push( Node({
                    data: address(0),
                    parent: pointer,
                    children: new uint256[](32)
                }));
                geomap[node][pointer].children[c] = geomap[node].length - 1;
            }
            pointer = geomap[node][pointer].children[c];
        }

        Node storage cur_node = geomap[node][pointer]; // storageで参照を取得
        cur_node.data = addr;

        emit GeoENSRecordChanged(node, geohash, addr);
    }

    function supportsInterface(bytes4 interfaceID) public pure returns (bool) {
        return interfaceID == ERC2390 || super.supportsInterface(interfaceID);
    }
}
```

## セキュリティ上の考慮事項
このコントラクトはENSリゾルバーと同様の機能を持っているため、そちらのセキュリティ上の考慮事項を参照してください。
さらに、このコントラクトにはデータプライバシーの側面があります。
ユーザーは8文字未満のジオハッシュを指定してgeoAddrを呼び出し、クエリ領域を定義します。
軽量クライアントを使用するユーザーは、クエリ領域を接続しているフルノードに漏らします。
サードパーティ(Infuraなど)が運営するノードに依存するユーザーも、クエリ領域を漏らします。
自身のフルノードを実行するか、信頼できるフルノードにアクセスできるユーザーは、位置データを一切漏らしません。

ほとんどの位置情報サービスの動作方式を考えると、クエリ領域にはユーザーの実際の位置が含まれる可能性が高いです。
APIアクセス、軽量、フルノードの違いによるプライバシーへの影響は常にありましたが、今回は粗い粒度のユーザー位置情報が関係しているため、その影響が強調されています。

## 著作権
著作権およびそれに関連する権利は[CC0](../LICENSE.md)により放棄されています。