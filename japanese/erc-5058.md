---
original: ba9ac69b466c436676216030786cf7d70633e2804a42d4a400e1493279bf4465
---

---
eip: 5058
title: ロック可能な非代替性トークン
description: ロック可能な EIP-721 トークン
author: Tyler (@radiocaca)、Alex (@gojazdev)、John (@sfumato00)
discussions-to: https://ethereum-magicians.org/t/eip-5058-erc-721-lockable-standard/9201
status: 停滞
type: Standards Track
category: ERC
created: 2022-04-30
requires: 20, 165, 721
---

## 概要

私たちは、[EIP-721](./eip-721.md) 標準に安全なロック機能を拡張することを提案します。NFT 所有者は、`setLockApprovalForAll()` または `lockApprove()` を通じて、オペレーターにNFTをロックする権限を承認します。承認されたオペレーターは `lock()` を使ってNFTをロックします。ロックされたNFTは、ロック期間が終了するまで転送できません。即座の使用例は、オーナーのウォレットから離れることなく、NFTをスマートコントラクトに参加させることです。

## 動機

[EIP-721](./eip-721.md) によって可能になった NFT は、需要が爆発的に増加しています。総市場価値とエコシステムは、従来の意味での人気のある知的財産に匹敵する多くのブルーチップ NFT とともに、さらに成長し続けています。この大きな成功にもかかわらず、改善の余地があります。流動性は常に NFT にとっての最大の課題の1つでした。この流動性の課題に取り組むためのいくつかの試みがなされています。NFTFi や BendDAO などがその例です。現在主流の EIP-721 標準を利用するこれらのプロジェクトでは、参加する NFT をプロジェクトのコントラクトに転送する必要があり、これはオーナーにとって不便と危険が伴います:

1. スマートコントラクトのリスク: バグや脆弱性によって、NFT が失われたり盗まれたりする可能性があります。
2. 効用の喪失: NFT には、プロフィール写真や自慢の権利などの効用価値がありますが、NFT がオーナーの管理下にない場合はこれらの効用が失われます。
3. エアドロップの喪失: オーナーは、NFT に付与されるエアドロップを直接受け取ることができなくなります。一部のエアドロップの価値や価格変動を考えると、エアドロップを逸失したり適時に受け取れないことは、オーナーに財務的な影響を与える可能性があります。

これらはすべて良くないユーザーエクスペリエンスであり、私たちは EIP-721 標準にネイティブのロック機能を採用することで改善できると考えています:

1. スマートコントラクトに転送するのではなく、NFT は自己管理下にあるがロックされた状態になります。
2. NFT がロックされている間は、その転送が禁止されます。その他の特性は影響を受けません。
3. オーナーはエアドロップを自分で受け取ったり請求したりできます。

NFT の価値は、コレクション価値と効用価値の2つの側面で反映されます。コレクション価値は、ホルダーのウォレットが永遠にNFTの所有権を保持することを保証する必要があります。効用価値は、ホルダーが他のプロジェクトでNFTの所有権を確認できることを保証する必要があります。これらの両方の側面は、NFTがオーナーのウォレットに留まることを要求します。

提案された標準は、EIP-721 標準を拡張してロック、ステーキング、レンディング、クラウドファンディングなどの一般的な NFTFi ユースケースをネイティブにサポートすることで、基礎となるNFTアセットを安全かつ便利に管理できるようにします。この提案された標準により、NFTオーナーがNFTFiプロジェクトにより積極的に参加するようになり、ひいてはNFTエコシステム全体の活性化につながると考えています。

## 仕様

この文書の中の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119 で説明されているように解釈されるものとします。

ロック可能な EIP-721 **MUST** `IERC5058` インターフェースを実装する:

```solidity
// SPDX-License-Identifier: CC0-1.0

pragma solidity ^0.8.8;

/**
 * @dev EIP-721 Non-Fungible Token Standard, optional lockable extension
 * ERC721 Token that can be locked for a certain period and cannot be transferred.
 * This is designed for a non-escrow staking contract that comes later to lock a user's NFT
 * while still letting them keep it in their wallet.
 * This extension can ensure the security of user tokens during the staking period.
 * If the nft lending protocol is compatible with this extension, the trouble caused by the NFT
 * airdrop can be avoided, because the airdrop is still in the user's wallet
 */
interface IERC5058 {
    /**
     * @dev Emitted when `tokenId` token is locked by `operator` from `from`.
     */
    event Locked(address indexed operator, address indexed from, uint256 indexed tokenId, uint256 expired);

    /**
     * @dev Emitted when `tokenId` token is unlocked by `operator` from `from`.
     */
    event Unlocked(address indexed operator, address indexed from, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables `approved` to lock the `tokenId` token.
     */
    event LockApproval(address indexed owner, address indexed approved, uint256 indexed tokenId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to lock all of its tokens.
     */
    event LockApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the locker who is locking the `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function lockerOf(uint256 tokenId) external view returns (address locker);

    /**
     * @dev Lock `tokenId` token until the block number is greater than `expired` to be unlocked.
     *
     * Requirements:
     *
     * - `tokenId` token must be owned by `owner`.
     * - `expired` must be greater than block.number
     * - If the caller is not `owner`, it must be approved to lock this token
     * by either {lockApprove} or {setLockApprovalForAll}.
     *
     * Emits a {Locked} event.
     */
    function lock(uint256 tokenId, uint256 expired) external;

    /**
     * @dev Unlock `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` token must be owned by `owner`.
     * - the caller must be the operator who locks the token by {lock}
     *
     * Emits a {Unlocked} event.
     */
    function unlock(uint256 tokenId) external;

    /**
     * @dev Gives permission to `to` to lock `tokenId` token.
     *
     * Requirements:
     *
     * - The caller must own the token or be an approved lock operator.
     * - `tokenId` must exist.
     *
     * Emits an {LockApproval} event.
     */
    function lockApprove(address to, uint256 tokenId) external;

    /**
     * @dev Approve or remove `operator` as an lock operator for the caller.
     * Operators can call {lock} for any token owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` cannot be the caller.
     *
     * Emits an {LockApprovalForAll} event.
     */
    function setLockApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns the account lock approved for `tokenId` token.
     *
     * Requirements:
     *
     * - `tokenId` must exist.
     */
    function getLockApproved(uint256 tokenId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to lock all of the assets of `owner`.
     *
     * See {setLockApprovalForAll}
     */
    function isLockApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Returns if the `tokenId` token is locked.
     */
    function isLocked(uint256 tokenId) external view returns (bool);

    /**
     * @dev Returns the `tokenId` token lock expired time.
     */
    function lockExpiredTime(uint256 tokenId) external view returns (uint256);
}
```

## 根拠

### NFT ロックの承認

NFT オーナーは、承認機能を通じて、他の信頼できるオペレーターにNFTをロックする権限を与えることができます。`lockApprove()` 関数は特定のNFTについてのみ承認しますが、`setLockApprovalForAll()` は所有するコレクションのすべてのNFTについて承認します。ユーザーがNFTFiプロジェクトに参加する際、プロジェクトのコントラクトは `lock()` を呼び出してユーザーのNFTをロックします。ロックされたNFTは転送できませんが、NFTFiプロジェクトのコントラクトは `unlock()` 関数を使ってNFTのロックを解除することができます。

### NFT のロック/ロック解除

承認されたプロジェクトのコントラクトは、`lock` メソッドを使ってNFTをロックする権限を持っています。ロックされたNFTは、ロック期間が終了するまで転送できません。プロジェクトのコントラクトは、`unlock` 関数を使ってNFTのロックを事前に解除することもできます。ただし、ロックされたNFTのアドレスのみがそのNFTのロックを解除する権限を持っています。

### NFT ロック期間

NFTをロックする際は、現在のブロック番号よりも大きいロック期限ブロック番号を指定する必要があります。現在のブロック番号がロック期限ブロック番号を超えると、NFTは自動的に解放され、転送できるようになります。

### バウンドNFT

バウンドNFTは、この EIP の拡張機能で、NFTロック期間中にバウンドNFTをミントする機能を実装したものです。バウンドNFTはロックされたNFTのメタデータと同一ですが、転送可能です。ただし、バウンドNFTはNFTがロック中にのみ存在し、NFTのロックが解除されると破棄されます。
バウンドNFTは、レンディングやコントラクトへのステーキング資格情報として使用できます。資格情報はコントラクトにロックできますが、ユーザーにも所有させることができます。NFTレンディングでは、バウンドNFTをユーザーにレンタルできます。このコンセンサスが全プロジェクトで受け入れられれば、バウンドNFTはNFTにさらなる創造性をもたらすでしょう。

### バウンドNFT ファクトリ

バウンドNFT ファクトリは、Uniswapの[EIP-20](./eip-20.md) ペアファクトリのようなバウンドNFTの共通ファクトリです。create2 メソッドを使用して、任意のNFTの決定論的なバウンドNFTコントラクトアドレスを作成します。作成されたバウンドNFTコントラクトは、元のNFTコントラクトによってのみ制御できます。

## 下位互換性

この標準は EIP-721 と互換性があります。

## テストケース

Hardhat を使って書かれたテストケースは[こちら](../assets/eip-5058/test/test.ts)にあります。

## 参考実装

この標準の実装は[assets](../assets/eip-5058/ERC5058.sol)フォルダにあります。

## セキュリティ上の考慮事項

NFTがロックされると転送できなくなるため、他のプロジェクトのコントラクトにロック権限を与える前に、そのプロジェクトのコントラクトがNFTのロックを解除できることを確認する必要があります。そうしないと、NFTが永久にロックされるリスクがあります。プロジェクトでの使用に合理的なロック期間を設定することをお勧めします。NFTは自動的にロック解除されるため、リスクを一定程度軽減できます。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。