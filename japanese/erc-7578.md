---
original: 0c1546f8eaf838830b17b88bd6542a77472157500e7c016dbcd36bb145d3fad4
---

---
eip: 7578
title: 物理資産の償還
description: 物理資産支援NFTの所有者に、基礎となる物理資産に関する現実世界の情報を容易に入手できるようにします。
author: Lee Vidor (@V1d0r), David Tan <david@emergentx.org>, Lee Smith <lee@emergentx.org>, Gabriel Stoica (@gabrielstoica)
discussions-to: https://ethereum-magicians.org/t/erc-7578-physical-asset-redemption/17556
status: 審査中
type: Standards Track
category: ERC
created: 2023-08-01
requires: 165, 721
---

## 概要

このプロポーザルは[ERC-721](./eip-721.md)の拡張であり、NFTの基礎となる物理資産に関する追加の機能と情報を実装します。物理資産支援NFTの所有者が、基礎となる物理資産の真正性を確認し、その償還を容易に行えるようにするための情報を取り込みます。このプロポーザルの主な目的は、関係者の詳細を開示することで透明性を高め、NFT所有者と各物理資産の所有者/保有者との法的関係を定義し、容易に利用できるようにすることです。このプロポーザルにより、トークン発行者は基礎となる物理資産と関係する主要当事者に関する一連の標準化された情報を正確に組み込む責任を負うことになります。

## 動機

NFTの最初の利用事例は主に、デジタル資産の所有権を表すことでした。現実世界の資産をトークン化する傾向が高まると予想されるため、NFTの利用事例は物理資産を中心に急速に広がり、拡大していくことが予想されます。基礎となる物理資産に関する標準化された情報が組み込まれておらず、関係する主要当事者の透明性が欠如していることは、NFT所有者や潜在的なユーザーにとって不必要な障壁となり、特定の物理資産の所有権を表すためにNFTを使用することの普及を阻害する可能性があります。

容易に利用可能な情報を提供し、トークン化された経済の普及を促進するため、このプロポーザルでは、各発行されたトークンに、基礎となる物理資産の真正性の確認と償還の容易化を可能にする一連の事前定義された変数を含めることを要求しています。

## 仕様

この文書における「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」のキーワードは、RFC 2119に記載されているように解釈されるものとします。

トークンがミントされる際は、事前にプロパティを初期化する必要があり、各フィールドは以下のように定義されます:

- **トークン発行者**: NFTをミントする個人または事業体の識別
  <br> _トークン発行者は、物理資産とデジタル表現を結びつける重要な人物です。トークン発行者を特定し開示することで、NFT所有者に即座に参照ポイントが提供され、NFT発行者の信頼性を評価することができます。同時に、トークン発行者に責任を課すことで、NFTのミント プロセスの全体的な改善と標準化につながります。トークン発行者は、競争上の優位性を得るために、ベストプラクティスと認知度を競うことになります。評判の良いNFT発行者は、AMLやKYCの要件を満たすために、物理資産のミント前に、その物理資産の法的所有者に関する情報を保持しているはずです。理想的には、NFT発行者は名称で識別されますが、サービスプロバイダーが発行する一意の識別番号やネットワークIDでも識別できるでしょう。その場合、サービスプロバイダーがNFT発行者に関する関連情報を保管しています。_

- **資産保有者**: 基礎となる物理資産の法的所有者の識別
  <br> _基礎となる物理資産の償還と法的権利の行使の観点から、NFT所有者が基礎となる物理資産の法的所有者を特定することは(法的な観点から)不可欠です。これにより、NFT所有者は法的なカウンターパーティリスクを考慮できます。NFT発行者が基礎となる物理資産の法的所有者であると仮定することはできません。したがって、NFT所有者がこの追加情報に即座にアクセスできることが重要です。NFT発行者の身元と同様に、法的所有者は理想的には名称で識別されますが、サービスプロバイダーが発行する一意の識別番号やネットワークIDでも識別できるでしょう。その場合、サービスプロバイダーが法的所有者に関する関連情報を保管しています。_

- **保管場所**: 基礎となる物理資産の保管場所の識別
  <br> _特定の物理資産には特定の保管条件が必要です。不適切に保管された物理資産のデジタル表現は、NFTの価値に大きな影響を与える可能性があります。保管場所を開示し、NFT所有者が直接アクセスできるようにすることで、基礎となる物理資産の保管リスクを評価できます。さらに、基礎となる物理資産の償還の執行に関する第二の連絡先を提供します。_

- **条件**: 法的関係の識別
  <br> _NFT所有者とベースとなる物理資産の法的所有者との間の法的関係の開示と容易なアクセスは、トークン発行者に、関係する主要当事者の法的権利を規定し、定義することを促します。さらに、NFT所有者が法的リスクと執行の評価を行うことができます。理想的には、実際の法的文書(契約書や利用規約など)へのリンクを埋め込むことで情報が提供されます。NFTを介して利用可能な情報が多いほど、NFT所有者は基礎となる物理資産の償還の執行に関連する法的リスクをより適切に評価できます。_

- **管轄**: 準拠法と管轄
  <br> _適用法は法的契約の開示の拡張であり、法的契約の詳細を確認することなく、NFT所有者またはスマートコントラクトに、執行が管轄される法域を即座に提供します。これにより、管轄リスクの即時評価が可能になります。_

- **宣言価値**: 基礎資産の価値
  <br> _保険などの一部のサービスは、NFTおよび基礎となる物理資産の価値に関連付けられています。宣言価値を定義することで、NFTを特定の方法でカテゴリ化でき、宣言価値は基礎となる資産の価値の目安を提供します。基礎となる物理資産の宣言価値は、必ずしも市場価値を表すものではありません。_

`terms`パラメーターは、IPFSに保存された文書へのHTTPリンクである必要があります。これにより、文書の不変性が確保され、NFT所有者によって検証できるようになります。

トークンが有効なプロパティを持って焼却される場合、プロパティを削除する必要があります。

### コントラクトインターフェース

```solidity
pragma solidity ^0.8.21;

/**
 * @notice ERC-7578標準に従って物理資産を表すために必要なフィールドをカプセル化した構造体
 * @param tokenIssuer トークンをミントするネットワークまたはエンティティ
 * @param assetHolder 物理資産の法的所有者
 * @param storageLocation 資産が保管されている物理的な場所
 * @param terms IPFSのコントラクト、合意書、または利用規約へのリンク
 * @param jurisdiction 条件に定められた法的根拠
 * @param declaredValue トークンミント時の宣言価値
 */
struct Properties {
    string tokenIssuer;
    string assetHolder;
    string storageLocation;
    string terms;
    string jurisdiction;
    Amount declaredValue;
}

/**
 * @notice 物理資産の宣言価値を記述するフィールドをカプセル化した構造体
 * @param currency 金額の通貨
 * @param value 金額の値
 */
struct Amount {
    string currency;
    uint256 value;
}

/**
 * @notice ERC-7578に準拠したコントラクトの必須インターフェース
 */
interface IERC7578 {
    /**
     * @notice `tokenId`トークンのプロパティが設定されたときに発行されるイベント
     * @param tokenId トークンのID
     * @param properties トークンのプロパティ
     */
    event PropertiesSet(uint256 indexed tokenId, Properties properties);

    /**
     * @notice `tokenId`トークンのプロパティが削除されたときに発行されるイベント
     * @param tokenId トークンのID
     */
    event PropertiesRemoved(uint256 indexed tokenId);

    /**
     * @notice `tokenId`トークンのすべてのプロパティを取得する
     * @dev トークンが存在しない場合は、エラーを返しません
     * @param tokenId ミントされたトークンのトークンID
     */
    function getProperties(uint256 tokenId) external view returns (Properties memory properties);
}
```

`properties`が設定されると、`PropertiesSet(uint256 indexed tokenId, Properties properties)`イベントが発行されます。

`properties`が削除されると、`PropertiesRemoved(uint256 indexed tokenId)`イベントが発行されます。

`getProperties(uint256 tokenId)`関数は、トークンの一意の`properties`を返す必要があります。ERC-721トークンが焼却されているか、プロパティが設定されていない場合は、空の`Properties`構造体を返す必要があります。

## 根拠

トークンのプロパティを事前に初期化しないと、トークンで表される資産の出所を確立できないリスクがあります。

プロパティに対するコントラクトレベルの検証は使用されていません。これは、宣言されたデータの正確性はトークン発行者の責任であると考えられているためです。これにより、トークン発行者とトークン自体への信頼が構築されます。

## 下位互換性

このスタンダードはERC-721と互換性があります。

## 参考実装

OpenZeppelin ERC-721 v5ライブラリを使用したこのプロポーザルを含む[ERC-721](./eip-721.md)の例:

```solidity
pragma solidity ^0.8.21;

import { ERC721 } from "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import { IERC7578, Properties, Amount } from "./interfaces/IERC7578.sol";

/**
 * @title ERC7578
 * @author DESAT
 * @notice ERC-7578: Physical Asset Redemption標準の実装
 **/
contract ERC7578 is ERC721, IERC7578 {
    /**
     * @notice トークンのプロパティが初期化されていない場合に発生するエラー
     */
    error PropertiesUninitialized();

    /**
     * @notice `tokenId`トークンのプロパティを保持するマッピング
     */
    mapping(uint256 tokenId => Properties) private _properties;

    /**
     * @notice [ERC-721](./eip-721.md)依存コントラクトを初期化し、トークンコレクションの`name`と`symbol`を設定
     */
    constructor(string memory _name, string memory _symbol) ERC721(_name, _symbol) {}

    /**
     * @notice `tokenId`トークンのERC-7578プロパティを初期化
     */
    function setProperties(uint256 tokenId, Properties calldata properties) public {
        _properties[tokenId] = Properties({
            tokenIssuer: properties.tokenIssuer,
            assetHolder: properties.assetHolder,
            storageLocation: properties.storageLocation,
            terms: properties.terms,
            jurisdiction: properties.jurisdiction,
            declaredValue: Amount({
                currency: properties.declaredValue.currency,
                value: properties.declaredValue.value
            })
        });

        emit PropertiesSet(tokenId, _properties[tokenId]);
    }

    /**
     * @inheritdoc IERC7578
     */
    function getProperties(uint256 tokenId) public view override returns (Properties memory properties) {
        properties = _properties[tokenId];
    }

    /**
     * @notice `tokenId`トークンのプロパティを削除
     * @param tokenId プロパティを削除するトークンのID
     */
    function _removeProperties(uint256 tokenId) internal {
        delete _properties[tokenId];
        emit PropertiesRemoved(tokenId);
    }

    /**
     * @notice {_update}関数のオーバーライド。`tokenId`トークンのプロパティを削除するか、
     * ミント前にプロパティが設定されているかを確認
     * @param tokenId ミントまたは焼却されるトークンのID
     */
    function _update(address to, uint256 tokenId, address auth) internal virtual override returns (address) {
        address from = _ownerOf(tokenId);
        if (to == address(0)) {
            _removeProperties(tokenId);
        } else if (from == address(0)) {
            if (bytes(_properties[tokenId].tokenIssuer).length == 0) revert PropertiesUninitialized();
}

        return super._update(to, tokenId, auth);
    }
}
```

## セキュリティ上の考慮事項

トークンのプロパティの真正性を確保するために、`setProperties()`メソッドは、信頼できる外部所有アカウント(EOA)またはコントラクトのみが呼び出すべきです。この信頼できるエンティティは、プロパティが実際の物理的属性を正確に反映していることを検証する必要があります。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)により放棄されています。