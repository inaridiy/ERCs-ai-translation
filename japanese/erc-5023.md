---
original: 37fce8f47b5603d6a2ad98857116b5b8281bc3c685beaee3df575af5d6b332f9
---

---
eip: 5023
title: 共有可能な非代替性トークン
description: 複数の所有者によって共有可能な価値保持トークンのインターフェイス
author: Jarno Marttila (@yaruno), Martin Moravek (@mmartinmo)
discussions-to: https://ethereum-magicians.org/t/new-nft-concept-shareable-nfts/8681
status: 最終
type: 標準トラック
category: ERC
created: 2022-01-28
requires: 165
---

## 概要

このEIPは、非代替性の価値保持型の共有可能なトークンのインターフェイスを標準化します。共有可能性は、既存のトークンの新しい受信者に対するコピーのミントによって実現されます。共有とそれに関連するイベントにより、誰が誰に何を共有したかを示すグラフを構築できます。

## 動機

[EIP-721](./eip-721.md)や[EIP-1155](./eip-1155.md)などのNFTスタンダードは、希少なデジタルリソースを標準化するために開発されてきました。しかし、多くの非代替性のデジタルリソースは希少である必要はありません。

我々は、新しいタイプのインセンティブメカニズムを使って、エコシステムの正の外部性をキャプチャしようと試みてきました。これらのメカニズムは、反ライバル的な論理を示し、会計の単位として機能し、共有の媒体として機能します。我々は、共有可能なトークンが、インセンティブとしても機能し、典型的にデジタルの性質を持ち、共有されるほど価値が高まるアイテムの表現としても機能することを構想しています。

これらの要件から、共有可能なNFTと、特に非譲渡性の共有可能なNFTと呼ばれる変種を定義することにしました。これらの共有可能なNFTは、ほぼゼロのトランザクションコストで、デジタル商品と同じように「共有」することができます。我々はそれらを使って、経済システムの中で正の外部性を表す会計単位としての反ライバル的な価値をキャプチャしてきました。

EIP-721やEIP-1155などの一般的なNFTスタンダードは、共有の様式を定義していません。代わりに、ERC標準は、NFTコントラクトの実装が満たすべきトークンのミントやトークンの取引などの典型的な競合的なユースケースのインターフェイスを定義しています。 'standard contract implementations'は、これらのスタンダードの定義を超えて機能を拡張することができます。我々が実験で設計・開発してきた共有可能なトークンは、インターフェイスレベルでトークン標準互換性を持つように設計されています。しかし、トークンコントラクトの実装には、「共有可能性」の要件などの実験の要件に合わせて拡張された機能が含まれています。標準的なトークンの定義を反映すると、トークンの共有可能性は、オリジナルのバージョンを保持しつつ、既存のトークンを別のパーティーに再ミントすることと考えることができます。

共有は興味深い概念で、さまざまな方法で考えられ、認識されます。例えば、共有について話すとき、デジタルコピーのようなものと考えることができます。つまり、デジタルリソースのコピーを与えながら、自分でバージョンを保持することです。共有は分数的であったり、ある資源を使用する権利を与えることであったりします。共有可能性の概念と共有可能性の文脈は様々な形態をとることができ、共有可能なトークンのインスタンスに対して異なる種類の実装を使用する可能性があります。したがって、このインターフェイスが特定のトークンタイプを要求することは制限していません。

共有可能なトークンは、コントラクトの実装レベルで非譲渡性にすることができます。そうすることで、共有可能な非譲渡性トークンになります。参考実装では、我々のユースケースから一般的なケースを抽出し、共有可能な非譲渡性NFTを共有可能NFTインターフェイスを使って定義しています。

我々は、共有可能性の概念を満たすために実装されるべき最小限の機能を定義するこのようなインターフェイス実装の抽象レベルの定義から、より広い視聴者が恩恵を受けるべきだと考えています。

## 仕様

この文書の中の "MUST"、"MUST NOT"、"REQUIRED"、"SHALL"、"SHALL NOT"、"SHOULD"、"SHOULD NOT"、"RECOMMENDED"、"MAY"、"OPTIONAL" というキーワードは、RFC 2119に記載されているように解釈されるものとします。

```solidity
///  Note: the ERC-165 identifier for this interface is 0xded6338b
interface IERC5023 is IERC165 {

  /// @dev This emits when a token is shared, reminted and given to another wallet that isn't function caller
  event Share(address indexed from, address indexed to, uint256 indexed tokenId, uint256 derivedFromtokenId);

  /// @dev Shares, remints an existing token, gives a newly minted token a fresh token id, keeps original token at function callers possession and transfers newly minted token to receiver which should be another address than function caller. 
  function share(address to, uint256 tokenIdToBeShared) external returns(uint256 newTokenId);

} 
```

Share イベントは、関数メソッド share が正常に呼び出され、指定されたトークンIDに基づいて新しいトークンがミントされ、受信者に転送された場合に発行されることが期待されています。

## 根拠

現在のNFTスタンダードは譲渡可能な非代替性トークンを定義していますが、共有可能な非代替性トークンは定義していません。共有可能なNFTを作成するためには、共有の基本原則、つまりシェアリングイベントと共有の関数メソッドを定義するインターフェイスで、既存のNFTコントラクトを拡張する必要があります。トークンの譲渡可能性の扱いは、コントラクトの実装者に委ねられています。譲渡が可能な場合、共有可能なトークンは既存のトークンと同様の動作をしますが、共有される際にはトークンのバージョンが保持されます。譲渡が無効化された場合、共有可能なトークンは共有可能な非譲渡性トークンになり、他人に付与したり共有したりすることはできますが、譲渡することはできません。

ボブはアリスと一緒にプロジェクトに取り組んでいます。ボブはプロジェクトに尽力したことを示すユニークなNFTを獲得しましたが、ボブの業績は自分だけの功績ではないと感じています。ボブはアリスにもトークンを共有して、アリスもプロジェクトに尽力したことを認めたいと思っています。ボブはコントラクトの `Share` メソッドを呼び出して、共有したいトークンのIDとアドレスを指定することで、トークンの共有を開始します。新しいトークンがアリスにミントされ、`Share` イベントが発行されて、ボブがどのトークンIDをアリスに共有したかが記録されます。

時間の経過とともに、共有イベントの情報から、ツリー状の構造が形成されます。ボブがアリスに共有し、アリスがさらにチャーリーとデビッドに共有した場合、共有活動から基本的なツリー構造が形成されます。この共有イベントのデータは後に、トークンが表す共有活動の情報を得るために利用できます。

```text
B -> A -> C 
      \
       >  D
```

これらのツリー構造は、さらに集約・折りたたまれて、共有活動に基づくソーシャルグラフなどのネットワーク表現に変換できます。例えば、ボブがアリスに共有し、アリスがチャーリーに別のトークンを共有し、ボブがチャーリーに共有した場合、共有活動を通じてこれらの当事者間に接続が形成されます。

```text
 B----A----C         
  \_______/
```

## 下位互換性

この提案は、EIP-721およびEIP-1155との下位互換性を持っています。

## 参考実装

以下の参考実装は、我々のパイロットの1つの一般的なユースケースを示しています。この場合、共有可能な非譲渡性トークンは、コントラクトの所有者が功績トークンとして与えることを決めたコミュニティへの貢献を表しています。コントラクトの所有者は功績トークンをミントし、人に与えることができます。このトークンは、受け取った人がさらに他の参加者や影響を与えた人と共有することができます。

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0;

import "./IERC5023.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721.sol";
import "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";
import "@openzeppelin/contracts/utils/Address.sol";
import "@openzeppelin/contracts/utils/Context.sol";
import "@openzeppelin/contracts/utils/Strings.sol";
import "@openzeppelin/contracts/utils/introspection/ERC165.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/IERC721Metadata.sol";
import "@openzeppelin/contracts/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract ShareableERC721 is ERC721URIStorage, Ownable, IERC5023 /* EIP165 */ {

  string baseURI;

  uint256 internal _currentIndex;
    
  constructor(string memory _name, string memory _symbol) ERC721(_name, _symbol) {}

  function mint(
        address account,
        uint256 tokenId
    ) external onlyOwner {
        _mint(account, tokenId);
  }

  function setTokenURI(
        uint256 tokenId, 
        string memory tokenURI
    ) external {
        _setTokenURI(tokenId, tokenURI);
  }

  function setBaseURI(string memory baseURI_) external {
        baseURI = baseURI_;
  }
    
  function _baseURI() internal view override returns (string memory) {
        return baseURI;
  }

  function share(address to, uint256 tokenIdToBeShared) external returns(uint256 newTokenId) {
      require(to != address(0), "ERC721: mint to the zero address");
      require(_exists(tokenIdToBeShared), "ShareableERC721: token to be shared must exist");
      
      require(msg.sender == ownerOf(tokenIdToBeShared), "Method caller must be the owner of token");

      string memory _tokenURI = tokenURI(tokenIdToBeShared);
      _mint(to, _currentIndex);
      _setTokenURI(_currentIndex, _tokenURI);

      emit Share(msg.sender, to, _currentIndex, tokenIdToBeShared);

      return _currentIndex;
  }

  function transferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        revert('In this reference implementation tokens are not transferrable');
    }

    function safeTransferFrom(
        address from,
        address to,
        uint256 tokenId
    ) public virtual override {
        revert('In this reference implementation tokens are not transferrable');
    }
}

```

## セキュリティ上の考慮事項

参考実装はそのままでは本番環境で使用しないでください。
このスタンダードの実装に直接関連するその他のセキュリティ上の考慮事項はありません。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)によって放棄されています。