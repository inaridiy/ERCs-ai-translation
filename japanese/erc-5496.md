---
original: 3919cc3b60679cd3f25527042b756c2256d915a3ef9adbca14a3c97bd2d5fe85
---

---
eip: 5496
title: マルチ特権管理NFT拡張
description: EIP-721用の共有可能なマルチ特権NFTを作成する
author: Jeremy Z (@wnft)
discussions-to: https://ethereum-magicians.org/t/eip-5496-multi-privilege-management-extension-for-erc-721/10427
status: 最終コール
last-call-deadline: 2022-11-29
type: Standards Track
category: ERC
created: 2022-07-30
requires: 721
---


## 概要

このEIPは、[EIP-721](./eip-721.md)を拡張して、NFTの共有可能な多様な特権を提供するインターフェースを定義します。特権は、オンチェーン(投票権、エアドロップの請求権)またはオフチェーン(オンラインストアのクーポン、地元のレストランの割引、空港のVIPラウンジへのアクセスなど)にすることができます。各NFTには多数の特権が含まれ、特権の保有者は特権を他者に確実に譲渡できます。特権は非共有または共有可能です。共有可能な特権は複製でき、プロバイダーは伝播経路に応じて詳細を調整できます。有効期限も各特権に設定できます。

## 動機

この規格は、NFTに付随する特権を実時間で効率的に管理することを目的としています。多くのNFTは、プロフィール写真やアートコレクションとしてだけでなく、さまざまなシナリオで実用的な機能を持っています。たとえば、ファッションストアはそのNFTの保有者に割引を提供する可能性があり、DAOメンバーのNFT保有者は財務提案に投票できる可能性があり、dAppはブルーチップNFTの保有者などの特定のグループを引き付けるためにエアドロップイベントを作成する可能性があり、食料品店はメンバーシップカードをオンチェーンで発行(NFTとして)し、メンバーが食料品店で買い物をする際に特定の特権を与える可能性があります。NFTの所有者が必ずしも自分の特権を使いたくないケースがあります。NFTコレクションが持つさまざまな特権のデータを記録し、それらを管理するインターフェースを提供することで、所有権を失うことなく特権を譲渡または販売できるようになります。

[EIP-721](./eip-721.md)は所有権とその移転のみを記録しており、NFTの特権はオンチェーンに記録されていません。この拡張機能により、商人/プロジェクトは特定のグループに特権を付与でき、特権の所有者は各特権を個別に管理できるようになります。これにより、NFTが実際に役立つ可能性が大きく広がります。

たとえば、航空会社がCrypto Punk保有者にシリーズの[EIP-721](./eip-721.md)/[EIP-1155](./eip-1155.md)トークンを発行して特権を与え、クラブに参加してもらおうとしています。しかし、これらのトークンが元のNFTに関連付けられていないため、元のNFTが譲渡された場合、これらの特権は元の保有者の手に残り、新しい保有者は自動的に特権を享受できません。
そこで、特権を基礎となるNFTに結び付けつつ、ユーザーが特権を個別に管理できるようなインターフェースを提案します。

## 仕様

この文書の中の「MUST」、「MUST NOT」、「REQUIRED」、「SHALL」、「SHALL NOT」、「SHOULD」、「SHOULD NOT」、「RECOMMENDED」、「MAY」、「OPTIONAL」というキーワードは、RFC 2119に記載されているように解釈されるものとします。

この規格に準拠するすべてのコントラクトは、`IERC5496`インターフェースを実装しなければなりません。**共有可能な多特権拡張**は、EIP-721コントラクトにとって任意のものです。

```solidity
/// @title EIP-721用の多特権拡張
///  Note: このインターフェースのEIP-165識別子は0x076e1bbbです
interface IERC5496{
    /// @notice `owner`がNFTの`特権保有者`を変更したときに発行されます。
    event PrivilegeAssigned(uint256 tokenId, uint256 privilegeId, address user, uint256 expires);
    /// @notice `コントラクトオーナー`が`特権の総数`を変更したときに発行されます
    event PrivilegeTotalChanged(uint256 newTotal, uint256 oldTotal);

    /// @notice NFTの特権保有者を設定します。
    /// @dev expiresは30日未満である必要があります
    /// `msg.sender`がトークンIDの承認者または所有者でない場合は例外をスローします。
    /// @param tokenId 特権を設定するNFT
    /// @param privilegeId 設定する特権
    /// @param user 特権を設定するユーザー
    /// @param expires 特権保有者が持つ期間
    function setPrivilege(uint256 tokenId, uint256 privilegeId, address user, uint256 expires) external;

    /// @notice 特権の有効期限タイムスタンプを返します
    /// @param tokenId 照会対象のNFTの識別子
    /// @param privilegeId 照会対象の特権の識別子
    /// @return ユーザーが特定の特権を持っているかどうか
    function privilegeExpires(uint256 tokenId, uint256 privilegeId) external view returns(uint256);

    /// @notice ユーザーが特定の特権を持っているかどうかを確認します
    /// @param tokenId 照会対象のNFTの識別子
    /// @param privilegeId 照会対象の特権の識別子
    /// @param user 照会対象のユーザーのアドレス
    /// @return ユーザーが特定の特権を持っているかどうか
    function hasPrivilege(uint256 tokenId, uint256 privilegeId, address user) external view returns(bool);
}
```

この規格を実装するすべてのコントラクトは、特権の設定前に最大特権数を設定する必要があります。`privilegeId`は最大特権数を超えてはいけません。

`PrivilegeAssigned`イベントは、`setPrivilege`が呼び出されたときに必ず発行されなければなりません。

`PrivilegeTotalChanged`イベントは、コレクションの`特権の総数`が変更されたときに必ず発行されなければなりません。

`supportsInterface`メソッドは、`0x076e1bbb`で呼び出されたときに`true`を返さなければなりません。

```solidity
/// @title クローン可能な拡張 - EIP-721用のオプション
interface IERC721Cloneable {
    /// @notice NFTの`特権`をクローン可能に設定したときに発行されます。
    event PrivilegeCloned(uint tokenId, uint privId, address from, address to);

    /// @notice 特定の特権をクローン可能に設定します
    /// @param tokenId 照会対象のNFTの識別子
    /// @param privilegeId 照会対象の特権の識別子
    /// @param referrer 紹介者のアドレス
    /// @return 操作が成功したかどうか
    function clonePrivilege(uint tokenId, uint privId, address referrer) external returns (bool);
}
```

`PrivilegeCloned`イベントは、`clonePrivilege`が呼び出されたときに必ず発行されなければなりません。

準拠コントラクトでは、[EIP-1271](./eip-1271.md)を使ってシグネチャを検証することをお勧めします。

## 根拠

### 共有可能な特権

特権が非共有の場合、特権保有者の数はNFTの数に制限されます。共有可能な特権では、元の特権保有者が特権をコピーして他人に与えることができ、自分の特権を譲渡する必要がありません。この仕組みにより、特権の普及と NFTの採用が大幅に促進されます。

### 有効期限の型

特権の有効期限タイムスタンプは`uint256`型の変数に格納されます。

### 紹介者の受益者

たとえば、地元のピザ店が30%オフのクーポンを提供し、店主が消費者に友人と共有するよう奨励したとします。その場合、友人もクーポンを受け取ることができます。トムが店から30%オフのクーポンを受け取り、それをアリスと共有したとします。アリスもクーポンを受け取り、アリスの紹介者はトムです。特定のケースでは、トムが店から追加の報酬を受け取る可能性があります。これにより、商人は消費者の間でプロモーションを広めやすくなります。

### 提案: NFTの譲渡

NFTの所有者が他のユーザーに所有権を譲渡した場合、「特権」には影響はありません。ただし、まだ有効な特権がある場合に、所有者が`unwrap()`を通じて元の[EIP-721](./eip-721.md)トークンを引き出そうとすると、エラーが発生する可能性があります。特権保有者の権利を保護するため、特権の最終有効期限を確認します。

```solidity
function unwrap(uint256 tokenId, address to) external {
    require(getBlockTimestamp() >= privilegeBook[tokenId].lastExpiresAt, "privilege not yet expired");

    require(ownerOf(tokenId) == msg.sender, "not owner");

    _burn(tokenId);

    IERC721(nft).transferFrom(address(this), to, tokenId);

    emit Unwrap(nft, tokenId, msg.sender, to);
}
```

## 下位互換性

このEIPは、EIP-721標準に準拠するあらゆる種類のNFTと互換性があります。元の[EIP-721](./eip-721.md)標準に干渉することなく、より多くの機能とデータ構造を追加するだけです。

## テストケース

テストケースは、リファレンス実装で実装されています。

### テストコード

[test.js](../assets/eip-5496/test/test.js)

ターミナルで実行:

```shell
truffle test ./test/test.js
```

[testCloneable.js](../assets/eip-5496/test/testCloneable.js)

ターミナルで実行:

```shell
truffle test ./test/testCloneable.js
```

## リファレンス実装

```solidity
// SPDX-License-Identifier: CC0-1.0
pragma solidity ^0.8.0; 

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/introspection/IERC165.sol";
import "./IERC5496.sol";

contract ERC5496 is ERC721, IERC5496 {
    struct PrivilegeRecord {
        address user;
        uint256 expiresAt;
    }
    struct PrivilegeStorage {
        uint lastExpiresAt;
        // privId => PrivilegeRecord
        mapping(uint => PrivilegeRecord) privilegeEntry;
    }

    uint public privilegeTotal;
    // tokenId => PrivilegeStorage
    mapping(uint => PrivilegeStorage) public privilegeBook;
    mapping(address => mapping(address => bool)) private privilegeDelegator;

    constructor(string memory name_, string memory symbol_)
    ERC721(name_,symbol_)
    {
    
    }

    function setPrivilege(
        uint tokenId,
        uint privId,
        address user,
        uint64 expires
    ) external virtual {
        require((hasPrivilege(tokenId, privId, ownerOf(tokenId)) && _isApprovedOrOwner(msg.sender, tokenId)) || _isDelegatorOrHolder(msg.sender, tokenId, privId), "ERC721: transfer caller is not owner nor approved");
        require(expires < block.timestamp + 30 days, "expire time invalid");
        require(privId < privilegeTotal, "invalid privilege id");
        privilegeBook[tokenId].privilegeEntry[privId].user = user;
        if (_isApprovedOrOwner(msg.sender, tokenId)) {
            privilegeBook[tokenId].privilegeEntry[privId].expiresAt = expires;
            if (privilegeBook[tokenId].lastExpiresAt < expires) {
                privilegeBook[tokenId].lastExpiresAt = expires;
            }
        }
        emit PrivilegeAssigned(tokenId, privId, user, uint64(privilegeBook[tokenId].privilegeEntry[privId].expiresAt));
    }

    function hasPrivilege(
        uint256 tokenId,
        uint256 privId,
        address user
    ) public virtual view returns(bool) {
        if (privilegeBook[tokenId].privilegeEntry[privId].expiresAt >= block.timestamp){
            return privilegeBook[tokenId].privilegeEntry[privId].user == user;
        }
        return ownerOf(tokenId) == user;
    }

    function privilegeExpires(
        uint256 tokenId,
        uint256 privId
    ) public virtual view returns(uint256){
        return privilegeBook[tokenId].privilegeEntry[privId].expiresAt;
    }

    function _setPrivilegeTotal(
        uint total
    ) internal {
        emit PrivilegeTotalChanged(total, privilegeTotal);
        privilegeTotal = total;
    }

    function getPrivilegeInfo(uint tokenId, uint privId) external view returns(address user, uint256 expiresAt) {
        return (privilegeBook[tokenId].privilegeEntry[privId].user, privilegeBook[tokenId].privilegeEntry[privId].expiresAt);
    }

    function setDelegator(address delegator, bool enabled) external
{
    privilegeDelegator[msg.sender][delegator] = enabled;
}

function _isDelegatorOrHolder(address delegator, uint256 tokenId, uint privId) internal virtual view returns (bool) {
    address holder = privilegeBook[tokenId].privilegeEntry[privId].user;
     return (delegator == holder || isApprovedForAll(holder, delegator) || privilegeDelegator[holder][delegator]);
}

function supportsInterface(bytes4 interfaceId) public override virtual view returns (bool) {
    return interfaceId == type(IERC5496).interfaceId || super.supportsInterface(interfaceId);
}
}
```

## セキュリティ上の考慮事項

実装では、特権の設定やクローンを許可するユーザーを慎重に検討する必要があります。

## 著作権

著作権およびそれに関連する権利は[CC0](../LICENSE.md)で放棄されています。